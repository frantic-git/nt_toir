
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСтатусов.ЗагрузитьЗначения(Параметры.СписокСледующихСтатусов.ВыгрузитьЗначения());
	ВыделенныйСтатус = Параметры.ВыделенныйСтатус;
	СпособСогласования = Параметры.СпособСогласования;
	
	ЗаполнитьСписокСтатусовСОбязательнымКомментарием();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ВыделенныйСтатус) Тогда
		НайденноеЗначение = СписокСтатусов.НайтиПоЗначению(ВыделенныйСтатус);
		Если НайденноеЗначение <> Неопределено Тогда
			Элементы.СписокСтатусов.ТекущаяСтрока = НайденноеЗначение.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбранныйСтатус = Неопределено;
	Если СписокСтатусов.Количество() = 1 Тогда
		ВыбранныйСтатус = СписокСтатусов[0].Значение;
	Иначе
		ТекущиеДанные = Элементы.СписокСтатусов.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ВыбранныйСтатус = ТекущиеДанные.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыбранныйСтатус = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран статус!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Если Не СписокСтатусовСОбязательнымКомментарием.НайтиПоЗначению(ВыбранныйСтатус) = Неопределено
		И Не ЗначениеЗаполнено(Комментарий) Тогда
		ТекстСообщения = НСтр("ru = 'Согласно настройкам способа согласования, для установки выбранного статуса необходимо заполнить комментарий!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Закрыть(Новый Структура("ВыбранныйСтатус, Комментарий", ВыбранныйСтатус, Комментарий));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокСтатусов

&НаКлиенте
Процедура СписокСтатусовПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.СписокСтатусов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
	    Возврат;
	КонецЕсли;
	
	ТекущийСтатус = ТекущиеДанные.Значение;
	Если Не СписокСтатусовСОбязательнымКомментарием.НайтиПоЗначению(ТекущийСтатус) = Неопределено Тогда
		Элементы.Комментарий.ОтметкаНезаполненного = Истина;
	Иначе
		Элементы.Комментарий.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокСтатусовСОбязательнымКомментарием()
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_НастройкиСтатусовСогласованияДокументов.Статус КАК Статус
	               |ИЗ
	               |	РегистрСведений.торо_НастройкиСтатусовСогласованияДокументов КАК торо_НастройкиСтатусовСогласованияДокументов
	               |ГДЕ
	               |	торо_НастройкиСтатусовСогласованияДокументов.СпособСогласования = &СпособСогласования
	               |	И торо_НастройкиСтатусовСогласованияДокументов.Статус В(&СписокСтатусов)
	               |	И торо_НастройкиСтатусовСогласованияДокументов.КомментарийОбязателен";
	
	Запрос.УстановитьПараметр("СпособСогласования", СпособСогласования);
	Запрос.УстановитьПараметр("СписокСтатусов", СписокСтатусов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	    Возврат;
	КонецЕсли;
	
	ВыборкаСтатусов = РезультатЗапроса.Выбрать();
	Пока ВыборкаСтатусов.Следующий() Цикл
	    СписокСтатусовСОбязательнымКомментарием.Добавить(ВыборкаСтатусов.Статус);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти