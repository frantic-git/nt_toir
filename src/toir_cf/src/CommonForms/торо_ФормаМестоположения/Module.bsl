#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Документ") И ЗначениеЗаполнено(Параметры.Документ) Тогда
		Документ			= Параметры.Документ;
		ОбъектРемонта		= Параметры.ОбъектРемонта;
		СтруктураКоординат	= ПолучитьКоординаты(Документ, ОбъектРемонта);
		КоординатыВысота	= СтруктураКоординат.Высота;
		КоординатыДолгота	= СтруктураКоординат.Долгота;
		КоординатыШирота	= СтруктураКоординат.Широта;	
	Иначе
		КоординатыВысота	= Параметры.Высота;
		КоординатыДолгота	= Параметры.Долгота;
		КоординатыШирота	= Параметры.Широта;
		ОбъектРемонта		= Параметры.ОбъектРемонта;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КоординатыДолгота) И НЕ ЗначениеЗаполнено(КоординатыШирота) Тогда
		Элементы.ФормаИзменитьКоординаты.Пометка = Истина;
	Иначе
		HTML = ПолучитьHTMLдокумент();
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПросмотр") И Параметры.ТолькоПросмотр <> Неопределено Тогда
		Элементы.ФормаИзменитьКоординаты.Пометка = Элементы.ФормаИзменитьКоординаты.Пометка И НЕ Параметры.ТолькоПросмотр;
		Элементы.ФормаИзменитьКоординаты.Доступность = НЕ Параметры.ТолькоПросмотр;
		Элементы.ФормаСохранитьИЗакрыть.Доступность = НЕ Параметры.ТолькоПросмотр;
	КонецЕсли;
	
	Элементы.КоординатыВысота.Доступность = Элементы.ФормаИзменитьКоординаты.Пометка;
	Элементы.КоординатыДолгота.Доступность = Элементы.ФормаИзменитьКоординаты.Пометка;
	Элементы.КоординатыШирота.Доступность = Элементы.ФормаИзменитьКоординаты.Пометка;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьКоординаты(Команда)
	
	Кнопка = Элементы.ФормаИзменитьКоординаты;
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	Элементы.КоординатыВысота.Доступность = Кнопка.Пометка;
	Элементы.КоординатыДолгота.Доступность = Кнопка.Пометка;
	Элементы.КоординатыШирота.Доступность = Кнопка.Пометка;
	
	Если НЕ Кнопка.Пометка Тогда 
		HTML = ПолучитьHTMLдокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	СтруктураКоординат = Новый Структура("Высота, Долгота, Широта", КоординатыВысота, КоординатыДолгота, КоординатыШирота);
	Закрыть(СтруктураКоординат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
&НаСервере
Функция ПолучитьHTMLдокумент()
	
	Если НЕ ЗначениеЗаполнено(КоординатыДолгота) И НЕ ЗначениеЗаполнено(КоординатыДолгота) Тогда 
		Возврат "";
	КонецЕсли;
	
	МакетКарты = ПолучитьОбщийМакет("торо_ЯндексКарта");
	ТекстДокумента = МакетКарты.ПолучитьТекст();
	ТекстДокумента = СтрЗаменить(ТекстДокумента, "COORDS_W", Формат(КоординатыШирота, "ЧРД=."));
	ТекстДокумента = СтрЗаменить(ТекстДокумента, "COORDS_H", Формат(КоординатыДолгота, "ЧРД=."));
	ТекстДокумента = СтрЗаменить(ТекстДокумента, "OBJECT_NAME", Строка(ОбъектРемонта)); 
	
	Возврат ТекстДокумента;
	
КонецФункции

&НаСервере 
Функция ПолучитьКоординаты(Документ, ОбъектРемонта)

	СтруктураКоординат = Новый Структура("Высота, Широта, Долгота", 0, 0, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.Высота КАК Высота,
	               |	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.Долгота КАК Долгота,
	               |	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.Широта КАК Широта
	               |ИЗ
	               |	Документ.торо_ВнешнееОснованиеДляРабот.ОбследованноеОборудование КАК торо_ВнешнееОснованиеДляРаботОбследованноеОборудование
	               |ГДЕ
	               |	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.Ссылка = &Документ
	               |	И торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.ОбъектРемонта = &ОбъектРемонта
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	торо_ВыявленныеДефектыСписокДефектов.ОтказавшийЭлемент,
	               |	торо_ВыявленныеДефектыСписокДефектов.Высота,
	               |	торо_ВыявленныеДефектыСписокДефектов.Долгота,
	               |	торо_ВыявленныеДефектыСписокДефектов.Широта
	               |ИЗ
	               |	Документ.торо_ВыявленныеДефекты.СписокДефектов КАК торо_ВыявленныеДефектыСписокДефектов
	               |ГДЕ
	               |	торо_ВыявленныеДефектыСписокДефектов.Ссылка = &Документ
	               |	И торо_ВыявленныеДефектыСписокДефектов.ОтказавшийЭлемент = &ОбъектРемонта";
	
	  Запрос.УстановитьПараметр("Документ", Документ);
	  Запрос.УстановитьПараметр("ОбъектРемонта", ОбъектРемонта);
	  
	  Результат = Запрос.Выполнить();
	  Выборка = Результат.Выбрать();
	  
	  Если Выборка.Следующий() Тогда
		  СтруктураКоординат.Высота = Выборка.Высота;
		  СтруктураКоординат.Долгота = Выборка.Долгота;
		  СтруктураКоординат.Широта = Выборка.Широта;	  
	  КонецЕсли;
	  
	  Возврат СтруктураКоординат;
	
КонецФункции


#КонецОбласти