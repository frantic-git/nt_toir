#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПроверитьВозможностьОбновленияМодели(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВсегдаЗагружатьАвтоматически = Константы.торо_АвтоматическиОбновлятьПоставляемуюМодельПоказателейKPI.Получить();
	
	ЗаполнитьТекстВопроса();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВсегдаЗагружатьАвтоматическиПриИзменении(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВсегдаЗагружатьАвтоматическиПриИзменении(Элемент)
	Элементы.Нет.Доступность = Не ВсегдаЗагружатьАвтоматически;
КонецПроцедуры

&НаКлиенте
Процедура Да(Команда)
	ОбработкаВыбораПользователя();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Нет(Команда)
	ОбработкаВыбораПользователя(Ложь);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьВозможностьОбновленияМодели(Отказ)
	Если Не (Константы.торо_ЗадатьВопросОбОбновленииПоставляемойМодели.Получить()
				И ПравоДоступа("Добавление", Метаданные.Справочники.торо_ПоказателиKPI)
				И ПравоДоступа("Изменение", Метаданные.Справочники.торо_ПоказателиKPI)
				И ПравоДоступа("Добавление", Метаданные.Справочники.торо_ВариантыАнализаПоказателейKPI)
				И ПравоДоступа("Изменение", Метаданные.Справочники.торо_ВариантыАнализаПоказателейKPI)
				И ПравоДоступа("Чтение", Метаданные.Справочники.ЕдиницыИзмерения)) Тогда
	    Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстВопроса()
	АктуальнаяВерсияМоделиKPI = торо_ПоказателиKPI.АктуальнаяВерсияПоставляемойМодели();
	ЗагруженнаяВерсияМоделиKPI = Константы.торо_ЗагруженнаяВерсияПоставляемойМодели.Получить();
	Если Не ЗначениеЗаполнено(ЗагруженнаяВерсияМоделиKPI) Тогда
	    ЗагруженнаяВерсияМоделиKPI = "1.1";
	КонецЕсли;
	
	Элементы.ДекорацияТекстВопроса.Заголовок = СтрШаблон(Элементы.ДекорацияТекстВопроса.Заголовок, АктуальнаяВерсияМоделиKPI, ЗагруженнаяВерсияМоделиKPI);
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПользователя(ОбновитьМодельПоказателей = Истина)
	Если ОбновитьМодельПоказателей Тогда
	    торо_ПоказателиKPI.ВосстановитьПоставляемуюМодельПоказателей();
	КонецЕсли;
	
	Константы.торо_АвтоматическиОбновлятьПоставляемуюМодельПоказателейKPI.Установить(ВсегдаЗагружатьАвтоматически);
	Константы.торо_ЗадатьВопросОбОбновленииПоставляемойМодели.Установить(Ложь);
КонецПроцедуры

#КонецОбласти