
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКЗагружаемомуФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = ФильтрЗагружаемыхФайлов();
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите загружаемый проект:'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКЗагружаемомуФайлуНачалоВыбораЗавершение", ЭтотОбъект);
	ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	Если ПутьКЗагружаемомуФайлу <> "" Тогда
		ЭтотОбъект.Закрыть(ПутьКЗагружаемомуФайлу);
	Иначе
		ТекстСообщения = НСтр("ru = 'Файл не выбран.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ПутьКЗагружаемомуФайлу");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ФильтрЗагружаемыхФайлов()
	
	Возврат НСтр("ru = 'Все файлы (*.mpp; *.xml)|*.mpp;*.xml|'")
	      + НСтр("ru = 'Файлы MS Project (*.mpp)|*.mpp|'")
	      + НСтр("ru = 'Файлы обмена XML (*.xml)|*.xml|'");
	
КонецФункции

&НаКлиенте
Процедура ПутьКЗагружаемомуФайлуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКЗагружаемомуФайлу = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

