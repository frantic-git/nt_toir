
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
   ВыборкаПользователей = Справочники.Пользователи.Выбрать();
   
   Пока ВыборкаПользователей.Следующий() Цикл
 	   ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ВыборкаПользователей.ИдентификаторПользователяИБ); 
	   
	   Если ПользовательИБ <> Неопределено Тогда 
		  СтрТЗ = ТаблицаПользователей.Добавить();
		  СтрТЗ.Пользователь = ВыборкаПользователей.Ссылка;
		  СтрТЗ.ТекущаяОрганизация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		      "НастройкиТОиР",
		      "ОсновнаяОрганизация",
		      Справочники.Организации.ПустаяСсылка(),,
		      ПользовательИБ.Имя);
	   КонецЕсли;   
  
   КонецЦикла;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьФлаги(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьФлаги(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(ПередатьВыбранныхПользователей());
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьФлаги(НужноУстановить = Неопределено)
	
	Для каждого Строка Из ТаблицаПользователей Цикл 
		Строка.Выбран = ?(НужноУстановить = Неопределено, НЕ Строка.Выбран, НужноУстановить);
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ПередатьВыбранныхПользователей()
	
	МассивВозврата = Новый Массив;
	Для каждого Строка Из  ТаблицаПользователей Цикл 
		Если Строка.Выбран Тогда
			МассивВозврата.Добавить(Строка.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивВозврата;
	
КонецФункции 
#КонецОбласти


