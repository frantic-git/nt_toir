#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ОсновнойСклад");
	КонецЕсли;
	
	торо_ЗаполнениеДокументов.ЗаполнитьСтандартныеРеквизитыШапкиПоУмолчанию(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ФОИспользоватьСерии = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ФОИспользоватьХарактеристики = Константы.торо_ИспользоватьХарактеристикиНоменклатуры.Получить();
	КонтролироватьОстаки = Константы.торо_ИспользоватьКонтрольОтрицательныхОстатков.Получить();
	
	Блокировка = Новый БлокировкаДанных;
	
	// Чтобы никто не списал вместе с нами и мы могли правильно зарезервировать	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Если ФОИспользоватьХарактеристики Тогда 
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	КонецЕсли;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	Если ФОИспользоватьСерии Тогда 
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия", "Серия");
	КонецЕсли;
	
	// Чтобы никто не добавил/убавил в заказ, чтобы правильно зарезервировать
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗаказыНаВнутреннееПотребление");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.УстановитьЗначение("ЗаказНаВнутреннееПотребление", ЗаказНаВнутреннееПотребление);
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Если ФОИспользоватьХарактеристики Тогда 
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	КонецЕсли;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	Если ФОИспользоватьСерии Тогда 
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия", "Серия");
	КонецЕсли;
	
	// Чтобы никто не зарезервировал на этот заказ больше, чем нужно
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.торо_РезервыНаСкладах");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Если ФОИспользоватьХарактеристики Тогда 
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	КонецЕсли;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	Если ФОИспользоватьСерии Тогда 
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия", "Серия");
	КонецЕсли;		
	ЭлементБлокировки.УстановитьЗначение("ЗаказНаВнутреннееПотребление", ЗаказНаВнутреннееПотребление);

	Блокировка.Заблокировать();
	
	Движения.торо_РезервыНаСкладах.Записывать = Истина;
	Если ОтменаРезерва Тогда
		Для Каждого СтрокаТЧ Из Товары Цикл 
			Движение = Движения.торо_РезервыНаСкладах.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Склад = Склад;
			Движение.ЗаказНаВнутреннееПотребление = ЗаказНаВнутреннееПотребление;
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТЧ);
		КонецЦикла;
	Иначе
		Запрос = Новый Запрос;
		ТекстЗапроса = "ВЫБРАТЬ
		               |	Таблица.Ссылка КАК Ссылка,
		               |	Таблица.Номенклатура КАК Номенклатура,
		               |	&Характеристика КАК Характеристика,
		               |	СУММА(Таблица.Количество) КАК Количество,
		               |	&Серия КАК Серия,
		               |	&Склад КАК Склад,
		               |	&ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
		               |	Таблица.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		               |ПОМЕСТИТЬ ТабличнаяЧасть
		               |ИЗ
		               |	Документ.торо_РезервПодВнутреннийЗаказ.Товары КАК Таблица
		               |ГДЕ
		               |	Таблица.Ссылка = &Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Таблица.Номенклатура,
		               |	Таблица.Ссылка,
		               |	Таблица.Характеристика,
		               |	Таблица.Серия,
		               |	Таблица.Номенклатура.ЕдиницаИзмерения
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Склад,
		               |	Номенклатура,
		               |	Характеристика,
		               |	Серия,
		               |	ЗаказНаВнутреннееПотребление
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	торо_РезервыНаСкладахОстатки.Склад КАК Склад,
		               |	торо_РезервыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		               |	торо_РезервыНаСкладахОстатки.Характеристика КАК Характеристика,
		               |	торо_РезервыНаСкладахОстатки.Серия КАК Серия,
		               |	торо_РезервыНаСкладахОстатки.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
		               |	торо_РезервыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		               |ПОМЕСТИТЬ РезервыПоЗаказу
		               |ИЗ
		               |	РегистрНакопления.торо_РезервыНаСкладах.Остатки(
		               |			&Дата,
		               |			(ЗаказНаВнутреннееПотребление, Номенклатура, Характеристика, Серия, Склад) В
		               |				(ВЫБРАТЬ
		               |					Таблица.ЗаказНаВнутреннееПотребление,
		               |					Таблица.Номенклатура,
		               |					Таблица.Характеристика,
		               |					Таблица.Серия,
		               |					Таблица.Склад
		               |				ИЗ
		               |					ТабличнаяЧасть КАК Таблица)) КАК торо_РезервыНаСкладахОстатки
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	ЗаказНаВнутреннееПотребление,
		               |	Склад,
		               |	Номенклатура,
		               |	Характеристика,
		               |	Серия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ЗаказыНаВнутреннееПотреблениеОстатки.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
		               |	ЗаказыНаВнутреннееПотреблениеОстатки.Номенклатура КАК Номенклатура,
		               |	ЗаказыНаВнутреннееПотреблениеОстатки.Характеристика КАК Характеристика,
		               |	ЗаказыНаВнутреннееПотреблениеОстатки.Склад КАК Склад,
		               |	ЗаказыНаВнутреннееПотреблениеОстатки.КОформлениюОстаток КАК КОформлениюОстаток
		               |ПОМЕСТИТЬ Заказы
		               |ИЗ
		               |	РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки(
		               |			&Дата,
		               |			(ЗаказНаВнутреннееПотребление, Номенклатура, Характеристика, Склад) В
		               |				(ВЫБРАТЬ
		               |					Таблица.ЗаказНаВнутреннееПотребление,
		               |					Таблица.Номенклатура,
		               |					Таблица.Характеристика,
		               |					Таблица.Склад
		               |				ИЗ
		               |					ТабличнаяЧасть КАК Таблица)) КАК ЗаказыНаВнутреннееПотреблениеОстатки
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	ЗаказНаВнутреннееПотребление,
		               |	Склад,
		               |	Номенклатура,
		               |	Характеристика
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		               |	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
		               |	ТоварыНаСкладахОстатки.Склад КАК Склад,
		               |	ТоварыНаСкладахОстатки.Серия КАК Серия,
		               |	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК ВНаличииОстаток
		               |ПОМЕСТИТЬ ТоварыНаСкладах
		               |ИЗ
		               |	РегистрНакопления.ТоварыНаСкладах.Остатки(
		               |			&Дата,
		               |			(Номенклатура, Характеристика, Серия, Склад) В
		               |				(ВЫБРАТЬ
		               |					Таблица.Номенклатура,
		               |					Таблица.Характеристика,
		               |					Таблица.Серия,
		               |					Таблица.Склад
		               |				ИЗ
		               |					ТабличнаяЧасть КАК Таблица)) КАК ТоварыНаСкладахОстатки
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Номенклатура,
		               |	Характеристика,
		               |	Склад,
		               |	Серия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	торо_РезервыНаСкладахОстатки.Склад КАК Склад,
		               |	торо_РезервыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		               |	торо_РезервыНаСкладахОстатки.Характеристика КАК Характеристика,
		               |	торо_РезервыНаСкладахОстатки.Серия КАК Серия,
		               |	торо_РезервыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		               |ПОМЕСТИТЬ ВсеРезервы
		               |ИЗ
		               |	РегистрНакопления.торо_РезервыНаСкладах.Остатки(
		               |			&Дата,
		               |			(Номенклатура, Характеристика, Серия, Склад) В
		               |				(ВЫБРАТЬ
		               |					Таблица.Номенклатура,
		               |					Таблица.Характеристика,
		               |					Таблица.Серия,
		               |					Таблица.Склад
		               |				ИЗ
		               |					ТабличнаяЧасть КАК Таблица)) КАК торо_РезервыНаСкладахОстатки
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Склад,
		               |	Номенклатура,
		               |	Характеристика,
		               |	Серия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	торо_РезервыНаРукахОстатки.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
		               |	торо_РезервыНаРукахОстатки.Номенклатура КАК Номенклатура,
		               |	торо_РезервыНаРукахОстатки.Характеристика КАК Характеристика,
		               |	торо_РезервыНаРукахОстатки.Склад КАК Склад,
		               |	торо_РезервыНаРукахОстатки.Серия КАК Серия,
		               |	торо_РезервыНаРукахОстатки.КоличествоОстаток КАК КоличествоОстаток
		               |ПОМЕСТИТЬ РезервыНаРуках
		               |ИЗ
		               |	РегистрНакопления.торо_РезервыНаРуках.Остатки(
		               |			&Дата,
		               |			(ЗаказНаВнутреннееПотребление, Номенклатура, Характеристика, Серия, Склад) В
		               |				(ВЫБРАТЬ
		               |					Таблица.ЗаказНаВнутреннееПотребление,
		               |					Таблица.Номенклатура,
		               |					Таблица.Характеристика,
		               |					Таблица.Серия,
		               |					Таблица.Склад
		               |				ИЗ
		               |					ТабличнаяЧасть КАК Таблица)) КАК торо_РезервыНаРукахОстатки
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	ЗаказНаВнутреннееПотребление,
		               |	Склад,
		               |	Номенклатура,
		               |	Характеристика,
		               |	Серия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТабличнаяЧасть.Склад КАК Склад,
		               |	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
		               |	ТабличнаяЧасть.Характеристика КАК Характеристика,
		               |	СУММА(ТабличнаяЧасть.Количество) КАК Количество,
		               |	ТабличнаяЧасть.Серия КАК Серия,
		               |	ТабличнаяЧасть.Ссылка КАК Ссылка,
		               |	СУММА(ЕСТЬNULL(Резервы.КоличествоОстаток, 0) + ЕСТЬNULL(торо_РезервыНаРуках.КоличествоОстаток, 0)) КАК РезервПоЗаказу,
		               |	СУММА(ЕСТЬNULL(РезервыВсе.КоличествоОстаток, 0)) КАК РезервПоСкладу,
		               |	СУММА(ЕСТЬNULL(Заказы.КОформлениюОстаток, 0)) КАК Заказано,
		               |	СУММА(ЕСТЬNULL(ОстаткиНаСкладах.ВНаличииОстаток, 0)) КАК ОстатокНаСкладе,
		               |	ТабличнаяЧасть.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
		               |	ТабличнаяЧасть.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		               |ИЗ
		               |	ТабличнаяЧасть КАК ТабличнаяЧасть
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РезервыПоЗаказу КАК Резервы
		               |		ПО ТабличнаяЧасть.Склад = Резервы.Склад
		               |			И ТабличнаяЧасть.Номенклатура = Резервы.Номенклатура
		               |			И ТабличнаяЧасть.Характеристика = Резервы.Характеристика
		               |			И ТабличнаяЧасть.ЗаказНаВнутреннееПотребление = Резервы.ЗаказНаВнутреннееПотребление
		               |			И ТабличнаяЧасть.Серия = Резервы.Серия
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки КАК Заказы
		               |		ПО ТабличнаяЧасть.Склад = Заказы.Склад
		               |			И ТабличнаяЧасть.Номенклатура = Заказы.Номенклатура
		               |			И ТабличнаяЧасть.Характеристика = Заказы.Характеристика
		               |			И ТабличнаяЧасть.ЗаказНаВнутреннееПотребление = Заказы.ЗаказНаВнутреннееПотребление
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыНаСкладах КАК ОстаткиНаСкладах
		               |		ПО ТабличнаяЧасть.Склад = ОстаткиНаСкладах.Склад
		               |			И ТабличнаяЧасть.Номенклатура = ОстаткиНаСкладах.Номенклатура
		               |			И ТабличнаяЧасть.Характеристика = ОстаткиНаСкладах.Характеристика
		               |			И ТабличнаяЧасть.Серия = ОстаткиНаСкладах.Серия
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВсеРезервы КАК РезервыВсе
		               |		ПО ТабличнаяЧасть.Склад = РезервыВсе.Склад
		               |			И ТабличнаяЧасть.Номенклатура = РезервыВсе.Номенклатура
		               |			И ТабличнаяЧасть.Характеристика = РезервыВсе.Характеристика
		               |			И ТабличнаяЧасть.Серия = РезервыВсе.Серия
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РезервыНаРуках КАК торо_РезервыНаРуках
		               |		ПО ТабличнаяЧасть.Склад = торо_РезервыНаРуках.Склад
		               |			И ТабличнаяЧасть.Номенклатура = торо_РезервыНаРуках.Номенклатура
		               |			И ТабличнаяЧасть.Характеристика = торо_РезервыНаРуках.Характеристика
		               |			И ТабличнаяЧасть.ЗаказНаВнутреннееПотребление = торо_РезервыНаРуках.ЗаказНаВнутреннееПотребление
		               |			И ТабличнаяЧасть.Серия = торо_РезервыНаРуках.Серия
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТабличнаяЧасть.Ссылка,
		               |	ТабличнаяЧасть.Склад,
		               |	ТабличнаяЧасть.Номенклатура,
		               |	ТабличнаяЧасть.Характеристика,
		               |	ТабличнаяЧасть.Серия,
		               |	ТабличнаяЧасть.ЗаказНаВнутреннееПотребление,
		               |	ТабличнаяЧасть.ЕдиницаИзмерения
		               |ИТОГИ
		               |	СУММА(Количество),
		               |	СУММА(РезервПоЗаказу),
		               |	СУММА(РезервПоСкладу),
		               |	МИНИМУМ(Заказано),
		               |	СУММА(ОстатокНаСкладе)
		               |ПО
		               |	Номенклатура,
		               |	Характеристика";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.УстановитьПараметр("ЗаказНаВнутреннееПотребление", ЗаказНаВнутреннееПотребление);
		Запрос.УстановитьПараметр("Дата", ТекущаяДата());
		
		Если ФОИспользоватьСерии Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Серия", "Таблица.Серия");
		Иначе
			Запрос.УстановитьПараметр("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка());
		КонецЕсли;
		
		Если ФОИспользоватьХарактеристики Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Характеристика", "Таблица.Характеристика");
		Иначе
			Запрос.УстановитьПараметр("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		РезЗапроса = Запрос.Выполнить();
		Если НЕ РезЗапроса.Пустой() Тогда 
			
			ВыборкаНоменклатур = РезЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатур.Следующий() Цикл
				ВыборкаХарактеристик = ВыборкаНоменклатур.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаХарактеристик.Следующий() Цикл
					КоличествоСерий = 0;
					ВыборкаСерий = ВыборкаХарактеристик.Выбрать();
					Пока ВыборкаСерий.Следующий() Цикл 
						КоличествоСерий = КоличествоСерий + 1;
						ПредставлениеНоменклатуры = ?(ФОИспользоватьХарактеристики И ЗначениеЗаполнено(ВыборкаСерий.Характеристика), 
						СтрШаблон("%1, %2", ВыборкаСерий.Номенклатура, ВыборкаСерий.Характеристика), ВыборкаСерий.Номенклатура);
						СвободныйОстаток = ВыборкаСерий.ОстатокНаСкладе - ВыборкаСерий.РезервПоСкладу; 
						Если ВыборкаСерий.Заказано <= 0 Тогда 
							Отказ = Истина;
								ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
								НСтр("ru = 'Потребность по внутреннему заказу для номенклатуры <%1> закрыта'"), ПредставлениеНоменклатуры));
						КонецЕсли;
						Если КонтролироватьОстаки И СвободныйОстаток < 0 Тогда
							Отказ = Истина;
								ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
								НСтр("ru = 'Обнаружен отрицательный остаток по номенклатуре <%1>'"), ПредставлениеНоменклатуры));
						КонецЕсли;
						Если ВыборкаСерий.Количество > СвободныйОстаток Тогда
							Отказ = Истина;
							ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
								НСтр("ru = 'Резерв по номенклатуре <%1> превышает свободный остаток на складе на %2 %3'"), ПредставлениеНоменклатуры, 
								ВыборкаСерий.Количество - СвободныйОстаток, ВыборкаСерий.ЕдиницаИзмерения));	
						КонецЕсли;
						Если ВыборкаСерий.РезервПоЗаказу >= ВыборкаСерий.Заказано Тогда
							Отказ = Истина;
							ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
								НСтр("ru = 'Резерв по номенклатуре <%1> уже закрывает/превышает потребность на %2 %3'"), ПредставлениеНоменклатуры, 
								ВыборкаСерий.РезервПоЗаказу - ВыборкаСерий.Заказано, ВыборкаСерий.ЕдиницаИзмерения));	
						КонецЕсли;
						Если ВыборкаСерий.Количество + ВыборкаСерий.РезервПоЗаказу > ВыборкаСерий.Заказано Тогда
							Отказ = Истина;
							ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
								НСтр("ru = 'Резерв по номенклатуре <%1> с учетом текущего документа превышает потребность на %2 %3'"), ПредставлениеНоменклатуры,
								ВыборкаСерий.Количество + ВыборкаСерий.РезервПоЗаказу - ВыборкаСерий.Заказано, ВыборкаСерий.ЕдиницаИзмерения));	
						КонецЕсли;
						
						Движение = Движения.торо_РезервыНаСкладах.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						Движение.Период = Дата;
						Движение.Склад = Склад;
						Движение.Номенклатура = ВыборкаСерий.Номенклатура;
						Если ФОИспользоватьСерии Тогда
							Движение.Серия = ВыборкаСерий.Серия;
						КонецЕсли; 	
						Если ФОИспользоватьХарактеристики Тогда
							Движение.Характеристика = ВыборкаСерий.Характеристика;
						КонецЕсли;
						Движение.ЗаказНаВнутреннееПотребление = ЗаказНаВнутреннееПотребление;
						Движение.Количество = ВыборкаСерий.Количество;
						Движение.ДокументРезерв = Ссылка;
						
					КонецЦикла;
					
					Если КоличествоСерий > 1 Тогда 
						Если ВыборкаХарактеристик.Количество + ВыборкаХарактеристик.РезервПоЗаказу > ВыборкаХарактеристик.Заказано Тогда
							Отказ = Истина;
							ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
								НСтр("ru = 'Суммарный резерв по номенклатуре <%1> с учетом текущего документа превышает потребность на %2 %3'"), 
								ПредставлениеНоменклатуры, ВыборкаХарактеристик.Количество + ВыборкаХарактеристик.РезервПоЗаказу - ВыборкаХарактеристик.Заказано,
								ВыборкаСерий.ЕдиницаИзмерения));	
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;		
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Если ОбъектКопирования.ОтменаРезерва Тогда 
		ОтменаРезерва = Ложь;
		Для Каждого Строка Из Товары Цикл
			Строка.Количество = - Строка.Количество;
			Строка.КоличествоУпаковок = - Строка.КоличествоУпаковок;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Для Каждого Строка Из Товары Цикл 
		Если Строка.Количество <= 0 Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'В строке № %1 резервируемое количество должно быть > 0'"), Строка.НомерСтроки));
		КонецЕсли;
	КонецЦикла;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ФОИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
	Если ФОИспользоватьХарактеристикиНоменклатуры Тогда
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
	
	ФОИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Серия");
	Если ФОИспользоватьСерииНоменклатуры = Истина тогда
		НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, Отказ, МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда
		Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли