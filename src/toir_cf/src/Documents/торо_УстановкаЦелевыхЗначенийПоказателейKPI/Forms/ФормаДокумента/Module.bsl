#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Показатели") Тогда
		// Открытие формы из обработки "торо_КорректировкаЦелевыхЗначенийПоказателейKPI.
		
		Для Каждого Показатель Из Параметры.ЗначенияЗаполнения.Показатели Цикл
			НоваяСтрока = Объект.КлючевыеПоказатели.Добавить();
			НоваяСтрока.Показатель = Показатель;
		КонецЦикла;
		
		ОбновитьЗначенияРеквизитовКлючевыхПоказателей();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбновитьЗначенияРеквизитовКлючевыхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	ИндексСтроки = 0;
	ШаблонСообщения = НСтр("ru='В строке %1 табличной части не заполнено ни одно из полей ""Организация"", ""Подразделение"", ""Объект ремонта"".'");

	Для каждого Строка из Объект.КлючевыеПоказатели Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Организация) 
			И НЕ ЗначениеЗаполнено(Строка.Подразделение)
			И НЕ ЗначениеЗаполнено(Строка.ОбъектРемонта) Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ИндексСтроки+1);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.КлючевыеПоказатели["+ИндексСтроки+"].Организация",, Отказ);
		КонецЕсли;
		ИндексСтроки = ИндексСтроки + 1;
	КонецЦикла;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	ЗапросДублей = Новый Запрос;
	ЗапросДублей.Текст =
	"ВЫБРАТЬ
	|	КлючевыеПоказатели.Показатель,
	|	КлючевыеПоказатели.Организация,
	|	КлючевыеПоказатели.Подразделение,
	|	КлючевыеПоказатели.ОбъектРемонта,
	|	1 КАК КоличествоСтрок
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	&КлючевыеПоказатели КАК КлючевыеПоказатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Показатель,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.ОбъектРемонта,
	|	СУММА(ТаблицаДокумента.КоличествоСтрок) КАК КоличествоСтрок
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Показатель,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.ОбъектРемонта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокумента.КоличествоСтрок) > 1";
	
	ЗапросДублей.УстановитьПараметр("КлючевыеПоказатели", Объект.КлючевыеПоказатели.Выгрузить());
	ВыборкаДублей = ЗапросДублей.Выполнить().Выбрать();
	
	ШаблонСообщения = НСтр("ru='В таблице дублируются строки с аналитикой: Показатель ""%1"", Организация ""%2"", Подразделение ""%3"", Объект ремонта ""%4"".'");
	Пока ВыборкаДублей.Следующий() Цикл
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ВыборкаДублей.Показатель, ВыборкаДублей.Организация, ВыборкаДублей.Подразделение, ВыборкаДублей.ОбъектРемонта);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбновитьЗначенияРеквизитовКлючевыхПоказателей();
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	УправлениеФормойПоТекущемуПоказателю();
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Оповестить("ПроведенаУстановкаЦелевыхЗначений", Объект.Ссылка, ЭтотОбъект);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

	Если ИмяСобытия = "ИзмененПоказательKPI" Тогда
		ОбработатьИзменениеПоказателя(Параметр);	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеОграниченоПоВремениПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
	Если Не ЗначениеОграниченоПоВремени Тогда
		ДатаПо = Дата(1, 1, 1);
		ДатаПоПриИзменении(Элементы.ДатаПо);
	КонецЕсли;
	
	Элементы.ДатаПо.ТолькоПросмотр = Не ЗначениеОграниченоПоВремени;
	Элементы.ДатаПо.ОтметкаНезаполненного = ЗначениеОграниченоПоВремени;
	Элементы.ДатаПо.АвтоОтметкаНезаполненного = ЗначениеОграниченоПоВремени;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПоПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
		
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
КонецПроцедуры

&НаКлиенте
Процедура КритическоеЗначениеПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
КонецПроцедуры

&НаКлиенте
Процедура МинимальноеЗначениеПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
КонецПроцедуры

&НаКлиенте
Процедура КритическийМинимумПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
КонецПроцедуры

&НаКлиенте
Процедура МаксимальноеЗначениеПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
КонецПроцедуры

&НаКлиенте
Процедура КритическийМаксимумПриИзменении(Элемент)
	
	ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(Элемент.Имя);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлючевыеПоказатели 

&НаКлиенте
Процедура КлючевыеПоказателиПриАктивизацииСтроки(Элемент)
	
	УправлениеФормойПоТекущемуПоказателю();	
	
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеПоказателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "КлючевыеПоказателиЦелевойТрендПоказателя" Тогда
		
		ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ЦелевойТрендПоказателя) Тогда
			ПоказатьЗначение(, ТекущиеДанные.Показатель);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеПоказателиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Копирование Тогда
		УправлениеФормойПоТекущемуПоказателю();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеПоказателиПослеУдаления(Элемент)
	
	Если Не ЗначенияВТаблице И Объект.КлючевыеПоказатели.Количество() = 0 Тогда
		ОчиститьЗначенияРеквизитовФормыЦелевыхПоказателей();
		УправлениеГруппойЦелевыхЗначений()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеПоказателиПоказательПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
	
	ИдентификаторыСтрок = Новый Массив;
	ИдентификаторыСтрок.Добавить(ТекущиеДанные.ПолучитьИдентификатор());
	ОбновитьЗначенияРеквизитовКлючевыхПоказателей(ИдентификаторыСтрок);

	УправлениеФормойПоТекущемуПоказателю();
	
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеПоказателиЗначениеОграниченоПоВремениПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
	
	Если Не ТекущиеДанные.ЗначениеОграниченоПоВремени Тогда
		ТекущиеДанные.ДатаПо = Дата(1, 1, 1);
		ДатаПо = Дата(1, 1, 1);
	КонецЕсли;	
	
	Элементы.ДатаПо.ТолькоПросмотр = Не ЗначениеОграниченоПоВремени;
	Элементы.ДатаПо.ОтметкаНезаполненного = ЗначениеОграниченоПоВремени;
	Элементы.ДатаПо.АвтоОтметкаНезаполненного = ЗначениеОграниченоПоВремени;
	
КонецПроцедуры

#КонецОбласти  

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УправлениеФормойПоТекущемуПоказателю()
	
	УправлениеГруппойЦелевыхЗначений();
	
	Если Не ЗначенияВТаблице Тогда
		ОбновитьЗначенияРеквизитовФормыЦелевыхПоказателей();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияВТаблице(Команда)
	
	ЗначенияВТаблице = Не ЗначенияВТаблице;

	Элементы.КлючевыеПоказателиЗначенияВТаблице.Пометка = ЗначенияВТаблице;
	
	Элементы.ГруппаЦелевыеЗначения.Видимость = Не ЗначенияВТаблице;
	Элементы.КлючевыеПоказателиГруппаЦелевыеЗначения.Видимость = ЗначенияВТаблице;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеГруппойЦелевыхЗначений()
	
	ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
	
	Элементы.ДатаПо.ТолькоПросмотр = Не ЗначениеОграниченоПоВремени;
    Элементы.ДатаПо.ОтметкаНезаполненного = ЗначениеОграниченоПоВремени;
	Элементы.ДатаПо.АвтоОтметкаНезаполненного = ЗначениеОграниченоПоВремени;
	
	Если ТекущиеДанные = Неопределено 
		ИЛИ ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка") Тогда
		
		Если ЗначенияВТаблице Тогда
			Элементы.КлючевыеПоказателиГруппаЦелевыеЗначения.ТолькоПросмотр = Истина;
		Иначе	
			Элементы.ГруппаЦелевыеЗначения.ТолькоПросмотр = Истина;
			Элементы.СтраницыЦелевыхЗначений.ТекущаяСтраница = Элементы.СтраницаОдноЗначение;
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Максимизация")
		ИЛИ ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Минимизация") Тогда
		
		Если ЗначенияВТаблице Тогда
			Элементы.КлючевыеПоказателиГруппаЦелевыеЗначения.ТолькоПросмотр = Ложь;
			Элементы.КлючевыеПоказателиГруппаОдноЗначение.ТолькоПросмотр = Ложь;
			Элементы.КлючевыеПоказателиГруппаДиапозонЗначений.ТолькоПросмотр = Истина;
		Иначе
			Элементы.ГруппаЦелевыеЗначения.ТолькоПросмотр = Ложь;
			Элементы.СтраницыЦелевыхЗначений.ТекущаяСтраница = Элементы.СтраницаОдноЗначение;
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон") Тогда
		
		Если ЗначенияВТаблице Тогда
			Элементы.КлючевыеПоказателиГруппаЦелевыеЗначения.ТолькоПросмотр = Ложь;
			Элементы.КлючевыеПоказателиГруппаОдноЗначение.ТолькоПросмотр = Истина;
			Элементы.КлючевыеПоказателиГруппаДиапозонЗначений.ТолькоПросмотр = Ложь;
		Иначе
			Элементы.ГруппаЦелевыеЗначения.ТолькоПросмотр = Ложь;
			Элементы.СтраницыЦелевыхЗначений.ТекущаяСтраница = Элементы.СтраницаДиапазонЗначений;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	
	
&НаСервере
Процедура ОбновитьЗначенияРеквизитовКлючевыхПоказателей(ИдентификаторыСтрок = Неопределено)
	
	Если ИдентификаторыСтрок = Неопределено Тогда
		СтрокиКлючевыхПоказателей = Объект.КлючевыеПоказатели;
	Иначе  
		
		СтрокиКлючевыхПоказателей = Новый Массив;
		
		Для Каждого Идентификатор Из ИдентификаторыСтрок Цикл
			СтрокиКлючевыхПоказателей.Добавить(Объект.КлючевыеПоказатели.НайтиПоИдентификатору(Идентификатор));	
		КонецЦикла;	
	КонецЕсли;
	
	Показатели = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиКлючевыхПоказателей, "Показатель");
	РеквизитыПоказателей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Показатели, "ТипПоказателя, ЦелевойТренд");
	
	Для Каждого Строка Из СтрокиКлючевыхПоказателей Цикл
		ОбновитьЗначенияРеквизитовСтрокиКлючевыхПоказателей(Строка, РеквизитыПоказателей);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначенияРеквизитовСтрокиКлючевыхПоказателей(Строка, РеквизитыПоказателей)
	
	РеквизитыПоказателя = РеквизитыПоказателей.Получить(Строка.Показатель);
	
	Строка.ТипПоказателя = РеквизитыПоказателя.ТипПоказателя;
	Строка.ЦелевойТрендПоказателя = РеквизитыПоказателя.ЦелевойТренд;
	
	Если Строка.ЦелевойТрендПоказателя = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка()
		ИЛИ Строка.ЦелевойТрендПоказателя = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.Максимизация
		ИЛИ Строка.ЦелевойТрендПоказателя = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.Минимизация Тогда
				
		Строка.МинимальноеЗначение = 0;
		Строка.КритическийМинимум = 0;
		Строка.МаксимальноеЗначение = 0;
		Строка.КритическийМаксимум = 0;
		
	ИначеЕсли Строка.ЦелевойТрендПоказателя = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон Тогда
		
		Строка.Значение = 0;
		Строка.КритическоеЗначение = 0;
	КонецЕсли;

	
	Если Строка.ТипПоказателя = Перечисления.торо_ТипыПоказателейKPI.ПоказательОрганизации Тогда
		Строка.Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ Строка.ТипПоказателя = Перечисления.торо_ТипыПоказателейKPI.ПоказательОбъектаРемонта Тогда
		Строка.ОбъектРемонта = Справочники.торо_ОбъектыРемонта.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеРеквизитаФормыЦелевыхЗначенийПриИзменении(ИмяЭлемента)
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные[ИмяЭлемента] = ЭтотОбъект[ИмяЭлемента];
	
КонецПроцедуры	

&НаКлиенте 
Процедура ОбновитьЗначенияРеквизитовФормыЦелевыхПоказателей()
	
	ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка")
		ИЛИ ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Максимизация")
		ИЛИ ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Минимизация") Тогда
		
		Значение = ТекущиеДанные.Значение;
		КритическоеЗначение = ТекущиеДанные.КритическоеЗначение; 
				
	ИначеЕсли ТекущиеДанные.ЦелевойТрендПоказателя = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон") Тогда
		
		МинимальноеЗначение = ТекущиеДанные.МинимальноеЗначение;
		КритическийМинимум = ТекущиеДанные.КритическийМинимум;
		МаксимальноеЗначение = ТекущиеДанные.МаксимальноеЗначение;
		КритическийМаксимум = ТекущиеДанные.КритическийМаксимум;
	КонецЕсли;
	
	ЗначениеОграниченоПоВремени = ТекущиеДанные.ЗначениеОграниченоПоВремени;
	ДатаПо = ТекущиеДанные.ДатаПо;
	
КонецПроцедуры	

&НаКлиенте 
Процедура ОчиститьЗначенияРеквизитовФормыЦелевыхПоказателей()
	
	ЗначениеОграниченоПоВремени = Ложь;
	ДатаПо =Дата(1,1,1);;
	
	Значение = 0;
	КритическоеЗначение = 0;
	
	МинимальноеЗначение = 0;
	КритическийМинимум = 0;
	МаксимальноеЗначение = 0;
	КритическийМаксимум = 0;
	
КонецПроцедуры   

&НаКлиенте
Процедура ОбработатьИзменениеПоказателя(Показатель)
	
	СтрокиКлючевыхПоказателей = Новый Массив;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Показатель", Показатель);
	
	КлючевыеПоказателиОтбор = Объект.КлючевыеПоказатели.НайтиСтроки(СтруктураОтбора);
	Для Каждого Строка Из КлючевыеПоказателиОтбор Цикл
		СтрокиКлючевыхПоказателей.Добавить(Строка.ПолучитьИдентификатор());
	КонецЦикла;
	
	ОбновитьЗначенияРеквизитовКлючевыхПоказателей(СтрокиКлючевыхПоказателей);
	
	ТекущиеДанные = Элементы.КлючевыеПоказатели.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено И ТекущиеДанные.Показатель = Показатель Тогда
		УправлениеГруппойЦелевыхЗначений();
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
