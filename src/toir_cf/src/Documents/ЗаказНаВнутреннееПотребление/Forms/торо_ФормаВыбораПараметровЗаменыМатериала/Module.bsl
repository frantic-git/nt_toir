#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ДоступноКЗамене", ДоступноКЗамене);
	Параметры.Свойство("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Параметры.Свойство("КоличествоМатериалаИзДокумента", КоличествоМатериалаИзДокумента);
	Параметры.Свойство("КоличествоАналогаИзДокумента", КоличествоАналогаИзДокумента);
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ЕдиницаИзмерения = Справочники.УпаковкиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	ЕдиницаИзмеренияИсходная = ЕдиницаИзмерения;
	
	ЗаменитьКоличество = ДоступноКЗамене;
	
	НоменклатураАналог = Неопределено;
	Параметры.Свойство("НоменклатураАналог", НоменклатураАналог);
	
	ПредставлениеЕдиницыХранения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоменклатураАналог, "ЕдиницаИзмерения");
	Элементы.ЕдиницаИзмерения.ПодсказкаВвода = ПредставлениеЕдиницыХранения;
	
	ПараметрыВыбораУпаковки = Новый Структура("Номенклатура", НоменклатураАналог);
	ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.УпаковкиНоменклатуры"), ПараметрыВыбораУпаковки);
	
	Для каждого ЗначениеВыбора Из ДанныеВыбора Цикл
		Элементы.ЕдиницаИзмерения.СписокВыбора.Добавить(ЗначениеВыбора.Значение, ЗначениеВыбора.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаменитьКоличествоПриИзменении(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заменить(Команда)
	
	СтруктураВозврата = Новый Структура("Заменить, КоличествоАналога, ЕдиницаИзмерения", ЗаменитьКоличество, КоличествоАналога, ЕдиницаИзмерения);
	Оповестить("ВыборПараметровЗаменыМатериалаЗаказаНаВП", СтруктураВозврата);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаменитьКоличествоПриИзменении(Элемент)
	
	Если ЗаменитьКоличество > ДоступноКЗамене Тогда
		ЗаменитьКоличество = ДоступноКЗамене;
	КонецЕсли;
	
	КоличествоАналога = КоличествоАналогаИзДокумента * КратностьЗамены(ЗаменитьКоличество, КоличествоМатериалаИзДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоАналогаПриИзменении(Элемент)
	
	ЗаменитьКоличество = КоличествоМатериалаИзДокумента * КратностьЗамены(КоличествоАналога, КоличествоАналогаИзДокумента);
	Если ЗаменитьКоличество > ДоступноКЗамене Тогда
		ЗаменитьКоличествоПриИзменении(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	ЕдиницаИзмеренияИсходная = ЕдиницаИзмерения;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция КратностьЗамены(КоличествоКЗамене, КоличествоМатериала)
	
	Кратность = КоличествоКЗамене / КоличествоМатериала;
	//ТекКратность = Цел(Кратность);
	
	Возврат Кратность;
	
КонецФункции

#КонецОбласти