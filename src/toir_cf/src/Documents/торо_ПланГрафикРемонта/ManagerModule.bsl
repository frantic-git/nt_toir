#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс
// Функция предназначена для обновления доступности документа для редактирования.
//
// Параметры:
//		Ссылка			- ДокументСсылка - ссылка на документ, 
//		ВидОперации		- ПеречислениеСсылка - вид операции документа,
//		ТаблицаID	- ТаблицаЗначений - план ремонтов документа, таблица с колонкой "ID".
//
// Возвращаемое значение: 
//		Массив - массив доступных для корректировки строк.
Функция ОбновитьДоступностьДляРедактирования(Ссылка, ВидОперации, ТаблицаID) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаID.ID КАК ID
	               |ПОМЕСТИТЬ ТаблицаID
	               |ИЗ
	               |	&ТаблицаID КАК ТаблицаID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_СоответствиеКорректировокДокументамППР.Корректировка КАК Корректировка
	               |ПОМЕСТИТЬ торо_СоответствиеКорректировокДокументамППР
	               |ИЗ
	               |	РегистрСведений.торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР
	               |ГДЕ
	               |	торо_СоответствиеКорректировокДокументамППР.Корректируемый = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID
	               |ПОМЕСТИТЬ РемонтыКорректировок
	               |ИЗ
	               |	Документ.торо_ПланГрафикРемонта.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |ГДЕ
	               |	торо_ПланГрафикРемонтаПланРемонтов.Ссылка В
	               |			(ВЫБРАТЬ
	               |				торо_СоответствиеКорректировокДокументамППР.Корректировка
	               |			ИЗ
	               |				торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_ОстановочныеРемонты.IDЗависимого
	               |ИЗ
	               |	РегистрСведений.торо_ОстановочныеРемонты.СрезПоследних(
	               |			,
	               |			IDЗависимого В
	               |				(ВЫБРАТЬ
	               |					ТаблицаID.ID
	               |				ИЗ
	               |					ТаблицаID)) КАК торо_ОстановочныеРемонты
	               |ГДЕ
	               |	НЕ торо_ОстановочныеРемонты.Отвязан
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_ПлановыеРемонтныеРаботы.ID
	               |ИЗ
	               |	РегистрСведений.торо_ПлановыеРемонтныеРаботы.СрезПоследних(
	               |			,
	               |			ID В
	               |					(ВЫБРАТЬ
	               |						ТаблицаID.ID
	               |					ИЗ
	               |						ТаблицаID)
	               |				И Регистратор ССЫЛКА Документ.торо_ЗакрытиеЗаявокИРемонтов) КАК торо_ПлановыеРемонтныеРаботы
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_СкользящийПланРабот.ID_Ремонта
	               |ИЗ
	               |	РегистрСведений.торо_СкользящийПланРабот КАК торо_СкользящийПланРабот
	               |ГДЕ
	               |	торо_СкользящийПланРабот.ID_Ремонта В
	               |			(ВЫБРАТЬ
	               |				ТаблицаID.ID
	               |			ИЗ
	               |				ТаблицаID)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаID.ID КАК ID
	               |ПОМЕСТИТЬ РемонтыСКорректировкой
	               |ИЗ
	               |	ТаблицаID КАК ТаблицаID
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РемонтыКорректировок КАК РемонтыКорректировок
	               |		ПО ТаблицаID.ID = РемонтыКорректировок.ID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТаблицаID.ID КАК ID,
	               |	МИНИМУМ(ВЫБОР
	               |			КОГДА НЕ(НЕ торо_ОбщиеДанныеПоРемонтам.IDРемонта ЕСТЬ NULL
	               |							И (торо_ОбщиеДанныеПоРемонтам.ЕстьЗаявка
	               |								ИЛИ торо_ОбщиеДанныеПоРемонтам.ЕстьНаряд
	               |								ИЛИ торо_ОбщиеДанныеПоРемонтам.ЕстьАкт)
	               |						ИЛИ НЕ РемонтыСКорректировкой.ID ЕСТЬ NULL)
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ ЛОЖЬ
	               |		КОНЕЦ) КАК ДоступенДляРедактирования
	               |ИЗ
	               |	ТаблицаID КАК ТаблицаID
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	               |		ПО ТаблицаID.ID = торо_ОбщиеДанныеПоРемонтам.IDРемонта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РемонтыСКорректировкой КАК РемонтыСКорректировкой
	               |		ПО ТаблицаID.ID = РемонтыСКорректировкой.ID
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаID.ID";
	
	Запрос.УстановитьПараметр("ТаблицаID", ТаблицаID);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	
	МассивДоступныхДляКорректировкиСтрок = Запрос.Выполнить().Выгрузить();
	
	Возврат МассивДоступныхДляКорректировкиСтрок;
КонецФункции

// Функция - Заполнить доступность для редактирования полная.
//
// Параметры:
//  парамПланРемонтов							 - ТаблицаЗначений - все строки плана ремонтов;
//  парамМассивДоступныхДляКорректировкиСтрок	 - ТаблицаЗначений - Доступные для редактирования строки.
// Возвращаемое значение:
//  ТаблицаЗначений - результат запроса с указанием доступных для редактирования ремонтов.
Функция ЗаполнитьДоступностьДляРедактированияПолная(парамПланРемонтов,парамМассивДоступныхДляКорректировкиСтрок) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаКонСт КАК ДатаКон,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаНачСт КАК ДатаНач,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон КАК ДатаКонКорр,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач КАК ДатаНачКорр,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения КАК СпособВыполнения,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Отменен КАК Отменен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Замещен КАК Замещен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ЗамещенСт КАК ЗамещенСт,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_базы_расчета КАК ID_базы_расчета,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_замещающего КАК ID_замещающего,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель КАК Исполнитель,
						|	торо_ПланГрафикРемонтаПланРемонтов.Склад КАК Склад,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Перенесенный КАК Перенесенный,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта КАК СуммаРемонта,
						|	торо_ПланГрафикРемонтаПланРемонтов.СрокПоНормативу КАК СрокПоНормативу,
						|	торо_ПланГрафикРемонтаПланРемонтов.СодержитВыходные КАК СодержитВыходные
	               |ПОМЕСТИТЬ торо_ПланГрафикРемонтаПланРемонтов
	               |ИЗ
	               |	&ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаДоступенДляРедакторования.ID КАК ID,
	               |	ТаблицаДоступенДляРедакторования.ДоступенДляРедактирования КАК ДоступенДляРедактирования
	               |ПОМЕСТИТЬ ТаблицаДоступенДляРедакторования
	               |ИЗ
	               |	&ТаблицаДоступенДляРедакторования КАК ТаблицаДоступенДляРедакторования
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	ВЫБОР
	               |		КОГДА торо_ПланГрафикРемонтаПланРемонтов.Перенесенный
	               |				И торо_ПланГрафикРемонтаПланРемонтов.ДатаКонКорр <> ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА торо_ПланГрафикРемонтаПланРемонтов.ДатаКонКорр
	               |		ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ДатаКон
	               |	КОНЕЦ КАК ДатаКон,
	               |	ВЫБОР
	               |		КОГДА торо_ПланГрафикРемонтаПланРемонтов.Перенесенный
	               |				И торо_ПланГрафикРемонтаПланРемонтов.ДатаНачКорр <> ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА торо_ПланГрафикРемонтаПланРемонтов.ДатаНачКорр
	               |		ИНАЧЕ торо_ПланГрафикРемонтаПланРемонтов.ДатаНач
	               |	КОНЕЦ КАК ДатаНач,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения КАК СпособВыполнения,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Отменен КАК Отменен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Замещен КАК Замещен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ЗамещенСт КАК ЗамещенСт,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_базы_расчета КАК ID_базы_расчета,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_замещающего КАК ID_замещающего,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель КАК Исполнитель,
						|	торо_ПланГрафикРемонтаПланРемонтов.Склад КАК Склад,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Перенесенный КАК Перенесенный,
	               |	ЕСТЬNULL(ТаблицаДоступенДляРедакторования.ДоступенДляРедактирования, ИСТИНА) КАК ДоступенДляРедактирования,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта КАК СуммаРемонта,
						|	торо_ПланГрафикРемонтаПланРемонтов.СрокПоНормативу КАК СрокПоНормативу,
						|	торо_ПланГрафикРемонтаПланРемонтов.СодержитВыходные КАК СодержитВыходные
	               |ИЗ
	               |	торо_ПланГрафикРемонтаПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДоступенДляРедакторования КАК ТаблицаДоступенДляРедакторования
	               |		ПО торо_ПланГрафикРемонтаПланРемонтов.ID = ТаблицаДоступенДляРедакторования.ID";
	
	Запрос.УстановитьПараметр("ПланРемонтов",парамПланРемонтов);
	Запрос.УстановитьПараметр("ТаблицаДоступенДляРедакторования",парамМассивДоступныхДляКорректировкиСтрок);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Функция предназначена для рассчета стоимости ремонтов.
//
// Параметры:
//		ПланРемонтов							- ТаблицаЗначений - план ремонтов документа,
//		Ссылка									- ДокументССылка - ссылка на документ,
//		МассивДоступныхДляКорректировкиСтрок	- Массив - массив доступных для корректировки строк.
//		ТекущаяСтрока - ДанныеФормыЭлементКоллеции, Структура - текущая строка плана ремонтов.
//
// Возвращаемое значение: 
//		ТаблицаЗначений - план ремонтов с рассчитанной стоимостью.
Функция РассчитатьСтоимостиРемонтов(ПланРемонтов, Ссылка, МассивДоступныхДляКорректировкиСтрок, ТекущаяСтрока = Неопределено) Экспорт
	
	ВремТаб = Новый МенеджерВременныхТаблиц();
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = ВремТаб;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач КАК Период,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель КАК Исполнитель,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот КАК Объект,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения КАК СпособВыполнения
	               |ПОМЕСТИТЬ ТЧПланРемонтов
	               |ИЗ
	               |	&ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МассивДоступныхДляКорректировкиСтрок.ID КАК ID,
	               |	МассивДоступныхДляКорректировкиСтрок.ДоступенДляРедактирования КАК ДоступенДляРедактирования
	               |ПОМЕСТИТЬ МассивДоступныхДляКорректировкиСтрок
	               |ИЗ
	               |	&МассивДоступныхДляКорректировкиСтрок КАК МассивДоступныхДляКорректировкиСтрок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЧПланРемонтов.ID КАК ID,
	               |	ТЧПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	ТЧПланРемонтов.Период КАК Период,
	               |	ТЧПланРемонтов.Исполнитель КАК Исполнитель,
	               |	ТЧПланРемонтов.Объект КАК Объект,
	               |	ТЧПланРемонтов.СпособВыполнения КАК СпособВыполнения
	               |ПОМЕСТИТЬ ТабДанные
	               |ИЗ
	               |	ТЧПланРемонтов КАК ТЧПланРемонтов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МассивДоступныхДляКорректировкиСтрок КАК МассивДоступныхДляКорректировкиСтрок
	               |		ПО ТЧПланРемонтов.ID = МассивДоступныхДляКорректировкиСтрок.ID
	               |			И (МассивДоступныхДляКорректировкиСтрок.ДоступенДляРедактирования)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабДанные.Период КАК ДатаНач,
	               |	КурсыВалют.Период КАК Период,
	               |	КурсыВалют.Кратность КАК Кратность,
	               |	КурсыВалют.Курс КАК Курс,
	               |	КурсыВалют.Валюта КАК Валюта
	               |ПОМЕСТИТЬ ТабДатВалют
	               |ИЗ
	               |	ТабДанные КАК ТабДанные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	               |		ПО (КурсыВалют.Период <= ТабДанные.Период)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабДатВалют.ДатаНач КАК ДатаНач,
	               |	ТабДатВалют.Валюта КАК Валюта,
	               |	МАКСИМУМ(ТабДатВалют.Период) КАК Период
	               |ПОМЕСТИТЬ ТабМаксДатВалют
	               |ИЗ
	               |	ТабДатВалют КАК ТабДатВалют
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабДатВалют.ДатаНач,
	               |	ТабДатВалют.Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ТабДатВалют.ДатаНач КАК ДатаНач,
	               |	ТабДатВалют.Курс КАК Курс,
	               |	ТабДатВалют.Кратность КАК Кратность,
	               |	ТабДатВалют.Валюта КАК Валюта,
	               |	ТабДатВалют.Период КАК Период
	               |ПОМЕСТИТЬ ТабКурсовВалют
	               |ИЗ
	               |	ТабДатВалют КАК ТабДатВалют
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабМаксДатВалют КАК ТабМаксДатВалют
	               |		ПО ТабДатВалют.ДатаНач = ТабМаксДатВалют.ДатаНач
	               |			И ТабДатВалют.Период = ТабМаксДатВалют.Период
	               |			И ТабДатВалют.Валюта = ТабМаксДатВалют.Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаРемРабот.Период КАК ДатаНач,
	               |	Квалификации.Период КАК Период,
	               |	Квалификации.Стоимость КАК Стоимость,
	               |	Квалификации.Валюта КАК Валюта,
	               |	Квалификации.Квалификация КАК Квалификация
	               |ПОМЕСТИТЬ ТабДатКвалификаций
	               |ИЗ
	               |	ТабДанные КАК ТаблицаРемРабот
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СтоимостьЧасаКвалификации КАК Квалификации
	               |		ПО (Квалификации.Период <= ТаблицаРемРабот.Период)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабДатКвалификаций.ДатаНач КАК ДатаНач,
	               |	ТабДатКвалификаций.Квалификация КАК Квалификация,
	               |	МАКСИМУМ(ТабДатКвалификаций.Период) КАК Период
	               |ПОМЕСТИТЬ ТабМаксДатКвалификаций
	               |ИЗ
	               |	ТабДатКвалификаций КАК ТабДатКвалификаций
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабДатКвалификаций.ДатаНач,
	               |	ТабДатКвалификаций.Квалификация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ТабДатКвалификаций.ДатаНач КАК ДатаНач,
	               |	ТабДатКвалификаций.Валюта КАК Валюта,
	               |	ТабДатКвалификаций.Стоимость КАК Стоимость,
	               |	ТабДатКвалификаций.Квалификация КАК Квалификация
	               |ПОМЕСТИТЬ ТабСтоимостейКвалификаций
	               |ИЗ
	               |	ТабДатКвалификаций КАК ТабДатКвалификаций
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабМаксДатКвалификаций КАК ТабМаксДатКвалификаций
	               |		ПО ТабДатКвалификаций.ДатаНач = ТабМаксДатКвалификаций.ДатаНач
	               |			И ТабДатКвалификаций.Период = ТабМаксДатКвалификаций.Период
	               |			И ТабДатКвалификаций.Квалификация = ТабМаксДатКвалификаций.Квалификация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ТабДанные.Объект КАК ОбъектРемонтныхРабот,
	               |	ТабДанные.ВидРемонтныхРабот КАК ВидРемонта,
	               |	ТабДанные.Период КАК Дата,
	               |	ТабДанные.ID КАК ID,
	               |	ТабДанные.СпособВыполнения КАК СпособВыполнения,
	               |	торо_НормативныеРемонтыОборудования.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	торо_НормативныеРемонтыОборудования.НормативныйРемонт КАК НормативныйРемонт
	               |ПОМЕСТИТЬ НормативныеРемонты_период
	               |ИЗ
	               |	ТабДанные КАК ТабДанные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	               |		ПО ТабДанные.Объект = торо_НормативныеРемонтыОборудования.ОбъектРемонта
	               |			И ТабДанные.ВидРемонтныхРабот = торо_НормативныеРемонтыОборудования.ВидРемонта
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТабДанные.Объект,
	               |	ТабДанные.ВидРемонтныхРабот,
	               |	ТабДанные.Период,
	               |	ТабДанные.ID,
	               |	ТабДанные.СпособВыполнения,
	               |	торо_ЦепочкиРемонтаПоследовательностьРемонтов.ГрафикРемонтныхРабот,
	               |	торо_ЦепочкиРемонтаПоследовательностьРемонтов.НормативныйРемонт
	               |ИЗ
	               |	ТабДанные КАК ТабДанные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_РемонтныеЦиклыОборудования КАК торо_РемонтныеЦиклыОборудования
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ЦепочкиРемонта.ПоследовательностьРемонтов КАК торо_ЦепочкиРемонтаПоследовательностьРемонтов
	               |			ПО торо_РемонтныеЦиклыОборудования.ВидЦепочки = торо_ЦепочкиРемонтаПоследовательностьРемонтов.Ссылка
	               |		ПО ТабДанные.Объект = торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов
	               |			И (ТабДанные.ВидРемонтныхРабот = торо_ЦепочкиРемонтаПоследовательностьРемонтов.ВидРемонта)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныйРемонт,
	               |	Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты_период.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты_период.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты_период.Дата КАК Дата,
	               |	НормативныеРемонты_период.ID КАК ID,
	               |	НормативныеРемонты_период.СпособВыполнения КАК СпособВыполнения,
	               |	НормативныеРемонты_период.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	НормативныеРемонты_период.НормативныйРемонт КАК НормативныйРемонт,
	               |	МАКСИМУМ(торо_ВерсииТехКарт.Период) КАК Период
	               |ПОМЕСТИТЬ НормативныеРемонты_макспериод
	               |ИЗ
	               |	НормативныеРемонты_период КАК НормативныеРемонты_период
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
	               |		ПО НормативныеРемонты_период.НормативныйРемонт = торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |			И НормативныеРемонты_период.Дата >= торо_ВерсииТехКарт.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НормативныеРемонты_период.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты_период.ВидРемонта,
	               |	НормативныеРемонты_период.ID,
	               |	НормативныеРемонты_период.Дата,
	               |	НормативныеРемонты_период.СпособВыполнения,
	               |	НормативныеРемонты_период.ГрафикРемонтныхРабот,
	               |	НормативныеРемонты_период.НормативныйРемонт
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныйРемонт,
	               |	Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты_макспериод.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты_макспериод.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты_макспериод.Дата КАК Период,
	               |	НормативныеРемонты_макспериод.ID КАК ID,
	               |	НормативныеРемонты_макспериод.СпособВыполнения КАК СпособВыполнения,
	               |	НормативныеРемонты_макспериод.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	торо_ВерсииТехКарт.ТехКарта КАК Ремонт
	               |ПОМЕСТИТЬ НормативныеРемонты
	               |ИЗ
	               |	НормативныеРемонты_макспериод КАК НормативныеРемонты_макспериод
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
	               |		ПО НормативныеРемонты_макспериод.НормативныйРемонт = торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |			И НормативныеРемонты_макспериод.Период = торо_ВерсииТехКарт.Период
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидРемонта,
	               |	Ремонт,
	               |	СпособВыполнения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Ремонт КАК Ремонт,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты.Период КАК Период,
	               |	НормативныеРемонты.ID КАК ID,
	               |	НормативныеРемонты.СпособВыполнения КАК СпособВыполнения,
	               |	торо_Состав.Состав КАК ТехКарта,
	               |	торо_Состав.Количество КАК Количество
	               |ПОМЕСТИТЬ НормативныеРемонты_техкарты
	               |ИЗ
	               |	НормативныеРемонты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СоставИерархическихТехКартРемонтов КАК торо_Состав
	               |		ПО НормативныеРемонты.Ремонт = торо_Состав.ТехКарта
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Ремонт,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта,
	               |	НормативныеРемонты.Период,
	               |	НормативныеРемонты.ID,
	               |	НормативныеРемонты.СпособВыполнения,
	               |	НормативныеРемонты.Ремонт,
	               |	1
	               |ИЗ
	               |	НормативныеРемонты КАК НормативныеРемонты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныеРемонты.Ремонт
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Период КАК Период,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура КАК Номенклатура,
	               |	торо_ТехКартыМатериальныеЗатраты.Характеристика КАК Характеристика,
	               |	торо_ТехКартыМатериальныеЗатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	СУММА(торо_ТехКартыМатериальныеЗатраты.Количество) КАК Количество,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты.ID КАК ID
	               |ПОМЕСТИТЬ НоменклатураКоличество
	               |ИЗ
	               |	НормативныеРемонты_техкарты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.МатериальныеЗатраты КАК торо_ТехКартыМатериальныеЗатраты
	               |		ПО НормативныеРемонты.ТехКарта = торо_ТехКартыМатериальныеЗатраты.Ссылка
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НормативныеРемонты.Период,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура,
	               |	торо_ТехКартыМатериальныеЗатраты.Характеристика,
	               |	торо_ТехКартыМатериальныеЗатраты.ЕдиницаИзмерения,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта,
	               |	НормативныеРемонты.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Период,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Номенклатура,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Характеристика,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Номенклатура.ЕдиницаИзмерения,
	               |	СУММА(торо_НормыЗапчастейНаОбъектыРемонта.Количество),
	               |	НормативныеРемонты.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта,
	               |	НормативныеРемонты.ID
	               |ИЗ
	               |	НормативныеРемонты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормыЗапчастейНаОбъектыРемонта КАК торо_НормыЗапчастейНаОбъектыРемонта
	               |		ПО НормативныеРемонты.ОбъектРемонтныхРабот = торо_НормыЗапчастейНаОбъектыРемонта.ОбъектРемонта
	               |			И НормативныеРемонты.ВидРемонта = торо_НормыЗапчастейНаОбъектыРемонта.ВидРемонта
	               |			И (торо_НормыЗапчастейНаОбъектыРемонта.Приоритет)
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Номенклатура,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Характеристика,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта,
	               |	НормативныеРемонты.Период,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Номенклатура.ЕдиницаИзмерения,
	               |	НормативныеРемонты.ID
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Период,
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НоменклатураКоличество.Период КАК Период,
	               |	НоменклатураКоличество.Номенклатура КАК Номенклатура,
	               |	НоменклатураКоличество.Характеристика КАК Характеристика,
	               |	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК ПериодЦена,
	               |	ЦеныНоменклатуры.ВидЦены КАК ТипЦен
	               |ПОМЕСТИТЬ НоменклатураПериодЦен
	               |ИЗ
	               |	НоменклатураКоличество КАК НоменклатураКоличество
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	               |			ПО ЦеныНоменклатуры.ВидЦены = Константы.торо_ТипЦеныДляРасчетаСебестоимостиРемонта
	               |		ПО НоменклатураКоличество.Номенклатура = ЦеныНоменклатуры.Номенклатура
	               |			И НоменклатураКоличество.Характеристика = ЦеныНоменклатуры.Характеристика
	               |			И НоменклатураКоличество.Период >= ЦеныНоменклатуры.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НоменклатураКоличество.Период,
	               |	НоменклатураКоличество.Номенклатура,
	               |	НоменклатураКоличество.Характеристика,
	               |	ЦеныНоменклатуры.ВидЦены
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ПериодЦена,
	               |	ТипЦен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	НормативныеРемонты.ТехКарта КАК Ссылка,
	               |	торо_ТехКартыСписокОпераций.Операция КАК Операция,
	               |	торо_ТехКартыСписокОпераций.Операция.Расценка КАК ОперацияРасценка,
	               |	торо_ТехКартыСписокОпераций.Операция.Валюта КАК ОперацияВалюта,
	               |	НормативныеРемонты.ID КАК ID,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта КАК ВидРемонта,
	               |	торо_ТехКартыСписокОпераций.Количество КАК Количество,
	               |	НормативныеРемонты.Период КАК Период
	               |ПОМЕСТИТЬ ТабОперацийПодрядный
	               |ИЗ
	               |	НормативныеРемонты_техкарты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	               |		ПО НормативныеРемонты.ТехКарта = торо_ТехКартыСписокОпераций.Ссылка
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(НоменклатураКоличество.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) * ЕСТЬNULL(ТабКурсовВалют.Курс, 0)) КАК Стоимость,
	               |	НоменклатураКоличество.ID КАК ID
	               |ПОМЕСТИТЬ ИтоговаяТаб
	               |ИЗ
	               |	НоменклатураКоличество КАК НоменклатураКоличество
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураПериодЦен КАК НоменклатураПериодЦен
	               |		ПО НоменклатураКоличество.Период = НоменклатураПериодЦен.Период
	               |			И НоменклатураКоличество.Номенклатура = НоменклатураПериодЦен.Номенклатура
	               |			И НоменклатураКоличество.Характеристика = НоменклатураПериодЦен.Характеристика
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	               |		ПО (НоменклатураПериодЦен.Номенклатура = ЦеныНоменклатуры.Номенклатура)
	               |			И (НоменклатураПериодЦен.Характеристика = ЦеныНоменклатуры.Характеристика)
	               |			И (НоменклатураПериодЦен.ПериодЦена = ЦеныНоменклатуры.Период)
	               |			И (НоменклатураПериодЦен.ТипЦен = ЦеныНоменклатуры.ВидЦены)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабКурсовВалют КАК ТабКурсовВалют
	               |		ПО (ЦеныНоменклатуры.Валюта = ТабКурсовВалют.Валюта)
	               |			И НоменклатураКоличество.Период = ТабКурсовВалют.ДатаНач
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НоменклатураКоличество.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СУММА(ЕСТЬNULL(ТабСтоимостейКвалификаций.Стоимость, 0) * ЕСТЬNULL(ТабКурсовВалют.Курс, 0) / ЕСТЬNULL(ТабКурсовВалют.Кратность, 1) * торо_ТехКартыТрудовыеЗатраты.ВремяРаботы / 3600 * торо_ТехКартыТрудовыеЗатраты.Количество * НормативныеРемонты.Количество),
	               |	НормативныеРемонты.ID
	               |ИЗ
	               |	Справочник.торо_ТехКарты.ТрудовыеЗатраты КАК торо_ТехКартыТрудовыеЗатраты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НормативныеРемонты_техкарты КАК НормативныеРемонты
	               |		ПО торо_ТехКартыТрудовыеЗатраты.Ссылка = НормативныеРемонты.ТехКарта
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТабСтоимостейКвалификаций КАК ТабСтоимостейКвалификаций
	               |		ПО торо_ТехКартыТрудовыеЗатраты.Квалификация = ТабСтоимостейКвалификаций.Квалификация
	               |			И (НормативныеРемонты.Период = ТабСтоимостейКвалификаций.ДатаНач)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТабКурсовВалют КАК ТабКурсовВалют
	               |		ПО (ТабСтоимостейКвалификаций.Валюта = ТабКурсовВалют.Валюта)
	               |			И (ТабКурсовВалют.ДатаНач = ТабСтоимостейКвалификаций.ДатаНач)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	торо_ТехКартыТрудовыеЗатраты.Квалификация,
	               |	НормативныеРемонты.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СУММА(ТабОперацийПодрядный.ОперацияРасценка * ТабОперацийПодрядный.Количество * ЕСТЬNULL(ТабКурсовВалют.Курс, 0) / ЕСТЬNULL(ТабКурсовВалют.Кратность, 1)),
	               |	ТабОперацийПодрядный.ID
	               |ИЗ
	               |	ТабОперацийПодрядный КАК ТабОперацийПодрядный
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТабКурсовВалют КАК ТабКурсовВалют
	               |		ПО ТабОперацийПодрядный.ОперацияВалюта = ТабКурсовВалют.Валюта
	               |			И ТабОперацийПодрядный.Период = ТабКурсовВалют.ДатаНач
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабОперацийПодрядный.ID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИтоговаяТаб.ID КАК ID,
	               |	СУММА(ИтоговаяТаб.Стоимость) КАК Стоимость
	               |ИЗ
	               |	ИтоговаяТаб КАК ИтоговаяТаб
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИтоговаяТаб.ID";
	
	
	Если ТекущаяСтрока = Неопределено Тогда
		Запрос.УстановитьПараметр("ПланРемонтов",ПланРемонтов);
		Запрос.УстановитьПараметр("МассивДоступныхДляКорректировкиСтрок",МассивДоступныхДляКорректировкиСтрок);
	Иначе
		Запрос.УстановитьПараметр("ПланРемонтов",ПланРемонтов.Скопировать(Новый Структура("ID", ТекущаяСтрока.ID)));
		Запрос.УстановитьПараметр("МассивДоступныхДляКорректировкиСтрок",МассивДоступныхДляКорректировкиСтрок.Скопировать(Новый Структура("ID", ТекущаяСтрока.ID)));
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	СтрокиДоступныеДляРедактирования = МассивДоступныхДляКорректировкиСтрок.НайтиСтроки(Новый Структура("ДоступенДляРедактирования", Истина));
	IDРемонтовДоступныеДляРедактирования = МассивДоступныхДляКорректировкиСтрок.Скопировать(СтрокиДоступныеДляРедактирования, "ID");
	
	// Очистка существующих стоимостей для работ выполняемых подразделениями.
	// Необходимо, если стоимость стала равна 0 и если для подрядчиков уже есть стоимость.
	Если ТекущаяСтрока = Неопределено Тогда   
		
		Для Каждого IDРемонта Из IDРемонтовДоступныеДляРедактирования Цикл
			СтрокаТЧ = ПланРемонтов.Найти(IDРемонта, "ID");
			Если СтрокаТЧ <> Неопределено Тогда
				СтрокаТЧ.СуммаРемонта = 0;
			КонецЕсли;
		КонецЦикла;
	Иначе
		СтрокаТЧ = ПланРемонтов.Найти(ТекущаяСтрока.ID, "ID");
		Если СтрокаТЧ <> Неопределено Тогда
			СтрокаТЧ.СуммаРемонта = 0;
		КонецЕсли;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = ПланРемонтов.Найти(Выборка.ID, "ID");
		Если СтрокаТЧ <> Неопределено тогда
			
			СтрокаТЧ.СуммаРемонта = Выборка.Стоимость;
		КонецЕсли;
	КонецЦикла;
	
	ПеревестиСтоимостиВТекущие(ПланРемонтов, Ссылка, IDРемонтовДоступныеДляРедактирования, ТекущаяСтрока);
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат ПланРемонтов;
	Иначе
		
		СтрокаТЧ = ПланРемонтов.Найти(ТекущаяСтрока.ID, "ID");
		Если СтрокаТЧ <> Неопределено Тогда
			Возврат СтрокаТЧ.СуммаРемонта;
		Иначе 
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПланРемонтов;
	
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ПеревестиСтоимостиВТекущие(ПланРемонтов, Ссылка, IDРемонтовДоступныеДляРедактирования, ТекущаяСтрока = Неопределено) 
	
	Если ТекущаяСтрока <> Неопределено Тогда
		
		Если IDРемонтовДоступныеДляРедактирования.Найти(ТекущаяСтрока.ID) = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		РезультатЗапроса = ПолучитьЗначенияКоэффициентов(
			Ссылка, ТекущаяСтрока.ДатаНач, 
			?(ТекущаяСтрока.ОбъектРемонтныхРабот.ВнешнийОбъект, Справочники.Организации.ПустаяСсылка(), ТекущаяСтрока.ОбъектРемонтныхРабот.Организация),
			?(ТекущаяСтрока.ОбъектРемонтныхРабот.ВнешнийОбъект, Справочники.СтруктураПредприятия.ПустаяСсылка(), ТекущаяСтрока.ОбъектРемонтныхРабот.Подразделение),
			ТекущаяСтрока.ОбъектРемонтныхРабот.Направление,
			ТекущаяСтрока.ОбъектРемонтныхРабот,
			ТекущаяСтрока.ВидРемонтныхРабот);
		
		Выборка = РезультатЗапроса.выбрать();
		
		Строка = ПланРемонтов.Найти(ТекущаяСтрока.ID, "ID");
		Если Строка <> Неопределено Тогда
			
			ИтоговыйКоэффициент = 1;
			Пока Выборка.Следующий() Цикл
				ИтоговыйКоэффициент = ИтоговыйКоэффициент * Выборка.ПоказательКоэффициента;
			КонецЦикла;
			
			Строка.СуммаРемонта = Строка.СуммаРемонта * ИтоговыйКоэффициент;
		КонецЕсли;
		
	Иначе
				
		РезультатЗапроса = ПолучитьЗначенияКоэффициентовТаб(ПланРемонтов, IDРемонтовДоступныеДляРедактирования, Ссылка.ДатаПланирования);
		
		ВыборкаОР = РезультатЗапроса.выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОР.Следующий() Цикл
			
			ВыборкаВР = ВыборкаОР.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаВР.Следующий() Цикл
				
				Выборка = ВыборкаВР.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				ИтоговыйКоэффициент = 1;
				
				Пока Выборка.Следующий() Цикл
					ИтоговыйКоэффициент = ИтоговыйКоэффициент * Выборка.ПоказательКоэффициента;
				КонецЦикла;
				
				СтруктураПоиска = Новый Структура("ВидРемонтныхРабот, ОбъектРемонтныхРабот",ВыборкаВР.ВидРемонтныхРабот,ВыборкаВР.ОбъектРемонтныхРабот);
				
				ПланРемонтовОтбор = ПланРемонтов.НайтиСтроки(СтруктураПоиска);
				
				Для Каждого Строка из ПланРемонтовОтбор Цикл
					
					Если IDРемонтовДоступныеДляРедактирования.Найти(Строка.ID) = Неопределено Тогда
						Продолжить;
					КонецЕсли; 
					
					Строка.СуммаРемонта = Строка.СуммаРемонта * ИтоговыйКоэффициент;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначенияКоэффициентов(Ссылка,ДатаРемонта,МассивФиалов,МассивПодразделений,МассивНаправлений,МассивОбъектов,МассивВидовРемонта)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Коэффициент,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ПоказательКоэффициента,
	|	ВЫБОР
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта <> ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта <> ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 2
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 3
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|					ИЛИ торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			ТОГДА 4
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 5
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|					ИЛИ торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			ТОГДА 6
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 7
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 8
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 9
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 10
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления = ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 11
	|		ИНАЧЕ 12
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ ТабПараметров
	|ИЗ
	|	РегистрСведений.торо_КоэффициентыПереводаБазовыхЦенВТекущие.СрезПоследних(
	|			&Дата,
	|			КлассификаторРемонтов В (&КлассификаторРемонтов, ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка))
	|				И Направления В (&Направления, ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка))
	|				И ОбъектРемонта В (&ОбъектРемонта, ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка))
	|				И Подразделение В (&Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|				И Организация В (&Филиал, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))) КАК торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТабПараметров.Приоритет) КАК Приоритет,
	|	ТабПараметров.Коэффициент
	|ПОМЕСТИТЬ МинимальныеПриоритеты
	|ИЗ
	|	ТабПараметров КАК ТабПараметров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабПараметров.Коэффициент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабПараметров.Коэффициент,
	|	ЕСТЬNULL(ТабПараметров.ПоказательКоэффициента, 1) КАК ПоказательКоэффициента,
	|	МинимальныеПриоритеты.Приоритет
	|ИЗ
	|	МинимальныеПриоритеты КАК МинимальныеПриоритеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТабПараметров КАК ТабПараметров
	|		ПО МинимальныеПриоритеты.Приоритет = ТабПараметров.Приоритет
	|			И МинимальныеПриоритеты.Коэффициент = ТабПараметров.Коэффициент";
	Запрос.УстановитьПараметр("Дата",Ссылка.ДатаПланирования);	 // Дата
	Запрос.УстановитьПараметр("КлассификаторРемонтов",МассивВидовРемонта);	 // Виды обслуживания основных средств
	Запрос.УстановитьПараметр("Направления",МассивНаправлений);	 // Направления объектов ремонта
	Запрос.УстановитьПараметр("ОбъектРемонта",МассивОбъектов);	 // Объекты ремонта
	Запрос.УстановитьПараметр("Подразделение",МассивПодразделений);	 // Подразделение
	Запрос.УстановитьПараметр("Филиал",МассивФиалов);	 // Организация
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПолучитьЗначенияКоэффициентовТаб(ПланРемонтов, IDРемонтовДоступныеДляРедактирования, ДатаПланирования)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПланРемонтов.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ПланРемонтов.ID КАК ID
	|ПОМЕСТИТЬ ПланРемонтов
	|ИЗ
	|	&ПланРемонтов КАК ПланРемонтов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланРемонтов.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот
	|ПОМЕСТИТЬ ТабДанныхПредварительно
	|ИЗ
	|	ПланРемонтов КАК ПланРемонтов
	|ГДЕ
	|	ПланРемонтов.ID В(&IDРемонтовДоступныеДляРедактирования)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланРемонтов.ОбъектРемонтныхРабот,
	|	ПланРемонтов.ВидРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТабДанныхПредварительно.ОбъектРемонтныхРабот.ВнешнийОбъект
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ ТабДанныхПредварительно.ОбъектРемонтныхРабот.Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА ТабДанныхПредварительно.ОбъектРемонтныхРабот.ВнешнийОбъект
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ИНАЧЕ ТабДанныхПредварительно.ОбъектРемонтныхРабот.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ТабДанныхПредварительно.ОбъектРемонтныхРабот.Направление КАК Направление,
	|	ТабДанныхПредварительно.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ТабДанныхПредварительно.ВидРемонтныхРабот КАК ВидРемонтныхРабот
	|ПОМЕСТИТЬ ТабДанных
	|ИЗ
	|	ТабДанныхПредварительно КАК ТабДанныхПредварительно
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение,
	|	Организация,
	|	Направление,
	|	ОбъектРемонтныхРабот,
	|	ВидРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабДанных.ОбъектРемонтныхРабот,
	|	ТабДанных.ВидРемонтныхРабот,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Коэффициент,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ПоказательКоэффициента,
	|	ВЫБОР
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта <> ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта <> ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 2
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 3
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|					ИЛИ торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			ТОГДА 4
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 5
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|					ИЛИ торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			ТОГДА 6
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 7
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 8
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 9
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления <> ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 10
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта = ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления = ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА 11
	|		ИНАЧЕ 12
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ ТабПараметров
	|ИЗ
	|	РегистрСведений.торо_КоэффициентыПереводаБазовыхЦенВТекущие.СрезПоследних(&Дата, ) КАК торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабДанных КАК ТабДанных
	|		ПО (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов В (ТабДанных.ВидРемонтныхРабот, ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)))
	|			И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Направления В (ТабДанных.Направление, ЗНАЧЕНИЕ(Справочник.торо_НаправленияОбъектовРемонтныхРабот.ПустаяСсылка)))
	|			И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ОбъектРемонта В (ТабДанных.ОбъектРемонтныхРабот, ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)))
	|			И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Подразделение В (ТабДанных.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
	|			И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Организация В (ТабДанных.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТабПараметров.Приоритет) КАК Приоритет,
	|	ТабПараметров.Коэффициент,
	|	ТабПараметров.ОбъектРемонтныхРабот,
	|	ТабПараметров.ВидРемонтныхРабот
	|ПОМЕСТИТЬ МинимальныеПриоритеты
	|ИЗ
	|	ТабПараметров КАК ТабПараметров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабПараметров.Коэффициент,
	|	ТабПараметров.ОбъектРемонтныхРабот,
	|	ТабПараметров.ВидРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабПараметров.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ТабПараметров.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ТабПараметров.Коэффициент,
	|	ЕСТЬNULL(ТабПараметров.ПоказательКоэффициента, 1) КАК ПоказательКоэффициента,
	|	МинимальныеПриоритеты.Приоритет
	|ИЗ
	|	МинимальныеПриоритеты КАК МинимальныеПриоритеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабПараметров КАК ТабПараметров
	|		ПО МинимальныеПриоритеты.Приоритет = ТабПараметров.Приоритет
	|			И МинимальныеПриоритеты.Коэффициент = ТабПараметров.Коэффициент
	|			И МинимальныеПриоритеты.ОбъектРемонтныхРабот = ТабПараметров.ОбъектРемонтныхРабот
	|			И МинимальныеПриоритеты.ВидРемонтныхРабот = ТабПараметров.ВидРемонтныхРабот
	|ИТОГИ ПО
	|	ОбъектРемонтныхРабот,
	|	ВидРемонтныхРабот";
	
	Запрос.УстановитьПараметр("Дата",ДатаПланирования);
	Запрос.УстановитьПараметр("ПланРемонтов", ПланРемонтов);
	Запрос.УстановитьПараметр("IDРемонтовДоступныеДляРедактирования", IDРемонтовДоступныеДляРедактирования);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
// КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// План-график ремонта
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.торо_ПланГрафикРемонта";
	КомандаПечати.Идентификатор = "МакетПланаРемонтовНаПериод";
	КомандаПечати.Представление = НСтр("ru = 'План-график ППР'");
	КомандаПечати.Обработчик = "торо_Печать.ОбработатьКомандуПечатиППР";
	КомандаПечати.СразуНаПринтер = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
	"НастройкиТОиР",
	"ПечатьДокументовБезПредварительногоПросмотра",
	Ложь);
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ИменаОбъектовМетаданных = РегистрыСведений.торо_ВводНаОсновании.ИменаДоступныхОбъектовМетаданныхДляВводаНаОсновании(
		 Метаданные.Документы.торо_ПланГрафикРемонта.Имя);
		 
	Для Каждого ИмяОбъектаМетаданных Из ИменаОбъектовМетаданных Цикл
		Документы[ИмяОбъектаМетаданных].ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);		 
	КонецЦикла;		 
	
	КомандаКорректировки = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	торо_СозданиеНаОсновании.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСозданияНаОсновании);
		
КонецПроцедуры

// Добавляет команду создания документа "План-график ППР".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.торо_ПланГрафикРемонта) Тогда
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.торо_ПланГрафикРемонта.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.торо_ПланГрафикРемонта);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "торо_ИспользоватьППР";
        Возврат КомандаСоздатьНаОсновании;
	КонецЕсли; 
	
    Возврат Неопределено;
	
КонецФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПланаРемонтовНаПериод") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"МакетПланаРемонтовНаПериод", 
		"План-график ППР", 
		ПечатьПланГрафикППР(МассивОбъектов, ПараметрыПечати),
		,
		"Документ.торо_ПланГрафикРемонта.ПФ_MXL_МакетПланаРемонтовНаПериод");
	КонецЕсли;
	
КонецПроцедуры

// Процедура вывода на экран печатной формы документа
// торо_ПланГрафикРемонта.
//  
Функция ПечатьПланГрафикППР(МассивОбъектов, ПараметрыПечати)
	
	ТаблицаПлана = Новый ТабличныйДокумент;		
	МакетТаблицы =  УправлениеПечатью.МакетПечатнойФормы("Документ.торо_ПланГрафикРемонта.ПФ_MXL_МакетПланаРемонтовНаПериод");
	
	ЭтоПервый = Истина;
	
	Для каждого ПланГрафикРемонта из МассивОбъектов Цикл
		
		Если НЕ ЭтоПервый Тогда
			ТаблицаПлана.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ЭтоПервый = Ложь;
		
		СписокПодчиненных = Неопределено;
		
		Если ПараметрыПечати.Свойство("Параметры") Тогда
			СтруктураПараметровПечати = ПараметрыПечати.Параметры;
			СписокПодчиненных = СтруктураПараметровПечати.СписокПодчиненных;
		Иначе
			СтруктураПараметровПечати = торо_ПечатьСервер.СтруктураПараметровППР(ПланГрафикРемонта);
		КонецЕсли;
		
		ПериодКон=СтруктураПараметровПечати.ДатаКонца;
		ПериодНач=СтруктураПараметровПечати.ДатаНачала;
		ОтборПечатнойФормы=СтруктураПараметровПечати.ОтборФормы;	
		
		ОтборПечатнойФормы[0].Имя="ОбъектРемонтныхРабот";
		ОтборПечатнойФормы[1].Имя="ВидРемонтныхРабот";
		
		ИнтервалРазбиения = торо_ПечатьСервер.ПолучитьИнтервалРазбиения(СтруктураПараметровПечати.ИнтервалРазбиения);
		
		Если (ПланГрафикРемонта = Неопределено) Тогда
			ТекстСообщения = НСтр("ru = 'Не выбран документ ""План-график ремонтов"".'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Иначе
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	торо_ПланГрафикРемонтаОбъектыРемонта.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРаботПлан,
			|	МИНИМУМ(торо_ПланГрафикРемонтаОбъектыРемонта.НомерСтроки) КАК НомерСтроки
			|ПОМЕСТИТЬ ОбъектыРемонта
			|ИЗ
			|	Документ.торо_ПланГрафикРемонта.ОбъектыРемонта КАК торо_ПланГрафикРемонтаОбъектыРемонта
			|ГДЕ
			|	торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	торо_ПланГрафикРемонтаОбъектыРемонта.ОбъектРемонтныхРабот
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка,
			|	торо_ПланГрафикРемонтаПланРемонтов.НомерСтроки,
			|	торо_ПланГрафикРемонтаПланРемонтов.ID,
			|	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач,
			|	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель,
			|	торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот,
			|	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения,
			|	торо_ПланГрафикРемонтаПланРемонтов.Отменен,
			|	торо_ПланГрафикРемонтаПланРемонтов.Склад,
			|	торо_ПланГрафикРемонтаПланРемонтов.Замещен,
			|	торо_ПланГрафикРемонтаПланРемонтов.ID_базы_расчета,
			|	торо_ПланГрафикРемонтаПланРемонтов.ID_замещающего,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНачСт,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКонСт,
			|	торо_ПланГрафикРемонтаПланРемонтов.ЗамещенСт,
			|	торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта,
			|	торо_ПланГрафикРемонтаОбъектыРемонта.НомерСтроки КАК НомерОР
			|ИЗ
			|	Документ.торо_ПланГрафикРемонта.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
			|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыРемонта КАК торо_ПланГрафикРемонтаОбъектыРемонта
			|		ПО торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот = торо_ПланГрафикРемонтаОбъектыРемонта.ОбъектРемонтныхРаботПлан
			|ГДЕ
			|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка = &Ссылка";
			
			Если Не СтруктураПараметровПечати.ВыводитьВсеРемонты Тогда
				Запрос.Текст = Запрос.Текст +
				"	И торо_ПланГрафикРемонтаПланРемонтов.ДатаКон МЕЖДУ &ДатаНачала И &ДатаОкончания
				|   И торо_ПланГрафикРемонтаПланРемонтов.ДатаНач МЕЖДУ &ДатаНачала И &ДатаОкончания";
				Запрос.УстановитьПараметр("ДатаНачала", СтруктураПараметровПечати.ДатаНачала);
				Запрос.УстановитьПараметр("ДатаОкончания", СтруктураПараметровПечати.ДатаКонца);
			КонецЕсли;
			
			торо_ПечатьСервер.ДобавитьПараметрыОтбора(ОтборПечатнойФормы,Запрос,"торо_ПланГрафикРемонтаПланРемонтов",СписокПодчиненных);
			
			Запрос.Текст = Запрос.Текст + 
			" И торо_ПланГрафикРемонтаПланРемонтов.Отменен = ЛОЖЬ 
			| И торо_ПланГрафикРемонтаПланРемонтов.Замещен = ЛОЖЬ
			|УПОРЯДОЧИТЬ ПО
			|	НомерОР";
			
			Запрос.УстановитьПараметр("Ссылка", ПланГрафикРемонта);
			ПланРемонтов = Запрос.Выполнить().Выгрузить();
			
			Наименование = "";
			
			Область = МакетТаблицы.ПолучитьОбласть("Заголовок");	
			Область.Параметры.НачалоПериода = Формат(ПериодНач, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""");
			Область.Параметры.КонецПериода = Формат(ПериодКон, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""");
			Область.Параметры.Организация = торо_ЗаполнениеДокументов.ПолучитьПредставлениеОрганизацииДляПечати(ПланГрафикРемонта.Организация);;
			Область.Параметры.Подразделение = ПланГрафикРемонта.Подразделение;
			Область.Параметры.Номер = ПланГрафикРемонта.Номер;
			Область.Параметры.Название = Наименование;
			Если ПланГрафикРемонта.ВидОперации = Перечисления.торо_ВидыОперацийПланаГрафикаППР.Корректировка Тогда                                               			
				Область.Параметры.Корректировка =  СтрШаблон(НСтр("ru = 'корректировка документа %1'"), ПланГрафикРемонта.ДокументОснование);			
			КонецЕсли;
			
			ТаблицаПлана.Вывести(Область);
			
			ПериодовВОтчете = (НачалоДня(ПериодКон) - НачалоДня(ПериодНач))/(60*60*24);
			
			Область = МакетТаблицы.ПолучитьОбласть("Шапка");
			
			ШиринаТаблицы = Область.ШиринаТаблицы;
			
			ТаблицаПлана.Вывести(Область);
			
			ТемпДата = НачалоДня(ПериодНач);
			
			ПериодКонРасчетный = ПериодКон;
			
			МассивВременныхИнтервалов = Новый Массив;
			
			торо_ПечатьСервер.ЗаполнитьИнтервалы(ПериодНач,ПериодКон,ИнтервалРазбиения,ТаблицаПлана,МакетТаблицы,МассивВременныхИнтервалов,ШиринаТаблицы);
			
			Область = МакетТаблицы.ПолучитьОбласть("ПравыйКрайЗаглушка");
			ТаблицаПлана.Присоединить(Область);
			
			// Непосредственно вывод данных по объекту
			
			НСТР = 1;
			
			СписокХранения = Новый СписокЗначений;
			
			Для каждого СтрокаТаблицы из ПланРемонтов Цикл
				ОбъектДляДобавления =  СтрокаТаблицы.ОбъектРемонтныхРабот;
				
				Если СписокХранения.НайтиПоЗначению(ОбъектДляДобавления)=Неопределено Тогда
					СписокХранения.Добавить(ОбъектДляДобавления);			
				КонецЕсли;
				
			КонецЦикла;
			
			МассивОбъектов = СписокХранения.ВыгрузитьЗначения();
			Для каждого ОбъектДляОтчета из МассивОбъектов Цикл
				Область = МакетТаблицы.ПолучитьОбласть("СтрокаЗаполнения");
				ФорматнаяОбласть = Область.Область("НаимПрим");
				ФорматнаяОбласть.Примечание.Текст = ОбъектДляОтчета.ПлановыйГрафикРаботы.Наименование;
				
				Область.Параметры.НПП = НСТР;
				Область.Параметры.НаименовОбъекта = торо_ЗаполнениеДокументов.ПолучитьПредоставленияОРДляПечати(ОбъектДляОтчета);
				Область.Параметры.ТехнологическийН = ОбъектДляОтчета.ТехНомер;
				Область.Параметры.ЗаводскойН = ОбъектДляОтчета.ЗаводскойНомер;
				Область.Параметры.ИнвентарныйН = ОбъектДляОтчета.ИнвентарныйНомер;
				
				Результ = ПоследнийРемонт(ОбъектДляОтчета,ПланГрафикРемонта.ДатаПланирования);
				
				ПредставлениеВидаРемонта = "ВВЭ";
				Если ТипЗнч(Результ[0].Объект) = Тип("СправочникСсылка.торо_ВидыРемонтов") Тогда
					ПредставлениеВидаРемонта = торо_ЗаполнениеДокументов.ПолучитьПредоставленияВРДляПечати(Результ[0].Объект);
				ИначеЕсли ЗначениеЗаполнено(Результ[0].Объект) Тогда
					ПредставлениеВидаРемонта = Результ[0].Объект;
				КонецЕсли;
				
				Область.Параметры.Вид = ПредставлениеВидаРемонта;
				ДатаДляФормата = Дата(Результ[0].Период);
				Область.Параметры.Дата = Формат( ДатаДляФормата, "ДФ = ""дд/ММ/гггг""");
				
				ТаблицаПлана.Вывести(Область);	
				
				линСплошная = новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				линНикакая = новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
				
				Условие = Новый Структура;
				Условие.Вставить("ОбъектРемонтныхРабот", ОбъектДляОтчета);
				Условие.Вставить("Отменен", Ложь);
				НайденныеСтроки = ПланРемонтов.НайтиСтроки(Условие); 		
				для Сч = 1 по  МассивВременныхИнтервалов.Количество() Цикл
					
					ДанныеПоРемонтамЗаПериод = Новый Массив;
					Если ИнтервалРазбиения = Перечисления.Периодичность.День Тогда
						ВидыРемонтовЗаПериод = Новый Массив;
					КонецЕсли;
					
					ТекущийПериодНач = МассивВременныхИнтервалов[Сч-1].НачИнт;
					ТекущийПериодКон = МассивВременныхИнтервалов[Сч-1].КонИнт;
					
					Область = МакетТаблицы.ПолучитьОбласть("ЗначенияДня");
					ОбластьФормат =  Область.Область();
					
					Для Каждого ОбъектСравнения из НайденныеСтроки цикл 
						
						Если ОбъектСравнения.ДатаКон<ОбъектСравнения.ДатаНач Тогда
							КонечнаяДата = КонецДня(ОбъектСравнения.ДатаНач);	
						Иначе
							КонечнаяДата = ОбъектСравнения.ДатаКон;
						КонецЕсли; 
						
						Если  ((ТекущийПериодКон>=ОбъектСравнения.ДатаНач) и (ТекущийПериодНач<=КонечнаяДата)) или
							((ТекущийПериодКон>ОбъектСравнения.ДатаНач) и (ТекущийПериодНач<=КонечнаяДата)) Тогда
							ОбластьФормат.ЦветФона = новый Цвет(255, 251, 240);
							ОбластьФормат.ГраницаСлева =линНикакая;
							ОбластьФормат.ГраницаСправа = линСплошная;
							
							ПредставлениеВидаРемонта = торо_ЗаполнениеДокументов.ПолучитьПредоставленияВРДляПечати(ОбъектСравнения.ВидРемонтныхРабот);
							Если НачалоДня(ОбъектСравнения.ДатаНач) = НачалоДня(ОбъектСравнения.ДатаКон) Тогда
								ПредставлениеПродолжительностиРаботы = Формат(ОбъектСравнения.ДатаНач, "ДЛФ=D");
							Иначе
								ПредставлениеПродолжительностиРаботы = Формат(ОбъектСравнения.ДатаНач, "ДЛФ=D") + " - " + Формат(ОбъектСравнения.ДатаКон, "ДЛФ=D");
							КонецЕсли;	
							
							Если ИнтервалРазбиения = Перечисления.Периодичность.День Тогда
								ВидыРемонтовЗаПериод.Добавить(ПредставлениеВидаРемонта);
							КонецЕсли;

							ДанныеПоРемонту = СтрШаблон("%1 (%2)", ПредставлениеВидаРемонта, ПредставлениеПродолжительностиРаботы);  
							ДанныеПоРемонтамЗаПериод.Добавить(ДанныеПоРемонту);  
						КонецЕсли;	
					КонецЦикла;	
					
					Если ИнтервалРазбиения = Перечисления.Периодичность.День Тогда
						Область.Параметры.Заполнение = СтрСоединить(ВидыРемонтовЗаПериод, ", ");
						ОбластьФормат.Примечание.Текст = СтрСоединить(ДанныеПоРемонтамЗаПериод, Символы.ПС);
					Иначе	
						Область.Параметры.Заполнение = СтрСоединить(ДанныеПоРемонтамЗаПериод, Символы.ПС); 
					КонецЕсли;
					
					ТаблицаПлана.Присоединить(Область);
					
				КонецЦикла;
				Область = МакетТаблицы.ПолучитьОбласть("ПравыйКрайЗаглушка2");
				ТаблицаПлана.Присоединить(Область);
				
				НСТР = НСТР + 1;
				
			КонецЦикла;
			
			ОбластьПодвал = МакетТаблицы.ПолучитьОбласть("Подвал");
			ОбластьПодвал.Область(1,1,1, ШиринаТаблицы).ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2);
			ТаблицаПлана.Вывести(ОбластьПодвал);
			
			Область = МакетТаблицы.ПолучитьОбласть("Пояснение");	
			ТаблицаПлана.Вывести(Область);
			
		КонецЕсли;
		
		ТабДок = Новый ТабличныйДокумент;
		ТабДок.КлючПараметровПечати = "торо_ПечатьПланГрафика_МакетПланаРемонтовНаПериод";
		ТабДок.ИмяПараметровПечати = "торо_ПечатьПланГрафика_МакетПланаРемонтовНаПериод";
		ТабДок.ВставитьОбласть(ТаблицаПлана.Область());
		
	КонецЦикла;
	
	ТабДок.ТолькоПросмотр = Истина;
	Возврат ТабДок;
	
КонецФункции

// Функция предназначена для вывода таблицы последних ремонтов.
//
// Параметры: 
//      Объект - объект.
//
// Возвращаемое значение:
//		ДляВозврата - таблица значений.
//
Функция ПоследнийРемонт(Объект,ДатаПостроения)
	
	ЗапросКРегистру = новый Запрос;
	
	ЗапросКРегистру.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	                        |	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.ДатаОкончания КАК Период,
	                        |	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.ВидРемонтныхРабот КАК Объект
	                        |ИЗ
	                        |	РегистрСведений.торо_ЗавершенныеРемонтныеРаботы.СрезПоследних(, ОбъектРемонта = &ОбъектРемонта И ДатаОкончания <= &Дата) КАК торо_ЗавершенныеРемонтныеРаботыСрезПоследних
	                        |
	                        |УПОРЯДОЧИТЬ ПО
	                        |	Период УБЫВ";
	
	ЗапросКРегистру.УстановитьПараметр("ОбъектРемонта", Объект);
	ЗапросКРегистру.УстановитьПараметр("Дата", ДатаПостроения);
	результат = ЗапросКРегистру.Выполнить();
	
	дляВозврата =  результат.Выгрузить();
	
	Если дляВозврата.Количество()=0 Тогда
		строка = дляВозврата.Добавить();
		строка.Период = Объект.ДатаВводаВЭксплуатацию;
		строка.Объект = "ВВЭ";
	КонецЕсли;
	
	Возврат дляВозврата;
	
КонецФункции // ПоследнийРемонт()

Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Настройки.ПриПолученииСлужебныхРеквизитов = Истина;
	
КонецПроцедуры

Процедура ПриПолученииСлужебныхРеквизитов(Реквизиты) Экспорт
	
	Реквизиты.Добавить("СтатусДокумента");
	Реквизиты.Добавить("ИсторияСтатусов.*");
	Реквизиты.Добавить("Согласующие.*");
		
КонецПроцедуры

Функция ПодобратьРемонтыДляКорректировки(мДатаОкончанияПланирования, пОснование, пДатаПланирования, пСсылка, ОтборПоОР = Ложь, ОРдляОтбора = Неопределено) Экспорт
	
	Запрос = Новый Запрос();
	Если ОтборПоОР Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТабОРдляОтбора.ОбъектРемонтныхРабот КАК ОР,
		               |	ТабОРдляОтбора.ВидРемонтныхРабот КАК ВР
		               |ПОМЕСТИТЬ ВТ_ОРдляОтборДокумент
		               |ИЗ
		               |	&ТабОРдляОтбора КАК ТабОРдляОтбора
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВТ_ОРдляОтборДокумент.ОР КАК ОР,
		               |	ЕСТЬNULL(торо_ЦепочкиРемонтаПоследовательностьРемонтов.ВидРемонта, ВТ_ОРдляОтборДокумент.ВР) КАК ВР
		               |ПОМЕСТИТЬ ВТ_ОРдляОтбор
		               |ИЗ
		               |	ВТ_ОРдляОтборДокумент КАК ВТ_ОРдляОтборДокумент
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ЦепочкиРемонта.ПоследовательностьРемонтов КАК торо_ЦепочкиРемонтаПоследовательностьРемонтов
		               |		ПО ВТ_ОРдляОтборДокумент.ВР = торо_ЦепочкиРемонтаПоследовательностьРемонтов.Ссылка
							|;";
	Иначе
		Запрос.Текст = "";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СоответствиеКорректировокДокументамППР.Корректировка КАК Корректировка
	|ПОМЕСТИТЬ ВТ_Основания
	|ИЗ
	|	РегистрСведений.торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР
	|ГДЕ
	|	торо_СоответствиеКорректировокДокументамППР.Корректируемый = &Основание
	|	И торо_СоответствиеКорректировокДокументамППР.Корректировка <> &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	|	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ОбщиеДанныеПоРемонтам.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВТ_IDремонтов
	|ИЗ
	|	Документ.торо_ПланГрафикРемонта.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основания КАК ВТ_Основания
	|		ПО торо_ПланГрафикРемонтаПланРемонтов.Ссылка = ВТ_Основания.Корректировка
	|			И (&отбор)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	|		ПО торо_ПланГрафикРемонтаПланРемонтов.ID = торо_ОбщиеДанныеПоРемонтам.IDРемонта
	|ГДЕ
	|	НЕ торо_ОбщиеДанныеПоРемонтам.Отменен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID КАК ID,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Регистратор КАК Регистратор,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Исполнитель КАК Исполнитель,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СпособВыполнения КАК СпособВыполнения,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Отменен КАК Отменен,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Замещен КАК Замещен,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID_базы_расчета КАК ID_базы_расчета,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID_замещающего КАК ID_замещающего,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Замещен КАК ЗамещенСт,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СтоимостьРемонта КАК СтоимостьРемонта,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(торо_ПлановыеРемонтныеРаботыСрезПоследних.Регистратор КАК Документ.торо_ЗакрытиеЗаявокИРемонтов) ССЫЛКА Документ.торо_ЗакрытиеЗаявокИРемонтов, ЛОЖЬ) КАК Перенесенный,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СрокПоНормативу КАК СрокПоНормативу
	|ПОМЕСТИТЬ ВТ_ПлановыеРемонтныеРаботыСрез
	|ИЗ
	|	РегистрСведений.торо_ПлановыеРемонтныеРаботы.СрезПоследних(
	|			,
	|			ID В
	|					(ВЫБРАТЬ
	|						ВТ_IDремонтов.ID
	|					ИЗ
	|						ВТ_IDремонтов)
	|				И Регистратор <> &Ссылка) КАК торо_ПлановыеРемонтныеРаботыСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_IDремонтов.ID КАК ID,
	|	ВТ_IDремонтов.ОбъектРемонта КАК ОбъектРемонтныхРабот,
	|	ВТ_IDремонтов.ВидРемонта КАК ВидРемонтныхРабот,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала) КАК ДатаНач,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаОкончания, торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания) КАК ДатаКон,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Исполнитель КАК Исполнитель,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СпособВыполнения КАК СпособВыполнения,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Отменен КАК Отменен,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Замещен КАК Замещен,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID_базы_расчета КАК ID_базы_расчета,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID_замещающего КАК ID_замещающего,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала) КАК ДатаНачСт,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаОкончания, торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания) КАК ДатаКонСт,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Замещен КАК ЗамещенСт,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СтоимостьРемонта КАК СуммаРемонта,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(торо_ПлановыеРемонтныеРаботыСрезПоследних.Регистратор КАК Документ.торо_ЗакрытиеЗаявокИРемонтов) ССЫЛКА Документ.торо_ЗакрытиеЗаявокИРемонтов, ЛОЖЬ) КАК Перенесенный,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СрокПоНормативу КАК СрокПоНормативу
	|ПОМЕСТИТЬ ВТ_ПочтиИтог
	|ИЗ
	|	ВТ_IDремонтов КАК ВТ_IDремонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_IDремонтов.ID = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СвернутыеФактическиеДатыРемонтов КАК торо_СвернутыеФактическиеДатыРемонтов
	|		ПО ВТ_IDремонтов.ID = торо_СвернутыеФактическиеДатыРемонтов.IDРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПлановыеРемонтныеРаботыСрез КАК торо_ПлановыеРемонтныеРаботыСрезПоследних
	|		ПО (торо_ПлановыеРемонтныеРаботыСрезПоследних.ID = ВТ_IDремонтов.ID)
	|ГДЕ
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала) МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонтныхРабот,
	|	ВидРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_НормативныеРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_НормативныеРемонтыОборудования.ВидРемонта КАК ВидРемонта,
	|	торо_НормативныеРемонтыОборудования.СпособВыполнения КАК СпособВыполнения,
	|	торо_НормативныеРемонтыОборудования.ОбъектРемонта.КонтрагентИсполнительРемонта КАК ОбъектРемонтаКонтрагентИсполнительРемонта,
	|	торо_НормативныеРемонтыОборудования.ОбъектРемонта.ПодразделениеИсполнитель КАК ОбъектРемонтаПодразделениеИсполнитель
	|ПОМЕСТИТЬ ВТ_Нормативы
	|ИЗ
	|	ВТ_ПочтиИтог КАК ВТ_ПочтиИтог
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудованияУдаленные КАК торо_НормативныеРемонтыОборудованияУдаленные
	|			ПО торо_НормативныеРемонтыОборудования.ОбъектРемонта = торо_НормативныеРемонтыОборудованияУдаленные.ОбъектРемонта
	|				И торо_НормативныеРемонтыОборудования.ВидРемонта = торо_НормативныеРемонтыОборудованияУдаленные.ВидРемонта
	|		ПО ВТ_ПочтиИтог.ОбъектРемонтныхРабот = торо_НормативныеРемонтыОборудования.ОбъектРемонта
	|			И ВТ_ПочтиИтог.ВидРемонтныхРабот = торо_НормативныеРемонтыОборудования.ВидРемонта
	|			И (ВТ_ПочтиИтог.СпособВыполнения ЕСТЬ NULL)
	|ГДЕ
	|	торо_НормативныеРемонтыОборудованияУдаленные.ОбъектРемонта ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПочтиИтог.ID КАК ID,
	|	ВТ_ПочтиИтог.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ВТ_ПочтиИтог.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ВТ_ПочтиИтог.ДатаНач КАК ДатаНач,
	|	ВТ_ПочтиИтог.ДатаКон КАК ДатаКон,
	|	ВЫБОР
	|		КОГДА ВТ_ПочтиИтог.Исполнитель ЕСТЬ NULL
	|				И ЕСТЬNULL(ВТ_ПочтиИтог.СпособВыполнения, ВТ_Нормативы.СпособВыполнения) = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ)
	|			ТОГДА ВТ_Нормативы.ОбъектРемонтаПодразделениеИсполнитель
	|		КОГДА ВТ_ПочтиИтог.Исполнитель ЕСТЬ NULL
	|				И ЕСТЬNULL(ВТ_ПочтиИтог.СпособВыполнения, ВТ_Нормативы.СпособВыполнения) = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|			ТОГДА ВТ_Нормативы.ОбъектРемонтаКонтрагентИсполнительРемонта
	|		ИНАЧЕ ВТ_ПочтиИтог.Исполнитель
	|	КОНЕЦ КАК Исполнитель,
	|	ЕСТЬNULL(ВТ_ПочтиИтог.СпособВыполнения, ВТ_Нормативы.СпособВыполнения) КАК СпособВыполнения,
	|	ВТ_ПочтиИтог.Отменен КАК Отменен,
	|	ВТ_ПочтиИтог.Склад КАК Склад,
	|	ВТ_ПочтиИтог.Замещен КАК Замещен,
	|	ВТ_ПочтиИтог.ID_базы_расчета КАК ID_базы_расчета,
	|	ВТ_ПочтиИтог.ID_замещающего КАК ID_замещающего,
	|	ВТ_ПочтиИтог.ДатаНачСт КАК ДатаНачСт,
	|	ВТ_ПочтиИтог.ДатаКонСт КАК ДатаКонСт,
	|	ВТ_ПочтиИтог.ЗамещенСт КАК ЗамещенСт,
	|	ВТ_ПочтиИтог.СуммаРемонта КАК СуммаРемонта,
	|	ВТ_ПочтиИтог.Перенесенный КАК Перенесенный,
	|	ВТ_ПочтиИтог.СрокПоНормативу КАК СрокПоНормативу
	|ИЗ
	|	ВТ_ПочтиИтог КАК ВТ_ПочтиИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Нормативы КАК ВТ_Нормативы
	|		ПО ВТ_ПочтиИтог.ОбъектРемонтныхРабот = ВТ_Нормативы.ОбъектРемонта
	|			И ВТ_ПочтиИтог.ВидРемонтныхРабот = ВТ_Нормативы.ВидРемонта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНач";
	
	Запрос.УстановитьПараметр("Основание",пОснование);
	Запрос.УстановитьПараметр("ДатаНачала",пДатаПланирования);
	Запрос.УстановитьПараметр("ДатаОкончания",мДатаОкончанияПланирования);
	Запрос.УстановитьПараметр("Ссылка",пСсылка);
	Запрос.УстановитьПараметр("ТабОРдляОтбора",ОРдляОтбора);
	Запрос.УстановитьПараметр("ФОИспользоватьСкользящееПланирование", ПолучитьФункциональнуюОпцию("торо_ИспользоватьСкользящееПланирование"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И (&отбор)",?(ОтборПоОР,"ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРдляОтбор КАК ВТ_ОРдляОтбора ПО торо_ПланГрафикРемонтаПланРемонтов.ОбъектРемонтныхРабот = ВТ_ОРдляОтбора.ОР И торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот = ВТ_ОРдляОтбора.ВР",""));

	ТЗ_ПланРемонтов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТЗ_ПланРемонтов;
	
КонецФункции

Функция ПодобратьОбъектыДляКорректировки(пОснование,пСсылка)Экспорт
	
	Запрос = Новый Запрос;	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_СоответствиеКорректировокДокументамППР.Корректировка КАК Корректировка
	|ПОМЕСТИТЬ ВТ_Основания
	|ИЗ
	|	РегистрСведений.торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР
	|ГДЕ
	|	торо_СоответствиеКорректировокДокументамППР.Корректируемый = &Основание
	|	И торо_СоответствиеКорректировокДокументамППР.Корректировка <> &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ПланГрафикРемонтаОбъектыРемонта.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	торо_ПланГрафикРемонтаОбъектыРемонта.ВидРемонтныхРабот КАК ВидРемонтныхРабот
	|ИЗ
	|	Документ.торо_ПланГрафикРемонта.ОбъектыРемонта КАК торо_ПланГрафикРемонтаОбъектыРемонта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основания КАК ВТ_Основания
	|		ПО торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка = ВТ_Основания.Корректировка
	|
	|УПОРЯДОЧИТЬ ПО
	|	торо_ПланГрафикРемонтаОбъектыРемонта.НомерСтроки";
	
	Запрос.УстановитьПараметр("Основание",пОснование);
	Запрос.УстановитьПараметр("Ссылка",пСсылка);

	ТЗ_ОбъектыРемонта = Запрос.Выполнить().Выгрузить();
	
	Возврат ТЗ_ОбъектыРемонта;
	
КонецФункции

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ПланГрафикРемонта.ОбъектыРемонта КАК РемонтыОборудования
	|	ПО ЭтотСписок.Ссылка = РемонтыОборудования.Ссылка
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта 
	|    ПО РемонтыОборудования.ОбъектРемонтныхРабот = торо_ОбъектыРемонта.Ссылка
	|;
	|РазрешитьЧтение
	|ГДЕ 
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
    |ГДЕ
	|	ДляВсехСтрок(&Ограничение_ОР)";

	ОграничениеОР = торо_УправлениеДоступом.ПолучитьОграничениеДоступаДляОбъектаРемонтаВТаблице();
	Ограничение.Текст = СтрЗаменить(Ограничение.Текст, "&Ограничение_ОР", ОграничениеОР);
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	// Журнал ремонтов
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'Журнал запланированных ремонтов'");
	Команда.Идентификатор 		= "ЖурналЗапланированныхРемонтов";
	Команда.МножественныйВыбор = Ложь;
	Команда.Обработчик         = "торо_ОтчетыКлиент.ОткрытьЖурналРемонтовППР";
	
	// План-фактный анализ
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.торо_ПланФактныйАнализППР) Тогда
		Команда = КомандыОтчетов.Добавить();
		Команда.Представление      = НСтр("ru = 'План-фактный анализ ППР'");
		Команда.Идентификатор 		= "ПланФактныйАнализППР";
		Команда.МножественныйВыбор = Ложь;
		Команда.Обработчик         = "торо_ОтчетыКлиент.ОткрытьПланФактныйАнализППР";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
