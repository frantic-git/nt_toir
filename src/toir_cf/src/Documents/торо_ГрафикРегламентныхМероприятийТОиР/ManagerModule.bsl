#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс
// Функция предназначена для обновления доступности документа для редактирования.
//
// Параметры:
//		Ссылка			- ДокументСсылка - ссылка на документ, 
//		ВидОперации		- ПеречислениеСсылка - вид операции документа,
//		ТаблицаID	- ТаблицаЗначений - план ремонтов документа, таблица с колонкой "ID".
//
// Возвращаемое значение: 
//		Массив - массив доступных для корректировки строк.
Функция ОбновитьДоступностьДляРедактирования(Ссылка, ВидОперации, ТаблицаID) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаID.ID КАК ID
	               |ПОМЕСТИТЬ ТаблицаID
	               |ИЗ
	               |	&ТаблицаID КАК ТаблицаID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_СоответствиеКорректировокДокументамППР.Корректировка КАК Корректировка
	               |ПОМЕСТИТЬ торо_СоответствиеКорректировокДокументамППР
	               |ИЗ
	               |	РегистрСведений.торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР
	               |ГДЕ
	               |	торо_СоответствиеКорректировокДокументамППР.Корректируемый = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID
	               |ПОМЕСТИТЬ РемонтыКорректировок
	               |ИЗ
	               |	Документ.торо_ГрафикРегламентныхМероприятийТОиР.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |ГДЕ
	               |	торо_ПланГрафикРемонтаПланРемонтов.Ссылка В
	               |			(ВЫБРАТЬ
	               |				торо_СоответствиеКорректировокДокументамППР.Корректировка
	               |			ИЗ
	               |				торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаID.ID КАК ID
	               |ПОМЕСТИТЬ РемонтыСКорректировкой
	               |ИЗ
	               |	ТаблицаID КАК ТаблицаID
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РемонтыКорректировок КАК РемонтыКорректировок
	               |		ПО ТаблицаID.ID = РемонтыКорректировок.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_СкользящийПланРегламентныхМероприятий.ID_Ремонта
	               |ИЗ
	               |	РегистрСведений.торо_СкользящийПланРегламентныхМероприятий КАК торо_СкользящийПланРегламентныхМероприятий
	               |ГДЕ
	               |	торо_СкользящийПланРегламентныхМероприятий.ID_Ремонта В
	               |			(ВЫБРАТЬ
	               |				ТаблицаID.ID
	               |			ИЗ
	               |				ТаблицаID)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаID.ID КАК ID,
	               |	ВЫБОР
	               |		КОГДА НЕ(НЕ торо_ОбщиеДанныеПоРемонтам.IDРемонта ЕСТЬ NULL
	               |						И (торо_ОбщиеДанныеПоРемонтам.ЕстьЗаявка
	               |							ИЛИ торо_ОбщиеДанныеПоРемонтам.ЕстьНаряд
	               |							ИЛИ торо_ОбщиеДанныеПоРемонтам.ЕстьАкт)
	               |					ИЛИ НЕ РемонтыСКорректировкой.ID ЕСТЬ NULL)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ДоступенДляРедактирования
	               |ИЗ
	               |	ТаблицаID КАК ТаблицаID
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	               |		ПО ТаблицаID.ID = торо_ОбщиеДанныеПоРемонтам.IDРемонта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РемонтыСКорректировкой КАК РемонтыСКорректировкой
	               |		ПО (РемонтыСКорректировкой.ID = ТаблицаID.ID)";
	
	Запрос.УстановитьПараметр("ТаблицаID", ТаблицаID);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);
	
	МассивДоступныхДляКорректировкиСтрок = Запрос.Выполнить().Выгрузить();
	
	Возврат МассивДоступныхДляКорректировкиСтрок;
КонецФункции

// Функция - Заполнить доступность для редактирования полная.
//
// Параметры:
//  парамПланРемонтов							 - ТаблицаЗначений - все строки плана ремонтов;
//  парамМассивДоступныхДляКорректировкиСтрок	 - ТаблицаЗначений - Доступные для редактирования строки.
// Возвращаемое значение:
//  ТаблицаЗначений - результат запроса с указанием доступных для редактирования ремонтов.
Функция ЗаполнитьДоступностьДляРедактированияПолная(парамПланРемонтов,парамМассивДоступныхДляКорректировкиСтрок) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаКонСт КАК ДатаКон,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаНачСт КАК ДатаНач,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СписокОбъектовРемонта КАК СписокОбъектовРемонта,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения КАК СпособВыполнения,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Отменен КАК Отменен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Замещен КАК Замещен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ЗамещенСт КАК ЗамещенСт,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_базы_расчета КАК ID_базы_расчета,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_замещающего КАК ID_замещающего,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель КАК Исполнитель,
						|	торо_ПланГрафикРемонтаПланРемонтов.Склад КАК Склад,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта КАК СуммаРемонта,
						|	торо_ПланГрафикРемонтаПланРемонтов.СрокПоНормативу КАК СрокПоНормативу,
						|	торо_ПланГрафикРемонтаПланРемонтов.СодержитВыходные КАК СодержитВыходные
	               |ПОМЕСТИТЬ торо_ПланГрафикРемонтаПланРемонтов
	               |ИЗ
	               |	&ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаДоступенДляРедакторования.ID КАК ID,
	               |	ТаблицаДоступенДляРедакторования.ДоступенДляРедактирования КАК ДоступенДляРедактирования
	               |ПОМЕСТИТЬ ТаблицаДоступенДляРедакторования
	               |ИЗ
	               |	&ТаблицаДоступенДляРедакторования КАК ТаблицаДоступенДляРедакторования
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон КАК ДатаКон,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач КАК ДатаНач,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СписокОбъектовРемонта КАК СписокОбъектовРемонта,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения КАК СпособВыполнения,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Отменен КАК Отменен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Замещен КАК Замещен,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ЗамещенСт КАК ЗамещенСт,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_базы_расчета КАК ID_базы_расчета,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID_замещающего КАК ID_замещающего,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель КАК Исполнитель,
						|	торо_ПланГрафикРемонтаПланРемонтов.Склад КАК Склад,
	               |	ЕСТЬNULL(ТаблицаДоступенДляРедакторования.ДоступенДляРедактирования, ИСТИНА) КАК ДоступенДляРедактирования,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта КАК СуммаРемонта,
						|	торо_ПланГрафикРемонтаПланРемонтов.СрокПоНормативу КАК СрокПоНормативу,
						|	торо_ПланГрафикРемонтаПланРемонтов.СодержитВыходные КАК СодержитВыходные
	               |ИЗ
	               |	торо_ПланГрафикРемонтаПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДоступенДляРедакторования КАК ТаблицаДоступенДляРедакторования
	               |		ПО торо_ПланГрафикРемонтаПланРемонтов.ID = ТаблицаДоступенДляРедакторования.ID";
	
	Запрос.УстановитьПараметр("ПланРемонтов",парамПланРемонтов);
	Запрос.УстановитьПараметр("ТаблицаДоступенДляРедакторования",парамМассивДоступныхДляКорректировкиСтрок);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Функция предназначена для рассчета стоимости ремонтов.
//
// Параметры:
//		ПланРемонтов - ТаблицаЗначений - план ремонтов документа,
//		Ссылка - ДокументССылка - ссылка на документ,
//		МассивДоступныхДляКорректировкиСтрок - Массив	- массив доступных для корректировки строк.
//		ТекущаяСтрока - ДанныеФормыЭлементКоллекции, Структура - текущая строка плана ремонтов.
//
// Возвращаемое значение:
//		ТаблицаЗначений - план ремонтов с рассчитанной стоимостью.
Функция РассчитатьСтоимостиРемонтов(ПланРемонтов, Ссылка, МассивДоступныхДляКорректировкиСтрок, ТекущаяСтрока = Неопределено) Экспорт
	
	ВремТаб = Новый  МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВремТаб;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ПланГрафикРемонтаПланРемонтов.ID КАК ID,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач КАК Период,
	               |	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель КАК Исполнитель,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СписокОбъектовРемонта КАК Объект,
	               |	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения КАК СпособВыполнения
	               |ПОМЕСТИТЬ ТЧПланРемонтов
	               |ИЗ
	               |	&ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МассивДоступныхДляКорректировкиСтрок.ID КАК ID,
	               |	МассивДоступныхДляКорректировкиСтрок.ДоступенДляРедактирования КАК ДоступенДляРедактирования
	               |ПОМЕСТИТЬ МассивДоступныхДляКорректировкиСтрок
	               |ИЗ
	               |	&МассивДоступныхДляКорректировкиСтрок КАК МассивДоступныхДляКорректировкиСтрок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЧПланРемонтов.ID КАК ID,
	               |	ТЧПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	               |	ТЧПланРемонтов.Период КАК Период,
	               |	ТЧПланРемонтов.Исполнитель КАК Исполнитель,
	               |	ТЧПланРемонтов.Объект КАК Объект,
	               |	ТЧПланРемонтов.СпособВыполнения КАК СпособВыполнения
	               |ПОМЕСТИТЬ ТабДанные
	               |ИЗ
	               |	ТЧПланРемонтов КАК ТЧПланРемонтов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МассивДоступныхДляКорректировкиСтрок КАК МассивДоступныхДляКорректировкиСтрок
	               |		ПО ТЧПланРемонтов.ID = МассивДоступныхДляКорректировкиСтрок.ID
	               |			И (МассивДоступныхДляКорректировкиСтрок.ДоступенДляРедактирования)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабДанные.Период КАК ДатаНач,
	               |	КурсыВалют.Период КАК Период,
	               |	КурсыВалют.Кратность КАК Кратность,
	               |	КурсыВалют.Курс КАК Курс,
	               |	КурсыВалют.Валюта КАК Валюта
	               |ПОМЕСТИТЬ ТабДатВалют
	               |ИЗ
	               |	ТабДанные КАК ТабДанные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	               |		ПО (КурсыВалют.Период <= ТабДанные.Период)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабДатВалют.ДатаНач КАК ДатаНач,
	               |	ТабДатВалют.Валюта КАК Валюта,
	               |	МАКСИМУМ(ТабДатВалют.Период) КАК Период
	               |ПОМЕСТИТЬ ТабМаксДатВалют
	               |ИЗ
	               |	ТабДатВалют КАК ТабДатВалют
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабДатВалют.ДатаНач,
	               |	ТабДатВалют.Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ТабДатВалют.ДатаНач КАК ДатаНач,
	               |	ТабДатВалют.Курс КАК Курс,
	               |	ТабДатВалют.Кратность КАК Кратность,
	               |	ТабДатВалют.Валюта КАК Валюта,
	               |	ТабДатВалют.Период КАК Период
	               |ПОМЕСТИТЬ ТабКурсовВалют
	               |ИЗ
	               |	ТабДатВалют КАК ТабДатВалют
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабМаксДатВалют КАК ТабМаксДатВалют
	               |		ПО ТабДатВалют.ДатаНач = ТабМаксДатВалют.ДатаНач
	               |			И ТабДатВалют.Период = ТабМаксДатВалют.Период
	               |			И ТабДатВалют.Валюта = ТабМаксДатВалют.Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаРемРабот.Период КАК ДатаНач,
	               |	Квалификации.Период КАК Период,
	               |	Квалификации.Стоимость КАК Стоимость,
	               |	Квалификации.Валюта КАК Валюта,
	               |	Квалификации.Квалификация КАК Квалификация
	               |ПОМЕСТИТЬ ТабДатКвалификаций
	               |ИЗ
	               |	ТабДанные КАК ТаблицаРемРабот
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СтоимостьЧасаКвалификации КАК Квалификации
	               |		ПО (Квалификации.Период <= ТаблицаРемРабот.Период)
	               |			И (ТаблицаРемРабот.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабДатКвалификаций.ДатаНач КАК ДатаНач,
	               |	ТабДатКвалификаций.Квалификация КАК Квалификация,
	               |	МАКСИМУМ(ТабДатКвалификаций.Период) КАК Период
	               |ПОМЕСТИТЬ ТабМаксДатКвалификаций
	               |ИЗ
	               |	ТабДатКвалификаций КАК ТабДатКвалификаций
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабДатКвалификаций.ДатаНач,
	               |	ТабДатКвалификаций.Квалификация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ТабДатКвалификаций.ДатаНач КАК ДатаНач,
	               |	ТабДатКвалификаций.Валюта КАК Валюта,
	               |	ТабДатКвалификаций.Стоимость КАК Стоимость,
	               |	ТабДатКвалификаций.Квалификация КАК Квалификация
	               |ПОМЕСТИТЬ ТабСтоимостейКвалификаций
	               |ИЗ
	               |	ТабДатКвалификаций КАК ТабДатКвалификаций
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабМаксДатКвалификаций КАК ТабМаксДатКвалификаций
	               |		ПО ТабДатКвалификаций.ДатаНач = ТабМаксДатКвалификаций.ДатаНач
	               |			И ТабДатКвалификаций.Период = ТабМаксДатКвалификаций.Период
	               |			И ТабДатКвалификаций.Квалификация = ТабМаксДатКвалификаций.Квалификация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ТабДанные.Объект КАК ОбъектРемонтныхРабот,
	               |	ТабДанные.ВидРемонтныхРабот КАК ВидРемонта,
	               |	ТабДанные.Период КАК Дата,
	               |	ТабДанные.ID КАК ID,
	               |	ТабДанные.СпособВыполнения КАК СпособВыполнения,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.НормативныйРемонт КАК НормативныйРемонт
	               |ПОМЕСТИТЬ НормативныеРемонты_период
	               |ИЗ
	               |	ТабДанные КАК ТабДанные
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_РегламентныеМероприятияИСпискиОбъектов КАК торо_РегламентныеМероприятияИСпискиОбъектов
	               |		ПО ТабДанные.Объект = торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов
	               |			И ТабДанные.ВидРемонтныхРабот = торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты_период.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты_период.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты_период.Дата КАК Дата,
	               |	НормативныеРемонты_период.ID КАК ID,
	               |	НормативныеРемонты_период.СпособВыполнения КАК СпособВыполнения,
	               |	НормативныеРемонты_период.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	НормативныеРемонты_период.НормативныйРемонт КАК НормативныйРемонт,
	               |	МАКСИМУМ(торо_ВерсииТехКарт.Период) КАК Период
	               |ПОМЕСТИТЬ НормативныеРемонты_макспериод
	               |ИЗ
	               |	НормативныеРемонты_период КАК НормативныеРемонты_период
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
	               |		ПО НормативныеРемонты_период.НормативныйРемонт = торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |			И НормативныеРемонты_период.Дата >= торо_ВерсииТехКарт.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НормативныеРемонты_период.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты_период.ВидРемонта,
	               |	НормативныеРемонты_период.ID,
	               |	НормативныеРемонты_период.Дата,
	               |	НормативныеРемонты_период.СпособВыполнения,
	               |	НормативныеРемонты_период.ГрафикРемонтныхРабот,
	               |	НормативныеРемонты_период.НормативныйРемонт
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныйРемонт,
	               |	Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты_макспериод.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты_макспериод.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты_макспериод.Дата КАК Период,
	               |	НормативныеРемонты_макспериод.ID КАК ID,
	               |	НормативныеРемонты_макспериод.СпособВыполнения КАК СпособВыполнения,
	               |	НормативныеРемонты_макспериод.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	торо_ВерсииТехКарт.ТехКарта КАК Ремонт
	               |ПОМЕСТИТЬ НормативныеРемонты
	               |ИЗ
	               |	НормативныеРемонты_макспериод КАК НормативныеРемонты_макспериод
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
	               |		ПО НормативныеРемонты_макспериод.НормативныйРемонт = торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |			И НормативныеРемонты_макспериод.Период = торо_ВерсииТехКарт.Период
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидРемонта,
	               |	Ремонт,
	               |	СпособВыполнения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Ремонт КАК Ремонт,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты.Период КАК Период,
	               |	НормативныеРемонты.ID КАК ID,
	               |	НормативныеРемонты.СпособВыполнения КАК СпособВыполнения,
	               |	торо_Состав.Состав КАК ТехКарта,
	               |	торо_Состав.Количество КАК Количество
	               |ПОМЕСТИТЬ НормативныеРемонты_техкарты
	               |ИЗ
	               |	НормативныеРемонты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СоставИерархическихТехКартРемонтов КАК торо_Состав
	               |		ПО НормативныеРемонты.Ремонт = торо_Состав.ТехКарта
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Ремонт,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта,
	               |	НормативныеРемонты.Период,
	               |	НормативныеРемонты.ID,
	               |	НормативныеРемонты.СпособВыполнения,
	               |	НормативныеРемонты.Ремонт,
	               |	1
	               |ИЗ
	               |	НормативныеРемонты КАК НормативныеРемонты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныеРемонты.Ремонт
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НормативныеРемонты.Период КАК Период,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура КАК Номенклатура,
				   |	торо_ТехКартыМатериальныеЗатраты.Характеристика КАК Характеристика,
	               |	торо_ТехКартыМатериальныеЗатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	СУММА(торо_ТехКартыМатериальныеЗатраты.Количество) КАК Количество,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта КАК ВидРемонта,
	               |	НормативныеРемонты.ID КАК ID,
	               |	0 КАК Приоритет
	               |ПОМЕСТИТЬ ТабНоменклатуры
	               |ИЗ
	               |	НормативныеРемонты_техкарты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.МатериальныеЗатраты КАК торо_ТехКартыМатериальныеЗатраты
	               |		ПО НормативныеРемонты.ТехКарта = торо_ТехКартыМатериальныеЗатраты.Ссылка
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НормативныеРемонты.Период,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура,
				   |	торо_ТехКартыМатериальныеЗатраты.Характеристика,
	               |	торо_ТехКартыМатериальныеЗатраты.ЕдиницаИзмерения,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта,
	               |	НормативныеРемонты.ID
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Период,
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабНоменклатуры.Номенклатура КАК Номенклатура,
	               |	МИНИМУМ(ТабНоменклатуры.Приоритет) КАК Приоритет
	               |ПОМЕСТИТЬ ТабМинПриоритетов
	               |ИЗ
	               |	ТабНоменклатуры КАК ТабНоменклатуры
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабНоменклатуры.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабНоменклатуры.Период КАК Период,
	               |	ТабНоменклатуры.Номенклатура КАК Номенклатура,
				   |	ТабНоменклатуры.Характеристика КАК Характеристика,
	               |	ТабНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТабНоменклатуры.Количество КАК Количество,
	               |	ТабНоменклатуры.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	ТабНоменклатуры.ВидРемонта КАК ВидРемонта,
	               |	ТабНоменклатуры.ID КАК ID
	               |ПОМЕСТИТЬ НоменклатураКоличество
	               |ИЗ
	               |	ТабНоменклатуры КАК ТабНоменклатуры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабМинПриоритетов КАК ТабМинПриоритетов
	               |		ПО (НЕ ТабНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	               |			И ТабНоменклатуры.Номенклатура = ТабМинПриоритетов.Номенклатура
	               |			И ТабНоменклатуры.Приоритет = ТабМинПриоритетов.Приоритет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НоменклатураКоличество.Период КАК Период,
	               |	НоменклатураКоличество.Номенклатура КАК Номенклатура,
				   |	НоменклатураКоличество.Характеристика КАК Характеристика,
	               |	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК ПериодЦена,
	               |	ЦеныНоменклатуры.ВидЦены КАК ТипЦен
	               |ПОМЕСТИТЬ НоменклатураПериодЦен
	               |ИЗ
	               |	НоменклатураКоличество КАК НоменклатураКоличество
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	               |			ПО ЦеныНоменклатуры.ВидЦены = Константы.торо_ТипЦеныДляРасчетаСебестоимостиРемонта
	               |		ПО НоменклатураКоличество.Номенклатура = ЦеныНоменклатуры.Номенклатура
				   |			И НоменклатураКоличество.Характеристика = ЦеныНоменклатуры.Характеристика
	               |			И НоменклатураКоличество.Период >= ЦеныНоменклатуры.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НоменклатураКоличество.Период,
	               |	НоменклатураКоличество.Номенклатура,
				   |	НоменклатураКоличество.Характеристика,
	               |	ЦеныНоменклатуры.ВидЦены
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ПериодЦена,
	               |	ТипЦен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	НормативныеРемонты.ТехКарта КАК Ссылка,
	               |	торо_ТехКартыСписокОпераций.Операция КАК Операция,
	               |	торо_ТехКартыСписокОпераций.Операция.Расценка КАК ОперацияРасценка,
	               |	торо_ТехКартыСписокОпераций.Операция.Валюта КАК ОперацияВалюта,
	               |	НормативныеРемонты.ID КАК ID,
	               |	НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	               |	НормативныеРемонты.ВидРемонта КАК ВидРемонта,
	               |	торо_ТехКартыСписокОпераций.Количество КАК Количество,
	               |	НормативныеРемонты.Период КАК Период
	               |ПОМЕСТИТЬ ТабОперацийПодрядный
	               |ИЗ
	               |	НормативныеРемонты_техкарты КАК НормативныеРемонты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	               |		ПО НормативныеРемонты.ТехКарта = торо_ТехКартыСписокОпераций.Ссылка
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(НоменклатураКоличество.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) * ЕСТЬNULL(ТабКурсовВалют.Курс, 0)) КАК Стоимость,
	               |	НоменклатураКоличество.ID КАК ID
	               |ПОМЕСТИТЬ ИтоговаяТаб
	               |ИЗ
	               |	НоменклатураКоличество КАК НоменклатураКоличество
	               |		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураПериодЦен КАК НоменклатураПериодЦен
	               |		ПО НоменклатураКоличество.Период = НоменклатураПериодЦен.Период
	               |			И НоменклатураКоличество.Номенклатура = НоменклатураПериодЦен.Номенклатура
				   |			И НоменклатураКоличество.Характеристика = НоменклатураПериодЦен.Характеристика
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	               |		ПО (НоменклатураПериодЦен.Номенклатура = ЦеныНоменклатуры.Номенклатура)
				   |			И (НоменклатураПериодЦен.Характеристика = ЦеныНоменклатуры.Характеристика)
	               |			И (НоменклатураПериодЦен.ПериодЦена = ЦеныНоменклатуры.Период)
	               |			И (НоменклатураПериодЦен.ТипЦен = ЦеныНоменклатуры.ВидЦены)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабКурсовВалют КАК ТабКурсовВалют
	               |		ПО (ЦеныНоменклатуры.Валюта = ТабКурсовВалют.Валюта)
	               |			И НоменклатураКоличество.Период = ТабКурсовВалют.ДатаНач
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НоменклатураКоличество.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СУММА(ЕСТЬNULL(ТабСтоимостейКвалификаций.Стоимость, 0) * ЕСТЬNULL(ТабКурсовВалют.Курс, 0) / ЕСТЬNULL(ТабКурсовВалют.Кратность, 1) * торо_ТехКартыТрудовыеЗатраты.ВремяРаботы / 3600 * торо_ТехКартыТрудовыеЗатраты.Количество * НормативныеРемонты.Количество),
	               |	НормативныеРемонты.ID
	               |ИЗ
	               |	Справочник.торо_ТехКарты.ТрудовыеЗатраты КАК торо_ТехКартыТрудовыеЗатраты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НормативныеРемонты_техкарты КАК НормативныеРемонты
	               |		ПО торо_ТехКартыТрудовыеЗатраты.Ссылка = НормативныеРемонты.ТехКарта
	               |			И (НормативныеРемонты.СпособВыполнения = ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТабСтоимостейКвалификаций КАК ТабСтоимостейКвалификаций
	               |		ПО торо_ТехКартыТрудовыеЗатраты.Квалификация = ТабСтоимостейКвалификаций.Квалификация
	               |			И (НормативныеРемонты.Период = ТабСтоимостейКвалификаций.ДатаНач)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТабКурсовВалют КАК ТабКурсовВалют
	               |		ПО (ТабСтоимостейКвалификаций.Валюта = ТабКурсовВалют.Валюта)
	               |			И (ТабКурсовВалют.ДатаНач = ТабСтоимостейКвалификаций.ДатаНач)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НормативныеРемонты.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СУММА(ТабОперацийПодрядный.ОперацияРасценка * ТабОперацийПодрядный.Количество * ЕСТЬNULL(ТабКурсовВалют.Курс, 0) / ЕСТЬNULL(ТабКурсовВалют.Кратность, 1)),
	               |	ТабОперацийПодрядный.ID
	               |ИЗ
	               |	ТабОперацийПодрядный КАК ТабОперацийПодрядный
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТабКурсовВалют КАК ТабКурсовВалют
	               |		ПО ТабОперацийПодрядный.ОперацияВалюта = ТабКурсовВалют.Валюта
	               |			И ТабОперацийПодрядный.Период = ТабКурсовВалют.ДатаНач
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабОперацийПодрядный.ID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИтоговаяТаб.ID КАК ID,
	               |	СУММА(ИтоговаяТаб.Стоимость) КАК Стоимость
	               |ИЗ
	               |	ИтоговаяТаб КАК ИтоговаяТаб
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИтоговаяТаб.ID";
	
	
	Если ТекущаяСтрока = Неопределено Тогда
		Запрос.УстановитьПараметр("ПланРемонтов",ПланРемонтов);
		Запрос.УстановитьПараметр("МассивДоступныхДляКорректировкиСтрок",МассивДоступныхДляКорректировкиСтрок);
	Иначе
		Запрос.УстановитьПараметр("ПланРемонтов",ПланРемонтов.Скопировать(Новый Структура("ID", ТекущаяСтрока.ID)));
		Запрос.УстановитьПараметр("МассивДоступныхДляКорректировкиСтрок",МассивДоступныхДляКорректировкиСтрок.Скопировать(Новый Структура("ID", ТекущаяСтрока.ID)));
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	СтрокиДоступныеДляРедактирования = МассивДоступныхДляКорректировкиСтрок.НайтиСтроки(Новый Структура("ДоступенДляРедактирования", Истина));
	IDРемонтовДоступныеДляРедактирования = МассивДоступныхДляКорректировкиСтрок.Скопировать(СтрокиДоступныеДляРедактирования, "ID");
	
	// Очистка существующих стоимостей для работ выполняемых подразделениями.
	// Необходимо, если стоимость стала равна 0 и если для подрядчиков уже есть стоимость.
	Если ТекущаяСтрока = Неопределено Тогда
		Для Каждого IDРемонта Из IDРемонтовДоступныеДляРедактирования Цикл
			СтрокаТЧ = ПланРемонтов.Найти(IDРемонта, "ID");
			Если СтрокаТЧ <> Неопределено Тогда
				СтрокаТЧ.СуммаРемонта = 0;
			КонецЕсли;
		КонецЦикла;
	Иначе
		СтрокаТЧ = ПланРемонтов.Найти(ТекущаяСтрока.ID, "ID");
		Если СтрокаТЧ <> Неопределено Тогда
			СтрокаТЧ.СуммаРемонта = 0;
		КонецЕсли;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = ПланРемонтов.Найти(Выборка.ID, "ID");
		Если СтрокаТЧ <> Неопределено тогда
			
			СтрокаТЧ.СуммаРемонта = Выборка.Стоимость;
			
		КонецЕсли;
	КонецЦикла;
	
	ПеревестиСтоимостиВТекущие(ПланРемонтов, Ссылка, IDРемонтовДоступныеДляРедактирования, ТекущаяСтрока);
	
	Возврат ПланРемонтов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПеревестиСтоимостиВТекущие(ПланРемонтов, Ссылка, IDРемонтовДоступныеДляРедактирования, ТекущаяСтрока = Неопределено) 
	
	Если ТекущаяСтрока <> Неопределено Тогда
		
		Если IDРемонтовДоступныеДляРедактирования.Найти(ТекущаяСтрока.ID) = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		РезультатЗапроса = ПолучитьЗначенияКоэффициентов(Ссылка, ТекущаяСтрока.ДатаНач, ТекущаяСтрока.ОбъектРемонтныхРабот, ТекущаяСтрока.ВидРемонтныхРабот);
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Строка = ПланРемонтов.Найти(ТекущаяСтрока.ID, "ID");
		Если Строка <> Неопределено Тогда
			
			ИтоговыйКоэффициент = 1;
			Пока Выборка.Следующий() Цикл
				ИтоговыйКоэффициент = ИтоговыйКоэффициент * Выборка.ПоказательКоэффициента;
			КонецЦикла;
			
			Строка.СуммаРемонта = Строка.СуммаРемонта * ИтоговыйКоэффициент;
		КонецЕсли;
		
	Иначе
				
		РезультатЗапроса = ПолучитьЗначенияКоэффициентовТаб(ПланРемонтов, IDРемонтовДоступныеДляРедактирования, Ссылка.ДатаПланирования);
		
		ВыборкаОР = РезультатЗапроса.выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОР.Следующий() Цикл
			
			ВыборкаВР = ВыборкаОР.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаВР.Следующий() Цикл
				
				Выборка = ВыборкаВР.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				ИтоговыйКоэффициент = 1;
				
				Пока Выборка.Следующий() Цикл
					ИтоговыйКоэффициент = ИтоговыйКоэффициент * Выборка.ПоказательКоэффициента;
				КонецЦикла;
				
				Если ТипЗнч(ВыборкаВР.ОбъектРемонтныхРабот) = Тип("СправочникСсылка.торо_ОбъектыРемонта") Тогда
					СтруктураПоиска = Новый Структура("ВидРемонтныхРабот, ОбъектРемонтныхРабот", ВыборкаВР.ВидРемонтныхРабот, ВыборкаВР.ОбъектРемонтныхРабот);
				Иначе 
					СтруктураПоиска = Новый Структура("ВидРемонтныхРабот, СписокОбъектовРемонта", ВыборкаВР.ВидРемонтныхРабот, ВыборкаВР.ОбъектРемонтныхРабот);
				КонецЕсли;
				
				ПланРемонтовОтбор = ПланРемонтов.НайтиСтроки(СтруктураПоиска);
				
				Для Каждого Строка из ПланРемонтовОтбор Цикл
					
					Если IDРемонтовДоступныеДляРедактирования.Найти(Строка.ID) = Неопределено Тогда
						Продолжить;
					КонецЕсли;       
					
					Строка.СуммаРемонта = Строка.СуммаРемонта * ИтоговыйКоэффициент;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначенияКоэффициентов(Ссылка,ДатаРемонта,МассивОбъектов,МассивВидовРемонта)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Коэффициент,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ПоказательКоэффициента,
	|	ВЫБОР
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов <> ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов <> ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов = ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 2
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов = ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ ТабПараметров
	|ИЗ
	|	РегистрСведений.торо_КоэффициентыПереводаБазовыхЦенВТекущиеРегл.СрезПоследних(
	|			&Дата,
	|			КлассификаторРемонтов В (&КлассификаторРемонтов, ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка))
	|				И СписокОбъектов В (&СписокОбъектов, ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка))) КАК торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТабПараметров.Приоритет) КАК Приоритет,
	|	ТабПараметров.Коэффициент
	|ПОМЕСТИТЬ МинимальныеПриоритеты
	|ИЗ
	|	ТабПараметров КАК ТабПараметров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабПараметров.Коэффициент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабПараметров.Коэффициент,
	|	ЕСТЬNULL(ТабПараметров.ПоказательКоэффициента, 1) КАК ПоказательКоэффициента,
	|	МинимальныеПриоритеты.Приоритет
	|ИЗ
	|	МинимальныеПриоритеты КАК МинимальныеПриоритеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТабПараметров КАК ТабПараметров
	|		ПО МинимальныеПриоритеты.Приоритет = ТабПараметров.Приоритет
	|			И МинимальныеПриоритеты.Коэффициент = ТабПараметров.Коэффициент";
	Запрос.УстановитьПараметр("Дата",Ссылка.ДатаПланирования);	 // Дата
	Запрос.УстановитьПараметр("КлассификаторРемонтов",МассивВидовРемонта);	 // Виды обслуживания основных средств
	Запрос.УстановитьПараметр("СписокОбъектов",МассивОбъектов);	 // Списки объектов регламентного мероприятия
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПолучитьЗначенияКоэффициентовТаб(ПланРемонтов, IDРемонтовДоступныеДляРедактирования, ДатаПланирования)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПланРемонтов.СписокОбъектовРемонта КАК ОбъектРемонтныхРабот,
	|	ПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ПланРемонтов.ID КАК ID
	|ПОМЕСТИТЬ ПланРемонтов
	|ИЗ
	|	&ПланРемонтов КАК ПланРемонтов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланРемонтов.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ПланРемонтов.ВидРемонтныхРабот КАК ВидРемонтныхРабот
	|ПОМЕСТИТЬ ТабДанных
	|ИЗ
	|	ПланРемонтов КАК ПланРемонтов
	|ГДЕ
	|	ПланРемонтов.ID В(&IDРемонтовДоступныеДляРедактирования)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланРемонтов.ОбъектРемонтныхРабот,
	|	ПланРемонтов.ВидРемонтныхРабот
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонтныхРабот,
	|	ВидРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабДанных.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ТабДанных.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.Коэффициент КАК Коэффициент,
	|	торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.ПоказательКоэффициента КАК ПоказательКоэффициента,
	|	ВЫБОР
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов <> ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 0
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов <> ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов <> ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов = ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 2
	|		КОГДА торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|				И торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов = ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ ТабПараметров
	|ИЗ
	|	РегистрСведений.торо_КоэффициентыПереводаБазовыхЦенВТекущиеРегл.СрезПоследних(&Дата, ) КАК торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабДанных КАК ТабДанных
	|		ПО (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.КлассификаторРемонтов В (ТабДанных.ВидРемонтныхРабот, ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)))
	|			И (торо_КоэффициентыПереводаБазовыхЦенВТекущиеСрезПоследних.СписокОбъектов В (ТабДанных.ОбъектРемонтныхРабот, ЗНАЧЕНИЕ(Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПустаяСсылка)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТабПараметров.Приоритет) КАК Приоритет,
	|	ТабПараметров.Коэффициент КАК Коэффициент,
	|	ТабПараметров.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ТабПараметров.ВидРемонтныхРабот КАК ВидРемонтныхРабот
	|ПОМЕСТИТЬ МинимальныеПриоритеты
	|ИЗ
	|	ТабПараметров КАК ТабПараметров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабПараметров.Коэффициент,
	|	ТабПараметров.ОбъектРемонтныхРабот,
	|	ТабПараметров.ВидРемонтныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабПараметров.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ТабПараметров.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ТабПараметров.Коэффициент КАК Коэффициент,
	|	ЕСТЬNULL(ТабПараметров.ПоказательКоэффициента, 1) КАК ПоказательКоэффициента,
	|	МинимальныеПриоритеты.Приоритет КАК Приоритет
	|ИЗ
	|	МинимальныеПриоритеты КАК МинимальныеПриоритеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабПараметров КАК ТабПараметров
	|		ПО МинимальныеПриоритеты.Приоритет = ТабПараметров.Приоритет
	|			И МинимальныеПриоритеты.Коэффициент = ТабПараметров.Коэффициент
	|			И МинимальныеПриоритеты.ОбъектРемонтныхРабот = ТабПараметров.ОбъектРемонтныхРабот
	|			И МинимальныеПриоритеты.ВидРемонтныхРабот = ТабПараметров.ВидРемонтныхРабот
	|ИТОГИ ПО
	|	ОбъектРемонтныхРабот,
	|	ВидРемонтныхРабот";
	
	Запрос.УстановитьПараметр("Дата", ДатаПланирования);
	Запрос.УстановитьПараметр("ПланРемонтов", ПланРемонтов);
	Запрос.УстановитьПараметр("IDРемонтовДоступныеДляРедактирования", IDРемонтовДоступныеДляРедактирования);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
// КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// План-график регламентных мероприятий
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.торо_ГрафикРегламентныхМероприятийТОиР";
	КомандаПечати.Идентификатор = "МакетПланаРемонтовНаПериод";
	КомандаПечати.Представление = НСтр("ru = 'График регламентных мероприятий'");
	КомандаПечати.Обработчик = "торо_Печать.ОбработатьКомандуПечатиППР";
	КомандаПечати.СразуНаПринтер = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
	"НастройкиТОиР",
	"ПечатьДокументовБезПредварительногоПросмотра",
	Ложь);
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ИменаОбъектовМетаданных = РегистрыСведений.торо_ВводНаОсновании.ИменаДоступныхОбъектовМетаданныхДляВводаНаОсновании(
		 Метаданные.Документы.торо_ГрафикРегламентныхМероприятийТОиР.Имя);
		 
	Для Каждого ИмяОбъектаМетаданных Из ИменаОбъектовМетаданных Цикл
		Документы[ИмяОбъектаМетаданных].ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);		 
	КонецЦикла;		 
	
	КомандаКорректировки = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании); 
	
	торо_СозданиеНаОсновании.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "График регламентных мероприятий".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.торо_ГрафикРегламентныхМероприятийТОиР) Тогда
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.торо_ГрафикРегламентныхМероприятийТОиР.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.торо_ГрафикРегламентныхМероприятийТОиР);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "торо_ИспользоватьРегламентныеМероприятия";
        Возврат КомандаСоздатьНаОсновании;
	КонецЕсли; 
	
    Возврат Неопределено;
	
КонецФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПланаРемонтовНаПериод") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"МакетПланаРемонтовНаПериод", 
		"План-график регламентных мероприятий", 
		ПечатьПланГрафикРегламентныхМероприятий(МассивОбъектов, ПараметрыПечати),
		,
		"Документ.торо_ГрафикРегламентныхМероприятийТОиР.ПФ_MXL_МакетПланаРемонтовНаПериод");
	КонецЕсли;
КонецПроцедуры

// Процедура вывода на экран печатной формы документа
// торо_ПланГрафикРемонта.
//  
Функция ПечатьПланГрафикРегламентныхМероприятий(МассивОбъектов, ПараметрыПечати)
	
	ТаблицаПлана = Новый ТабличныйДокумент;		
	МакетТаблицы =  УправлениеПечатью.МакетПечатнойФормы("Документ.торо_ГрафикРегламентныхМероприятийТОиР.ПФ_MXL_МакетПланаРемонтовНаПериод");

	ЭтоПервый = Истина;
	
	Для каждого ПланГрафикРемонта из МассивОбъектов Цикл
		
		Если НЕ ЭтоПервый Тогда
			ТаблицаПлана.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ЭтоПервый = Ложь;
		
		Если ПараметрыПечати.Свойство("Параметры") Тогда
			СтруктураПараметровПечати = ПараметрыПечати.Параметры;
		Иначе
			СтруктураПараметровПечати = торо_ПечатьСервер.СтруктураПараметровППР(ПланГрафикРемонта);
		КонецЕсли;
		
		// ПараметрыПечати.Параметры.ВидРемонта
		ПериодКон=СтруктураПараметровПечати.ДатаКонца;
		ПериодНач=СтруктураПараметровПечати.ДатаНачала;
		ОтборПечатнойФормы=СтруктураПараметровПечати.ОтборФормы;	
		
		ОтборПечатнойФормы[0].Имя="СписокОбъектовРемонта";
		ОтборПечатнойФормы[1].Имя="ВидРемонтныхРабот";	
		
		ИнтервалРазбиения = торо_ПечатьСервер.ПолучитьИнтервалРазбиения(СтруктураПараметровПечати.ИнтервалРазбиения);		
		
		Если (ПланГрафикРемонта = Неопределено) Тогда
			ТекстСообщения = НСтр("ru = 'Не выбран документ ""График регламентных мероприятий"".'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Иначе
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	торо_ГрафикРегламентныхМероприятийТОиРМаршруты.СписокОбъектовРемонта КАК ОбъектРемонтныхРабот,
			|	МИНИМУМ(торо_ГрафикРегламентныхМероприятийТОиРМаршруты.НомерСтроки) КАК НомерСтроки
			|ПОМЕСТИТЬ ОбъектыРемонта
			|ИЗ
			|	Документ.торо_ГрафикРегламентныхМероприятийТОиР.Маршруты КАК торо_ГрафикРегламентныхМероприятийТОиРМаршруты
			|ГДЕ
			|	торо_ГрафикРегламентныхМероприятийТОиРМаршруты.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	торо_ГрафикРегламентныхМероприятийТОиРМаршруты.СписокОбъектовРемонта
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка,
			|	торо_ПланГрафикРемонтаПланРемонтов.НомерСтроки,
			|	торо_ПланГрафикРемонтаПланРемонтов.ID,
			|	торо_ПланГрафикРемонтаПланРемонтов.ВидРемонтныхРабот,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач,
			|	торо_ПланГрафикРемонтаПланРемонтов.Исполнитель,
			|	торо_ПланГрафикРемонтаПланРемонтов.СписокОбъектовРемонта КАК ОбъектРемонтныхРабот,
			|	торо_ПланГрафикРемонтаПланРемонтов.СпособВыполнения,
			|	торо_ПланГрафикРемонтаПланРемонтов.Отменен,
			|	торо_ПланГрафикРемонтаПланРемонтов.Склад,
			|	торо_ПланГрафикРемонтаПланРемонтов.Замещен,
			|	торо_ПланГрафикРемонтаПланРемонтов.ID_базы_расчета,
			|	торо_ПланГрафикРемонтаПланРемонтов.ID_замещающего,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНачСт,
			|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКонСт,
			|	торо_ПланГрафикРемонтаПланРемонтов.ЗамещенСт,
			|	торо_ПланГрафикРемонтаПланРемонтов.СуммаРемонта,
			|	торо_ПланГрафикРемонтаОбъектыРемонта.НомерСтроки КАК НомерОР
			|ИЗ
			|	Документ.торо_ГрафикРегламентныхМероприятийТОиР.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
			|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыРемонта КАК торо_ПланГрафикРемонтаОбъектыРемонта
			|		ПО торо_ПланГрафикРемонтаПланРемонтов.СписокОбъектовРемонта = торо_ПланГрафикРемонтаОбъектыРемонта.ОбъектРемонтныхРабот
			|ГДЕ
			|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка = &Ссылка";
			
			торо_ПечатьСервер.ДобавитьПараметрыОтбора(ОтборПечатнойФормы,Запрос,"торо_ПланГрафикРемонтаПланРемонтов");
			
			Запрос.Текст = Запрос.Текст + 
			" И торо_ПланГрафикРемонтаПланРемонтов.Отменен = ЛОЖЬ 
			|УПОРЯДОЧИТЬ ПО
			|	НомерОР";
			
			Запрос.УстановитьПараметр("Ссылка", ПланГрафикРемонта);
			ПланРемонтов = Запрос.Выполнить().Выгрузить();
			
			Наименование = "";
			
			Область = МакетТаблицы.ПолучитьОбласть("Заголовок");	
			Область.Параметры.НачалоПериода = Формат(ПериодНач, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""");
			Область.Параметры.КонецПериода = Формат(ПериодКон, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""");
			Область.Параметры.Организация = торо_ЗаполнениеДокументов.ПолучитьПредставлениеОрганизацииДляПечати(ПланГрафикРемонта.Организация);
			Область.Параметры.Подразделение = ПланГрафикРемонта.Подразделение;
			Область.Параметры.Номер = ПланГрафикРемонта.Номер;
			Область.Параметры.Название = Наименование;
			Если ПланГрафикРемонта.ВидОперации = Перечисления.торо_ВидыОперацийПланаГрафикаППР.Корректировка Тогда                                               			
				Область.Параметры.Корректировка =  СтрШаблон(НСтр("ru = 'корректировка документа %1'"), ПланГрафикРемонта.ДокументОснование);			
			КонецЕсли;
			
			ТаблицаПлана.Вывести(Область);
			
			ПериодовВОтчете = (НачалоДня(ПериодКон) - НачалоДня(ПериодНач))/(60*60*24);
			
			Область = МакетТаблицы.ПолучитьОбласть("Шапка");
			
			ШиринаТаблицы = Область.ШиринаТаблицы;
			
			ТаблицаПлана.Вывести(Область);
			
			ТемпДата = НачалоДня(ПериодНач);
			
			ПериодКонРасчетный = ПериодКон;
			
			МассивВременныхИнтервалов = Новый Массив;
			
			торо_ПечатьСервер.ЗаполнитьИнтервалы(ПериодНач,ПериодКон,ИнтервалРазбиения,ТаблицаПлана,МакетТаблицы,МассивВременныхИнтервалов,ШиринаТаблицы);			
			
			Область = МакетТаблицы.ПолучитьОбласть("ПравыйКрайЗаглушка");
			ТаблицаПлана.Присоединить(Область);
			
			// Непосредственно вывод данных по объекту
			
			НСТР = 1;
			
			СписокХранения = Новый СписокЗначений;
			
			Для каждого СтрокаТаблицы из ПланРемонтов Цикл
				ОбъектДляДобавления =  СтрокаТаблицы.ОбъектРемонтныхРабот;
				
				Если СписокХранения.НайтиПоЗначению(ОбъектДляДобавления)=Неопределено Тогда
					СписокХранения.Добавить(ОбъектДляДобавления);			
				КонецЕсли;
				
			КонецЦикла;
			
			МассивОбъектов = СписокХранения.ВыгрузитьЗначения();
			Для каждого ОбъектДляОтчета из МассивОбъектов Цикл
				Область = МакетТаблицы.ПолучитьОбласть("СтрокаЗаполнения");
				
				Область.Параметры.НПП = НСТР;
				Область.Параметры.НаименовОбъекта = ОбъектДляОтчета;
				
				Результ = ПоследнийРемонт(ОбъектДляОтчета,ПланГрафикРемонта.ДатаПланирования);
				
				Область.Параметры.Вид = ?( Результ[0].Объект = Неопределено,"не проводились",СокрЛП(торо_ЗаполнениеДокументов.ПолучитьПредоставленияВРДляПечати(Результ[0].Объект)));
				ДатаДляФормата = ?(ЗначениеЗаполнено(Результ[0].Период),Дата(Результ[0].Период),"");
				Область.Параметры.Дата = Формат( ДатаДляФормата, "ДФ = ""дд/ММ/гггг""");
				
				ТаблицаПлана.Вывести(Область);	
				
				линСплошная = новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
				линНикакая = новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
				
				Условие = Новый Структура;
				Условие.Вставить("ОбъектРемонтныхРабот", ОбъектДляОтчета);
				Условие.Вставить("Отменен", Ложь);
				НайденныеСтроки = ПланРемонтов.НайтиСтроки(Условие); 		
				для Сч = 1 по  МассивВременныхИнтервалов.Количество() Цикл
					
					БуфГод = "";
					ТекущийПериодНач = МассивВременныхИнтервалов[Сч-1].НачИнт;
					ТекущийПериодКон = МассивВременныхИнтервалов[Сч-1].КонИнт;
					
					Область = МакетТаблицы.ПолучитьОбласть("ЗначенияДня");
					ОбластьФормат =  Область.Область();
					
					Для Каждого ОбъектСравнения из НайденныеСтроки цикл 
						
						Если ОбъектСравнения.ДатаКон<ОбъектСравнения.ДатаНач Тогда
							КонечнаяДата = КонецДня(ОбъектСравнения.ДатаНач);	
						Иначе
							КонечнаяДата = ОбъектСравнения.ДатаКон;
						КонецЕсли; 
						
						Если  ((ТекущийПериодКон>=ОбъектСравнения.ДатаНач) и (ТекущийПериодНач<=КонечнаяДата)) или
							((ТекущийПериодКон>ОбъектСравнения.ДатаНач) и (ТекущийПериодНач<=КонечнаяДата)) Тогда
							ОбластьФормат.ЦветФона = новый Цвет(255, 251, 240);
							ОбластьФормат.ГраницаСлева =линНикакая;
							ОбластьФормат.ГраницаСправа = линСплошная;
							
							ПредставлениеВидаРемонта = торо_ЗаполнениеДокументов.ПолучитьПредоставленияВРДляПечати(ОбъектСравнения.ВидРемонтныхРабот);
							БуфГод = БуфГод + ПредставлениеВидаРемонта + ", ";
							
							Область.Параметры.заполнение = БуфГод;
							
							ОбластьФормат.Примечание.Текст =  ОбластьФормат.Примечание.Текст + ПредставлениеВидаРемонта 
							+ " (" + Лев(ОбъектСравнения.ДатаНач, 10) + " - " + Лев(ОбъектСравнения.ДатаКон, 10) + ") " + Символы.ПС;
						КонецЕсли;	
						
					КонецЦикла;	
					
					ТаблицаПлана.Присоединить(Область);
					
				КонецЦикла;
				Область = МакетТаблицы.ПолучитьОбласть("ПравыйКрайЗаглушка2");
				ТаблицаПлана.Присоединить(Область);
				
				
				НСТР = НСТР + 1;
				
			КонецЦикла;
			
			ОбластьПодвал = МакетТаблицы.ПолучитьОбласть("Подвал");
			ОбластьПодвал.Область(1,1,1, ШиринаТаблицы).ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2);
			ТаблицаПлана.Вывести(ОбластьПодвал);
			
			Область = МакетТаблицы.ПолучитьОбласть("Пояснение");	
			ТаблицаПлана.Вывести(Область);
			
			
		КонецЕсли;
		
		ТабДок = Новый ТабличныйДокумент;
		ТабДок.КлючПараметровПечати = "торо_ПечатьГрафикаРегламентныхМероприятий_МакетПланаРемонтовНаПериод";
		ТабДок.ИмяПараметровПечати = "торо_ПечатьГрафикаРегламентныхМероприятий_МакетПланаРемонтовНаПериод";
		ТабДок.ВставитьОбласть(ТаблицаПлана.Область());
		
	КонецЦикла;
	
	ТабДок.ТолькоПросмотр = Истина;
	Возврат ТабДок;
	
КонецФункции

// Функция предназначена для вывода таблицы последних ремонтов.
//
// Параметры: 
//      Объект - объект.
//
// Возвращаемое значение:
//		ДляВозврата - таблица значений.
//
Функция ПоследнийРемонт(Объект,ДатаПостроения)
	
	ЗапросКРегистру = новый Запрос;
	
	ЗапросКРегистру.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	                        |	торо_ЗавершенныеМероприятияСрезПоследних.ДатаОкончания КАК Период,
	                        |	торо_ЗавершенныеМероприятияСрезПоследних.ВидМероприятия КАК Объект
	                        |ИЗ
	                        |	РегистрСведений.торо_ЗавершенныеМероприятия.СрезПоследних(, СписокОбъектов = &ОбъектРемонта И ДатаОкончания <= &Дата) КАК торо_ЗавершенныеМероприятияСрезПоследних
	                        |
	                        |УПОРЯДОЧИТЬ ПО
	                        |	Период УБЫВ";
	
	ЗапросКРегистру.УстановитьПараметр("ОбъектРемонта", Объект);
	ЗапросКРегистру.УстановитьПараметр("Дата", ДатаПостроения);
	результат = ЗапросКРегистру.Выполнить();
	
	дляВозврата =  результат.Выгрузить();
	
	Если дляВозврата.Количество()=0 Тогда
		дляВозврата.Добавить();
	КонецЕсли;
	
	Возврат дляВозврата;
	
КонецФункции // ПоследнийРемонт()

Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Настройки.ПриПолученииСлужебныхРеквизитов = Истина;
	
КонецПроцедуры

Процедура ПриПолученииСлужебныхРеквизитов(Реквизиты) Экспорт
	
	Реквизиты.Добавить("СтатусДокумента");
	Реквизиты.Добавить("ИсторияСтатусов.*");
	Реквизиты.Добавить("Согласующие.*");
		
КонецПроцедуры

Процедура ЗаполнитьМаршрутыИОбъектыМаршрутов(Маршруты, Основание) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_СоответствиеКорректировокДокументамППР.Корректировка КАК Док
	               |ПОМЕСТИТЬ СписокДокументов
	               |ИЗ
	               |	РегистрСведений.торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР
	               |ГДЕ
	               |	торо_СоответствиеКорректировокДокументамППР.Корректируемый = &Основание
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Основание
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Док
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	торо_ПлановыеМероприятияСрезПоследних.ID КАК ИД,
	               |	торо_ПлановыеМероприятияСрезПоследних.Регистратор КАК Регистратор,
	               |	торо_ПлановыеМероприятияСрезПоследних.СписокОбъектов КАК СписокОбъектов,
	               |	торо_ПлановыеМероприятияСрезПоследних.ВидМероприятия КАК ВидМероприятия,
	               |	торо_ПлановыеМероприятияСрезПоследних.Исполнитель КАК Исполнитель,
	               |	торо_ПлановыеМероприятияСрезПоследних.ДатаНачала КАК ДатаНачала,
	               |	торо_ПлановыеМероприятияСрезПоследних.ДатаОкончания КАК ДатаОкончания,
	               |	торо_ПлановыеМероприятияСрезПоследних.СпособВыполнения КАК СпособВыполнения,
	               |	торо_ПлановыеМероприятияСрезПоследних.СтоимостьРемонта КАК СтоимостьРемонта
	               |ПОМЕСТИТЬ ИсхДанные
	               |ИЗ
	               |	РегистрСведений.торо_ПлановыеМероприятия.СрезПоследних(
	               |			,
	               |			Регистратор В
	               |					(ВЫБРАТЬ
	               |						таб.Док
	               |					ИЗ
	               |						СписокДокументов КАК таб)) КАК торо_ПлановыеМероприятияСрезПоследних
	               |ГДЕ
	               |	торо_ПлановыеМероприятияСрезПоследних.Отменен = ЛОЖЬ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИсхДанные.СписокОбъектов КАК СписокОбъектовРемонта,
	               |	ИсхДанные.ВидМероприятия КАК ВидМероприятия
	               |ИЗ
	               |	ИсхДанные КАК ИсхДанные";
				   
	Запрос.УстановитьПараметр("Основание", Основание);
	Результат = Запрос.Выполнить();
	Маршруты.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьПланРемонтов(ПланРемонтов, Основание, ДатаНачалаПланирования, ДатаОкончанияПланирования, Отборы=Неопределено, СсылкаТекДок = Неопределено) Экспорт
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если Отборы = Неопределено Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЛОЖЬ КАК СписокОбъектов,
		               |	ЛОЖЬ КАК ВидМероприятия
		               |ПОМЕСТИТЬ ВТОтборы";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		               |	Таб.СписокОбъектовРемонта КАК СписокОбъектов,
		               |	Таб.ВидМероприятия КАК ВидМероприятия
		               |ПОМЕСТИТЬ ВТОтборы
		               |ИЗ
		               |	&ТзОтборы КАК Таб
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	СписокОбъектов,
		               |	ВидМероприятия";
		Запрос.УстановитьПараметр("ТзОтборы", Отборы);
	КонецЕсли;
	
	Запрос.Выполнить();

	Запрос.Текст = "ВЫБРАТЬ
	|	торо_СоответствиеКорректировокДокументамППР.Корректировка КАК Док
	|ПОМЕСТИТЬ СписокДокументов
	|ИЗ
	|	РегистрСведений.торо_СоответствиеКорректировокДокументамППР КАК торо_СоответствиеКорректировокДокументамППР
	|ГДЕ
	|	торо_СоответствиеКорректировокДокументамППР.Корректируемый = &Основание
	|	И торо_СоответствиеКорректировокДокументамППР.Корректировка <> &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&Основание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Док
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.ID КАК ID
	|ПОМЕСТИТЬ ВТ_IDремонтов
	|ИЗ
	|	Документ.торо_ГрафикРегламентныхМероприятийТОиР.ПланРемонтов КАК торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокДокументов КАК СписокДокументов
	|		ПО торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.Ссылка = СписокДокументов.Док
	|			И (&Отбор)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_IDремонтов.ID КАК ID,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачала,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ПлановыеДаты
	|ИЗ
	|	ВТ_IDремонтов КАК ВТ_IDремонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_IDремонтов.ID = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ПлановыеМероприятияСрезПоследних.ID КАК ИД,
	|	торо_ПлановыеМероприятияСрезПоследних.Регистратор КАК Регистратор,
	|	торо_ПлановыеМероприятияСрезПоследних.СписокОбъектов КАК СписокОбъектов,
	|	торо_ПлановыеМероприятияСрезПоследних.ВидМероприятия КАК ВидМероприятия,
	|	торо_ПлановыеМероприятияСрезПоследних.Исполнитель КАК Исполнитель,
	|	торо_ПлановыеМероприятияСрезПоследних.ДатаНачала КАК ДатаНачала,
	|	торо_ПлановыеМероприятияСрезПоследних.ДатаОкончания КАК ДатаОкончания,
	|	торо_ПлановыеМероприятияСрезПоследних.СпособВыполнения КАК СпособВыполнения,
	|	торо_ПлановыеМероприятияСрезПоследних.СтоимостьРемонта КАК СтоимостьРемонта,
	|	торо_ПлановыеМероприятияСрезПоследних.СрокПоНормативу КАК СрокПоНормативу
	|ПОМЕСТИТЬ ИсхДанные
	|ИЗ
	|	РегистрСведений.торо_ПлановыеМероприятия.СрезПоследних(
	|			,
	|			ID В
	|					(ВЫБРАТЬ
	|						ВТ_IDремонтов.ID
	|					ИЗ
	|						ВТ_IDремонтов)
	|				И Регистратор <> &Ссылка) КАК торо_ПлановыеМероприятияСрезПоследних
	|ГДЕ
	|	торо_ПлановыеМероприятияСрезПоследних.Отменен = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсхДанные.ИД КАК ID,
	|	ИсхДанные.СписокОбъектов КАК СписокОбъектовРемонта,
	|	ИсхДанные.ВидМероприятия КАК ВидРемонтныхРабот,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, ЕСТЬNULL(ВТ_ПлановыеДаты.ДатаНачала, ИсхДанные.ДатаНачала)) КАК ДатаНач,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаОкончания, ЕСТЬNULL(ВТ_ПлановыеДаты.ДатаОкончания, ИсхДанные.ДатаОкончания)) КАК ДатаКон,
	|	ИсхДанные.Регистратор КАК ДокументИсточник,
	|	ИсхДанные.Исполнитель КАК Исполнитель,
	|	ИсхДанные.СпособВыполнения КАК СпособВыполнения,
	|	ЛОЖЬ КАК Отменен,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЛОЖЬ КАК Замещен,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, ЕСТЬNULL(ВТ_ПлановыеДаты.ДатаНачала, ИсхДанные.ДатаНачала)) КАК ДатаНачСт,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаОкончания, ЕСТЬNULL(ВТ_ПлановыеДаты.ДатаОкончания, ИсхДанные.ДатаОкончания)) КАК ДатаКонСт,
	|	ЛОЖЬ КАК ЗамещенСт,
	|	ИсхДанные.СтоимостьРемонта КАК СуммаРемонта,
	|	ИсхДанные.СрокПоНормативу КАК СрокПоНормативу
	|ПОМЕСТИТЬ Финальная
	|ИЗ
	|	ИсхДанные КАК ИсхДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СвернутыеФактическиеДатыРемонтов КАК торо_СвернутыеФактическиеДатыРемонтов
	|		ПО ИсхДанные.ИД = торо_СвернутыеФактическиеДатыРемонтов.IDРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПлановыеДаты КАК ВТ_ПлановыеДаты
	|		ПО ИсхДанные.ИД = ВТ_ПлановыеДаты.ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Финальная.ID КАК ID,
	|	Финальная.СписокОбъектовРемонта КАК СписокОбъектовРемонта,
	|	Финальная.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	Финальная.ДатаНач КАК ДатаНач,
	|	Финальная.ДатаКон КАК ДатаКон,
	|	Финальная.ДокументИсточник КАК ДокументИсточник,
	|	Финальная.Исполнитель КАК Исполнитель,
	|	Финальная.СпособВыполнения КАК СпособВыполнения,
	|	Финальная.Отменен КАК Отменен,
	|	Финальная.Склад КАК Склад,
	|	Финальная.Замещен КАК Замещен,
	|	Финальная.ДатаНачСт КАК ДатаНачСт,
	|	Финальная.ДатаКонСт КАК ДатаКонСт,
	|	Финальная.ЗамещенСт КАК ЗамещенСт,
	|	Финальная.СуммаРемонта КАК СуммаРемонта,
	|	Финальная.СрокПоНормативу КАК СрокПоНормативу
	|ИЗ
	|	Финальная КАК Финальная
	|ГДЕ
	|	Финальная.ДатаНач МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНач";	
				   
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("Ссылка", СсылкаТекДок);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачалаПланирования);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончанияПланирования);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И (&Отбор)",?(Отборы<>Неопределено,"ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборы КАК ВТОтборы ПО торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.СписокОбъектовРемонта = ВТОтборы.СписокОбъектов И торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.ВидРемонтныхРабот = ВТОтборы.ВидМероприятия",""));
	
	резЗапроса = Запрос.Выполнить();
	Если резЗапроса.Пустой() Тогда
		ПланРемонтов.Очистить();
	Иначе
		Выборка = резЗапроса.Выгрузить();
		ПланРемонтов.Загрузить(Выборка);
	КонецЕсли;
	
КонецПроцедуры 

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли

