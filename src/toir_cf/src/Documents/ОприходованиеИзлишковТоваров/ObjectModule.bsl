#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.торо_Инвентаризация") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Дата = ТекущаяДата();
		Для Каждого СтрокаИнвентаризации Из ДанныеЗаполнения.Товары Цикл
			Отклонение = СтрокаИнвентаризации.Количество - СтрокаИнвентаризации.КоличествоУчет;
			Если Отклонение > 0 Тогда 
				НоваяСтрокаДокумента = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаДокумента, СтрокаИнвентаризации);
				НоваяСтрокаДокумента.Количество = Отклонение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	// ТОиР
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("МассивСтрокНоменклатуры") 
		Тогда
		
		Для каждого Строка Из ДанныеЗаполнения.МассивСтрокНоменклатуры Цикл
			НС = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НС,Строка);
		КонецЦикла; 
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ОсновнойСклад");
	КонецЕсли;

	торо_ЗаполнениеДокументов.ЗаполнитьСтандартныеРеквизитыШапкиПоУмолчанию(ЭтотОбъект);
	// ТОиР

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ФОИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
	Если ФОИспользоватьХарактеристикиНоменклатуры = Истина тогда
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ);
	КонецЕсли;
	
	ФОИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Серия");
	Если ФОИспользоватьСерииНоменклатуры = Истина тогда
		НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
												Отказ,
												МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ФОИспользоватьСерии = Константы.ИспользоватьСерииНоменклатуры.Получить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОприходованиеИзлишковТоваров.Номенклатура КАК Номенклатура,
	               |	ОприходованиеИзлишковТоваров.Характеристика КАК Характеристика,
	               |	СУММА(ОприходованиеИзлишковТоваров.Количество) КАК Количество,
	               |	ОприходованиеИзлишковТоваров.Серия КАК Серия,
	               |	ОприходованиеИзлишковТоваров.Номенклатура.ВидНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры
	               |ИЗ
	               |	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваров
	               |ГДЕ
	               |	ОприходованиеИзлишковТоваров.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОприходованиеИзлишковТоваров.Номенклатура,
	               |	ОприходованиеИзлишковТоваров.Характеристика,
	               |	ОприходованиеИзлишковТоваров.Серия";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ТЧТовары = Запрос.Выполнить().Выгрузить();

	// регистр ТоварыНаСкладах Приход
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из ТЧТовары Цикл
		Если ТекСтрокаТовары.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар 
			Или ТекСтрокаТовары.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара Тогда
			
			Движение = Движения.ТоварыНаСкладах.Добавить();
			Движение.ВидДвижения    = ВидДвиженияНакопления.Приход;
			Движение.Период         = Дата;
			Движение.Номенклатура   = ТекСтрокаТовары.Номенклатура;
			Движение.Характеристика = ТекСтрокаТовары.Характеристика;
			Движение.Склад          = Склад;
			Движение.ВНаличии       = ТекСтрокаТовары.Количество;
		
			Если ФОИспользоватьСерии Тогда
				Движение.Серия      = ТекСтрокаТовары.Серия;
			КонецЕсли; 	
			
		КонецЕсли;	
	КонецЦикла;
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ЭтотОбъект.Движения.ТоварыНаСкладах.Записывать = Истина;
	ЭтотОбъект.Движения.Записать();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда
		 торо_Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли