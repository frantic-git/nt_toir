#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Исполнители") Тогда
	
		Для каждого Исполнитель Из Параметры.Исполнители Цикл
		
			НоваяСтрока = Исполнители.Добавить();
			НоваяСтрока.Исполнитель = Исполнитель.Значение;
		
		КонецЦикла; 
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсполнители

&НаКлиенте
Процедура ИсполнителиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Исполнитель = Элемент.ТекущиеДанные.Исполнитель;
	Если Исполнитель = Неопределено ИЛИ Исполнитель = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка") ИЛИ Исполнитель = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка") Тогда
		ТекстСообщения = НСтр("ru = 'Поле исполнитель не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Исполнитель = Элементы.Исполнители.ТекущиеДанные.Исполнитель;
	МассивТипов = Новый Массив;
		
	Если Исполнитель = Неопределено Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		МассивТипов.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	Иначе
		
		Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Контрагенты") Тогда		
			МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		Иначе
			МассивТипов.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
		КонецЕсли; 
		
	КонецЕсли;
	
	Элементы.ИсполнителиИсполнитель.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);			
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиИсполнительОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.Исполнители.ТекущиеДанные.Исполнитель = Неопределено;
	
КонецПроцедуры
	
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	Закрыть(Исполнители);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти