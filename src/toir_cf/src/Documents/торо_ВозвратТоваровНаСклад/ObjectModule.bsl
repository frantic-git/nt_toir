#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ИзРабочегоМестаПередачаВозврат") И ДанныеЗаполнения.ИзРабочегоМестаПередачаВозврат Тогда
		Таблица = ПолучитьИзВременногоХранилища(ДанныеЗаполнения.АдресХранилища);
		Дата = ТекущаяДата();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Для Каждого Строка Из Таблица Цикл
			НоваяСтрокаТЧ = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, Строка);
			НоваяСтрокаТЧ.Количество = Строка.КПеремещению;
			НоваяСтрокаТЧ.КоличествоУпаковок = Строка.КПеремещению;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ОсновнойСклад");
	КонецЕсли;
	
	торо_ЗаполнениеДокументов.ЗаполнитьСтандартныеРеквизитыШапкиПоУмолчанию(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ФОИспользоватьСерии = Константы.ИспользоватьСерииНоменклатуры.Получить();
	ФОИспользоватьХарактеристики = Константы.торо_ИспользоватьХарактеристикиНоменклатуры.Получить();
	ИспользоватьКонтрольОстатков = Константы.торо_ИспользоватьКонтрольОтрицательныхОстатков.Получить();
	
	Блокировка = Новый БлокировкаДанных;

	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.торо_ТоварыНаРуках");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.УстановитьЗначение("ЗаказНаВнутреннееПотребление", ЗаказНаВнутреннееПотребление);
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия", "Серия");

	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.торо_РезервыНаРуках");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.УстановитьЗначение("ЗаказНаВнутреннееПотребление", ЗаказНаВнутреннееПотребление);
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия", "Серия");	

	Блокировка.Заблокировать();
	
	Движения.торо_РезервыНаРуках.Записывать = Истина;
	Движения.торо_РезервыНаСкладах.Записывать = Истина;
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.торо_ТоварыНаРуках.Записывать = Истина; 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	               |	&Характеристика КАК Характеристика,
	               |	СУММА(ТабличнаяЧасть.Количество) КАК Количество,
	               |	&Серия КАК Серия,
	               |	ТабличнаяЧасть.Склад КАК Склад,
	               |	&ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление
	               |ПОМЕСТИТЬ ТабТовары
	               |ИЗ
	               |	Документ.торо_ВозвратТоваровНаСклад.Товары КАК ТабличнаяЧасть
	               |ГДЕ
	               |	ТабличнаяЧасть.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабличнаяЧасть.Серия,
	               |	ТабличнаяЧасть.Номенклатура,
	               |	ТабличнаяЧасть.Характеристика,
	               |	ТабличнаяЧасть.Склад
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Склад,
	               |	Номенклатура,
	               |	Характеристика,
	               |	Серия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ТоварыНаРукахОстатки.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
	               |	торо_ТоварыНаРукахОстатки.Номенклатура КАК Номенклатура,
	               |	торо_ТоварыНаРукахОстатки.Характеристика КАК Характеристика,
	               |	торо_ТоварыНаРукахОстатки.Склад КАК Склад,
	               |	торо_ТоварыНаРукахОстатки.Серия КАК Серия,
	               |	торо_ТоварыНаРукахОстатки.КоличествоОстаток КАК КоличествоОстаток
	               |ПОМЕСТИТЬ ТоварыНаРуках
	               |ИЗ
	               |	РегистрНакопления.торо_ТоварыНаРуках.Остатки(
	               |			&Дата,
	               |			ЗаказНаВнутреннееПотребление = &ЗаказНаВнутреннееПотребление
	               |				И (Номенклатура, Характеристика, Серия, Склад) В
	               |					(ВЫБРАТЬ
	               |						Таблица.Номенклатура,
	               |						Таблица.Характеристика,
	               |						Таблица.Серия,
	               |						Таблица.Склад
	               |					ИЗ
	               |						ТабТовары КАК Таблица)) КАК торо_ТоварыНаРукахОстатки
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	Характеристика,
	               |	Серия,
	               |	Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабТовары.Номенклатура КАК Номенклатура,
	               |	ТабТовары.Характеристика КАК Характеристика,
	               |	ТабТовары.Склад КАК Склад,
	               |	ТабТовары.Серия КАК Серия,
	               |	СУММА(ЕСТЬNULL(ТоварыНаРукахОстатки.КоличествоОстаток, 0) - ТабТовары.Количество) КАК ОтрОстаток,
	               |	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	               |ИЗ
	               |	ТабТовары КАК ТабТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыНаРуках КАК ТоварыНаРукахОстатки
	               |		ПО ТабТовары.Номенклатура = ТоварыНаРукахОстатки.Номенклатура
	               |			И ТабТовары.Характеристика = ТоварыНаРукахОстатки.Характеристика
	               |			И ТабТовары.Серия = ТоварыНаРукахОстатки.Серия
	               |			И ТабТовары.Склад = ТоварыНаРукахОстатки.Склад
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	               |		ПО ТабТовары.Номенклатура = СпрНоменклатура.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТабТовары.Номенклатура,
	               |	ТабТовары.Характеристика,
	               |	ТабТовары.Склад,
	               |	ТабТовары.Серия,
	               |	СпрНоменклатура.ЕдиницаИзмерения
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ЕСТЬNULL(ТоварыНаРукахОстатки.КоличествоОстаток, 0) - ТабТовары.Количество) < 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ЗаказНаВнутреннееПотребление", ЗаказНаВнутреннееПотребление);
	Если ФОИспользоватьСерии Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Серия", "ТабличнаяЧасть.Серия");
	Иначе
		Запрос.УстановитьПараметр("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка());
	КонецЕсли;
	Если ФОИспользоватьХарактеристики Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Характеристика", "ТабличнаяЧасть.Характеристика");
	Иначе
		Запрос.УстановитьПараметр("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	КонецЕсли;

	РезЗапроса = Запрос.Выполнить();
	Если НЕ РезЗапроса.Пустой() Тогда 
		Если ИспользоватьКонтрольОстатков Тогда
			ПолеДляВыводаОшибки = "Склад";
		Иначе
			ПолеДляВыводаОшибки = "";
		КонецЕсли; 
		
		ВыборкаОтрОстатков = РезЗапроса.Выбрать();
		
		Пока ВыборкаОтрОстатков.Следующий() Цикл
			
			ШаблонСообщения = "Номенклатура %Номенклатура% / %Характеристика% / %Серия%.
			|Превышено количество номенклатуры на руках на %Количество% %ЕдИзм%";
			
			ШаблонСообщения = СтрЗаменить(ШаблонСообщения, "%Номенклатура%", ВыборкаОтрОстатков.Номенклатура);
			ШаблонСообщения = СтрЗаменить(ШаблонСообщения, "%Количество%", - Число(ВыборкаОтрОстатков.ОтрОстаток));
			ШаблонСообщения = СтрЗаменить(ШаблонСообщения, "%ЕдИзм%", ВыборкаОтрОстатков.ЕдиницаИзмерения);
			
			Если ФОИспользоватьСерии Тогда 				
				ШаблонСообщения = СтрЗаменить(ШаблонСообщения, "%Серия%", ВыборкаОтрОстатков.Серия);
			Иначе 
				ШаблонСообщения = СтрЗаменить(ШаблонСообщения, " / %Серия%", "");
			КонецЕсли;
			
			Если ФОИспользоватьХарактеристики Тогда 				
				ШаблонСообщения = СтрЗаменить(ШаблонСообщения,"%Характеристика%", ВыборкаОтрОстатков.Характеристика);
			Иначе 
				ШаблонСообщения = СтрЗаменить(ШаблонСообщения, " / %Характеристика%", "");
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(ШаблонСообщения, ЭтотОбъект, ПолеДляВыводаОшибки, , Отказ);
			
		КонецЦикла;
		
		Если Отказ И ИспользоватьКонтрольОстатков Тогда 
			Возврат;
		ИначеЕсли НЕ ИспользоватьКонтрольОстатков Тогда  
			Отказ = Ложь;
		КонецЕсли;
	КонецЕсли;

	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЧТовары.Номенклатура КАК Номенклатура,
				   |	ТЧТовары.Характеристика КАК Характеристика,
				   |	ТЧТовары.Серия КАК Серия,
				   |	ТЧТовары.Склад КАК Склад,
				   |	ТЧТовары.Количество КАК Количество
	               |ИЗ
	               |	ТабТовары КАК ТЧТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_РезервыНаРукахОстатки.Номенклатура КАК Номенклатура,
	               |	торо_РезервыНаРукахОстатки.Характеристика КАК Характеристика,
	               |	торо_РезервыНаРукахОстатки.Склад КАК Склад,
	               |	торо_РезервыНаРукахОстатки.Серия КАК Серия,
	               |	торо_РезервыНаРукахОстатки.ДокументРезерв КАК ДокументРезерв,
	               |	торо_РезервыНаРукахОстатки.КоличествоОстаток КАК Количество,
	               |	торо_РезервыНаРукахОстатки.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление
	               |ИЗ
	               |	РегистрНакопления.торо_РезервыНаРуках.Остатки(
	               |			&Дата,
	               |			ЗаказНаВнутреннееПотребление = &ЗаказНаВнутреннееПотребление
	               |				И (Номенклатура, Характеристика, Серия, Склад) В
	               |					(ВЫБРАТЬ
	               |						Таблица.Номенклатура,
	               |						Таблица.Характеристика,
	               |						Таблица.Серия,
	               |						Таблица.Склад
	               |					ИЗ
	               |						ТабТовары КАК Таблица)) КАК торо_РезервыНаРукахОстатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Количество УБЫВ";
	РезЗапроса = Запрос.ВыполнитьПакет();
	ТЧТовары = РезЗапроса[0].Выгрузить();
	Если НЕ РезЗапроса[1].Пустой() Тогда 
		Резервы = РезЗапроса[1].Выгрузить();
		Резервы.Индексы.Добавить("Склад");
		Резервы.Индексы.Добавить("Номенклатура");
		Резервы.Индексы.Добавить("Характеристика");
		Резервы.Индексы.Добавить("Серия");
	Иначе
		Резервы = Новый ТаблицаЗначений;
	КонецЕсли;
	
	СтрПоиска = Новый Структура("Номенклатура, Характеристика, Склад, Серия");
	Для Каждого ТекСтрокаТовары Из ТЧТовары Цикл
		ДвижениеРасход = Движения.торо_ТоварыНаРуках.Добавить();
		ДвижениеПриход = Движения.ТоварыНаСкладах.Добавить();
		
		ДвижениеРасход.Период = Дата;
		ДвижениеРасход.Номенклатура = ТекСтрокаТовары.Номенклатура;
		ДвижениеРасход.Склад = ТекСтрокаТовары.Склад;
		ДвижениеРасход.Количество = ТекСтрокаТовары.Количество;
		Если ФОИспользоватьСерии Тогда
			ДвижениеРасход.Серия = ТекСтрокаТовары.Серия;
		КонецЕсли; 	
		Если ФОИспользоватьХарактеристики Тогда
			ДвижениеРасход.Характеристика = ТекСтрокаТовары.Характеристика;
		КонецЕсли; 
		
		ЗаполнитьЗначенияСвойств(ДвижениеПриход, ДвижениеРасход);
		
		ДвижениеРасход.ЗаказНаВнутреннееПотребление = ЗаказНаВнутреннееПотребление;
		ДвижениеПриход.ВНаличии = ТекСтрокаТовары.Количество;
		
		ДвижениеРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
		
		Если Резервы.Количество() Тогда
			ЗаполнитьЗначенияСвойств(СтрПоиска, ТекСтрокаТовары);
			НайденныеРезервы = Резервы.НайтиСтроки(СтрПоиска);
			Количество = ТекСтрокаТовары.Количество;
			Для Каждого Резерв Из НайденныеРезервы Цикл
				Если Количество = 0 Тогда
					Прервать;
				КонецЕсли;
				
				ДоступныйКПереносуРезерв = Мин(Количество, Резерв.Количество);
				
				ДвижениеПриход = Движения.торо_РезервыНаСкладах.Добавить();
				ДвижениеРасход = Движения.торо_РезервыНаРуках.Добавить();
				
				ЗаполнитьЗначенияСвойств(ДвижениеРасход, Резерв);				
				ДвижениеРасход.Период = Дата;
				ДвижениеРасход.Количество = ДоступныйКПереносуРезерв;
				ЗаполнитьЗначенияСвойств(ДвижениеПриход, ДвижениеРасход);
				Количество = Количество - ДоступныйКПереносуРезерв;
				
				ДвижениеРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
				ДвижениеПриход.ВидДвижения = ВидДвиженияНакопления.Приход;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ФОИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
	Если ФОИспользоватьХарактеристикиНоменклатуры Тогда
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
	
	ФОИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Серия");
	Если ФОИспользоватьСерииНоменклатуры = Истина тогда
		НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, Отказ, МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда
		Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли