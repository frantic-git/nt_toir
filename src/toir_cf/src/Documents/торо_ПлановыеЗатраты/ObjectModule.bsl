#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_ПлановыеРемонтныеРаботы.ID КАК РемонтыОборудования_ID,
		|	торо_ПлановыеРемонтныеРаботы.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	торо_ПлановыеРемонтныеРаботы.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот
		|ПОМЕСТИТЬ ВТ_Ремонты
		|ИЗ
		|	РегистрСведений.торо_ПлановыеРемонтныеРаботы КАК торо_ПлановыеРемонтныеРаботы
		|ГДЕ
		|	торо_ПлановыеРемонтныеРаботы.ID = &ID
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_ПлановыеМероприятияСрезПоследних.СписокОбъектов КАК СписокОбъектов,
		|	торо_ПлановыеМероприятияСрезПоследних.ВидМероприятия КАК ВидМероприятия,
		|	торо_ПлановыеМероприятияСрезПоследних.ID КАК ID
		|ПОМЕСТИТЬ ВТ_ПлановыеМероприятия
		|ИЗ
		|	РегистрСведений.торо_ПлановыеМероприятия.СрезПоследних КАК торо_ПлановыеМероприятияСрезПоследних
		|ГДЕ
		|	торо_ПлановыеМероприятияСрезПоследних.ID = &ID
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Ремонты.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	ВТ_Ремонты.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	ВТ_Ремонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	торо_НормативныеРемонтыОборудования.НормативныйРемонт КАК НормативныйРемонт
		|ПОМЕСТИТЬ ВТ_НормативныеРемонты
		|ИЗ
		|	ВТ_Ремонты КАК ВТ_Ремонты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
		|		ПО ВТ_Ремонты.ОбъектРемонтныхРабот = торо_НормативныеРемонтыОборудования.ОбъектРемонта
		|			И ВТ_Ремонты.ВидРемонтныхРабот = торо_НормативныеРемонтыОборудования.ВидРемонта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ПлановыеМероприятия.ID,
		|	ВТ_ПлановыеМероприятия.ВидМероприятия,
		|	ВТ_ПлановыеМероприятия.СписокОбъектов,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.НормативныйРемонт
		|ИЗ
		|	ВТ_ПлановыеМероприятия КАК ВТ_ПлановыеМероприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_РегламентныеМероприятияИСпискиОбъектов КАК торо_РегламентныеМероприятияИСпискиОбъектов
		|		ПО (торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов = ВТ_ПлановыеМероприятия.СписокОбъектов)
		|			И (торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия = ВТ_ПлановыеМероприятия.ВидМероприятия)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НормативныеРемонты.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	ВТ_НормативныеРемонты.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	ВТ_НормативныеРемонты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	торо_ВерсииТехКартСрезПоследних.ТехКарта КАК ТехКарта,
		|	торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты КАК ИдентификаторТехКарты
		|ПОМЕСТИТЬ ВТ_ВерсияТК
		|ИЗ
		|	ВТ_НормативныеРемонты КАК ВТ_НормативныеРемонты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(&Дата, ) КАК торо_ВерсииТехКартСрезПоследних
		|		ПО ВТ_НормативныеРемонты.НормативныйРемонт = торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_ПлановыеЗатратыТехКарты.ТехКарта КАК ТехКарта,
		|	торо_ПлановыеЗатратыТехКарты.ТехКарта_ID КАК ТехКарта_ID
		|ПОМЕСТИТЬ ВТ_ТехКартыПлановыхЗатрат
		|ИЗ
		|	Документ.торо_ПлановыеЗатраты.ТехКарты КАК торо_ПлановыеЗатратыТехКарты
		|ГДЕ
		|	торо_ПлановыеЗатратыТехКарты.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТехКартыПлановыхЗатрат.ТехКарта КАК ТехКарта,
		|	СУММА(1) КАК КоличествоОдинаковых
		|ПОМЕСТИТЬ ВТ_КоличествоОдинаковыхТК
		|ИЗ
		|	ВТ_ТехКартыПлановыхЗатрат КАК ВТ_ТехКартыПлановыхЗатрат
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТехКартыПлановыхЗатрат.ТехКарта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.ID КАК ID,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.РемонтнаяРабота КАК РемонтнаяРабота,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Родитель_ID КАК Родитель_ID
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот.РемонтныеРаботы КАК торо_АктОВыполненииЭтапаРаботРемонтныеРаботы
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.РемонтыОборудования_ID = &ID
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	торо_АктОВыполненииРегламентногоМероприятияРемонтныеРаботы.ID,
		|	торо_АктОВыполненииРегламентногоМероприятияРемонтныеРаботы.РемонтнаяРабота,
		|	торо_АктОВыполненииРегламентногоМероприятияРемонтныеРаботы.Родитель_ID
		|ИЗ
		|	Документ.торо_АктОВыполненииРегламентногоМероприятия.РемонтныеРаботы КАК торо_АктОВыполненииРегламентногоМероприятияРемонтныеРаботы
		|ГДЕ
		|	торо_АктОВыполненииРегламентногоМероприятияРемонтныеРаботы.РемонтыОборудования_ID = &ID
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВерсияТК.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	ВТ_ВерсияТК.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	ВТ_ВерсияТК.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	торо_СоставИерархическихТехКартРемонтов.Состав КАК Состав,
		|	торо_СоставИерархическихТехКартРемонтов.Количество КАК Количество,
		|	торо_СоставИерархическихТехКартРемонтов.ТехКарта КАК ТехКарта
		|ПОМЕСТИТЬ ВТ_ВложенныеТК
		|ИЗ
		|	ВТ_ВерсияТК КАК ВТ_ВерсияТК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СоставИерархическихТехКартРемонтов КАК торо_СоставИерархическихТехКартРемонтов
		|		ПО ВТ_ВерсияТК.ТехКарта = торо_СоставИерархическихТехКартРемонтов.ТехКарта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВерсияТК.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	ВТ_ВерсияТК.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	ВТ_ВерсияТК.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	ВТ_ВерсияТК.ТехКарта КАК ТехКарта,
		|	1 КАК Количество,
		|	ВТ_ВерсияТК.ИдентификаторТехКарты КАК ИдентификаторТехКарты
		|ПОМЕСТИТЬ ВТ_ТехКарты
		|ИЗ
		|	ВТ_ВерсияТК КАК ВТ_ВерсияТК
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ВложенныеТК.РемонтыОборудования_ID,
		|	ВТ_ВложенныеТК.ВидРемонтныхРабот,
		|	ВТ_ВложенныеТК.ОбъектРемонтныхРабот,
		|	ВТ_ВложенныеТК.Состав,
		|	ВТ_ВложенныеТК.Количество,
		|	ВТ_ВложенныеТК.ТехКарта
		|ИЗ
		|	ВТ_ВложенныеТК КАК ВТ_ВложенныеТК
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТехКарты.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	ВТ_ТехКарты.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	ВТ_ТехКарты.ТехКарта КАК ТехКарта,
		|	торо_ТехКартыСписокОпераций.ID КАК ID,
		|	торо_ТехКартыСписокОпераций.Операция КАК Операция,
		|	торо_ТехКартыСписокОпераций.Количество * ВТ_ТехКарты.Количество КАК Количество,
		|	ВТ_ТехКарты.ИдентификаторТехКарты КАК ИдентификаторТехКарты
		|ПОМЕСТИТЬ ВТ_ОперацииБезIDТК
		|ИЗ
		|	ВТ_ТехКарты КАК ВТ_ТехКарты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
		|		ПО ВТ_ТехКарты.ТехКарта = торо_ТехКартыСписокОпераций.Ссылка
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(торо_ТехКартыСписокОпераций.Операция) = ТИП(Справочник.торо_ТехнологическиеОперации)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТехКартыПлановыхЗатрат.ТехКарта_ID КАК ТехКарта_ID,
		|	торо_ВерсииТехКарт.ИдентификаторТехКарты КАК ИдентификаторТехКарты,
		|	торо_ВерсииТехКарт.ТехКарта КАК ТехКарта
		|ПОМЕСТИТЬ ВТ_ТехКартыСИдентификаторами
		|ИЗ
		|	ВТ_ТехКартыПлановыхЗатрат КАК ВТ_ТехКартыПлановыхЗатрат
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
		|		ПО ВТ_ТехКартыПлановыхЗатрат.ТехКарта = торо_ВерсииТехКарт.ИдентификаторТехКарты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОперацииБезIDТК.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	ВТ_ОперацииБезIDТК.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	ВТ_ОперацииБезIDТК.ТехКарта КАК ТехКарта,
		|	ВТ_ОперацииБезIDТК.ID КАК ID,
		|	ВТ_ОперацииБезIDТК.Операция КАК Операция,
		|	ВТ_ОперацииБезIDТК.Количество / ВТ_КоличествоОдинаковыхТК.КоличествоОдинаковых КАК Количество,
		|	ВТ_ТехКартыСИдентификаторами.ТехКарта_ID КАК ТехКарта_ID,
		|	""В этой строке 36 символов как в ИД  "" КАК УстановленныйИД
		|ИЗ
		|	ВТ_ОперацииБезIDТК КАК ВТ_ОперацииБезIDТК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТехКартыСИдентификаторами КАК ВТ_ТехКартыСИдентификаторами
		|		ПО ВТ_ОперацииБезIDТК.ТехКарта = ВТ_ТехКартыСИдентификаторами.ТехКарта
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоОдинаковыхТК КАК ВТ_КоличествоОдинаковыхТК
		|		ПО (ВТ_ТехКартыСИдентификаторами.ИдентификаторТехКарты = ВТ_КоличествоОдинаковыхТК.ТехКарта)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ТехКартыСИдентификаторами.ТехКарта_ID,
		|	ВТ_ОперацииБезIDТК.Операция,
		|	ВТ_ОперацииБезIDТК.РемонтыОборудования_ID,
		|	ВТ_ОперацииБезIDТК.ТехКарта,
		|	ВТ_ОперацииБезIDТК.ОбъектРемонтныхРабот,
		|	ВТ_ОперацииБезIDТК.ID,
		|	ВТ_ОперацииБезIDТК.Количество / ВТ_КоличествоОдинаковыхТК.КоличествоОдинаковых";
	
	Запрос.УстановитьПараметр("ID", РемонтыОборудования_ID);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаРаботИзАкта = РезультатЗапроса[6].Выгрузить();
	ТаблицаРемонтыхРабот = РезультатЗапроса[11].Выгрузить();
	
	Движения.торо_ПроцентВыполненныхРабот.Записывать = Истина;

	Для каждого СтрокаРемРабот Из ТаблицаРемонтыхРабот Цикл
		// регистр торо_ПроцентВыполненныхРабот 
		Движение = Движения.торо_ПроцентВыполненныхРабот.Добавить();
		
		Идентификатор = СтрокаРемРабот.ID;
		
		СтруктураОтбора = Новый Структура("УстановленныйИД", Идентификатор); 
		НайденныеСтроки = ТаблицаРемонтыхРабот.НайтиСтроки(СтруктураОтбора);		
		
		СтруктураОтбораИзАкта = Новый Структура("Родитель_ID, РемонтнаяРабота", СтрокаРемРабот.ТехКарта_ID, СтрокаРемРабот.Операция);
		НайденныеСтрокиИзАкта = ТаблицаРаботИзАкта.НайтиСтроки(СтруктураОтбораИзАкта);
		Если НайденныеСтрокиИзАкта.Количество() = 0 И НайденныеСтроки.Количество() <> 0 Тогда
			Идентификатор = Строка(Новый УникальныйИдентификатор());	
		ИначеЕсли НайденныеСтрокиИзАкта.Количество() = 0 И НайденныеСтроки.Количество() = 0 Тогда	
		    Идентификатор = СтрокаРемРабот.ID;
		ИначеЕсли НайденныеСтрокиИзАкта.Количество() <> 0 Тогда	
			Для каждого СтрокаИзАкта Из НайденныеСтрокиИзАкта Цикл
				СтруктураОтбора = Новый Структура("УстановленныйИД", СтрокаИзАкта.ID);
				НайденныеСтроки2 = ТаблицаРемонтыхРабот.НайтиСтроки(СтруктураОтбора);
				Если СтрокаИзАкта.ID <> Идентификатор и НайденныеСтроки2.Количество() = 0 Тогда
					Идентификатор = СтрокаИзАкта.ID;
				КонецЕсли; 
			КонецЦикла; 	    
		КонецЕсли; 
		
		СтрокаРемРабот.УстановленныйИД = Идентификатор;
		
		Движение.Период = Дата;
		Движение.IDРемонта = СтрокаРемРабот.РемонтыОборудования_ID;
		Движение.IDОперации = Идентификатор; 
		Движение.Операция = СтрокаРемРабот.Операция;
		Движение.IDРодителя = СтрокаРемРабот.ТехКарта_ID;
		Движение.ВидДокумента = Перечисления.торо_ВидДокумента.ПлановыеЗатраты;
		Движение.Процент = 100 * СтрокаРемРабот.Количество;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
