#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УстановкаЦенНоменклатурыТовары.Номенклатура КАК Номенклатура,
	               |	УстановкаЦенНоменклатурыТовары.ВидЦены КАК ВидЦены,
	               |	УстановкаЦенНоменклатурыТовары.Характеристика КАК Характеристика,
	               |	УстановкаЦенНоменклатурыТовары.Цена КАК Цена,
	               |	УстановкаЦенНоменклатурыТовары.Валюта КАК Валюта,
	               |	УстановкаЦенНоменклатурыТовары.ЕдиницаИзмерения КАК ДокЕдиницаИзмерения,
	               |	УстановкаЦенНоменклатурыТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВЫБОР
	               |		КОГДА УстановкаЦенНоменклатурыТовары.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяССылка)
	               |			ТОГДА 1
	               |		ИНАЧЕ УстановкаЦенНоменклатурыТовары.ЕдиницаИзмерения.Коэффициент
	               |	КОНЕЦ КАК Коэффициент,
	               |	УстановкаЦенНоменклатурыТовары.Ссылка КАК Ссылка,
	               |	УстановкаЦенНоменклатурыТовары.Ссылка.Дата КАК Дата
	               |ПОМЕСТИТЬ ВТ_ТоварыДокумента
	               |ИЗ
	               |	Документ.УстановкаЦенНоменклатуры.Товары КАК УстановкаЦенНоменклатурыТовары
	               |ГДЕ
	               |	УстановкаЦенНоменклатурыТовары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура,
	               |	ВТ_ТоварыДокумента.ВидЦены КАК ВидЦены,
	               |	ВТ_ТоварыДокумента.Характеристика КАК Характеристика,
	               |	ВТ_ТоварыДокумента.Цена КАК Цена,
	               |	ВТ_ТоварыДокумента.Валюта КАК Валюта,
	               |	ВТ_ТоварыДокумента.ДокЕдиницаИзмерения КАК ДокЕдиницаИзмерения,
	               |	ВТ_ТоварыДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВТ_ТоварыДокумента.Коэффициент КАК Коэффициент,
	               |	ВТ_ТоварыДокумента.Дата КАК Период
	               |ИЗ
	               |	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура,
	               |	ЦеныНоменклатуры.Характеристика КАК Характеристика,
	               |	ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	               |	ЦеныНоменклатуры.Период КАК Период,
	               |	ЦеныНоменклатуры.Регистратор КАК Регистратор
	               |ИЗ
	               |	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	               |		ПО ВТ_ТоварыДокумента.Номенклатура = ЦеныНоменклатуры.Номенклатура
	               |			И ВТ_ТоварыДокумента.ВидЦены = ЦеныНоменклатуры.ВидЦены
	               |			И ВТ_ТоварыДокумента.Характеристика = ЦеныНоменклатуры.Характеристика
	               |			И ВТ_ТоварыДокумента.Дата = ЦеныНоменклатуры.Период
	               |			И ВТ_ТоварыДокумента.Ссылка <> ЦеныНоменклатуры.Регистратор";
				   
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	резЗапроса = ПакетЗапросов[1];
	
	Если резЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = резЗапроса.Выбрать();
	
	// регистр ЦеныНоменклатуры
	Движения.ЦеныНоменклатуры.Записывать = Истина;
	Движения.ЦеныНоменклатуры.Очистить();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ЦеныНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		Если Выборка.ЕдиницаИзмерения <> Выборка.ДокЕдиницаИзмерения Тогда
			Движение.Цена = ?(Выборка.Коэффициент = 0, 0, Движение.Цена / Выборка.Коэффициент);
		КонецЕсли;
		
	КонецЦикла;

	Дубли = ПакетЗапросов[2].Выбрать();
	Пока Дубли.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Для номенклатуры %1 и вида цены %2 документом %3 уже установлена цена на эту дату'"), Дубли.Номенклатура, Дубли.ВидЦены, Дубли.Регистратор));
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровУслугТовары.Характеристика КАК Характеристика,
		|	ПоступлениеТоваровУслугТовары.Упаковка КАК ЕдиницаИзмерения,
		|	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
		|	ПоступлениеТоваровУслугТовары.Номенклатура.ВидНоменклатуры.ИспользоватьХарактеристики КАК ХарактеристикиИспользуются,
		|	ПоступлениеТоваровУслугТовары.Ссылка.Валюта КАК Валюта
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КонецЦикла; 
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") Тогда 
		
		Валюта = ДанныеЗаполнения.ВидЦены.ВалютаЦены;
		
		ЗаполнитьЗначенияСвойств(ВидыЦен.Добавить(), ДанныеЗаполнения);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ОприходованиеИзлишковТовары.Номенклатура КАК Номенклатура,
		|	ОприходованиеИзлишковТовары.Характеристика КАК Характеристика,
		|	ОприходованиеИзлишковТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ОприходованиеИзлишковТовары.Цена КАК Цена,
		|	ОприходованиеИзлишковТовары.Номенклатура.ВидНоменклатуры.ИспользоватьХарактеристики КАК ХарактеристикиИспользуются 
		|ИЗ
		|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТовары
		|ГДЕ
		|	ОприходованиеИзлишковТовары.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Валюта = Валюта;
			КонецЦикла; 
		КонецЕсли;
				
	КонецЕсли;
	
	торо_ЗаполнениеДокументов.ЗаполнитьСтандартныеРеквизитыШапкиПоУмолчанию(ЭтотОбъект, Новый Структура("Ответственный"));
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда
		 торо_Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли