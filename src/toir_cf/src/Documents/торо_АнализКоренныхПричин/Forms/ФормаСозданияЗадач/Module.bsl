#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("МассивСтрок") Тогда
		Для каждого Строка Из Параметры.МассивСтрок Цикл
			НоваяСтрока = ТаблицаЗаданий.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
			НоваяСтрока.ПроверятьВыполнение = Ложь;
			НоваяСтрока.Флаг = Истина;
		КонецЦикла; 
	КонецЕсли;
	Если Параметры.Свойство("АнализСсылка") Тогда
		АнализСсылка = Параметры.АнализСсылка;
	КонецЕсли; 
	Если Параметры.Свойство("ОбъектРемонта") Тогда
		ОбъектРемонта = Параметры.ОбъектРемонта;
	КонецЕсли; 
	Если Параметры.Свойство("КраткоеОписание") Тогда
		КраткоеОписание = Параметры.КраткоеОписание;
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	МассивЗаданий = Новый Массив();
	ПолеНеЗаполнено = Ложь;
	Для каждого Мероприятие Из ТаблицаЗаданий Цикл
		Если Мероприятие.Флаг Тогда
			Если Не ЗначениеЗаполнено(Мероприятие.Описание) ИЛИ 
				Не ЗначениеЗаполнено(Мероприятие.Ответственный) ИЛИ
				Не ЗначениеЗаполнено(Мероприятие.Важность) ИЛИ
				Не ЗначениеЗаполнено(Мероприятие.СрокВыполнения) ИЛИ 
				(Мероприятие.ПроверятьВыполнение И Не ЗначениеЗаполнено(Мероприятие.Проверяющий)) ИЛИ 
				(Мероприятие.ПроверятьВыполнение И Не ЗначениеЗаполнено(Мероприятие.СрокПроверки)) Тогда
				
				ПолеНеЗаполнено = Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	Если ПолеНеЗаполнено Тогда
	
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Заполните обязательные поля!'"));
		Возврат;
	
	КонецЕсли;
	
	Для каждого Мероприятие Из ТаблицаЗаданий Цикл
		Если Мероприятие.Флаг Тогда
			МероприятиеСтруктура = Новый Структура("ПроверятьВыполнение, Проверяющий, Описание, Ответственный, СрокВыполнения, СрокПроверки, Важность, Задание, ИДМероприятия");
			ЗаполнитьЗначенияСвойств(МероприятиеСтруктура, Мероприятие); 
			Задание = СоздатьЗадание(МероприятиеСтруктура);
			МероприятиеСтруктура.Задание = Задание;
			МассивЗаданий.Добавить(МероприятиеСтруктура);
		КонецЕсли; 
	КонецЦикла;
	
	СтруктураОтправки = Новый Структура("МассивЗаданий", МассивЗаданий); 
	ОповеститьОВыборе(СтруктураОтправки);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция СоздатьЗадание(Мероприятие)
	
	БизнесПроцесс = БизнесПроцессы.Задание.СоздатьБизнесПроцесс();
	БизнесПроцесс.Автор = Пользователи.ТекущийПользователь();
	БизнесПроцесс.Дата = ТекущаяДата();
	Если ТипЗнч(Мероприятие.Ответственный) = Тип("СправочникСсылка.ВнешниеПользователи") Или
		ТипЗнч(Мероприятие.Ответственный) = Тип("СправочникСсылка.Пользователи") Тогда
		БизнесПроцесс.Исполнитель = Мероприятие.Ответственный;
	Иначе
		ФизическоеЛицо = Мероприятие.Ответственный.ФизическоеЛицо;
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Пользователи.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|ГДЕ
			|	Пользователи.ФизическоеЛицо = &ФизическоеЛицо";
		
		Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			БизнесПроцесс.Исполнитель = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЦикла;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(БизнесПроцесс.Исполнитель) Тогда
		Возврат БизнесПроцессы.Задание.ПустаяСсылка();
	КонецЕсли;
	
	БизнесПроцесс.Наименование = СтрШаблон(НСтр("ru = 'Корректирующее мероприятие по дефекту ""%1"" оборудования ""%2""'"), КраткоеОписание, ОбъектРемонта);
	БизнесПроцесс.Содержание = Мероприятие.Описание;
	Если Мероприятие.ПроверятьВыполнение Тогда
		БизнесПроцесс.НаПроверке = Мероприятие.ПроверятьВыполнение;
		БизнесПроцесс.Проверяющий = Мероприятие.Проверяющий;
		БизнесПроцесс.СрокПроверки = Мероприятие.СрокПроверки;
	КонецЕсли; 
	БизнесПроцесс.Предмет = АнализСсылка;
	БизнесПроцесс.Важность = Мероприятие.Важность;
	БизнесПроцесс.СрокИсполнения = Мероприятие.СрокВыполнения;
	
	БизнесПроцесс.Записать();
	БизнесПроцесс.Старт();	
	Возврат БизнесПроцесс.Ссылка;
КонецФункции

&НаКлиенте
Процедура СнятьВыделение(Команда)
	Для каждого Строка Из ТаблицаЗаданий Цикл
		Строка.Флаг = Ложь;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	Для каждого Строка Из ТаблицаЗаданий Цикл
		Строка.Флаг = Истина;
	КонецЦикла; 
КонецПроцедуры

#КонецОбласти



