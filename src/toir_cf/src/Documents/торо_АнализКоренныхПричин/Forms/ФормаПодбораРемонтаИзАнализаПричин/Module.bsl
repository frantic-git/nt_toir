
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не Параметры.Свойство("Анализ") Тогда
		ТекстСообщения = НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Анализ") Тогда
		Анализ = Параметры.Анализ;	
	КонецЕсли;		
	
	ЗаполнитьРемонтыОборудования();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура РемонтыОборудованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Элементы.РемонтыОборудования);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Выбрать(Команда)
	ТекущиеДанные = Элементы.РемонтыОборудования.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		СтруктураВыбора = Новый Структура();
		СтруктураВыбора.Вставить("ИДРемонта", ТекущиеДанные.ИДРемонта);
		СтруктураВыбора.Вставить("Ответственный", ТекущиеДанные.Ответственный);
		СтруктураВыбора.Вставить("ОбъектРемонта", ТекущиеДанные.ОбъектРемонта);
		СтруктураВыбора.Вставить("Описание", ТекущиеДанные.Описание);
		СтруктураВыбора.Вставить("СрокВыполнения", ТекущиеДанные.СрокВыполнения);
		ОповеститьОВыборе(СтруктураВыбора);		
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьРемонтыОборудования()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.Ссылка КАК Ссылка,
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.Описание КАК Описание,
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.ИДМероприятия КАК ИДРемонта,
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.Ответственный КАК Ответственный,
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.СрокВыполнения КАК СрокВыполнения,
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.Ссылка.ОбъектРемонта КАК ОбъектРемонта
		|ИЗ
		|	Документ.торо_АнализКоренныхПричин.КорректирующиеМероприятия КАК торо_АнализКоренныхПричинКорректирующиеМероприятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВнешниеОснованияДляРабот КАК торо_ВнешниеОснованияДляРабот
		|		ПО торо_АнализКоренныхПричинКорректирующиеМероприятия.ИДМероприятия = торо_ВнешниеОснованияДляРабот.ID
		|ГДЕ
		|	торо_АнализКоренныхПричинКорректирующиеМероприятия.Ссылка = &Ссылка
		|	И торо_АнализКоренныхПричинКорректирующиеМероприятия.Задание = &Задание
		|	И торо_ВнешниеОснованияДляРабот.ID ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Ссылка", Анализ);
	Запрос.УстановитьПараметр("Задание", БизнесПроцессы.Задание.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	РемонтыОборудования.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = РемонтыОборудования.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти






