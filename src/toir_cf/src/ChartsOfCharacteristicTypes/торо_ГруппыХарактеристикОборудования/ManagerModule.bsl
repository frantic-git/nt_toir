#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ОтобратьХарактеристикиОборудования(ТипЗначения, ПараметрыАвтозаполнения = Неопределено) Экспорт

	МассивОтобранный = Новый Массив;
	
	МассивРодителей = Новый Массив;
	МассивРодителей.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_торо_ОбъектыРемонта"));
	МассивРодителей.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_торо_НаправленияОбъектовРемонтныхРабот"));
	МассивРодителей.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_торо_ТиповыеОР"));
	
	Запрос = Новый Запрос;
	Запрос.Текст =	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	              	|	ТЧРеквизитов.Свойство КАК Свойство
	              	|ПОМЕСТИТЬ СписокСвойств
	              	|ИЗ
	              	|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
	              	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ТЧРеквизитов
	              	|		ПО (ТЧРеквизитов.Ссылка = НаборыДополнительныхРеквизитовИСведений.Ссылка)
	              	|ГДЕ
	              	|	НаборыДополнительныхРеквизитовИСведений.Ссылка В ИЕРАРХИИ(&Родитель)
	              	|	И НЕ НаборыДополнительныхРеквизитовИСведений.ПометкаУдаления
	              	|	И НЕ НаборыДополнительныхРеквизитовИСведений.КоличествоРеквизитов = """"
	              	|	И НЕ ТЧРеквизитов.ПометкаУдаления
	              	|	И ТЧРеквизитов.Свойство.Заголовок В(&Наименование)
	              	|;
	              	|
	              	|////////////////////////////////////////////////////////////////////////////////
	              	|ВЫБРАТЬ
	              	|	СписокСвойств.Свойство КАК Свойство,
	              	|	СписокСвойств.Свойство.ТипЗначения КАК ТипЗначения
	              	|ИЗ
	              	|	СписокСвойств КАК СписокСвойств";
	
	Запрос.УстановитьПараметр("Родитель", МассивРодителей);
	
	Если ПараметрыАвтозаполнения = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ТЧРеквизитов.Свойство.Заголовок В(&Наименование)", "");
	Иначе
		Запрос.УстановитьПараметр("Наименование", ПараметрыАвтозаполнения.ВыгрузитьКолонку("НаименованиеХарактеристики"));
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ТипЗначенияПодходит(ТипЗначения, Выборка.ТипЗначения) Тогда
			МассивОтобранный.Добавить(Выборка.Свойство);
		КонецЕсли;	
	КонецЦикла;

	Возврат МассивОтобранный;
	
КонецФункции

Процедура АктуализироватьГруппыХарактеристик(ДопРеквизитОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДопРеквизитОбъект.ЭтоДополнительноеСведение Тогда
		Возврат;
	КонецЕсли;
	
	Если ДопРеквизитОбъект.ПометкаУдаления Тогда
		УдалитьДопРеквизитИзГруппСАвтозаполнением(ДопРеквизитОбъект.Ссылка);
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоДопРеквизитОР(ДопРеквизитОбъект.НаборСвойств) Тогда
		Возврат;
	КонецЕсли;
	
	ГруппыГдеЕстьРеквизит = НайтиДопРеквизитВГруппахСАвтозаполнением(ДопРеквизитОбъект.Ссылка);
	ГруппыГдеДолженБытьРеквизит = НайтиГруппыСАвтозаполнениемДляДопРеквизита(ДопРеквизитОбъект.Заголовок, ДопРеквизитОбъект.ТипЗначения);
	
	Для каждого Группа из ГруппыГдеДолженБытьРеквизит Цикл
		Если ГруппыГдеЕстьРеквизит.Найти(Группа) = Неопределено Тогда
			ДобавитьДопРеквизитВГруппу(Группа, ДопРеквизитОбъект.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Группа из ГруппыГдеЕстьРеквизит Цикл
		Если ГруппыГдеДолженБытьРеквизит.Найти(Группа) = Неопределено Тогда
			УдалитьДопРеквизитьИзГруппы(Группа, ДопРеквизитОбъект.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ТипЗначенияПодходит(ТипЗначенияГруппы, ТипЗначенияДопРеквизита) Экспорт
	
	Если ОбщегоНазначения.КоллекцииИдентичны(ТипЗначенияГруппы.Типы(), ТипЗначенияДопРеквизита.Типы()) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоДопРеквизитОР(НаборСвойств) 
	
	МассивРодителей = Новый Массив;
	МассивРодителей.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_торо_ОбъектыРемонта"));
	МассивРодителей.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_торо_НаправленияОбъектовРемонтныхРабот"));
	МассивРодителей.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_торо_ТиповыеОР"));
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НаборыДополнительныхРеквизитовИСведений.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведений.Ссылка В ИЕРАРХИИ(&Родитель)
	|	И НаборыДополнительныхРеквизитовИСведений.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Родитель", МассивРодителей);
	Запрос.УстановитьПараметр("Ссылка", НаборСвойств);
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

Процедура УдалитьДопРеквизитИзГруппСАвтозаполнением(ДопРеквизит)
	
	МассивГрупп = НайтиДопРеквизитВГруппахСАвтозаполнением(ДопРеквизит);
	
	Для каждого Группа из МассивГрупп Цикл
		УдалитьДопРеквизитьИзГруппы(Группа, ДопРеквизит);
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиДопРеквизитВГруппахСАвтозаполнением(ДопРеквизит)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	торо_ГруппыХарактеристикОборудованияСписокХарактеристик.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.торо_ГруппыХарактеристикОборудования.СписокХарактеристик КАК торо_ГруппыХарактеристикОборудованияСписокХарактеристик
	|ГДЕ
	|	торо_ГруппыХарактеристикОборудованияСписокХарактеристик.Характеристика = &Характеристика
	|	И торо_ГруппыХарактеристикОборудованияСписокХарактеристик.Ссылка.ЗаполнятьАвтоматически";

	Запрос.УстановитьПараметр("Характеристика", ДопРеквизит);
	
	Результат = Запрос.Выполнить().Выгрузить();

	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция НайтиГруппыСАвтозаполнениемДляДопРеквизита(Наименование, ТипЗначения)
	
	МассивГрупп = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	торо_ГруппыХарактеристикОборудованияПараметрыАвтозаполнения.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РазличныеГруппы
	|ИЗ
	|	ПланВидовХарактеристик.торо_ГруппыХарактеристикОборудования.ПараметрыАвтозаполнения КАК торо_ГруппыХарактеристикОборудованияПараметрыАвтозаполнения
	|ГДЕ
	|	торо_ГруппыХарактеристикОборудованияПараметрыАвтозаполнения.НаименованиеХарактеристики = &НаименованиеХарактеристики
	|	И торо_ГруппыХарактеристикОборудованияПараметрыАвтозаполнения.Ссылка.ЗаполнятьАвтоматически
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазличныеГруппы.Ссылка КАК Ссылка,
	|	РазличныеГруппы.Ссылка.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	РазличныеГруппы КАК РазличныеГруппы";
	
	Запрос.УстановитьПараметр("НаименованиеХарактеристики", Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗначенияПодходит(Выборка.ТипЗначения, ТипЗначения) Тогда
			МассивГрупп.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивГрупп;
	
КонецФункции

Процедура УдалитьДопРеквизитьИзГруппы(Группа, ДопРеквизит)
	
	СтруктураПоиска = Новый Структура("Характеристика", ДопРеквизит);
	
	Попытка
		ГруппаОбъект = Группа.ПолучитьОбъект();
		СтрокиКУдалению = ГруппаОбъект.СписокХарактеристик.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаКУдалению из СтрокиКУдалению Цикл
			ГруппаОбъект.СписокХарактеристик.Удалить(СтрокаКУдалению);
		КонецЦикла;
		ГруппаОбъект.ОбменДанными.Загрузка = Истина;
		ГруппаОбъект.Записать();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("ОбновлениеГруппХарактеристикОборудования", УровеньЖурналаРегистрации.Ошибка,
		ПланыВидовХарактеристик.торо_ГруппыХарактеристикОборудования, Группа, ОписаниеОшибки);
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьДопРеквизитВГруппу(Группа, ДопРеквизит)
	
	Попытка
		ГруппаОбъект = Группа.ПолучитьОбъект();
		НоваяСтрока = ГруппаОбъект.СписокХарактеристик.Добавить();
		Новаястрока.Характеристика = ДопРеквизит;
		ГруппаОбъект.ОбменДанными.Загрузка = Истина;
		ГруппаОбъект.Записать();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("ОбновлениеГруппХарактеристикОборудования", УровеньЖурналаРегистрации.Ошибка,
		ПланыВидовХарактеристик.торо_ГруппыХарактеристикОборудования, Группа, ОписаниеОшибки);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

