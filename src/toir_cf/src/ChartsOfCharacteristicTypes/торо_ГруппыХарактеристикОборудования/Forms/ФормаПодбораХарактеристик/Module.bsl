
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивХарактеристик = Новый Массив;
	
	Если Параметры.Свойство("ТипЗначения") Тогда
		МассивХарактеристик = ПланыВидовХарактеристик.торо_ГруппыХарактеристикОборудования.ОтобратьХарактеристикиОборудования(Параметры.ТипЗначения);
	КонецЕсли;
	
	Если МассивХарактеристик.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют характеристики реквизитов, соответствующие указанному типу значения'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ПредставлениеГруппировкиОбщихСвойств",
		НСтр("ru = 'Общие (для нескольких наборов)'"),
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Ссылка", МассивХарактеристик, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		
	// Группировка свойств по наборам.
	ГруппировкаДанных = Список.КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ГруппировкаДанных.ИдентификаторПользовательскойНастройки = "ГруппировкаСвойствПоНаборам";
	ГруппировкаДанных.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ПоляГруппировки = ГруппировкаДанных.ПоляГруппировки;
	
	ЭлементГруппировкиДанных = ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ЭлементГруппировкиДанных.Поле = Новый ПолеКомпоновкиДанных("НаборСвойствГруппировка");
	ЭлементГруппировкиДанных.Использование = Истина;

КонецПроцедуры

#КонецОбласти
