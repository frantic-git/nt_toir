////////////////////////////////////////////////////////////////////////////////
// Модуль формы Справочник.Номенклатура.Форма.ФормаЗначенийРеквизитов
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗначенияНоменклатурыКонтрагента") Тогда
		
		ЗаполнитьПоЗначениямНоменклатурыКонтрагента(Параметры.ЗначенияНоменклатурыКонтрагента);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Родитель", Родитель);
	Результат.Вставить("ВидНоменклатуры", ВидНоменклатуры);
	Результат.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Результат.Вставить("СтавкаНДС", СтавкаНДС);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПоЗначениямНоменклатурыКонтрагента(Значения)
	
	Если Значения.Свойство("ЕдиницаИзмеренияКод") И Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		
		ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиПоКоду(Значения.ЕдиницаИзмеренияКод);
		
	КонецЕсли;
	
	Если Значения.Свойство("ЕдиницаИзмерения") И Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		
		ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиПоНаименованию(Значения.ЕдиницаИзмерения, Истина);
		
	КонецЕсли;
	
	Если Значения.Свойство("СтавкаНДС") И Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		
		СтавкиНДСПоКлючу = Новый Соответствие;
		ОбменСКонтрагентамиПереопределяемый.ЗаполнитьСоответствиеСтавокНДС(СтавкиНДСПоКлючу);
		СтавкаНДСПеречисление = СтавкиНДСПоКлючу[Значения.СтавкаНДС];
		СтавкаНДС = УчетНДСВызовСервера.СтавкаНДСПоЗначениюПеречисления(СтавкаНДСПеречисление);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

