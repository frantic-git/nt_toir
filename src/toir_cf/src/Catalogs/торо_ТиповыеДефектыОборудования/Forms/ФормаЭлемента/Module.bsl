#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств (Объект, Параметры.ЗначенияЗаполнения);
		Если Параметры.ЗначенияЗаполнения.Свойство("ДефектПричина")Тогда 
			Объект.ПричиныВозникновенияДефекта.Очистить();  
			Если ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ДефектПричина) Тогда 
				НоваяСтрока = Объект.ПричиныВозникновенияДефекта.Добавить();
				НоваяСтрока.Причина = Параметры.ЗначенияЗаполнения.ДефектПричина;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч (Параметры.Ключ) = Тип ("СправочникСсылка.торо_ТиповыеДефектыОборудования") И Параметры.ЗначенияЗаполнения.Количество()>0 Тогда 
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты"); 
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект) 
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект); 
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ВладелецФормы <> Неопределено Тогда
		СтруктураОповещения = ПолучитьСтруктуруОповещения();
		ЗакрыватьПриВыборе = Ложь;
		ОповеститьОВыборе(СтруктураОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого ТекДанные Из Объект.ПричиныВозникновенияДефекта Цикл
		Если НЕ ЗначениеЗаполнено(ТекДанные.Причина) Тогда
			ТекстСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Причина"" в таблице ""Непосредственные причины дефекта""!'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ПричиныВозникновенияДефекта",, Отказ);
			Возврат;
		КонецЕсли;
		
		Если Объект.ПричиныВозникновенияДефекта.Количество() > 1 Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("Причина", ТекДанные.Причина); 
			ДублиКвалификации = Объект.ПричиныВозникновенияДефекта.НайтиСтроки(Отбор);
			Если ДублиКвалификации.Количество()>1 Тогда 
				ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющаяся поля ""Причина"" ""(%1)"" в таблице непосредственные причины дефекта!'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.Причина);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.ПричиныВозникновенияДефекта",, Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка); 
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию() 
	 
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов() 
	
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры   

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов() 
	
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект); 
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент) 
	
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Функция ПолучитьСтруктуруОповещения()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Дефект", Объект.Ссылка);
	Результат.Вставить("ВидДефекта", Объект.ВидДефекта);
	Результат.Вставить("КритичностьДефекта", Объект.КритичностьДефекта);
	Результат.Вставить("ПричинаДефекта", ?(Объект.ПричиныВозникновенияДефекта.Количество() = 0, Неопределено, Объект.ПричиныВозникновенияДефекта[0].Причина));
	
	Результат.Вставить("ДефектОписание", Объект.ОписаниеДефекта);
	Результат.Вставить("ДефектОписаниеЯзык1", Объект.ОписаниеДефектаЯзык1);
	Результат.Вставить("ДефектОписаниеЯзык2", Объект.ОписаниеДефектаЯзык2);
	
	Результат.Вставить("ПоследствияДефекта", Объект.ПоследствиеДефекта);
	Результат.Вставить("ПоследствияДефектаЯзык1", Объект.ПоследствиеДефектаЯзык1);
	Результат.Вставить("ПоследствияДефектаЯзык2", Объект.ПоследствиеДефектаЯзык2);
	
	Результат.Вставить("НаправлениеНаУстранение", Объект.НаправлениеНаУстранение);
	Результат.Вставить("НаправлениеНаУстранениеЯзык1", Объект.НаправлениеНаУстранениеЯзык1);
	Результат.Вставить("НаправлениеНаУстранениеЯзык2", Объект.НаправлениеНаУстранениеЯзык2);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти



