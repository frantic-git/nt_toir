#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// ТЧ Пользователи
	Для Каждого ТекущиеДанные Из Объект.Оповещаемые Цикл		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Пользователь) Тогда
		    ТекстСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Пользователь"" в таблице ""Оповещаемые лица""!'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.Оповещаемые",, Отказ);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если Объект.РазделятьПолучателейПоОрганизациям И Объект.РазделятьПолучателейПоОрганизациям Тогда
			СтруктураПоиска = Новый Структура("Пользователь, Организация, Подразделение", ТекущиеДанные.Пользователь, ТекущиеДанные.Организация, ТекущиеДанные.Подразделение);
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Пользователь"", ""Организация"" и ""Подразделение"" ""(%1, %2, %3)"" в таблице ""Оповещаемые лица""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекущиеДанные.Пользователь, ?(ЗначениеЗаполнено(ТекущиеДанные.Организация), ТекущиеДанные.Организация, "<все организации>"), ?(ЗначениеЗаполнено(ТекущиеДанные.Подразделение), ТекущиеДанные.Подразделение, "<все подразделения>"));
		ИначеЕсли Объект.РазделятьПолучателейПоОрганизациям Тогда
			СтруктураПоиска = Новый Структура("Пользователь, Организация", ТекущиеДанные.Пользователь, ТекущиеДанные.Организация);
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Пользователь"" и ""Организация"" ""(%1 и %2)"" в таблице ""Оповещаемые лица""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекущиеДанные.Пользователь, ?(ЗначениеЗаполнено(ТекущиеДанные.Организация), ТекущиеДанные.Организация, "<все организации>"));
		ИначеЕсли Объект.РазделятьПолучателейПоПодразделениям Тогда
			СтруктураПоиска = Новый Структура("Пользователь, Подразделение", ТекущиеДанные.Пользователь, ТекущиеДанные.Подразделение);
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Пользователь"" и ""Подразделение"" ""(%1 и %2)"" в таблице ""Оповещаемые лица""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекущиеДанные.Пользователь, ?(ЗначениеЗаполнено(ТекущиеДанные.Подразделение), ТекущиеДанные.Подразделение, "<все подразделения>"));
		Иначе
			СтруктураПоиска = Новый Структура("Пользователь", ТекущиеДанные.Пользователь);
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Пользователь"" ""(%1)"" в таблице ""Оповещаемые лица""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекущиеДанные.Пользователь);
		КонецЕсли;
		
		НайденныеСтроки = Объект.Оповещаемые.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 1 Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.Оповещаемые",, Отказ);		
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	// ТЧ Пользователи
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РазделятьПолучателейПоПодразделениямПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура РазделятьПолучателейПоОрганизациямПриИзменении(Элемент)

	УправлениеФормой();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОповещаемые

&НаКлиенте
Процедура ОповещаемыеПользовательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Оповещаемые.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ТипЗнч(ТекущиеДанные.Пользователь) <> Тип("СправочникСсылка.Пользователи") Тогда
		ВыбратьТипПолучателя();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОповещаемыеПользовательОчистка(Элемент, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Оповещаемые.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
	    Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Пользователь = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ОповещаемыеПодразделение.Видимость = Объект.РазделятьПолучателейПоПодразделениям;
	Элементы.ОповещаемыеОрганизация.Видимость = Объект.РазделятьПолучателейПоОрганизациям;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТипПолучателя()
	
	СписокДоступныхТипов = Новый СписокЗначений;
	
	СписокДоступныхТипов.Добавить("Пользователи");
	СписокДоступныхТипов.Добавить("Ответственный по документу");
	СписокДоступныхТипов.Добавить("Сотрудники бригады-исполнителя");
	
	Если торо_РаботаССогласованиями.ПроверитьИспользованиеСогласованияДокументов() Тогда
		
		СписокДоступныхТипов.Добавить("Согласующие");
		СписокДоступныхТипов.Добавить("Оповещаемые");
		
	КонецЕсли;
	
	СписокДоступныхТипов.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ВыбратьТипПолучателяЗавершение", ЭтотОбъект), НСтр("ru = 'Кому формировать уведомления'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТипПолучателяЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    ВыбранныйТип = ВыбранныйЭлемент;
    
	Если ВыбранныйТип <> Неопределено Тогда
		Если ВыбранныйТип.Значение = "Пользователи" Тогда
            Элементы.Оповещаемые.ТекущиеДанные.Пользователь = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
        Иначе
            Элементы.Оповещаемые.ТекущиеДанные.Пользователь = ВыбранныйТип;
        КонецЕсли;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти