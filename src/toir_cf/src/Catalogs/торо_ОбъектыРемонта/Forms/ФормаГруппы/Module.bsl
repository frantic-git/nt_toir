#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
	Если Параметры.Свойство("СтруктураИерархии") И ЗначениеЗаполнено(Параметры.СтруктураИерархии) Тогда
		ТекСтруктураИерархии = Параметры.СтруктураИерархии;
	Иначе
		ТекСтруктураИерархии = Константы.торо_ИерархияДляВводаНовыхОР.Получить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭтоНовыйЭлемент = Истина;
		РодительИерархии = Параметры.РодительИерархии;
		Родитель = РодительИерархии;
	Иначе	
		Родитель = торо_РаботаСИерархией20.ПолучитьТекущегоРодителяВИерархии(Объект.Ссылка, ТекСтруктураИерархии,,Истина,Истина);
	КонецЕсли;
	
	Элементы.ФормаПечать.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ТекСтруктураИерархии", ТекСтруктураИерархии);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("РодительИерархии", РодительИерархии);
	КонецЕсли;
	
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Для обновления данных в дереве ОР.
	СтруктураРеквизитов = Новый Структура("Ссылка, ПометкаУдаления, Наименование", 
							Объект.Ссылка, Объект.ПометкаУдаления, Объект.Наименование);
							
	Если ЭтоНовыйЭлемент Тогда
		СтруктураРеквизитов.Вставить("ЭтоНовый", ЭтоНовыйЭлемент);						
		СтруктураРеквизитов.Вставить("СтруктураИерархии", ТекСтруктураИерархии);
		СтруктураРеквизитов.Вставить("РодительИерархии", РодительИерархии);
	КонецЕсли;
		
	Оповестить("ЗаписанОбъектРемонта", СтруктураРеквизитов, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти
