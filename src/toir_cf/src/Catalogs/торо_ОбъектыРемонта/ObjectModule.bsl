#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	Если НЕ ЭтоНовый() Тогда
		
		Если ПодразделениеИсполнитель <> Ссылка.ПодразделениеИсполнитель Тогда
			ДополнительныеСвойства.Вставить("НеобходимаРегистрацияЗаявокДляМобильныхБригад", Истина);
		КонецЕсли;
		
		Если ТиповойОР <> Ссылка.ТиповойОР Тогда
			ДополнительныеСвойства.Вставить("НеобходимаРегистрацияИзмененияКонтролируемыхПоказателей", Истина);
			ДополнительныеСвойства.Вставить("ИзмененТиповойОР", Истина);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("торо_РасширенныйРежимМТО") Тогда
			
			Параметры = Новый Структура;
			Параметры.Вставить("СкладМТРДоИзменения", Ссылка.СкладОбеспеченияМТР);
			Параметры.Вставить("ПодразделениеДоИзменения", Ссылка.Подразделение);
			Параметры.Вставить("ПодразделениеИсполнительДоИзменения", Ссылка.ПодразделениеИсполнитель);
			
			ДополнительныеСвойства.Вставить("ПараметрыАктуализацииЗаказовНаВнутреннееПотребление", Параметры);		
		КонецЕсли; 
	Иначе
		
		Если ЗначениеЗаполнено(ТиповойОР) Тогда
			ДополнительныеСвойства.Вставить("НеобходимаРегистрацияИзмененияКонтролируемыхПоказателей", Истина);
			ДополнительныеСвойства.Вставить("ИзмененТиповойОР", Истина);
		КонецЕсли; 	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		
		Если ДополнительныеСвойства.Свойство("ТекСтруктураИерархии") И ДополнительныеСвойства.Свойство("РодительИерархии") Тогда
			СсылкаНаОбъект = торо_ОбщегоНазначения.ПолучитьСсылкуНаОбъект(ЭтотОбъект);
			
			Попытка
				торо_РаботаСИерархией20.ВключитьНовыйОбъектРемонтаВИерархию(СсылкаНаОбъект, ДополнительныеСвойства.ТекСтруктураИерархии, ДополнительныеСвойства.РодительИерархии, Отказ, ОбменДанными.Загрузка, ТипОбъекта);
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Исключение'"), УровеньЖурналаРегистрации.Предупреждение, 
					Метаданные.Справочники.торо_ОбъектыРемонта,,ОписаниеОшибки());
				Отказ = Истина;
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("НеобходимаРегистрацияЗаявокДляМобильныхБригад") Тогда
		СтруктураПараметров = Новый Структура("ИзменениеОР, ОбъектРемонта", Истина, Ссылка);
		торо_ОбменMobileBrigadesСобытия.ЗарегистрироватьЗаявкиПриИзмененииПараметров(СтруктураПараметров);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеобходимаРегистрацияИзмененияКонтролируемыхПоказателей") Тогда
		УстановитьПривилегированныйРежим(Истина);
		НаборЗаписей = РегистрыСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОбъектРемонта.Установить(Ссылка);
		НаборЗаписей.Отбор.Показатель.Установить(ПланыВидовХарактеристик.торо_ИзмеряемыеПоказателиОбъектовРемонта.ПустаяСсылка());
		торо_ОбменMobileBrigadesСобытия.Обмен_mobileBrigades_ЗарегистрироватьИзменениеНабораЗаписейПередЗаписью(НаборЗаписей, Ложь, Истина);
		ПланыОбмена.ЗарегистрироватьИзменения(НаборЗаписей.ОбменДанными.Получатели, НаборЗаписей);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ИзмененТиповойОР") Тогда
		Если НЕ ДополнительныеСвойства.Свойство("НеОбновлятьРегистрНаличиеНормативов") Тогда
			торо_РаботаСНормативамиСервер.ОбновитьНаличиеНормативовОРПриИзмененииТиповогоОР(Ссылка);
		КонецЕсли;
	ИначеЕсли ДополнительныеСвойства.ЭтоНовый Тогда
		торо_РаботаСНормативамиСервер.ДобавитьПустуюЗаписьНаличияНормативов(Ссылка);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПараметрыАктуализацииЗаказовНаВнутреннееПотребление") Тогда
		
		СкладМТРДоИзменения = ДополнительныеСвойства.ПараметрыАктуализацииЗаказовНаВнутреннееПотребление.СкладМТРДоИзменения; 
	    ПодразделениеДоИзменения = ДополнительныеСвойства.ПараметрыАктуализацииЗаказовНаВнутреннееПотребление.ПодразделениеДоИзменения; 
		ПодразделениеИсполнительДоИзменения = ДополнительныеСвойства.ПараметрыАктуализацииЗаказовНаВнутреннееПотребление.ПодразделениеИсполнительДоИзменения;
		
		СталЗаполненСкладМТР = Не ЗначениеЗаполнено(СкладМТРДоИзменения) И ЗначениеЗаполнено(СкладОбеспеченияМТР);
		
		СкладВПодразделенииДоИзменения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодразделениеДоИзменения, "Склад");
		СкладПодразделения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "Склад");
		СталЗаполненСкладВПодразделении = Не ЗначениеЗаполнено(СкладВПодразделенииДоИзменения) И ЗначениеЗаполнено(СкладПодразделения);
		
		СкладВПодразделенииИсполнителяДоИзменения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодразделениеИсполнительДоИзменения, "Склад");
		СкладПодразделенияИсполнителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодразделениеИсполнитель, "Склад");
		СталЗаполненСкладВПодразделенииИсполнителе = Не ЗначениеЗаполнено(СкладВПодразделенииИсполнителяДоИзменения) И ЗначениеЗаполнено(СкладПодразделенияИсполнителя);
		
		Если СталЗаполненСкладМТР ИЛИ СталЗаполненСкладВПодразделении ИЛИ СталЗаполненСкладВПодразделенииИсполнителе Тогда
        	торо_МТОСервер.ОтметитьНеобходимостьАктуализацииЗаказовНаВнутреннееПотреблениеПриИзмененииСклада(Ссылка);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда
		НеУчаствуетВПланировании = Ложь;
		
		ШтрихКод = "";
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
			
		Если ДанныеЗаполнения.Свойство("Основание") Тогда 
			ТиповойОР = ДанныеЗаполнения.Основание; 
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("РодительИерархии") И ЗначениеЗаполнено(ДанныеЗаполнения.РодительИерархии)
			И ТипЗнч(ДанныеЗаполнения.РодительИерархии) = Тип("СправочникСсылка.торо_ОбъектыРемонта") Тогда
			РеквизитыРодителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.РодительИерархии, "Организация, Подразделение", Истина);
			Если НЕ ЗначениеЗаполнено(Организация) Тогда
				Организация = РеквизитыРодителя.Организация;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Подразделение) Тогда
				Подразделение = РеквизитыРодителя.Подразделение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("торо_ИспользоватьДокументыПринятияИСписанияОборудования") И НЕ ЭтоГруппа Тогда
		ЗначениеПеречисления = Перечисления.торо_СтатусыОРВУчете.НеПринятоКУчету;
		Структура = торо_СтатусыОРВУчете.НастройкиСтатуса(ЗначениеПеречисления);
		Если Структура <> Неопределено Тогда
			НеУчаствуетВПланировании = Структура.ЗначениеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда
		
		Если НЕ ЗначениеЗаполнено(Направление) Тогда 
			Направление = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ОсновноеНаправлениеОР");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПлановыйГрафикРаботы) Тогда 
			ПлановыйГрафикРаботы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиТОиР", "ОсновнойГрафикРаботы");
		КонецЕсли;
			
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НеПроверяемыеРеквизиты = Новый Массив;
	
	Если не ЭтоГруппа Тогда
		
		Если ВнешнийОбъект Тогда
			НеПроверяемыеРеквизиты.Добавить("Организация");
			НеПроверяемыеРеквизиты.Добавить("Подразделение");				
		Иначе 
			НеПроверяемыеРеквизиты.Добавить("Контрагент");
			Если Не ПолучитьФункциональнуюОпцию("торо_РасширенныйРежимМТО") Тогда
				НеПроверяемыеРеквизиты.Добавить("Организация");
				НеПроверяемыеРеквизиты.Добавить("Подразделение");			
			КонецЕсли; 
		КонецЕсли;
		
		Если НЕ ПолучитьФункциональнуюОпцию("торо_ИспользоватьФункциональныеМеста") Тогда
			НеПроверяемыеРеквизиты.Добавить("ТипОбъекта");
		КонецЕсли;
		
	Иначе	
		
		НеПроверяемыеРеквизиты.Добавить("Организация");
		НеПроверяемыеРеквизиты.Добавить("Подразделение");
			
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НеПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Нужно проверить, остались ли родители группами.
	// Регистр торо_ОбъектыРемонтаГруппы используется только в иерархиях, неизменяемых документами.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ИерархическиеСтруктурыОР.СтруктураИерархии КАК СтруктураИерархии,
	|	торо_ИерархическиеСтруктурыОР.РодительИерархии КАК РодительИерархии
	|ПОМЕСТИТЬ ВТ_ТаблицаРодителейПоИерархиям
	|ИЗ
	|	РегистрСведений.торо_ИерархическиеСтруктурыОР КАК торо_ИерархическиеСтруктурыОР
	|ГДЕ
	|	торо_ИерархическиеСтруктурыОР.ОбъектИерархии = &ОбъектИерархии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РодительИерархии,
	|	СтруктураИерархии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ТаблицаРодителейПоИерархиям.СтруктураИерархии КАК СтруктураИерархии,
	|	ВТ_ТаблицаРодителейПоИерархиям.РодительИерархии КАК РодительИерархии,
	|	СУММА(ВЫБОР
	|			КОГДА торо_ИерархическиеСтруктурыОР.ОбъектИерархии = &ОбъектИерархии
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК НовоеКоличествоПодчиненных
	|ПОМЕСТИТЬ ВТ_НовоеКоличествоПодчиненных
	|ИЗ
	|	ВТ_ТаблицаРодителейПоИерархиям КАК ВТ_ТаблицаРодителейПоИерархиям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ИерархическиеСтруктурыОР КАК торо_ИерархическиеСтруктурыОР
	|		ПО ВТ_ТаблицаРодителейПоИерархиям.СтруктураИерархии = торо_ИерархическиеСтруктурыОР.СтруктураИерархии
	|			И ВТ_ТаблицаРодителейПоИерархиям.РодительИерархии = торо_ИерархическиеСтруктурыОР.РодительИерархии
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаРодителейПоИерархиям.СтруктураИерархии,
	|	ВТ_ТаблицаРодителейПоИерархиям.РодительИерархии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РодительИерархии,
	|	СтруктураИерархии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НовоеКоличествоПодчиненных.СтруктураИерархии КАК СтруктураИерархии,
	|	ВТ_НовоеКоличествоПодчиненных.РодительИерархии КАК РодительИерархии
	|ИЗ
	|	РегистрСведений.торо_ОбъектыРемонтаГруппы КАК торо_ОбъектыРемонтаГруппы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НовоеКоличествоПодчиненных КАК ВТ_НовоеКоличествоПодчиненных
	|		ПО торо_ОбъектыРемонтаГруппы.ОбъектИерархии = ВТ_НовоеКоличествоПодчиненных.РодительИерархии
	|			И торо_ОбъектыРемонтаГруппы.СтруктураИерархии = ВТ_НовоеКоличествоПодчиненных.СтруктураИерархии
	|ГДЕ
	|	ВТ_НовоеКоличествоПодчиненных.НовоеКоличествоПодчиненных = 0";
	
	Запрос.УстановитьПараметр("ОбъектИерархии", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		торо_РаботаСИерархией20.ИзменитьЗаписьВРСторо_ОбъектыРемонтаГруппы(Выборка.РодительИерархии, Выборка.СтруктураИерархии, Ложь);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
    МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#КонецЕсли