#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	ФорматнаяСтрока = торо_РаботаСПользовательскимПредставлениемПовтИсп.ПолучитьФорматнуюСтроку("Справочники.торо_ИдентификаторыТехКарт");
	Если ФорматнаяСтрока = Неопределено Тогда
		МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	МассивЛокализуемыхПолей = Новый Массив;
	МассивЛокализуемыхПолей.Добавить("Наименование");
	МассивЛокализуемыхПолей.Добавить("НаименованиеПолное");
	МассивЛокализуемыхПолей.Добавить("Описание");
	
	СписокПолей = торо_РаботаСПользовательскимПредставлением.ПолучитьСписокПолей(ФорматнаяСтрока);
	
	Для Каждого Поле Из СписокПолей Цикл
		Поля.Добавить(Поле);
		
		Если МассивЛокализуемыхПолей.Найти(Поле) <> Неопределено Тогда
			Если МультиязычностьСервер.ИспользуетсяПервыйДополнительныйЯзык() Тогда
				Поля.Добавить(Поле + "Язык1");
			КонецЕсли;
			Если МультиязычностьСервер.ИспользуетсяВторойДополнительныйЯзык() Тогда
				Поля.Добавить(Поле +"Язык2");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ФорматнаяСтрока = торо_РаботаСПользовательскимПредставлениемПовтИсп.ПолучитьФорматнуюСтроку("Справочники.торо_ИдентификаторыТехКарт");
	Если ФорматнаяСтрока = Неопределено Тогда
		МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Представление = торо_РаботаСПользовательскимПредставлением.ПолучитьПредставлениеПоФорматнойСтроке(ФорматнаяСтрока, Данные, Истина);
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)  
	
	МультиязычностьСервер.ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка, Метаданные.Справочники.торо_ТехКарты);  
	
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

// Формирует печатные формы
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Техкарта") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
															"Техкарта", 
															"Технологическая карта ремонта", 
															СформироватьПечатнуюФорму (МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Функция СформироватьПечатнуюФорму(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТЕХКАРТА";
	УстановитьПривилегированныйРежим(Истина);
	ТабличныйДокумент = ПечатьТехкарты(МассивОбъектов, ПараметрыПечати);
	УстановитьПривилегированныйРежим(Ложь);
	
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТехкарты(МассивОбъектов, ПараметрыПечати)
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.торо_ТехКарты.ПФ_MXL_Техкарта");
	
	ОбластьЗаголовка = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	торо_ТехКарты.Наименование,
	|	торо_ТехКарты.НормаВремени КАК Продолжительность
	|ИЗ
	|	Справочник.торо_ТехКарты КАК торо_ТехКарты
	|ГДЕ
	|	торо_ТехКарты.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",МассивОбъектов[0]);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ОбластьЗаголовка.Параметры.Заполнить(Выборка);
	ОбластьЗаголовка.Параметры.Продолжительность = торо_ОбщегоНазначенияКлиентСервер.СформироватьЗаголовокПоПродолжительности(Выборка.Продолжительность);
	
	ТабДок.Вывести(ОбластьЗаголовка);
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	МатериальныеЗатраты.ЕдиницаИзмерения,
	|	МатериальныеЗатраты.Количество,
	|	МатериальныеЗатраты.Номенклатура,
	|	МатериальныеЗатраты.Операция,
	|	МатериальныеЗатраты.Качество
	|ПОМЕСТИТЬ ТабМатериальныеЗатраты
	|ИЗ
	|	&ТабМатериальныеЗатраты КАК МатериальныеЗатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабМатериальныеЗатраты.ЕдиницаИзмерения,
	|	ТабМатериальныеЗатраты.Количество,
	|	ТабМатериальныеЗатраты.Номенклатура,
	|	ТабМатериальныеЗатраты.Операция,
	|	ТабМатериальныеЗатраты.Качество
	|ПОМЕСТИТЬ МатериальныеЗатраты
	|ИЗ
	|	ТабМатериальныеЗатраты КАК ТабМатериальныеЗатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ТехКартыМатериальныеЗатраты.ЕдиницаИзмерения,
	|	торо_ТехКартыМатериальныеЗатраты.Количество,
	|	торо_ТехКартыМатериальныеЗатраты.Номенклатура,
	|	торо_ТехКартыМатериальныеЗатраты.Операция,
	|	торо_ТехКартыМатериальныеЗатраты.Качество
	|ИЗ
	|	Справочник.торо_ТехКарты.МатериальныеЗатраты КАК торо_ТехКартыМатериальныеЗатраты
	|ГДЕ
	|	торо_ТехКартыМатериальныеЗатраты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ТрудовыеЗатраты.ВремяРаботы,
	|	торо_ТрудовыеЗатраты.Квалификация,
	|	торо_ТрудовыеЗатраты.Количество,
	|	торо_ТрудовыеЗатраты.Операция
	|ПОМЕСТИТЬ ТабТрудовыеЗатраты
	|ИЗ
	|	&ТабТрудовыеЗатраты КАК торо_ТрудовыеЗатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ТрудовыеЗатраты.ВремяРаботы,
	|	торо_ТрудовыеЗатраты.Квалификация,
	|	торо_ТрудовыеЗатраты.Количество,
	|	торо_ТрудовыеЗатраты.Операция
	|ПОМЕСТИТЬ ТрудовыеЗатраты
	|ИЗ
	|	ТабТрудовыеЗатраты КАК торо_ТрудовыеЗатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ТрудовыеЗатраты.ВремяРаботы,
	|	торо_ТрудовыеЗатраты.Квалификация,
	|	торо_ТрудовыеЗатраты.Количество,
	|	торо_ТрудовыеЗатраты.Операция
	|ИЗ
	|	Справочник.торо_ТехКарты.ТрудовыеЗатраты КАК торо_ТрудовыеЗатраты
	|ГДЕ
	|	торо_ТрудовыеЗатраты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ИнструментыИТехника.ЕдиницаИзмерения,
	|	торо_ИнструментыИТехника.Инструмент,
	|	торо_ИнструментыИТехника.Количество,
	|	торо_ИнструментыИТехника.Операция
	|ПОМЕСТИТЬ ТабИнструментыИТехника
	|ИЗ
	|	&ТабИнструментыИТехника КАК торо_ИнструментыИТехника
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ИнструментыИТехника.ЕдиницаИзмерения,
	|	торо_ИнструментыИТехника.Инструмент,
	|	торо_ИнструментыИТехника.Количество,
	|	торо_ИнструментыИТехника.Операция
	|ПОМЕСТИТЬ ИнструментыИТехника
	|ИЗ
	|	ТабИнструментыИТехника КАК торо_ИнструментыИТехника
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ИнструментыИТехника.ЕдиницаИзмерения,
	|	торо_ИнструментыИТехника.Инструмент,
	|	торо_ИнструментыИТехника.Количество,
	|	торо_ИнструментыИТехника.Операция
	|ИЗ
	|	Справочник.торо_ТехКарты.ИнструментыИТехника КАК торо_ИнструментыИТехника
	|ГДЕ
	|	торо_ИнструментыИТехника.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ИзмеряемыеПоказатели.Показатель,
	|	торо_ИзмеряемыеПоказатели.Операция
	|ПОМЕСТИТЬ ТабИзмеряемыеПоказатели
	|ИЗ
	|	&ТабИзмеряемыеПоказатели КАК торо_ИзмеряемыеПоказатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ИзмеряемыеПоказатели.Показатель,
	|	торо_ИзмеряемыеПоказатели.Операция
	|ПОМЕСТИТЬ ИзмеряемыеПоказатели
	|ИЗ
	|	ТабИзмеряемыеПоказатели КАК торо_ИзмеряемыеПоказатели
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ИзмеряемыеПоказатели.Показатель,
	|	торо_ИзмеряемыеПоказатели.Операция
	|ИЗ
	|	Справочник.торо_ТехКарты.ИзмеряемыеПоказатели КАК торо_ИзмеряемыеПоказатели
	|ГДЕ
	|	торо_ИзмеряемыеПоказатели.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА торо_ТехКартыСписокОпераций.Операция.НаименованиеПолное = """"
	|			ТОГДА торо_ТехКартыСписокОпераций.Операция.Наименование
	|		ИНАЧЕ торо_ТехКартыСписокОпераций.Операция.НаименованиеПолное
	|   КОНЕЦ КАК Операция,
	|	торо_ТехКартыСписокОпераций.НормаВремениВСекундах,
	|	торо_ТехКартыСписокОпераций.СпособВыполнения
	|ПОМЕСТИТЬ ТабОпераций
	|ИЗ
	|	Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	|ГДЕ
	|	торо_ТехКартыСписокОпераций.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабОпераций.Операция КАК Операция,
	|	ТабОпераций.НормаВремениВСекундах КАК Продолжительность,
	|	ТабОпераций.СпособВыполнения КАК Способ,
	|	МатериальныеЗатраты.ЕдиницаИзмерения КАК ЕдИзм,
	|	МатериальныеЗатраты.Количество,
	|	МатериальныеЗатраты.Номенклатура,
	|	""МатериальныеЗатраты"" КАК ДопКолонка,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) * МатериальныеЗатраты.Количество * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) КАК Сумма,
	|	NULL КАК Квалификация,
	|	NULL КАК Время,
	|	NULL КАК Инструмент,
	|	NULL КАК Показатель
	|ИЗ
	|	ТабОпераций КАК ТабОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ МатериальныеЗатраты КАК МатериальныеЗатраты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютСрезПоследних
	|				ПО ЦеныНоменклатурыСрезПоследних.Валюта = КурсыВалютСрезПоследних.Валюта
	|			ПО МатериальныеЗатраты.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		ПО ТабОпераций.Операция = МатериальныеЗатраты.Операция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабОпераций.Операция,
	|	ТабОпераций.НормаВремениВСекундах,
	|	ТабОпераций.СпособВыполнения,
	|	NULL,
	|	ТрудовыеЗатраты.Количество,
	|	NULL,
	|	""ТрудовыеЗатраты"",
	|	ЕСТЬNULL(торо_СтоимостьЧасаКвалификацииСрезПоследних.Стоимость, 0) * ЕСТЬNULL(ТрудовыеЗатраты.Количество, 0) * ЕСТЬNULL(ТрудовыеЗатраты.ВремяРаботы, 0) * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) / 3600,
	|	ТрудовыеЗатраты.Квалификация,
	|	ТрудовыеЗатраты.ВремяРаботы,
	|	NULL,
	|	NULL
	|ИЗ
	|	ТабОпераций КАК ТабОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТрудовыеЗатраты КАК ТрудовыеЗатраты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СтоимостьЧасаКвалификации.СрезПоследних КАК торо_СтоимостьЧасаКвалификацииСрезПоследних
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютСрезПоследних
	|				ПО торо_СтоимостьЧасаКвалификацииСрезПоследних.Валюта = КурсыВалютСрезПоследних.Валюта
	|			ПО ТрудовыеЗатраты.Квалификация = торо_СтоимостьЧасаКвалификацииСрезПоследних.Квалификация
	|		ПО ТабОпераций.Операция = ТрудовыеЗатраты.Операция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабОпераций.Операция,
	|	ТабОпераций.НормаВремениВСекундах,
	|	ТабОпераций.СпособВыполнения,
	|	ИнструментыИТехника.ЕдиницаИзмерения,
	|	ИнструментыИТехника.Количество,
	|	NULL,
	|	""Инструменты"",
	|	NULL,
	|	NULL,
	|	NULL,
	|	ИнструментыИТехника.Инструмент,
	|	NULL
	|ИЗ
	|	ТабОпераций КАК ТабОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИнструментыИТехника КАК ИнструментыИТехника
	|		ПО ТабОпераций.Операция = ИнструментыИТехника.Операция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабОпераций.Операция,
	|	ТабОпераций.НормаВремениВСекундах,
	|	ТабОпераций.СпособВыполнения,
	|	NULL,
	|	NULL,
	|	NULL,
	|	""Показатели"",
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ИзмеряемыеПоказатели.Показатель
	|ИЗ
	|	ТабОпераций КАК ТабОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмеряемыеПоказатели КАК ИзмеряемыеПоказатели
	|		ПО (ИзмеряемыеПоказатели.Операция = ТабОпераций.Операция)
	|ИТОГИ
	|	МИНИМУМ(НормаВремениВСекундах),
	|	МИНИМУМ(СпособВыполнения)
	|ПО
	|	Операция
	|";
	Запрос.УстановитьПараметр("Ссылка",МассивОбъектов[0]);
	Запрос.УстановитьПараметр("ТабМатериальныеЗатраты",МассивОбъектов[0].МатериальныеЗатраты);
	Запрос.УстановитьПараметр("ТабТрудовыеЗатраты",МассивОбъектов[0].ТрудовыеЗатраты);
	Запрос.УстановитьПараметр("ТабИнструментыИТехника",МассивОбъектов[0].ИнструментыИТехника);
	Запрос.УстановитьПараметр("ТабИзмеряемыеПоказатели",МассивОбъектов[0].ИзмеряемыеПоказатели);
	
	ВыборкаОпераций = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьРазделитель 	= Макет.ПолучитьОбласть("ОбластьРазделитель");
	ОбластьОперация 	= Макет.ПолучитьОбласть("ОбластьОперация");
	
	ОбластьШапкаМатериальные = Макет.ПолучитьОбласть("ОбластьШапкаМатериальные");
	ОбластьДанныеМатериальные = Макет.ПолучитьОбласть("ОбластьДанныеМатериальные");
	
	ОбластьШапкаТрудовые = Макет.ПолучитьОбласть("ОбластьШапкаТрудовые");
	ОбластьДанныеТрудовые = Макет.ПолучитьОбласть("ОбластьДанныеТрудовые");
	
	ОбластьШапкаИнструменты = Макет.ПолучитьОбласть("ОбластьШапкаИнструменты");
	ОбластьДанныеИнструменты = Макет.ПолучитьОбласть("ОбластьДанныеИнструменты");
	
	ОбластьШапкаПоказатели = Макет.ПолучитьОбласть("ОбластьШапкаПоказатели");
	ОбластьДанныеПоказатели = Макет.ПолучитьОбласть("ОбластьДанныеПоказатели");
	
	инд0 = 5;
	инд = инд0;
	
	Пока ВыборкаОпераций.Следующий() Цикл
		ТабДок.Вывести(ОбластьРазделитель);
		ОбластьОперация.Параметры.Заполнить(ВыборкаОпераций);
		ОбластьОперация.Параметры.Продолжительность = торо_ОбщегоНазначенияКлиентСервер.СформироватьЗаголовокПоПродолжительности(ВыборкаОпераций.Продолжительность);
		ТабДок.Вывести(ОбластьОперация);
		
		инд0 = инд +6;
		инд = инд0;
		
		Выборка = ВыборкаОпераций.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		СтрПоиска = Новый Структура("ДопКолонка","МатериальныеЗатраты");
		
		ТабДок.Вывести(ОбластьШапкаМатериальные);
		
		Пока Выборка.НайтиСледующий(СтрПоиска) Цикл
			
			ОбластьДанныеМатериальные.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(ОбластьДанныеМатериальные);
			
			инд = инд + 1;
		КонецЦикла;
		
		Область = ТабДок.Область(инд0,3,инд,5);
		Область.Объединить();
		
		Выборка.Сбросить();
		
		СтрПоиска = Новый Структура("ДопКолонка","ТрудовыеЗатраты");
		
		инд0 = инд + 1;
		инд = инд + 1;
		
		ТабДок.Вывести(ОбластьШапкаТрудовые);
		
		Пока Выборка.НайтиСледующий(СтрПоиска) Цикл
			
			ОбластьДанныеТрудовые.Параметры.Заполнить(Выборка);
			ОбластьДанныеТрудовые.Параметры.Время = торо_ОбщегоНазначенияКлиентСервер.СформироватьЗаголовокПоПродолжительности(Выборка.Время);
			ТабДок.Вывести(ОбластьДанныеТрудовые);
			
			инд = инд + 1;
		КонецЦикла;
		
		Область = ТабДок.Область(инд0,3,инд,5);
		Область.Объединить();
		
		Выборка.Сбросить();
		
		СтрПоиска = Новый Структура("ДопКолонка","Инструменты");
		
		инд0 = инд + 1;
		инд = инд + 1;
		
		ТабДок.Вывести(ОбластьШапкаИнструменты);
		
		Пока Выборка.НайтиСледующий(СтрПоиска) Цикл
			
			ОбластьДанныеИнструменты.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(ОбластьДанныеИнструменты);
			
			инд = инд + 1;
		КонецЦикла;
		
		Область = ТабДок.Область(инд0,3,инд,5);
		Область.Объединить();
		
		Выборка.Сбросить();
		
		СтрПоиска = Новый Структура("ДопКолонка","Показатели");
		
		инд0 = инд + 1;
		инд = инд + 1;
		
		ТабДок.Вывести(ОбластьШапкаПоказатели);
		
		Пока Выборка.НайтиСледующий(СтрПоиска) Цикл
			
			ОбластьДанныеПоказатели.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(ОбластьДанныеПоказатели);
			
			инд = инд + 1;
		КонецЦикла;
		
		Область = ТабДок.Область(инд0,3,инд,5);
		Область.Объединить();
		Область.ГраницаСнизу = новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
	КонецЦикла;
	ТабДок.КлючПараметровПечати = "торо_Ремонты_Техкарта";
	ТабДок.ИмяПараметровПечати = "торо_Ремонты_Техкарта";

	Возврат ТабДок;
КонецФункции

#КонецОбласти

#КонецЕсли