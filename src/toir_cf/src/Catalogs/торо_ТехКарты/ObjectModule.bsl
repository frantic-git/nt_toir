#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоНовый() Тогда
		Если Ссылка.СодержитОпасныеОперации <> СодержитОпасныеОперации Тогда
			ДополнительныеСвойства.Вставить("ИзменилосьНаличиеОпасныхОпераций", Истина);
		КонецЕсли;
		
		Если Ссылка.СодержитРаботыПовышеннойОпасности <> СодержитРаботыПовышеннойОпасности Тогда
			ДополнительныеСвойства.Вставить("ИзменилосьНаличиеРаботПовышеннойОпасности", Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьЗаписиРегистраСоставИерархическихТехкартРемонтов();
	ОбновитьНаличиеОпасныхОперацийВВышележащихТехКартах();
	торо_ПланированиеРемонтов.ПометитьКОбновлениюПроектныеЗатратыНаРемонтыПриЗаписиТехКарты(Ссылка);
	
КонецПроцедуры

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
    МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьЗаписиРегистраСоставИерархическихТехкартРемонтов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыСписокОпераций.Операция ССЫЛКА Справочник.торо_ИдентификаторыТехКарт
	               |			ТОГДА торо_ВерсииТехКартСрезПоследних.ТехКарта
	               |		ИНАЧЕ торо_ТехКартыСписокОпераций.Операция
	               |	КОНЕЦ КАК Состав,
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыСписокОпераций.Операция ССЫЛКА Справочник.торо_ИдентификаторыТехКарт
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Операция_Техкарта,
	               |	торо_ТехКартыСписокОпераций.Количество
	               |ИЗ
	               |	Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(&ДатаПринятия) КАК торо_ВерсииТехКартСрезПоследних
	               |		ПО торо_ТехКартыСписокОпераций.Операция = торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты
	               |ГДЕ
	               |	торо_ТехКартыСписокОпераций.Ссылка = &Ссылка
	               |	И торо_ТехКартыСписокОпераций.Операция ССЫЛКА Справочник.торо_ИдентификаторыТехКарт";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("ДатаПринятия", новый Граница(ДатаПринятия,ВидГраницы.Включая));
	
	ТабДанных = Запрос.Выполнить().Выгрузить();
	ТабЗаписи = ТабДанных.Скопировать();
	
	Для каждого ТекСтрока Из ТабДанных Цикл
		ДополнитьТаблицуТехкарт(ТекСтрока.Состав,ТабЗаписи);
	КонецЦикла;
	
	ТабЗаписи.Свернуть("Состав","Количество");
	
	НаборЗаписей = РегистрыСведений.торо_СоставИерархическихТехкартРемонтов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Техкарта.Установить(Ссылка);
	
	Для каждого ТекСтрока Из ТабЗаписи Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Техкарта = Ссылка;
		Запись.Состав = ТекСтрока.Состав;
		Запись.Количество = ТекСтрока.Количество;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ДополнитьТаблицуТехкарт(техКарта,ТабЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыСписокОпераций.Операция ССЫЛКА Справочник.торо_ИдентификаторыТехКарт
	               |			ТОГДА торо_ВерсииТехКартСрезПоследних.ТехКарта
	               |		ИНАЧЕ торо_ТехКартыСписокОпераций.Операция
	               |	КОНЕЦ КАК Состав,
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыСписокОпераций.Операция ССЫЛКА Справочник.торо_ИдентификаторыТехКарт
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Операция_Техкарта,
	               |	торо_ТехКартыСписокОпераций.Количество
	               |ИЗ
	               |	Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(&ДатаПринятия) КАК торо_ВерсииТехКартСрезПоследних
	               |		ПО торо_ТехКартыСписокОпераций.Операция = торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты
	               |ГДЕ
	               |	торо_ТехКартыСписокОпераций.Ссылка = &Ссылка
	               |	И торо_ТехКартыСписокОпераций.Операция ССЫЛКА Справочник.торо_ИдентификаторыТехКарт";
	
	Запрос.УстановитьПараметр("Ссылка",техКарта);
	Запрос.УстановитьПараметр("ДатаПринятия", новый Граница(ДатаПринятия,ВидГраницы.Включая));
	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТабЗаписи.Добавить(),Выборка);
		ДополнитьТаблицуТехкарт(Выборка.Состав,ТабЗаписи);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьНаличиеОпасныхОперацийВВышележащихТехКартах() 
	
	Если НЕ ДополнительныеСвойства.Свойство("ИзменилосьНаличиеОпасныхОпераций") 
		И НЕ ДополнительныеСвойства.Свойство("ИзменилосьНаличиеРаботПовышеннойОпасности") Тогда
		Возврат;
	КонецЕсли;
		
	МассивТехКартВерхнегоУровня = торо_ТехнологическиеКарты.ПолучитьВерсииТехКартНаОдинУровеньВыше(Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	торо_ТехКарты.Ссылка КАК ТехКарта,
	|	торо_ТехКарты.СодержитОпасныеОперации КАК СодержитОпасныеОперации,
	|	торо_ТехКарты.СодержитРаботыПовышеннойОпасности КАК СодержитРаботыПовышеннойОпасности,
	|	торо_ТехКарты.ДатаПринятия КАК ДатаПринятия
	|ИЗ
	|	Справочник.торо_ТехКарты КАК торо_ТехКарты
	|ГДЕ
	|	торо_ТехКарты.Ссылка В(&МассивВерсий)
	|	И (торо_ТехКарты.СодержитОпасныеОперации <> &СодержитОпасныеОперации
	|			ИЛИ торо_ТехКарты.СодержитРаботыПовышеннойОпасности <> &СодержитРаботыПовышеннойОпасности)";
	
	Запрос.УстановитьПараметр("МассивВерсий", МассивТехКартВерхнегоУровня);
	Запрос.УстановитьПараметр("СодержитОпасныеОперации", СодержитОпасныеОперации);
	Запрос.УстановитьПараметр("СодержитРаботыПовышеннойОпасности", СодержитРаботыПовышеннойОпасности);
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	торо_ТехнологическиеКарты.ОбновитьНаличиеОпасныхОперацийВТехКартах(Выборка, СодержитОпасныеОперации, СодержитРаботыПовышеннойОпасности); 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли