#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоНовый() Тогда
		Если Ссылка.Опасная <> Опасная Тогда
			ДополнительныеСвойства.Вставить("ИзменилосьНаличиеОпасныхОпераций", Истина);
		КонецЕсли;
		
		Если Ссылка.РаботаПовышеннойОпасности <> РаботаПовышеннойОпасности Тогда
			ДополнительныеСвойства.Вставить("ИзменилосьНаличиеРаботПовышеннойОпасности", Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьФлагиПовышеннойОпасностиВТехКартах();
	
КонецПроцедуры

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьФлагиПовышеннойОпасностиВТехКартах()
	
	Если НЕ ДополнительныеСвойства.Свойство("ИзменилосьНаличиеОпасныхОпераций") 
		И НЕ ДополнительныеСвойства.Свойство("ИзменилосьНаличиеРаботПовышеннойОпасности") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	торо_ТехКартыСписокОпераций.Ссылка КАК ТехКарта,
	|	торо_ТехКартыСписокОпераций.Ссылка.СодержитОпасныеОперации КАК СодержитОпасныеОперации,
	|	торо_ТехКартыСписокОпераций.Ссылка.СодержитРаботыПовышеннойОпасности КАК СодержитРаботыПовышеннойОпасности,
	|	торо_ТехКартыСписокОпераций.Ссылка.ДатаПринятия КАК ДатаПринятия
	|ИЗ
	|	Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	|ГДЕ
	|	торо_ТехКартыСписокОпераций.Операция = &Операция
	|	И (торо_ТехКартыСписокОпераций.Ссылка.СодержитОпасныеОперации <> &СодержитОпасныеОперации
	|			ИЛИ торо_ТехКартыСписокОпераций.Ссылка.СодержитРаботыПовышеннойОпасности <> &СодержитРаботыПовышеннойОпасности)";
	
	Запрос.УстановитьПараметр("Операция", Ссылка);
	Запрос.УстановитьПараметр("СодержитОпасныеОперации", Опасная);
	Запрос.УстановитьПараметр("СодержитРаботыПовышеннойОпасности", РаботаПовышеннойОпасности);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	торо_ТехнологическиеКарты.ОбновитьНаличиеОпасныхОперацийВТехКартах(Выборка, Опасная, РаботаПовышеннойОпасности);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли