
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если ВариантОтображенияПоУмолчанию <> Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТипДиаграммы");
	КонецЕсли;
	
	// Не заменять на вызов ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива,
	// т.к. нужно чтобы работало в толстом клиенте.
	Для Каждого ЭлементМассива Из МассивНепроверяемыхРеквизитов Цикл
	
		ПорядковыйНомер = ПроверяемыеРеквизиты.Найти(ЭлементМассива);
		Если ПорядковыйНомер <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПорядковыйНомер);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ВариантОтображенияПоУмолчанию <> Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма Тогда
		ТипДиаграммы = Неопределено;
	КонецЕсли;
	
	Если ВыбратьСортировку <> Перечисления.торо_ВариантыСортировки.Лучшие
		И ВыбратьСортировку <> Перечисления.торо_ВариантыСортировки.Худшие Тогда
		КоличествоВыбираемыхЭлементов = 0;
	КонецЕсли;
	
	Если НЕ (ВариантОтображенияПоУмолчанию = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма
		И НЕ ТипДиаграммы = Перечисления.торо_ТипыДиаграммПоказателей.Измерительная
		И НЕ ТипАнализа = Перечисления.торо_ТипыАнализа.ДинамикаИзменения
		И НЕ ТипДиаграммы.Пустая()) Тогда
		
		ОтображатьЛегенду = Ложь;
		
	КонецЕсли;
	
	Если НЕ (ВариантОтображенияПоУмолчанию = Перечисления.торо_ВариантыОтображенияВариантовАнализа.Диаграмма
		И НЕ ТипДиаграммы = Перечисления.торо_ТипыДиаграммПоказателей.Измерительная
		И НЕ ТипДиаграммы.Пустая()) Тогда
		
		ОтображатьПодписи = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИДПоставляемойМодели = "";
	
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецЕсли

#КонецОбласти