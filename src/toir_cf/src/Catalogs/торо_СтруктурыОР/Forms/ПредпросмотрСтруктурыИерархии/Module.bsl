
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураПараметровИерархии = Новый Структура;
	СтруктураПараметровИерархии.Вставить("ТекСтруктураИерархии", Параметры.Ссылка);
	СтруктураПараметровИерархии.Вставить("ИзменяетсяДокументами", Параметры.ИзменяетсяДокументами);
	СтруктураПараметровИерархии.Вставить("СтроитсяАвтоматически", Параметры.СтроитсяАвтоматически);
	СтруктураПараметровИерархии.Вставить("РеквизитОР", Параметры.РеквизитОР);
	СтруктураПараметровИерархии.Вставить("ТипРеквизитаОР", Параметры.ТипРеквизитаОР);
	СтруктураПараметровИерархии.Вставить("РазрешенВводНовыхОР", Параметры.РазрешенВводНовыхОР);
	СтруктураПараметровИерархии.Вставить("КонечныеЭлементыВДереве", Параметры.КонечныеЭлементыВДереве);
	
	СтруктураПараметровИерархии.Вставить("ИерархическийСправочник", Ложь);
	Если Параметры.СтроитсяАвтоматически И Параметры.ТипРеквизитаОР <> "" Тогда
		СтруктураПараметровИерархии.ИерархическийСправочник = Метаданные.Справочники[Параметры.ТипРеквизитаОР].Иерархический;
	КонецЕсли;
	
	ТекСтруктураИерархии = Параметры.Ссылка;
	
	ВключенИерархическийПросмотр = Истина;
	
	ПустойРодитель = торо_РаботаСИерархией20.ПустойРодительПоСтруктуреИерархии(СтруктураПараметровИерархии);
	торо_РаботаСИерархией20.УстановитьЗапросВСпискеПоИерархии(Список, СтруктураПараметровИерархии, ПустойРодитель);
	торо_РаботаСИерархией20КлиентСервер.НачатьЗаполнениеДереваИерархии(Дерево, СтруктураПараметровИерархии, ПустойРодитель);
	
	ИспользоватьКартинкиТОР = Константы.торо_ИспользоватьКартинкиТиповыхОбъектов.Получить();
	Если ИспользоватьКартинкиТОР Тогда 
		Элементы.Дерево.ПутьКДаннымКартинкиСтроки = "Дерево.КартинкаОтображаемая";
	Иначе 
		Элементы.Дерево.ПутьКДаннымКартинкиСтроки = "Дерево.Картинка";
	КонецЕсли;
	
	Если ИспользоватьКартинкиТОР Тогда
		ОбновитьКартинкиЭлементовДереваНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДерево

&НаКлиенте
Процедура ДеревоПередРазворачиванием(Элемент, Строка, Отказ)
	
	торо_РаботаСИерархией20Клиент.ДеревоПередРазворачиванием(Дерево, Строка, СтруктураПараметровИерархии);
	Если ИспользоватьКартинкиТОР Тогда
		ОбновитьКартинкиЭлементовДереваНаСервере(Строка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	торо_РаботаСИерархией20Клиент.УправлениеОтборомВСписке(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	торо_РаботаСИерархией20Клиент.СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ИспользоватьКартинкиТОР = Константы.торо_ИспользоватьКартинкиТиповыхОбъектов.Получить();
	торо_РаботаСИерархией20.СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки, ИспользоватьКартинкиТОР);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере 
Процедура ОбновитьКартинкиЭлементовДереваНаСервере(ИдентификаторСтроки = 0)
	торо_РаботаСИерархией20.ОбновитьКартинкиЭлементовДереваНаСервере(Дерево, ИдентификаторСтроки);
КонецПроцедуры
#КонецОбласти
