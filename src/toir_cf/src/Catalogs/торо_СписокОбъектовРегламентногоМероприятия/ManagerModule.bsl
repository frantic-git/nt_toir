#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
//
// Параметры:
// КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Список объектов регламентного мероприятия
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Справочник.торо_СписокОбъектовРегламентногоМероприятия";
	КомандаПечати.Идентификатор = "СписокОбъектовРМ";
	КомандаПечати.Представление = НСтр("ru = 'Паспорт списка объектов РМ'");
	КомандаПечати.Обработчик = "торо_Печать.ЗапроситьДополнительныеПараметрыПечатиСпискаОбъектовРМ";
	КомандаПечати.СразуНаПринтер = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
	"НастройкиТОиР",
	"ПечатьДокументовБезПредварительногоПросмотра",
	Ложь);
	
КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СписокОбъектовРМ") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"СписокОбъектовРМ", 
		"Список объектов регламентного мероприятия", 
		ПечатьСпискаОбъектовРМ(МассивОбъектов, ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьСпискаОбъектовРМ(МассивОбъектов, ПараметрыПечати) 
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.торо_СписокОбъектовРегламентногоМероприятия.ПФ_MXL_СписокОбъектовРМ");
	ЭтоПервый = Истина;
	
	ПечатаемыеРазделы = ПараметрыПечати.ПечатаемыеРазделы;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_СписокОбъектовРегламентногоМероприятия.Ссылка КАК Ссылка,
	               |	торо_СписокОбъектовРегламентногоМероприятия.Код КАК Код,
	               |	торо_СписокОбъектовРегламентногоМероприятия.Наименование КАК Наименование,
	               |	торо_СписокОбъектовРегламентногоМероприятия.ИндивидуальноеОбслуживаниеОР КАК ИндивидуальноеОбслуживаниеОР,
	               |	торо_СписокОбъектовРегламентногоМероприятия.Комментарий КАК Комментарий,
	               |	торо_СписокОбъектовРегламентногоМероприятия.Родитель.Наименование КАК Родитель,
	               |	торо_СписокОбъектовРегламентногоМероприятия.ПодразделениеИсполнитель.Наименование КАК РемонтноеПодразделение
	               |ИЗ
	               |	Справочник.торо_СписокОбъектовРегламентногоМероприятия КАК торо_СписокОбъектовРегламентногоМероприятия
	               |ГДЕ
	               |	торо_СписокОбъектовРегламентногоМероприятия.Ссылка В(&МассивОбъектов)";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Если Не ЭтоПервый Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ЭтоПервый = Ложь;
		
		ЗаполнитьИВывестиОбласть(Макет, "ОсновнаяИнформация", ТабличныйДокумент, Выборка);
		
		Если ПечатаемыеРазделы.ВыводитьСписокОбъектов = Истина Тогда
			ПечатьСпискаОбъектовРМ_СписокОбъектовРМ(ТабличныйДокумент, Макет, Выборка.Ссылка);
		КонецЕсли;
		
		Если ПечатаемыеРазделы.ВыводитьМероприятия Или ПечатаемыеРазделы.ВыводитьРегламентныеМероприятия
			Или ПечатаемыеРазделы.ВыводитьСоставТК Или ПечатаемыеРазделы.ВыводитьКоэффициентыРемонтныхОсобенностей Тогда
		    ЗаполнитьИВывестиОбласть(Макет, "ШапкаМероприятий", ТабличныйДокумент);
		КонецЕсли;
		
		Если ПечатаемыеРазделы.ВыводитьМероприятия Тогда
			ПечатьСпискаОбъектовРМ_ЖурналМероприятий(ТабличныйДокумент, Макет, Выборка.Ссылка);
		КонецЕсли;
		
		Если ПечатаемыеРазделы.ВыводитьРегламентныеМероприятия Тогда
			ПечатьСпискаОбъектовРМ_РегламентныеМероприятия(ТабличныйДокумент, Макет, Выборка.Ссылка);
		КонецЕсли;
		
		Если ПечатаемыеРазделы.ВыводитьСоставТК Тогда
			ПечатьСпискаОбъектовРМ_СоставТК(ТабличныйДокумент, Макет, Выборка.Ссылка);
		КонецЕсли;
		
		Если ПечатаемыеРазделы.ВыводитьКоэффициентыРемонтныхОсобенностей Тогда
			ПечатьСпискаОбъектовРМ_КоэффициентыРемонтныхОсобенностей(ТабличныйДокумент, Макет, Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ТабличныйДокумент.КлючПараметровПечати = "торо_ПечатьСпискаОбъектовРМ";
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции

Процедура ПечатьСпискаОбъектовРМ_СписокОбъектовРМ(ТабличныйДокумент, Макет, Ссылка)
	ЗаполнитьИВывестиОбласть(Макет, "ШапкаСписокОбъектов", ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_МаршрутыРегламентныхМероприятий.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_МаршрутыРегламентныхМероприятий.НомерПоПорядку КАК Номер,
	               |	торо_МаршрутыРегламентныхМероприятий.ОбъектРемонта.Код КАК Код,
	               |	торо_МаршрутыРегламентныхМероприятий.ОбъектРемонта.АдресМестонахождения КАК Местонахождение,
	               |	торо_МаршрутыРегламентныхМероприятий.ОбъектРемонта.ИнвентарныйНомер КАК ИнвНомер,
	               |	торо_МаршрутыРегламентныхМероприятий.ОбъектРемонта.ЗаводскойНомер КАК ЗавНомер,
	               |	торо_МаршрутыРегламентныхМероприятий.ОбъектРемонта.ТехНомер КАК ТехНомер
	               |ИЗ
	               |	РегистрСведений.торо_МаршрутыРегламентныхМероприятий КАК торо_МаршрутыРегламентныхМероприятий
	               |ГДЕ
	               |	торо_МаршрутыРегламентныхМероприятий.СписокОбъектов = &Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Номер";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	    Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЗаполнитьИВывестиОбласть(Макет, "ШапкаТаблицыСпискаОбъектов", ТабличныйДокумент);
	ЗаполнитьИВывестиОбласть(Макет, "СтрокаТаблицыСпискаОбъектов", ТабличныйДокумент, Выборка);
	
	Возврат;
КонецПроцедуры

Процедура ПечатьСпискаОбъектовРМ_ЖурналМероприятий(ТабличныйДокумент, Макет, Ссылка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.ID КАК ID,
	|	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.Регистратор КАК Регистратор,
	|	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.ДатаНачала КАК ДатаНачала,
	|	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.ДатаОкончания КАК ДатаОкончания,
	|	торо_ЗавершенныеРемонтныеРаботыСрезПоследних.ВидМероприятия КАК ВидМероприятия
	|ПОМЕСТИТЬ ВТ_ЗавершенныеМероприятия
	|ИЗ
	|	РегистрСведений.торо_ЗавершенныеМероприятия.СрезПоследних(, СписокОбъектов = &СписокОбъектов) КАК торо_ЗавершенныеРемонтныеРаботыСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ВидМероприятия КАК ВидМероприятия,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.СписокОбъектов КАК СписокОбъектов,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID КАК ID,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Отменен КАК Отменен
	|ПОМЕСТИТЬ ВТ_ПлановыеМероприятия
	|ИЗ
	|	РегистрСведений.торо_ПлановыеМероприятия.СрезПоследних(, СписокОбъектов = &СписокОбъектов) КАК торо_ПлановыеРемонтныеРаботыСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ПлановыеМероприятия.ВидМероприятия КАК ВидМероприятия,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачала,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания КАК ДатаОкончания,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки КАК Регистратор
	|ИЗ
	|	ВТ_ПлановыеМероприятия КАК ВТ_ПлановыеМероприятия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗавершенныеМероприятия КАК ВТ_ЗавершенныеМероприятия
	|		ПО ВТ_ПлановыеМероприятия.ID = ВТ_ЗавершенныеМероприятия.ID
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_ПлановыеМероприятия.ID = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|ГДЕ
	|	НЕ ВТ_ПлановыеМероприятия.Отменен
	|	И ВТ_ЗавершенныеМероприятия.Регистратор ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЗавершенныеМероприятия.ВидМероприятия КАК ВидМероприятия,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, ВТ_ЗавершенныеМероприятия.ДатаОкончания) КАК ДатаНачалаФакт,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаОкончания, ВТ_ЗавершенныеМероприятия.ДатаОкончания) КАК ДатаОкончанияФакт,
	|	ВТ_ЗавершенныеМероприятия.Регистратор КАК Регистратор,
	|	ЕСТЬNULL(торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала, ВТ_ЗавершенныеМероприятия.ДатаОкончания) КАК ДатаНачалаПлан,
	|	ЕСТЬNULL(торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания, ВТ_ЗавершенныеМероприятия.ДатаОкончания) КАК ДатаОкончанияПлан
	|ИЗ
	|	ВТ_ЗавершенныеМероприятия КАК ВТ_ЗавершенныеМероприятия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_ЗавершенныеМероприятия.ID = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СвернутыеФактическиеДатыРемонтов КАК торо_СвернутыеФактическиеДатыРемонтов
	|		ПО ВТ_ЗавершенныеМероприятия.ID = торо_СвернутыеФактическиеДатыРемонтов.IDРемонта";
	Запрос.УстановитьПараметр("СписокОбъектов", Ссылка);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ЗаполнитьИВывестиОбласть(Макет, "ТаблицаПредстоящихМероприятий", ТабличныйДокумент);
	
	Если Не РезультатЗапроса[2].Пустой() Тогда
		ВыборкаПредстоящиеРемонты = РезультатЗапроса[2].Выбрать();
		
		ЗаполнитьИВывестиОбласть(Макет, "ШапкаТаблицыПредстоящихМероприятий", ТабличныйДокумент);
		ЗаполнитьИВывестиОбласть(Макет, "СтрокаТаблицыПредстоящихМероприятий", ТабличныйДокумент, ВыборкаПредстоящиеРемонты);
	КонецЕсли;
	
	ЗаполнитьИВывестиОбласть(Макет, "ТаблицаВыполненныхМероприятий", ТабличныйДокумент);
	
	Если Не РезультатЗапроса[3].Пустой() Тогда
		ВыборкаВыполненныеРемонты = РезультатЗапроса[3].Выбрать();
		
		ЗаполнитьИВывестиОбласть(Макет, "ШапкаТаблицыВыполненныхМероприятий", ТабличныйДокумент);
		ЗаполнитьИВывестиОбласть(Макет, "СтрокаТаблицыВыполненныхМероприятий", ТабличныйДокумент, ВыборкаВыполненныеРемонты);
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

Процедура ПечатьСпискаОбъектовРМ_РегламентныеМероприятия(ТабличныйДокумент, Макет, Ссылка)
	ЗаполнитьИВывестиОбласть(Макет, "ШапкаРегламентныеМероприятия", ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия КАК ВидМероприятия,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.НормативныйРемонт КАК НормативныйРемонт,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.СпособПланирования КАК СпособПланирования,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.СпособВыполнения КАК СпособВыполнения
	               |ПОМЕСТИТЬ ВТ_РегламентныеМероприятия
	               |ИЗ
	               |	РегистрСведений.торо_РегламентныеМероприятияИСпискиОбъектов КАК торо_РегламентныеМероприятияИСпискиОбъектов
	               |ГДЕ
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов = &СписокОбъектов
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидМероприятия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ПредшествующиеРегламентныеМероприятия.ВидМероприятия КАК ВидМероприятия,
	               |	торо_ПредшествующиеРегламентныеМероприятия.ПредшествующийВидМероприятия КАК ПредшествующийВидМероприятия
	               |ПОМЕСТИТЬ ВТ_ПредшествующиеМероприятия
	               |ИЗ
	               |	РегистрСведений.торо_ПредшествующиеРегламентныеМероприятия КАК торо_ПредшествующиеРегламентныеМероприятия
	               |ГДЕ
	               |	торо_ПредшествующиеРегламентныеМероприятия.СписокОбъектов = &СписокОбъектов
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидМероприятия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ВыходныеДокументы.ВидМероприятия КАК ВидМероприятия,
	               |	торо_ВыходныеДокументы.ВыходнойДокумент КАК ВыходнойДокумент
	               |ПОМЕСТИТЬ ВТ_ВыходныеДокументы
	               |ИЗ
	               |	РегистрСведений.торо_ВыходныеДокументы КАК торо_ВыходныеДокументы
	               |ГДЕ
	               |	торо_ВыходныеДокументы.СписокОбъектов = &СписокОбъектов
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидМероприятия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_РегламентныеМероприятия.ВидМероприятия КАК ВидМероприятия,
	               |	ВТ_РегламентныеМероприятия.НормативныйРемонт КАК НормативныйРемонт,
	               |	ВТ_РегламентныеМероприятия.СпособПланирования КАК СпособПланирования,
	               |	ВТ_РегламентныеМероприятия.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	ВТ_РегламентныеМероприятия.СпособВыполнения КАК СпособВыполнения,
	               |	ВТ_ПредшествующиеМероприятия.ПредшествующийВидМероприятия КАК ПредшествующийВидМероприятия,
	               |	NULL КАК ВыходнойДокумент,
	               |	""ПредшествующиеМероприятия"" КАК ИмяРаздела
	               |ПОМЕСТИТЬ ВТ_ИтоговаяТаблица
	               |ИЗ
	               |	ВТ_РегламентныеМероприятия КАК ВТ_РегламентныеМероприятия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредшествующиеМероприятия КАК ВТ_ПредшествующиеМероприятия
	               |		ПО ВТ_РегламентныеМероприятия.ВидМероприятия = ВТ_ПредшествующиеМероприятия.ВидМероприятия
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_РегламентныеМероприятия.ВидМероприятия,
	               |	ВТ_РегламентныеМероприятия.НормативныйРемонт,
	               |	ВТ_РегламентныеМероприятия.СпособПланирования,
	               |	ВТ_РегламентныеМероприятия.ГрафикРемонтныхРабот,
	               |	ВТ_РегламентныеМероприятия.СпособВыполнения,
	               |	NULL,
	               |	ВТ_ВыходныеДокументы.ВыходнойДокумент,
	               |	""ВыходныеДокументы""
	               |ИЗ
	               |	ВТ_РегламентныеМероприятия КАК ВТ_РегламентныеМероприятия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыходныеДокументы КАК ВТ_ВыходныеДокументы
	               |		ПО ВТ_РегламентныеМероприятия.ВидМероприятия = ВТ_ВыходныеДокументы.ВидМероприятия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ИтоговаяТаблица.ВидМероприятия КАК ВидМероприятия,
	               |	ВТ_ИтоговаяТаблица.НормативныйРемонт КАК ТехнологическаяКарта,
	               |	ВТ_ИтоговаяТаблица.СпособПланирования КАК СпособПланирования,
	               |	ВТ_ИтоговаяТаблица.ГрафикРемонтныхРабот КАК ГрафикРемонтныхРабот,
	               |	ВТ_ИтоговаяТаблица.СпособВыполнения КАК СпособВыполнения,
	               |	ВТ_ИтоговаяТаблица.ПредшествующийВидМероприятия КАК ПредшествующийВидМероприятия,
	               |	ВТ_ИтоговаяТаблица.ВыходнойДокумент КАК ВыходнойДокумент,
	               |	ВТ_ИтоговаяТаблица.ИмяРаздела КАК ИмяРаздела
	               |ИЗ
	               |	ВТ_ИтоговаяТаблица КАК ВТ_ИтоговаяТаблица
	               |ИТОГИ
	               |	МАКСИМУМ(ТехнологическаяКарта),
	               |	МАКСИМУМ(СпособПланирования),
	               |	МАКСИМУМ(ГрафикРемонтныхРабот),
	               |	МАКСИМУМ(СпособВыполнения)
	               |ПО
	               |	ВидМероприятия,
	               |	ИмяРаздела";
	
	Запрос.УстановитьПараметр("СписокОбъектов", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаРегламентныеМероприятия = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегламентныеМероприятия.Следующий() Цикл
		ШапкаВидаМероприятия = Макет.ПолучитьОбласть("ШапкаВидаМероприятия");
		ШапкаВидаМероприятия.Параметры.ВидМероприятия = торо_ЗаполнениеДокументов.ПолучитьПредоставленияВРДляПечати(ВыборкаРегламентныеМероприятия.ВидМероприятия);
		ТабличныйДокумент.Вывести(ШапкаВидаМероприятия);
		
		ЗаполнитьИВывестиОбласть(Макет, "ИнформацияОВидеМероприятия", ТабличныйДокумент, ВыборкаРегламентныеМероприятия);
		
		ВыборкаПредшествующийИВыходной = ВыборкаРегламентныеМероприятия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПредшествующийИВыходной.Следующий() Цикл
			Если ВыборкаПредшествующийИВыходной.ИмяРаздела = "ПредшествующиеМероприятия" Тогда
				ШапкаТаблицыРаздела = Макет.ПолучитьОбласть("ШапкаПредшествующиеМероприятия");
			Иначе
				ШапкаТаблицыРаздела = Макет.ПолучитьОбласть("ШапкаВыходныеДокументы");
			КонецЕсли;
			
			ЕстьДанные = Ложь;
		    ВыборкаДополнительнаяИнформацияВидаМероприятия = ВыборкаПредшествующийИВыходной.Выбрать();
			Пока ВыборкаДополнительнаяИнформацияВидаМероприятия.Следующий() Цикл
				Если ВыборкаДополнительнаяИнформацияВидаМероприятия.ИмяРаздела = "ПредшествующиеМероприятия" И Не ЗначениеЗаполнено(ВыборкаДополнительнаяИнформацияВидаМероприятия.ПредшествующийВидМероприятия)
					Или ВыборкаДополнительнаяИнформацияВидаМероприятия.ИмяРаздела = "ВыходныеДокументы" И Не ЗначениеЗаполнено(ВыборкаДополнительнаяИнформацияВидаМероприятия.ВыходнойДокумент) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ВыборкаДополнительнаяИнформацияВидаМероприятия.ИмяРаздела = "ПредшествующиеМероприятия" Тогда
					ИмяСтрокиТаблицыРаздела = "СтрокаПредшествующийВидМероприятия";
				Иначе
					ИмяСтрокиТаблицыРаздела = "СтрокаТаблицыВыходнойДокумент";
				КонецЕсли;
				
				ЗаполнитьИВывестиОбласть(Макет, ИмяСтрокиТаблицыРаздела, ШапкаТаблицыРаздела, ВыборкаДополнительнаяИнформацияВидаМероприятия);
				
				ЕстьДанные = Истина;
			КонецЦикла;
			
			Если ЕстьДанные Тогда
			    ТабличныйДокумент.Вывести(ШапкаТаблицыРаздела);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат;
КонецПроцедуры

Процедура ПечатьСпискаОбъектовРМ_СоставТК(ТабличныйДокумент, Макет, Ссылка)
	ЗаполнитьИВывестиОбласть(Макет, "СоставТехнологическихКарт", ТабличныйДокумент);
	
	РезультатыЗапроса = ПолучитьВидыРемонтаРМ(Ссылка);
	
	ХарактеристикиНоменклатурыИспользуются = Константы.торо_ИспользоватьХарактеристикиНоменклатуры.Получить();
	
	МассивВыведенныхТК = Новый Массив;
	ВыборкаВидовМероприятий = РезультатыЗапроса.РезультатЗапроса;
	
	Пока ВыборкаВидовМероприятий.Следующий() Цикл
		ЗаполнитьИВывестиОбласть(Макет, "ШапкаТаблицыТехнологическойКарты", ТабличныйДокумент);
		ЗаполнитьИВывестиОбласть(Макет, "СтрокаТаблицыТехнологическойКарты", ТабличныйДокумент, ВыборкаВидовМероприятий);
		
		МассивВыведенныхТК.Добавить(ВыборкаВидовМероприятий.ТехнологическаяКарта);
		ПодготовитьВложенныеТК(РезультатыЗапроса.ТаблицаВложенныхТК, ВыборкаВидовМероприятий.ТехнологическаяКарта, ВыборкаВидовМероприятий.ВидМероприятия, Макет, ТабличныйДокумент);
		
		ИмяПоследнейОбласти = "";
		
		ВыборкаОпераций = ВыборкаВидовМероприятий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОпераций.Следующий() Цикл
			Если Не ВыборкаОпераций.ДопКолонка = "МерыБезопасности" Тогда
				ЗаполнитьИВывестиОбласть(Макет, "ОперацияТК", ТабличныйДокумент, ВыборкаОпераций);
			КонецЕсли;
			
			ВыборкаВидДетальнойИнформации = ВыборкаОпераций.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаВидДетальнойИнформации.Следующий() Цикл
				ВыборкаДетальнаяИнформация = ВыборкаВидДетальнойИнформации.Выбрать();
				Если ВыборкаВидДетальнойИнформации.ДопКолонка = "МатериальныеЗатраты" Тогда
					ИмяОбластиШапки = ?(ХарактеристикиНоменклатурыИспользуются, "ШапкаТаблицыМатериальныхЗатратТОСХарактеристиками", "ШапкаТаблицыМатериальныхЗатратТО");
					ИмяОбластиСтроки = ?(ХарактеристикиНоменклатурыИспользуются, "СтрокаТаблицыМатериальныхЗатратТОСХарактеристиками", "СтрокаТаблицыМатериальныхЗатратТО");
				ИначеЕсли ВыборкаВидДетальнойИнформации.ДопКолонка = "Трудозатраты" Тогда
					ИмяОбластиШапки = "ШапкаТаблицыТрудозатратТО";
					ИмяОбластиСтроки = "СтрокаТаблицыТрудозатратТО";
				ИначеЕсли ВыборкаВидДетальнойИнформации.ДопКолонка = "Инструменты" Тогда
					ИмяОбластиШапки = "ШапкаТаблицыИнструментовТО";
					ИмяОбластиСтроки = "СтрокаТаблицыИнструментовТО";
				ИначеЕсли ВыборкаВидДетальнойИнформации.ДопКолонка = "Показатели" Тогда
				    ИмяОбластиШапки = "ШапкаТаблицыПоказателейТО";
					ИмяОбластиСтроки = "СтрокаТаблицыПоказателейТО";
				ИначеЕсли ВыборкаВидДетальнойИнформации.ДопКолонка = "МерыБезопасности" Тогда
				    ИмяОбластиШапки = "ШапкаТаблицыИнструкцийТК";
					ИмяОбластиСтроки = "СтрокаТаблицыИнструкцийТК";
				КонецЕсли;
				
				ЗаполнитьИВывестиОбласть(Макет, ИмяОбластиШапки, ТабличныйДокумент);
				ЗаполнитьИВывестиОбласть(Макет, ИмяОбластиСтроки, ТабличныйДокумент, ВыборкаДетальнаяИнформация);
				
				ИмяПоследнейОбласти = ИмяОбластиСтроки;
			КонецЦикла;
		КонецЦикла;
	
		Если ИмяПоследнейОбласти = "СтрокаТаблицыИнструкцийТК" Тогда
			ЗаполнитьИВывестиОбласть(Макет, "ГраницаДляИнструкцийТК", ТабличныйДокумент);
		ИначеЕсли ЗначениеЗаполнено(ИмяПоследнейОбласти) Тогда
			ЗаполнитьИВывестиОбласть(Макет, "ГраницаДляСоставаТК", ТабличныйДокумент);
		КонецЕсли;
			
	КонецЦикла;
	
	ЗаголовокТаблицыТехкарты = Макет.ПолучитьОбласть("ШапкаТаблицыТехнологическойКарты");
	ТаблВидыРемонта = Макет.ПолучитьОбласть("СтрокаТаблицыТехнологическойКарты");
	ВывестиТКБезОпераций(МассивВыведенныхТК, РезультатыЗапроса.ТаблицаВложенныхТК, Макет, ТабличныйДокумент, ЗаголовокТаблицыТехкарты, ТаблВидыРемонта);	
КонецПроцедуры

Функция ПолучитьВидыРемонтаРМ(Ссылка)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_СтоимостьЧасаКвалификацииСрезПоследних.Стоимость КАК Стоимость,
	               |	торо_СтоимостьЧасаКвалификацииСрезПоследних.Квалификация КАК Квалификация,
	               |	торо_СтоимостьЧасаКвалификацииСрезПоследних.Валюта КАК Валюта
	               |ПОМЕСТИТЬ ВТ_СтоимостьЧасаКвалификацииСрезПоследних
	               |ИЗ
	               |	РегистрСведений.торо_СтоимостьЧасаКвалификации.СрезПоследних КАК торо_СтоимостьЧасаКвалификацииСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
	               |	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	               |	ЦеныНоменклатурыСрезПоследних.Валюта КАК Валюта
	               |ПОМЕСТИТЬ ВТ_ЦеныНоменклатурыСрезПоследних
	               |ИЗ
	               |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ТекущаяДата, ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК Курс,
	               |	КурсыВалютСрезПоследних.Валюта КАК Валюта
	               |ПОМЕСТИТЬ ВТ_КурсыВалютСрезПоследних
	               |ИЗ
	               |	РегистрСведений.КурсыВалют.СрезПоследних(&ТекущаяДата, ) КАК КурсыВалютСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ВТ_ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
	               |	ВТ_ЦеныНоменклатурыСрезПоследних.Цена * ВТ_КурсыВалютСрезПоследних.Курс КАК Цена
	               |ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
	               |ИЗ
	               |	ВТ_ЦеныНоменклатурыСрезПоследних КАК ВТ_ЦеныНоменклатурыСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КурсыВалютСрезПоследних КАК ВТ_КурсыВалютСрезПоследних
	               |		ПО ВТ_ЦеныНоменклатурыСрезПоследних.Валюта = ВТ_КурсыВалютСрезПоследних.Валюта
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	Характеристика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_СтоимостьЧасаКвалификацииСрезПоследних.Квалификация КАК Квалификация,
	               |	ВТ_СтоимостьЧасаКвалификацииСрезПоследних.Стоимость * ВТ_КурсыВалютСрезПоследних.Курс КАК Стоимость
	               |ПОМЕСТИТЬ ВТ_СтоимостьЧасаКвалификации
	               |ИЗ
	               |	ВТ_СтоимостьЧасаКвалификацииСрезПоследних КАК ВТ_СтоимостьЧасаКвалификацииСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КурсыВалютСрезПоследних КАК ВТ_КурсыВалютСрезПоследних
	               |		ПО ВТ_СтоимостьЧасаКвалификацииСрезПоследних.Валюта = ВТ_КурсыВалютСрезПоследних.Валюта
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Квалификация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_СтоимостьЧасаКвалификацииСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_ЦеныНоменклатурыСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_КурсыВалютСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов КАК СписокОбъектов,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия КАК ВидМероприятия,
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.НормативныйРемонт КАК НормативныйРемонт
	               |ПОМЕСТИТЬ ВТ_РегламентныеМероприятия
	               |ИЗ
	               |	РегистрСведений.торо_РегламентныеМероприятияИСпискиОбъектов КАК торо_РегламентныеМероприятияИСпискиОбъектов
	               |ГДЕ
	               |	торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов В(&СписокОбъектов)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныйРемонт
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ВерсииТехКартСрезПоследних.ТехКарта КАК ТехКарта,
	               |	торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты КАК ИдентификаторТехКарты
	               |ПОМЕСТИТЬ ВТ_ВерсииТехКарт
	               |ИЗ
	               |	РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(
	               |			,
	               |			ИдентификаторТехКарты В
	               |				(ВЫБРАТЬ
	               |					ВТ_РегламентныеМероприятия.НормативныйРемонт
	               |				ИЗ
	               |					ВТ_РегламентныеМероприятия КАК ВТ_РегламентныеМероприятия)) КАК торо_ВерсииТехКартСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ТехКарта,
	               |	ИдентификаторТехКарты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ТехКартыСписокОпераций.Ссылка КАК ТехКарта,
	               |	торо_ТехКартыСписокОпераций.Продолжительность КАК Продолжительность,
	               |	торо_ТехКартыСписокОпераций.ID КАК ID,
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыСписокОпераций.Операция.НаименованиеПолное = """"
	               |				ИЛИ торо_ТехКартыСписокОпераций.Операция.НаименованиеПолное ЕСТЬ NULL
	               |			ТОГДА торо_ТехКартыСписокОпераций.Операция.Наименование
	               |		ИНАЧЕ торо_ТехКартыСписокОпераций.Операция.НаименованиеПолное
	               |	КОНЕЦ КАК Операция,
	               |	торо_ТехКартыСписокОпераций.Ссылка КАК Ссылка,
	               |	торо_ТехКартыСписокОпераций.СпособВыполнения КАК СпособВыполнения,
	               |	торо_ТехКартыСписокОпераций.Количество КАК Кратность,
	               |	ВТ_РегламентныеМероприятия.ВидМероприятия КАК ВидМероприятия,
	               |	ВТ_РегламентныеМероприятия.НормативныйРемонт КАК НормативныйРемонт
	               |ПОМЕСТИТЬ ВТ_СписокОпераций
	               |ИЗ
	               |	ВТ_РегламентныеМероприятия КАК ВТ_РегламентныеМероприятия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВерсииТехКарт КАК ВТ_ВерсииТехКарт
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	               |			ПО ВТ_ВерсииТехКарт.ТехКарта = торо_ТехКартыСписокОпераций.Ссылка
	               |		ПО ВТ_РегламентныеМероприятия.НормативныйРемонт = ВТ_ВерсииТехКарт.ИдентификаторТехКарты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Ссылка,
	               |	ID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_СписокОпераций.ВидМероприятия КАК ВидМероприятия,
	               |	ВТ_СписокОпераций.НормативныйРемонт КАК ТехнологическаяКарта,
	               |	ВТ_СписокОпераций.Операция КАК Операция,
	               |	ВТ_СписокОпераций.Продолжительность КАК Продолжительность,
	               |	ВТ_СписокОпераций.СпособВыполнения КАК СпособВыполнения,
	               |	""МатериальныеЗатраты"" КАК ДопКолонка,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура КАК Номенклатура,
	               |	торо_ТехКартыМатериальныеЗатраты.КоличествоУпаковок КАК Количество,
	               |	торо_ТехКартыМатериальныеЗатраты.Характеристика КАК Характеристика,
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыМатериальныеЗатраты.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	               |			ТОГДА торо_ТехКартыМатериальныеЗатраты.Номенклатура.ЕдиницаИзмерения
	               |		ИНАЧЕ торо_ТехКартыМатериальныеЗатраты.Упаковка
	               |	КОНЕЦ КАК ЕдиницаИзмерения,
	               |	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Цена, 0) * торо_ТехКартыМатериальныеЗатраты.Количество КАК Сумма,
	               |	NULL КАК Квалификация,
	               |	NULL КАК ВремяРаботы,
	               |	NULL КАК Инструмент,
	               |	NULL КАК Показатель,
	               |	NULL КАК МерыБезопасности,
	               |	NULL КАК НомерСтроки
	               |ИЗ
	               |	Справочник.торо_ТехКарты.МатериальныеЗатраты КАК торо_ТехКартыМатериальныеЗатраты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОпераций КАК ВТ_СписокОпераций
	               |		ПО торо_ТехКартыМатериальныеЗатраты.Ссылка = ВТ_СписокОпераций.Ссылка
	               |			И торо_ТехКартыМатериальныеЗатраты.ID = ВТ_СписокОпераций.ID
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныНоменклатуры КАК ВТ_ЦеныНоменклатуры
	               |		ПО торо_ТехКартыМатериальныеЗатраты.Номенклатура = ВТ_ЦеныНоменклатуры.Номенклатура
	               |			И торо_ТехКартыМатериальныеЗатраты.Характеристика = ВТ_ЦеныНоменклатуры.Характеристика
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_СписокОпераций.ВидМероприятия,
	               |	ВТ_СписокОпераций.НормативныйРемонт,
	               |	ВТ_СписокОпераций.Операция,
	               |	ВТ_СписокОпераций.Продолжительность,
	               |	ВТ_СписокОпераций.СпособВыполнения,
	               |	""Трудозатраты"",
	               |	NULL,
	               |	торо_ТехКартыТрудовыеЗатраты.Количество,
	               |	NULL,
	               |	NULL,
	               |	ВЫРАЗИТЬ(ЕСТЬNULL(ВТ_СтоимостьЧасаКвалификации.Стоимость, 0) * торо_ТехКартыТрудовыеЗатраты.Количество * торо_ТехКартыТрудовыеЗатраты.ВремяРаботы / 3600 КАК ЧИСЛО(10, 2)),
	               |	торо_ТехКартыТрудовыеЗатраты.Квалификация,
	               |	торо_ТехКартыТрудовыеЗатраты.ВремяРаботы,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL
	               |ИЗ
	               |	Справочник.торо_ТехКарты.ТрудовыеЗатраты КАК торо_ТехКартыТрудовыеЗатраты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОпераций КАК ВТ_СписокОпераций
	               |		ПО торо_ТехКартыТрудовыеЗатраты.Ссылка = ВТ_СписокОпераций.Ссылка
	               |			И торо_ТехКартыТрудовыеЗатраты.ID = ВТ_СписокОпераций.ID
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьЧасаКвалификации КАК ВТ_СтоимостьЧасаКвалификации
	               |		ПО торо_ТехКартыТрудовыеЗатраты.Квалификация = ВТ_СтоимостьЧасаКвалификации.Квалификация
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_СписокОпераций.ВидМероприятия,
	               |	ВТ_СписокОпераций.НормативныйРемонт,
	               |	ВТ_СписокОпераций.Операция,
	               |	ВТ_СписокОпераций.Продолжительность,
	               |	ВТ_СписокОпераций.СпособВыполнения,
	               |	""Инструменты"",
	               |	NULL,
	               |	торо_ТехКартыИнструментыИТехника.КоличествоУпаковок,
	               |	NULL,
	               |	ВЫБОР
	               |		КОГДА торо_ТехКартыИнструментыИТехника.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	               |			ТОГДА торо_ТехКартыИнструментыИТехника.Инструмент.ЕдиницаИзмерения
	               |		ИНАЧЕ торо_ТехКартыИнструментыИТехника.Упаковка
	               |	КОНЕЦ,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	торо_ТехКартыИнструментыИТехника.Инструмент,
	               |	NULL,
	               |	NULL,
	               |	NULL
	               |ИЗ
	               |	Справочник.торо_ТехКарты.ИнструментыИТехника КАК торо_ТехКартыИнструментыИТехника
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОпераций КАК ВТ_СписокОпераций
	               |		ПО торо_ТехКартыИнструментыИТехника.Ссылка = ВТ_СписокОпераций.Ссылка
	               |			И торо_ТехКартыИнструментыИТехника.ID = ВТ_СписокОпераций.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_СписокОпераций.ВидМероприятия,
	               |	ВТ_СписокОпераций.НормативныйРемонт,
	               |	ВТ_СписокОпераций.Операция,
	               |	ВТ_СписокОпераций.Продолжительность,
	               |	ВТ_СписокОпераций.СпособВыполнения,
	               |	""Показатели"",
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	торо_ТехКартыИзмеряемыеПоказатели.Показатель,
	               |	NULL,
	               |	NULL
	               |ИЗ
	               |	Справочник.торо_ТехКарты.ИзмеряемыеПоказатели КАК торо_ТехКартыИзмеряемыеПоказатели
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОпераций КАК ВТ_СписокОпераций
	               |		ПО торо_ТехКартыИзмеряемыеПоказатели.Ссылка = ВТ_СписокОпераций.Ссылка
	               |			И торо_ТехКартыИзмеряемыеПоказатели.ID = ВТ_СписокОпераций.ID
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_СписокОпераций.ВидМероприятия,
	               |	ВТ_СписокОпераций.НормативныйРемонт,
	               |	ВТ_СписокОпераций.Операция,
	               |	NULL,
	               |	NULL,
	               |	""МерыБезопасности"",
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	торо_ТехКартыМерыБезопасности.МераБезопасности,
	               |	торо_ТехКартыМерыБезопасности.НомерСтроки
	               |ИЗ
	               |	Справочник.торо_ТехКарты.МерыБезопасности КАК торо_ТехКартыМерыБезопасности
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОпераций КАК ВТ_СписокОпераций
	               |		ПО торо_ТехКартыМерыБезопасности.Ссылка = ВТ_СписокОпераций.Ссылка
	               |			И торо_ТехКартыМерыБезопасности.ID = ВТ_СписокОпераций.ID
	               |ИТОГИ
	               |	МАКСИМУМ(ТехнологическаяКарта),
	               |	МАКСИМУМ(Продолжительность),
	               |	МАКСИМУМ(СпособВыполнения),
	               |	СУММА(Сумма)
	               |ПО
	               |	ВидМероприятия,
	               |	Операция,
	               |	ДопКолонка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫБОР
	               |		КОГДА ТИПЗНАЧЕНИЯ(торо_ТехКартыСписокОпераций.Операция) = ТИП(Справочник.торо_ИдентификаторыТехКарт)
	               |			ТОГДА торо_ТехКартыСписокОпераций.Операция
	               |		ИНАЧЕ NULL
	               |	КОНЕЦ КАК НормативныйРемонт,
	               |	ВТ_РегламентныеМероприятия.СписокОбъектов КАК СписокОбъектов,
	               |	ВТ_РегламентныеМероприятия.ВидМероприятия КАК ВидМероприятия,
	               |	ВТ_РегламентныеМероприятия.НормативныйРемонт КАК Владелец
	               |ПОМЕСТИТЬ СписокТКПодготовка
	               |ИЗ
	               |	ВТ_РегламентныеМероприятия КАК ВТ_РегламентныеМероприятия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВерсииТехКарт КАК ВТ_ВерсииТехКарт
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.СписокОпераций КАК торо_ТехКартыСписокОпераций
	               |			ПО ВТ_ВерсииТехКарт.ТехКарта = торо_ТехКартыСписокОпераций.Ссылка
	               |		ПО ВТ_РегламентныеМероприятия.НормативныйРемонт = ВТ_ВерсииТехКарт.ИдентификаторТехКарты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокТКПодготовка.НормативныйРемонт КАК НормативныйРемонт,
	               |	СписокТКПодготовка.ВидМероприятия КАК ВидМероприятия,
	               |	СписокТКПодготовка.СписокОбъектов КАК СписокОбъектов,
	               |	СписокТКПодготовка.Владелец КАК Владелец
	               |ИЗ
	               |	СписокТКПодготовка КАК СписокТКПодготовка
	               |ГДЕ
	               |	НЕ СписокТКПодготовка.НормативныйРемонт ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("СписокОбъектов", Ссылка);
	Запрос.УстановитьПараметр("ВидЦены", Константы.торо_ТипЦеныДляРасчетаСебестоимостиРемонта.Получить());
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаПервогоУровня = Результат[11].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ВыборкаТК = Результат[13].Выгрузить();
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("РезультатЗапроса", ВыборкаПервогоУровня);
	СтруктураВозврата.Вставить("ТаблицаВложенныхТК", ВыборкаТК);
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ВывестиТКБезОпераций(МассивВыведенныхТК, ТаблицаВложенныхТК, Макет, ТабДок, ЗаголовокТаблицыТехкарты, ТаблВидыРемонта)
	Для Каждого Значение Из МассивВыведенныхТК Цикл
		ВыведенныеТК = ТаблицаВложенныхТК.НайтиСтроки(Новый Структура("Владелец", Значение));
		Для Каждого ЭлементНаУдаление Из ВыведенныеТК Цикл
			ТаблицаВложенныхТК.Удалить(ЭлементНаУдаление);
		КонецЦикла;
	КонецЦикла;
	
	ОставшиесяТК = Новый ТаблицаЗначений;
	ОставшиесяТК.Колонки.Добавить("НормативныйРемонт", Новый ОписаниеТипов("СправочникСсылка.торо_ИдентификаторыТехКарт"));
	ОставшиесяТК.Колонки.Добавить("ВидМероприятия", Новый ОписаниеТипов("СправочникСсылка.торо_ВидыРемонтов"));
	ОставшиесяТК.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Строка"));
	
	Для Каждого Значение Из ТаблицаВложенныхТК Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("НормативныйРемонт", Значение.Владелец);
		СтруктураПоиска.Вставить("ВидМероприятия", Значение.ВидМероприятия);
		Если ОставшиесяТК.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
			СтрокаОставшихся = ОставшиесяТК.Добавить();
			СтрокаОставшихся.НормативныйРемонт = Значение.Владелец;
			СтрокаОставшихся.ВидМероприятия = Значение.ВидМероприятия;
			СтрокаОставшихся.Сумма = "-";
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Значение Из ОставшиесяТК Цикл
		ТабДок.Вывести(ЗаголовокТаблицыТехкарты);
		ЗаполнитьШапкуТК(ТаблВидыРемонта, Табдок, Значение);
		
		ПодготовитьВложенныеТК(ТаблицаВложенныхТК, Значение.НормативныйРемонт, Значение.ВидМероприятия, Макет, Табдок);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьШапкуТК(ТаблВидыРемонта, Табдок, Значение)
	СтрокаТаблицыТК = ТаблВидыРемонта.ПолучитьОбласть("СтрокаТаблицыТехнологическойКарты");
	СтрокаТаблицыТК.Параметры.ВидМероприятия = Значение.ВидМероприятия;
	СтрокаТаблицыТК.Параметры.ТехнологическаяКарта = Значение.НормативныйРемонт;
	СтрокаТаблицыТК.Параметры.Сумма = Значение.Сумма;
	ТабДок.Вывести(СтрокаТаблицыТК);
КонецПроцедуры

Процедура ПодготовитьВложенныеТК(ТаблицаВложенныхТК, Владелец, ВидМероприятия, Макет, Табдок)
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Владелец", Владелец);
	СтруктураПоиска.Вставить("ВидМероприятия", ВидМероприятия);
	МассивВложенныхТК = ТаблицаВложенныхТК.НайтиСтроки(СтруктураПоиска);
	Если МассивВложенныхТК.Количество() > 0 Тогда
		ШапкаВложенныхТК = Макет.ПолучитьОбласть("ВложенныеТКШапка");
		ТабДок.Вывести(ШапкаВложенныхТК);
				
		ВывестиВложенныеТК(МассивВложенныхТК, Макет, Табдок);
	КонецЕсли;
КонецПроцедуры

Процедура ВывестиВложенныеТК(МассивВложенныхТК, Макет, Табдок)
	Для Каждого Значение Из МассивВложенныхТК Цикл
		ВложенныеТКСтроки = Макет.ПолучитьОбласть("ВложенныеТКСтроки");
		ВложенныеТКСтроки.Параметры.НаименованиеТК = Значение.НормативныйРемонт;
		ТабДок.Вывести(ВложенныеТКСтроки);
	КонецЦикла;
КонецПроцедуры

Процедура ПечатьСпискаОбъектовРМ_КоэффициентыРемонтныхОсобенностей(ТабличныйДокумент, Макет, Ссылка)
	ЗаполнитьИВывестиОбласть(Макет, "ШапкаКоэффициентыРемонтныхОсобенностей", ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних.Период КАК ДатаВвода,
	               |	торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних.ВидКоэффициента КАК ВидКоэффициента,
	               |	торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних.ЗначениеКоэффициента КАК ЗначениеКоэффициента
	               |ПОМЕСТИТЬ ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей
	               |ИЗ
	               |	РегистрСведений.торо_ЗначенияКоэффициентовРемонтныхОсобенностей.СрезПоследних(
	               |			,
	               |			ОбъектРемонта = &СписокОбъектовРМ
	               |				И Использование) КАК торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ВидКоэффициента
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей.ДатаВвода КАК ДатаВвода,
	               |	ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей.ВидКоэффициента КАК ВидКоэффициента,
	               |	ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей.ЗначениеКоэффициента КАК ЗначениеКоэффициента,
				   |	ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей.ЗначениеКоэффициента.Коэффициент КАК Коэффициент
	               |ИЗ
	               |	ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей КАК ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ВидыКоэффициентовРемонтныхОсобенностей КАК торо_ВидыКоэффициентовРемонтныхОсобенностей
	               |		ПО ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей.ВидКоэффициента = торо_ВидыКоэффициентовРемонтныхОсобенностей.Ссылка
	               |ГДЕ
	               |	НЕ ВТ_ЗначенияКоэффициентовРемонтныхОсобенностей.ВидКоэффициента.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СписокОбъектовРМ", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	    Возврат;
	КонецЕсли;
	
	ЗаполнитьИВывестиОбласть(Макет, "ШапкаТаблицыКоэффициентовРемонтныхОсобенностей", ТабличныйДокумент);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьИВывестиОбласть(Макет, "СтрокаТаблицыКоэффициентовРемонтныхОсобенностей", ТабличныйДокумент, Выборка);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьИВывестиОбласть(Макет, ИмяОбласти, ТабличныйДокумент, ВыборкаДанныхЗаполнения = Неопределено)
	Если ВыборкаДанныхЗаполнения = Неопределено Тогда
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		ТабличныйДокумент.Вывести(Область);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаДанныхЗаполнения.ТипЗаписи()) Тогда
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.Заполнить(ВыборкаДанныхЗаполнения);
		ЗаполнитьПредставлениеВР(Область);
		ТабличныйДокумент.Вывести(Область);
		
		Возврат;
	КонецЕсли;
	
	Пока ВыборкаДанныхЗаполнения.Следующий() Цикл
	    Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.Заполнить(ВыборкаДанныхЗаполнения);
		ЗаполнитьПредставлениеВР(Область);
		ТабличныйДокумент.Вывести(Область);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьПредставлениеВР(Область)
	Для Индекс = 0 По Область.Параметры.Количество() - 1 Цикл
		ТекущийПараметр = Область.Параметры.Получить(Индекс);
		Если ТипЗнч(ТекущийПараметр) = Тип("СправочникСсылка.торо_ВидыРемонтов") Тогда
			Область.Параметры.Установить(Индекс, торо_ЗаполнениеДокументов.ПолучитьПредоставленияВРДляПечати(ТекущийПараметр));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
