////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПЕРЕМЕННЫЕ

&НаКлиенте
Перем СтруктураФО;

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтруктураФО = ЗаполнитьСтруктуруФО();
	УстановитьФлажки(Команды.УстановитьФлажки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для каждого Флаг Из Элементы.ГруппаРазделыСпискаОбъектовРМ.ПодчиненныеЭлементы Цикл
		Если СтруктураФО.Свойство(Флаг.Имя) И Не СтруктураФО[Флаг.Имя] Тогда
		    Продолжить;
		КонецЕсли;
		
		ЭтотОбъект[Флаг.Имя] = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого Флаг Из Элементы.ГруппаРазделыСпискаОбъектовРМ.ПодчиненныеЭлементы Цикл
		ЭтотОбъект[Флаг.Имя] = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	СтруктураВозврата = Новый Структура;
	
	Для каждого Флаг Из Элементы.ГруппаРазделыСпискаОбъектовРМ.ПодчиненныеЭлементы Цикл
		СтруктураВозврата.Вставить(Флаг.Имя, ЭтотОбъект[Флаг.Имя]);
	КонецЦикла;
	
	Закрыть(СтруктураВозврата);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЗаполнитьСтруктуруФО()
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("ВыводитьРегламентныеМероприятия", ПолучитьФункциональнуюОпцию("торо_ИспользоватьРегламентныеМероприятия"));
	СтруктураВозврата.Вставить("ВыводитьКоэффициентыРемонтныхОсобенностей", ПолучитьФункциональнуюОпцию("торо_ИспользоватьКоэффициентыРемОсобенностей"));
	
	Возврат СтруктураВозврата;
КонецФункции

#КонецОбласти