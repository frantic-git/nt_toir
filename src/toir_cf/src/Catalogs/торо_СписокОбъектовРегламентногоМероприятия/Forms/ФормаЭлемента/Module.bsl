////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПЕРЕМЕННЫЕ
&НаКлиенте
Перем ПараметрыТекущейСтроки;
// Переменные необходимые для избавления от модальности
&НаКлиенте
Перем РегламентноеМероприятиеДобавляется;
&НаКлиенте
Перем СписокОбъектовДобавляется;
&НаКлиенте
Перем ВыходныеДокументыДобавляется; 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ПравоРедактирования = ПравоДоступа("Редактирование", Объект.Ссылка.Метаданные());
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбъектФормы = РеквизитФормыВЗначение("Объект");
		СсылкаНаЭлемент = торо_ОбщегоНазначения.ПолучитьСсылкуНаОбъект(ОбъектФормы);
		ЗначениеВРеквизитФормы(ОбъектФормы, "Объект");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.НормативныйРемонт,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СпособПланирования,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.ГрафикРемонтныхРабот,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СпособВыполнения,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СпособУчетаДатыОтсчета,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.НеУчаствуетВПланировании,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.РазрешитьСокращатьМежремонтныйПериодПриПланировании,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.НеУчитыватьПриПланированииГрафикРаботыОборудования,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.НеУчитыватьПриПланированииГрафикРемонтныхРабот,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.НеУчитыватьПродолжительность,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.Продолжительность,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.ИспользоватьКоэффициентыРемонтныхОсобенностей,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СпособРасчетаПродолжительности
		|ИЗ
		|	РегистрСведений.торо_РегламентныеМероприятияИСпискиОбъектов КАК торо_РегламентныеМероприятияИСпискиОбъектов
		|ГДЕ
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов = &СписокОбъектов";

	Запрос.УстановитьПараметр("СписокОбъектов", ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент));
	РезультатЗапроса = Запрос.Выполнить();

	Таб = РезультатЗапроса.Выгрузить();

	РегламентныеМероприятия.Загрузить(Таб);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_ПредшествующиеРегламентныеМероприятия.ВидМероприятия,
		|	торо_ПредшествующиеРегламентныеМероприятия.СписокОбъектов,
		|	торо_ПредшествующиеРегламентныеМероприятия.ПредшествующийВидМероприятия
		|ИЗ
		|	РегистрСведений.торо_ПредшествующиеРегламентныеМероприятия КАК торо_ПредшествующиеРегламентныеМероприятия
		|ГДЕ
		|	торо_ПредшествующиеРегламентныеМероприятия.СписокОбъектов = &СписокОбъектов";

	Запрос.УстановитьПараметр("СписокОбъектов", ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент));

	РезультатЗапроса = Запрос.Выполнить();

	ТЗ = РезультатЗапроса.Выгрузить();

	ПредшествующиеМероприятия.Загрузить(ТЗ);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_ВыходныеДокументы.СписокОбъектов,
		|	торо_ВыходныеДокументы.ВидМероприятия,
		|	торо_ВыходныеДокументы.ВыходнойДокумент
		|ИЗ
		|	РегистрСведений.торо_ВыходныеДокументы КАК торо_ВыходныеДокументы
		|ГДЕ
		|	торо_ВыходныеДокументы.СписокОбъектов = &СписокОбъектов";

	Запрос.УстановитьПараметр("СписокОбъектов", ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент));

	РезультатЗапроса = Запрос.Выполнить();

	ТабЗнач = РезультатЗапроса.Выгрузить();
	ВыходныеДокументы.Загрузить(ТабЗнач);
	
	НаборЗаписей = РегистрыСведений.торо_МаршрутыРегламентныхМероприятий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СписокОбъектов.Установить(?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент));
	НаборЗаписей.Прочитать();
	СписокОбъектов.Загрузить(НаборЗаписей.Выгрузить());
	СписокОбъектов.Сортировать("НомерПоПорядку Возр");
		
	КоличествоОР = СписокОбъектов.Количество();

	МодифицированностьВыхДок = Ложь;
	МодифицированностьПредшРемонтов = Ложь;
	МодифицированностьРегМер = Ложь;
	
	фоИспользоватьКоэффициентыРемОсобенностей = ПолучитьФункциональнуюОпцию("торо_ИспользоватьКоэффициентыРемОсобенностей");
	Элементы.ИспользоватьКоэффициентыРемонтныхОсобенностей.Видимость = фоИспользоватьКоэффициентыРемОсобенностей;
	Элементы.СтраницаКоэффициентыРемонтныхОсобенностей.Видимость = фоИспользоватьКоэффициентыРемОсобенностей;
	
	// СтандартныеПодсистемы.Свойства
	Контекст = Новый Структура();
	Контекст.Вставить("Объект",                     Объект);
	Контекст.Вставить("ИмяЭлементаДляРазмещения",   "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, Контекст);
	// Конец СтандартныеПодсистемы.Свойства
	
	флИндОбслужНачальный = Объект.ИндивидуальноеОбслуживаниеОР;
	
	УстановитьПараметрыЗапросовНаСервере();
	
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		КопируемыйОбъект = Параметры.ЗначениеКопирования;
		
		НаборЗаписей = РегистрыСведений.торо_РегламентныеМероприятияИСпискиОбъектов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СписокОбъектов.Установить(КопируемыйОбъект);
		НаборЗаписей.Прочитать();
		ТЗ = НаборЗаписей.Выгрузить();
		Если ТЗ.Количество() > 0 Тогда
			РегламентныеМероприятия.Загрузить(ТЗ);
			МодифицированностьРегМер = Истина;	
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.торо_ПредшествующиеРегламентныеМероприятия.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СписокОбъектов.Установить(КопируемыйОбъект);
		НаборЗаписей.Прочитать();
		ТЗ = НаборЗаписей.Выгрузить();
		Если ТЗ.Количество() > 0 Тогда
			ПредшествующиеМероприятия.Загрузить(ТЗ);
			МодифицированностьПредшРемонтов = Истина;	
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.торо_ВыходныеДокументы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СписокОбъектов.Установить(КопируемыйОбъект);
		НаборЗаписей.Прочитать();
		ТЗ = НаборЗаписей.Выгрузить();
		Если ТЗ.Количество() > 0 Тогда
			ВыходныеДокументы.Загрузить(ТЗ);
			МодифицированностьВыхДок = Истина;	
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.торо_МаршрутыРегламентныхМероприятий.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СписокОбъектов.Установить(КопируемыйОбъект);
		НаборЗаписей.Прочитать();
		ТЗ = НаборЗаписей.Выгрузить();
		Если ТЗ.Количество() > 0 Тогда
			СписокОбъектов.Загрузить(ТЗ);
			МодифицированностьВыхДок = Истина;	
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьСписокДоступныхВыходныхДокументов();
	
	ПерезаполнениеПриКопировании();
	
	// Имена таблиц формы для которых необходимо сохранять строку до редактирования
	ИменаТаблицФормы = Новый Массив;
	ИменаТаблицФормы.Добавить("РегламентныеМероприятия");
	
	СтруктураДанныхДоРедактирования = торо_СобытияФорм.ПодготовитьСтруктуруСохраненныхСтрок(ЭтотОбъект, ИменаТаблицФормы);	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если РегламентныеМероприятия.Количество() > 0 Тогда
		
		Элементы.РегламентныеМероприятия.ТекущаяСтрока = РегламентныеМероприятия[0];
		
	КонецЕсли;
	
	ПересчитатьНомераСтрок(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если РегламентныеМероприятия.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Таблица ""Регламентные мероприятия"" не заполнена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "РегламентныеМероприятия",, Отказ);
		
	КонецЕсли;
	
	Если СписокОбъектов.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Таблица ""Список объектов"" не заполнена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "СписокОбъектов",, Отказ);
		
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СообщитьОВозможномИзмененииПродолжительностиРемонта();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
			
	Если НЕ Отказ Тогда
		флИндОбслужНачальный = ТекущийОбъект.ИндивидуальноеОбслуживаниеОР;
		флСписокОбъектовМодифицированность = Ложь;
		СписокМодифицированныхМероприятий.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СообщитьОВозможномИзмененииПродолжительностиРемонта();
	флСписокОбъектовМодифицированность = Ложь;
	СписокМодифицированныхМероприятий.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Ссылка = ?(ЗначениеЗаполнено(ТекущийОбъект.Ссылка), ТекущийОбъект.Ссылка, СсылкаНаЭлемент);
	Если МодифицированностьРегМер Тогда
		
		Для каждого СтрТаб Из РегламентныеМероприятия Цикл
			ШаблонСообщения = НСтр("ru = 'Не удалось записать регламентные мероприятия: не заполнено поле ""%1"" в строке номер %2'");
			НомерСтроки = Строка(СтрТаб.ПолучитьИдентификатор()+1);
			
			Если НЕ ЗначениеЗаполнено(СтрТаб.ВидМероприятия) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения, "Вид мероприятия", НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрТаб.НормативныйРемонт) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения, "Нормативный ремонт", НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрТаб.СпособПланирования) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения, "Вид планирования", НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрТаб.ГрафикРемонтныхРабот) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения, "График ремонтных работ", НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрТаб.СпособВыполнения) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщения, "Способ выполнения", НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не Отказ Тогда
			Попытка
				
				Для Каждого СтрокаТЧ Из РегламентныеМероприятия Цикл
					
					СтрокаТЧ.СписокОбъектов = Ссылка;
					
				КонецЦикла;
				
				ТаблицаЗначенийРегламентныеМероприятия = РегламентныеМероприятия.Выгрузить();
				МассивСтрок = ТаблицаЗначенийРегламентныеМероприятия.НайтиСтроки(Новый Структура("СписокОбъектов", Ссылка));
				ТаблицаБуфер = ТаблицаЗначенийРегламентныеМероприятия.Скопировать(МассивСтрок);
				
				НаборРегламентныхМероприятий = РегистрыСведений.торо_РегламентныеМероприятияИСпискиОбъектов.СоздатьНаборЗаписей();
				НаборРегламентныхМероприятий.Отбор.СписокОбъектов.Установить(Ссылка);
				НаборРегламентныхМероприятий.Загрузить(ТаблицаБуфер);
				НаборРегламентныхМероприятий.Записать();
				
				МодифицированностьРегМер = Ложь;
			Исключение
				
				ТекстСообщения = НСтр("ru = 'Не удалось записать регламентные мероприятия для списка объектов: '") + ОписаниеОшибки();
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
				
				Возврат;
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если МодифицированностьПредшРемонтов Тогда 
		
		Попытка
			
			Для Каждого СтрокаТЧ Из ПредшествующиеМероприятия Цикл
					
				СтрокаТЧ.СписокОбъектов = Ссылка;
					
			КонецЦикла;
			
			ТаблицаЗначенийВидыРемонтныхРаботДляОтсчета = ПредшествующиеМероприятия.Выгрузить();
			МассивСтрок = ТаблицаЗначенийВидыРемонтныхРаботДляОтсчета.НайтиСтроки(Новый Структура("СписокОбъектов", Ссылка));
			ТаблицаБуфер = ТаблицаЗначенийВидыРемонтныхРаботДляОтсчета.Скопировать(МассивСтрок);
			
			ТаблицаПроверки = РегламентныеМероприятия.Выгрузить();
			
			Сч = ТаблицаБуфер.Количество() - 1;
			Пока Сч >= 0 И ТаблицаБуфер.Количество() > 0 Цикл
				
				МассивПоиска = ТаблицаПроверки.НайтиСтроки(
				Новый Структура("СписокОбъектов, ВидМероприятия", Ссылка, ТаблицаБуфер[Сч].ВидМероприятия));
				
				Если МассивПоиска.Количество() = 0 Тогда
					
					ТаблицаБуфер.Удалить(ТаблицаБуфер[Сч]);
					
				КонецЕсли; 
				
				Сч = Сч - 1;
				
			КонецЦикла;
			
			НаборЗаписей = РегистрыСведений.торо_ПредшествующиеРегламентныеМероприятия.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.СписокОбъектов.Установить(Ссылка);
			НаборЗаписей.Загрузить(ТаблицаБуфер);
			НаборЗаписей.Записать();
			
			МодифицированностьПредшРемонтов = Ложь;
			
		Исключение
			
			ТекстСообщения = НСтр("ru = 'Не удалось записать предшествующие ремонты списка объектов: '") + ОписаниеОшибки();
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Если МодифицированностьВыхДок Тогда 
		
		Попытка
			
			Для Каждого СтрокаТЧ Из ВыходныеДокументы Цикл
					
				СтрокаТЧ.СписокОбъектов = Ссылка;
					
			КонецЦикла;
			
			ТаблицаЗначенийВыходныеДокументы = ВыходныеДокументы.Выгрузить();
			МассивСтрок = ТаблицаЗначенийВыходныеДокументы.НайтиСтроки(Новый Структура("СписокОбъектов", Ссылка));
			ТаблицаБуфер = ТаблицаЗначенийВыходныеДокументы.Скопировать(МассивСтрок);
			
			ТаблицаПроверки = ВыходныеДокументы.Выгрузить();
			
			Сч = ТаблицаБуфер.Количество() - 1;
			Пока Сч >= 0 И ТаблицаБуфер.Количество() > 0 Цикл
				
				МассивПоиска = ТаблицаПроверки.НайтиСтроки(
				Новый Структура("СписокОбъектов, ВидМероприятия", Ссылка, ТаблицаБуфер[Сч].ВидМероприятия));
				
				Если МассивПоиска.Количество() = 0 Тогда
					
					ТаблицаБуфер.Удалить(ТаблицаБуфер[Сч]);
					
				КонецЕсли; 
				
				Сч = Сч - 1;
				
			КонецЦикла;
			
			НаборЗаписей = РегистрыСведений.торо_ВыходныеДокументы.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.СписокОбъектов.Установить(Ссылка);
			НаборЗаписей.Загрузить(ТаблицаБуфер);
			НаборЗаписей.Записать();
			
			МодифицированностьВыхДок = Ложь;
			
		Исключение
			
			ТекстСообщения = НСтр("ru = 'Не удалось записать выходные документы списка объектов: '") + ОписаниеОшибки();
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
	
	Попытка
		
		Для Каждого СтрокаТЧ Из СписокОбъектов Цикл
					
			СтрокаТЧ.СписокОбъектов = Ссылка;
			
		КонецЦикла;
		
		ТабСписокОбъектов = СписокОбъектов.Выгрузить();
		МассивСтрок = ТабСписокОбъектов.НайтиСтроки(Новый Структура("СписокОбъектов", Ссылка));
		ТаблицаБуфер = ТабСписокОбъектов.Скопировать(МассивСтрок,"СписокОбъектов, НомерПоПорядку, ОбъектРемонта");
		
		
		НаборСпискаОбъектов = РегистрыСведений.торо_МаршрутыРегламентныхМероприятий.СоздатьНаборЗаписей();
		НаборСпискаОбъектов.Отбор.СписокОбъектов.Установить(Ссылка);
		НаборСпискаОбъектов.Загрузить(ТаблицаБуфер);
		НаборСпискаОбъектов.Записать();
		
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось записать предшествующие ремонты списка объектов: '") + ОписаниеОшибки();
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат;
		
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	Элементы.ПредстоящиеМероприятия.Обновить();
	Элементы.ВыполненныеМероприятия.Обновить();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ТЧ Регламентные мероприятия
	Для Каждого ТекДанные Из РегламентныеМероприятия Цикл
		Если Не ЗначениеЗаполнено(ТекДанные.ВидМероприятия) Тогда
			ТекстСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Вид мероприятия"" в таблице ""Регламентные мероприятия""!'");
			Поле = СтрШаблон("РегламентныеМероприятия[%1].ВидМероприятия", РегламентныеМероприятия.Индекс(ТекДанные));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		КонецЕсли;
		
		МассивСтрок = РегламентныеМероприятия.НайтиСтроки(Новый Структура("ВидМероприятия", ТекДанные.ВидМероприятия));
		Если МассивСтрок.Количество() > 1 Тогда	
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Виды мероприятия"" ""(%1)"" в таблице ""Регламентные мероприятия""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "РегламентныеМероприятия",, Отказ);	
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекДанные.НормативныйРемонт) Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Технологическая карта"" для вида мероприятия ""(%1)"" в таблице ""Регламентные мероприятия""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);  
			Поле = СтрШаблон("РегламентныеМероприятия[%1].НормативныйРемонт", РегламентныеМероприятия.Индекс(ТекДанные));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекДанные.СпособВыполнения) Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Способ выполнения"" для вида мероприятия ""(%1)"" в таблице ""Регламентные мероприятия""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);    
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "РегламентныеМероприятия",, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекДанные.СпособПланирования) Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Способ планирования"" для вида мероприятия ""(%1)"" в таблице ""Регламентные мероприятия""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);   
			Поле = СтрШаблон("РегламентныеМероприятия[%1].СпособПланирования", РегламентныеМероприятия.Индекс(ТекДанные));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекДанные.ГрафикРемонтныхРабот) Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""График ремонтных работ"" для вида мероприятия ""(%1)"" в таблице ""Регламентные мероприятия""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "РегламентныеМероприятия",, Отказ);
		КонецЕсли;
		
		СтрокиПредМероприятий = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура("ВидМероприятия", ТекДанные.ВидМероприятия));
		
		Если СтрокиПредМероприятий.Количество() = 0 Тогда
			ШаблонСообщения = НСтр("ru = 'Для регламентного мероприятия ""%1"" не заполнены предшествующие мероприятия!!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "РегламентныеМероприятия",, Отказ);
		КонецЕсли;
	КонецЦикла;  
	// ТЧ Регламентные мероприятия
	
	// ТЧ ПредшествующиеМероприятия
	Для Каждого ТекДанные Из ПредшествующиеМероприятия Цикл
		Если Не ЗначениеЗаполнено(ТекДанные.ПредшествующийВидМероприятия) Тогда
			ТекстСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Предшедствующий вид мероприятия"" в таблице ""Предшедствующие мероприятия""!'");
			Поле = СтрШаблон("ПредшествующиеМероприятия[%1].ПредшествующийВидМероприятия", ПредшествующиеМероприятия.Индекс(ТекДанные));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		КонецЕсли;
		
		НайденныеСтроки = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура("ВидМероприятия, ПредшествующийВидМероприятия", ТекДанные.ВидМероприятия, ТекДанные.ПредшествующийВидМероприятия));
		Если НайденныеСтроки.Количество() > 1 Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Предшедствующий вид мероприятия"" ""(%1)"" в таблице ""Предшедствующие мероприятия""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ПредшествующийВидМероприятия);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"ПредшествующиеМероприятия",, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	// ТЧ ПредшествующиеМероприятия
	
	
	// ТЧ Выходные документы
	Для Каждого ТекДанные Из ВыходныеДокументы Цикл
		Если Не ЗначениеЗаполнено(ТекДанные.ВыходнойДокумент) Тогда
			ТекстСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Выходные документы"" в таблице ""Выходные документы""!'");
			Поле = СтрШаблон("ВыходныеДокументы[%1].ВыходнойДокумент", ВыходныеДокументы.Индекс(ТекДанные));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		КонецЕсли;
		
		МассивСтрок = ВыходныеДокументы.НайтиСтроки(Новый Структура("СписокОбъектов, ВидМероприятия, ВыходнойДокумент", ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент), ТекДанные.ВидМероприятия, ТекДанные.ВыходнойДокумент));
		Если МассивСтрок.Количество() > 1 Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Выходные документы"" ""(%1)"" в таблице ""Выходные документы""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВыходнойДокумент);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"ВыходныеДокументы",, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	// ТЧ Выходные документы
	
	// ТЧ Список Объектов
	Для Каждого ТекущиеДанные Из СписокОбъектов Цикл 
		Если ТекущиеДанные.ОбъектРемонта = ПредопределенноеЗначение("Справочник.торо_ОбъектыРемонта.ПустаяСсылка") Тогда
			ТекстСообщения = НСтр("ru = 'Обнаружено незаполненное поле ""Объекты ремонта"" в таблице ""Список объектов""!'");
			Поле = СтрШаблон("СписокОбъектов[%1].ОбъектРемонта", СписокОбъектов.Индекс(ТекущиеДанные));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		КонецЕсли;    
		
		МассивСтрок = СписокОбъектов.НайтиСтроки(Новый Структура("СписокОбъектов, ОбъектРемонта", ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент), ТекущиеДанные.ОбъектРемонта));
		Если МассивСтрок.Количество() > 1 Тогда
			ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Объекты ремонта"" ""(%1)"" в таблице ""Список объектов""!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекущиеДанные.ОбъектРемонта);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"СписокОбъектов",, Отказ);
		КонецЕсли;
	КонецЦикла;
	// ТЧ Список Объектов  
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПанельПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.СтраницаКоэффициентыРемонтныхОсобенностей Тогда
		ОбновитьКоэффициентыРемонтныхОсобенностей();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПредшествующиеМероприятия

&НаКлиенте
Процедура ПредшествующиеМероприятияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	ТекДанные = Элементы.РегламентныеМероприятия.ТекущиеДанные;
	Если ТекДанные = Неопределено Или Не Значениезаполнено(ТекДанные.ВидМероприятия) Тогда
		ТекстСообщения = НСтр("ru = 'Не указан вид мероприятия!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеМероприятияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		
		ТекДанныеНормативов = Элементы.РегламентныеМероприятия.ТекущиеДанные;
		ТекСтрока = Элемент.ТекущиеДанные;
		ТекСтрока.ВидМероприятия = ТекДанныеНормативов.ВидМероприятия;
		ТекСтрока.СписокОбъектов = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеМероприятияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	МодифицированностьПредшРемонтов = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеМероприятияПослеУдаления(Элемент)
	МодифицированностьПредшРемонтов = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеМероприятияПредшествующийВидМероприятияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокРегМероприятийДляВыбора = новый СписокЗначений;
	
	Для каждого текСтрока из РегламентныеМероприятия цикл
		СписокРегМероприятийДляВыбора.Добавить(текСтрока.ВидМероприятия);
	КонецЦикла;
	
	ПараметрыФормыСтруктура = Новый Структура("КлючНазначенияИспользования, СписокОтбора", "ПредшествующиеМероприятия", СписокРегМероприятийДляВыбора);	
	ФормаВыборка = ПолучитьФорму("Справочник.торо_ВидыРемонтов.ФормаВыбора", ПараметрыФормыСтруктура, Элемент);
	ФормаВыборка.Открыть();	
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеМероприятияПредшествующийВидМероприятияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	СписокРегМероприятийДляВыбора = новый СписокЗначений;
	
	Для каждого текСтрока из РегламентныеМероприятия цикл
		СписокРегМероприятийДляВыбора.Добавить(текСтрока.ВидМероприятия);
	КонецЦикла;
	
	Параметры.Отбор.Вставить("Ссылка", СписокРегМероприятийДляВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредшествующиеМероприятияПредшествующийВидМероприятияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СписокРегМероприятийДляВыбора = новый СписокЗначений;
	
	Для каждого текСтрока из РегламентныеМероприятия цикл
		СписокРегМероприятийДляВыбора.Добавить(текСтрока.ВидМероприятия);
	КонецЦикла;
	
	Параметры.Отбор.Вставить("Ссылка", СписокРегМероприятийДляВыбора);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОбъектов

&НаКлиенте
Процедура СписокОбъектовПослеУдаления(Элемент)
	КоличествоОР = СписокОбъектов.Количество();
	флСписокОбъектовМодифицированность = Истина;
	МодифицированностьВыхДок = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если НоваяСтрока И НЕ ОтменаРедактирования Тогда
		КоличествоОР = СписокОбъектов.Количество();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПередУдалением(Элемент, Отказ)
	ТекСтрока = Элемент.ТекущиеДанные;
	
	Если Не Отказ Тогда
		ПересчитатьНомераСтрок();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		ТекДанные = Элементы.СписокОбъектов.ТекущиеДанные;
		ТекДанные.СписокОбъектов = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент);
		ТекДанные.НомерПоПорядку = СписокОбъектов.Количество();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НЕ ОтменаРедактирования Тогда		
		Если НоваяСтрока и НЕ Отказ Тогда
			флСписокОбъектовМодифицированность = Истина;
		КонецЕсли;
		
		Если НЕ (Элемент.ТекущиеДанные = Неопределено Или Элементы.СписокОбъектов.ТекущиеДанные = Неопределено) Тогда
			МодифицированностьВыхДок = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовОбъектРемонтаПриИзменении(Элемент)
	флСписокОбъектовМодифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	МассивВыбранныхЗначений = Новый Массив;

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		МассивВыбранныхЗначений = ВыбранноеЗначение;
	Иначе
		МассивВыбранныхЗначений.Добавить(ВыбранноеЗначение);
	КонецЕсли;
	
	Если МассивВыбранныхЗначений.Количество() = 0 Тогда
		Возврат;  
	КонецЕсли;
	
	Для Каждого СсылкаНаОР ИЗ МассивВыбранныхЗначений Цикл
		МассивСтрок = СписокОбъектов.НайтиСтроки(Новый Структура("ОбъектРемонта",СсылкаНаОР));
		Если МассивСтрок.Количество() = 0 Тогда
			НС = СписокОбъектов.Добавить();
			НС.СписокОбъектов = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент);
			НС.ОбъектРемонта = СсылкаНаОР;
			НС.НомерПоПорядку = СписокОбъектов.Количество();
		Иначе
			ШаблонСообщения = НСтр("ru = 'Объект ремонта ""%1"" уже есть в списке!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, СсылкаНаОР);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		КонецЕсли;
	КонецЦикла;

	КоличествоОР = СписокОбъектов.Количество();
		
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	ПересчитатьНомераСтрок();
КонецПроцедуры

&НаКлиенте
Процедура СписокОбъектовПриИзменении(Элемент)
	ПересчитатьНомераСтрок();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРегламентныеМероприятия

&НаКлиенте
Процедура РегламентныеМероприятияПослеУдаления(Элемент)
	
	МодифицированностьРегМер = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияНормативныйРемонтПриИзменении(Элемент)
	
	ТекДанные = Элементы.РегламентныеМероприятия.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		ВМ = ТекДанные.ВидМероприятия;
		Если СписокМодифицированныхМероприятий.НайтиПоЗначению(ВМ) = Неопределено Тогда
			СписокМодифицированныхМероприятий.Добавить(ВМ);
		КонецЕсли;
	КонецЕсли;
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		ТекДанные = Элементы.РегламентныеМероприятия.ТекущиеДанные;
		Если Не ЗначениеЗаполнено(ТекДанные.СписокОбъектов) Тогда
			
			ТекДанные.СписокОбъектов = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент);
			
		КонецЕсли;
	КонецЕсли;
	
	Если Копирование Тогда 
		ТекДанные.ВидМероприятия = ПредопределенноеЗначение("Справочник.торо_ВидыРемонтов.ПустаяСсылка");
	КонецЕсли;
		
	// Запись данных до редактирования
	Если НЕ НоваяСтрока Тогда
		торо_СобытияФормКлиентСервер.СохранитьДанныеСтрокиДоРедактирования(СтруктураДанныхДоРедактирования, Элемент.Имя, Элемент.ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущееМероприятие = Элемент.ТекущиеДанные;
	Если Не НоваяСтрока И Не ЗначениеЗаполнено(ТекущееМероприятие.ВидМероприятия) Тогда
		МассивУдаляемыхСтрок = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура("ПредшествующийВидМероприятия, СписокОбъектов", СтруктураДанныхДоРедактирования[Элемент.Имя].ВидМероприятия , ТекущееМероприятие.СписокОбъектов));
		Для каждого Элем Из МассивУдаляемыхСтрок Цикл
			ПредшествующиеМероприятия.Удалить(Элем);
		КонецЦикла;
		
		МассивУдаляемыхСтрок = ВыходныеДокументы.НайтиСтроки(Новый Структура("ВидМероприятия, СписокОбъектов", СтруктураДанныхДоРедактирования[Элемент.Имя].ВидМероприятия, ТекущееМероприятие.СписокОбъектов));
		Для каждого Элем Из МассивУдаляемыхСтрок Цикл
			ВыходныеДокументы.Удалить(Элем);
		КонецЦикла;
		РегламентныеМероприятияПриАктивизацииСтроки(Элементы.РегламентныеМероприятия);
	ИначеЕсли Не ОтменаРедактирования И ТекущееМероприятие <> Неопределено И (СтруктураДанныхДоРедактирования.РегламентныеМероприятия = Неопределено ИЛИ ТекущееМероприятие.ВидМероприятия <> СтруктураДанныхДоРедактирования.РегламентныеМероприятия.ВидМероприятия) Тогда
		
		Если ЗначениеЗаполнено(ТекущееМероприятие.ВидМероприятия) Тогда 
			УжеДобавленыеПредшествующие = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура ("ВидМероприятия", ТекущееМероприятие.ВидМероприятия));
			Если УжеДобавленыеПредшествующие.Количество() = 0 Тогда 
				
				ПредшествующееМероприятие = ПредшествующиеМероприятия.Добавить();
				ПредшествующееМероприятие.ВидМероприятия = ТекущееМероприятие.ВидМероприятия;
				ПредшествующееМероприятие.СписокОбъектов = Объект.Ссылка;
				ПредшествующееМероприятие.ПредшествующийВидМероприятия = ТекущееМероприятие.ВидМероприятия;
				
				Элементы.ПредшествующиеМероприятия.ОтборСтрок = Новый ФиксированнаяСтруктура("ВидМероприятия", ТекущееМероприятие.ВидМероприятия);
			КонецЕсли;	
		КонецЕсли; 
		
		ГрафикРемРаб = ПолучитьЗначениеГрафикаРемРабНаСервере();
		
		Если НЕ ГрафикРемРаб = Неопределено Тогда
			Элемент.ТекущиеДанные.ГрафикРемонтныхРабот = ГрафикРемРаб;
		КонецЕсли;
		
		Элемент.ТекущиеДанные.СпособВыполнения = ПредопределенноеЗначение("Перечисление.СпособыСтроительства.Хозспособ");
		
		Элемент.ТекущиеДанные.СпособУчетаДатыОтсчета = ПредопределенноеЗначение("Перечисление.торо_СпособыУчетаДатыОтсчетаПриПланированииРемонтныхРабот.ИспользоватьДатуПланированияВКачествеДатыОтсчета");
		
		МодифицированностьРегМер = Истина;
		МодифицированностьПредшРемонтов = Истина;
		
		Если НоваяСтрока Тогда
			
			Элем = СписокМодифицированныхМероприятий.НайтиПоЗначению(ТекущееМероприятие.ВидМероприятия);
			
			Если Элем <> Неопределено Тогда
				СписокМодифицированныхМероприятий.Удалить(Элем);
			КонецЕсли;
			
		КонецЕсли;
		
		РегламентныеМероприятияПриАктивизацииСтроки(Элементы.РегламентныеМероприятия);			
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элементы.РегламентныеМероприятия.ТекущиеДанные;
		
	Если Не ТекСтрока = Неопределено Тогда 		
		торо_ОбщегоНазначенияКлиент.ЗапомнитьПараметрыТекущейСтроки("СтрРеглМероприятий", Элементы.РегламентныеМероприятия.ТекущаяСтрока, ПараметрыТекущейСтроки);
		Элементы.ПредшествующиеМероприятия.ОтборСтрок = Новый ФиксированнаяСтруктура("ВидМероприятия", ТекСтрока.ВидМероприятия);
		Элементы.ВыходныеДокументы.ОтборСтрок = Новый ФиксированнаяСтруктура("ВидМероприятия, СписокОбъектов", ТекСтрока.ВидМероприятия, ТекСтрока.СписокОбъектов);
	КонецЕсли;
	
	ОбновитьОтображениеПродолжительности();
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияПередУдалением(Элемент, Отказ)
	МассивУдаляемыхСтрок = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура("ПредшествующийВидМероприятия, СписокОбъектов", Элементы.РегламентныеМероприятия.ТекущиеДанные.ВидМероприятия, Элементы.РегламентныеМероприятия.ТекущиеДанные.СписокОбъектов));
	Для каждого Элем Из МассивУдаляемыхСтрок Цикл
		ПредшествующиеМероприятия.Удалить(Элем);
	КонецЦикла;
	
	МассивУдаляемыхСтрок = ВыходныеДокументы.НайтиСтроки(Новый Структура("ВидМероприятия, СписокОбъектов", Элементы.РегламентныеМероприятия.ТекущиеДанные.ВидМероприятия, Элементы.РегламентныеМероприятия.ТекущиеДанные.СписокОбъектов));
	Для каждого Элем Из МассивУдаляемыхСтрок Цикл
		ВыходныеДокументы.Удалить(Элем);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекДанные = Элементы.РегламентныеМероприятия.ТекущиеДанные;

	Если НЕ ОтменаРедактирования Тогда 		

		Если ТекДанные <> Неопределено Тогда
			
			МассивСтрок = РегламентныеМероприятия.НайтиСтроки(Новый Структура("ВидМероприятия", ТекДанные.ВидМероприятия));
			Если МассивСтрок.Количество() > 1 Тогда
				Если ЗначениеЗаполнено(ТекДанные.ВидМероприятия) Тогда
					ШаблонСообщения = НСтр("ru = 'Обнаружены повторяющиеся поля ""Виды мероприятия"" ""(%1)"" в таблице ""Регламентные мероприятия""!'");
					ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекДанные.ВидМероприятия);
				Иначе
					ТекстСообщения = НСтр("ru = 'Обнаружены повторяющиеся незаполненные поля ""Виды мероприятия"" в таблице ""Регламентные мероприятия""!'");
				КонецЕсли;
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"РегламентныеМероприятия",, Отказ);	
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не Отказ И Не НоваяСтрока И СтруктураДанныхДоРедактирования[Элемент.Имя] <> Неопределено
			И СтруктураДанныхДоРедактирования[Элемент.Имя].ВидМероприятия <> ТекДанные.ВидМероприятия
			И ЗначениеЗаполнено(ТекДанные.ВидМероприятия) Тогда
			СтарыйВМ = СтруктураДанныхДоРедактирования[Элемент.Имя].ВидМероприятия;
			НайденныеСтроки = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура("ВидМероприятия", СтарыйВМ));
			Для Каждого Найденная Из НайденныеСтроки Цикл
				Найденная.ВидМероприятия = ТекДанные.ВидМероприятия;
			КонецЦикла;
			НайденныеСтроки = ПредшествующиеМероприятия.НайтиСтроки(Новый Структура("ПредшествующийВидМероприятия", СтарыйВМ));
			Для Каждого Найденная Из НайденныеСтроки Цикл
				Найденная.ПредшествующийВидМероприятия = ТекДанные.ВидМероприятия;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Не НоваяСтрока Тогда
		// возврат старых значений
		ЗаполнитьЗначенияСвойств(ТекДанные,СтруктураДанныхДоРедактирования[Элемент.Имя]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикРемонтныхРаботПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СпособВыполненияПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СпособУчетаДатыОтсчетаПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НеИспользоватьВПланированииПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НеУчитыватьПриПланированииГрафикРемонтныхРаботПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НеУчитыватьПродолжительностьПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКоэффициентыРемонтныхОсобенностейПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияСпособПланированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыСтруктура = Новый Структура("КлючНазначенияИспользования", "СписокРегламентныхМероприятий");	
	ФормаВыборка = ПолучитьФорму("Справочник.торо_СпособыПланированияРемонтныхРабот.ФормаВыбора", ПараметрыФормыСтруктура, Элемент);
	ФормаВыборка.Открыть();	
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаПродолжительностиПриИзменении(Элемент)
	
	Если Элементы.РегламентныеМероприятия.ТекущиеДанные.СпособРасчетаПродолжительности = Неопределено Тогда    		
		Возврат;   		
	ИначеЕсли Элементы.РегламентныеМероприятия.ТекущиеДанные.СпособРасчетаПродолжительности = ПредопределенноеЗначение("Перечисление.торо_СпособыРасчетаПродолжительности.ПоПродолжительностиТехКарт") Тогда 
		
		МассивОР = Новый Массив;
		Для каждого Стр Из СписокОбъектов Цикл
			МассивОР.Добавить(Стр.ОбъектРемонта);
		КонецЦикла; 
		
		Если МассивОР.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен список объектов ремонта. Расчет не выполнен!'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Элементы.РегламентныеМероприятия.ТекущиеДанные.НормативныйРемонт )
			ИЛИ НЕ ЗначениеЗаполнено(Элементы.РегламентныеМероприятия.ТекущиеДанные.ВидМероприятия) Тогда
			
			ТекстСообщения = НСтр("ru = 'Не все поля заполнены для регламентного мероприятия! Расчет не выполнен!'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ПродолжительностьРемонта = ПерерассчитатьНаСервере(Объект.ИндивидуальноеОбслуживаниеОР,
		                           МассивОР, 
								   Элементы.РегламентныеМероприятия.ТекущиеДанные.НормативныйРемонт, 
								   Элементы.РегламентныеМероприятия.ТекущиеДанные.ВидМероприятия,
		                           КоличествоОР);
		Элементы.РегламентныеМероприятия.ТекущиеДанные.Продолжительность = ПродолжительностьРемонта;
		МодифицированностьРегМер = Истина;
		Модифицированность = Истина;
		ОбновитьОтображениеПродолжительности();
		
	ИначеЕсли Элементы.РегламентныеМероприятия.ТекущиеДанные.СпособРасчетаПродолжительности = ПредопределенноеЗначение("Перечисление.торо_СпособыРасчетаПродолжительности.ПроизвольноеЗначение") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("РасчитатьЗавершение", ЭтаФорма);
		ФормаПодбораПродолжительности = ОткрытьФорму ("ОбщаяФорма.торо_ФормаПодбораПродолжительности", ,,Объект.Ссылка,,,ОписаниеОповещения);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СпособУчетаДатыОтсчетаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	сз = Новый Массив;
	сз.Добавить(ПредопределенноеЗначение(
		"Перечисление.торо_СпособыУчетаДатыОтсчетаПриПланированииРемонтныхРабот.НеИспользоватьДатуВводаВЭксплуатацию"));
	сз.Добавить(ПредопределенноеЗначение(
		"Перечисление.торо_СпособыУчетаДатыОтсчетаПриПланированииРемонтныхРабот.ИспользоватьДатуПланированияВКачествеДатыОтсчета"));
	сз.Добавить(ПредопределенноеЗначение(
		"Перечисление.торо_СпособыУчетаДатыОтсчетаПриПланированииРемонтныхРабот.ПоПоследнемуРемонтуИлиДатеПланирования"));

	ПараметрыПолученияДанных.отбор = новый Структура("Ссылка",сз);
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияСпособПланированияПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияВидМероприятияПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияСпособПланированияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтруктураОтбора = Новый Структура("ИспользоватьПериодичность, ИспользоватьПараметрыНаработки", Истина, Ложь);
	ПараметрыПолученияДанных.Вставить("Отбор", СтруктураОтбора);
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеМероприятияРазрешитьСокращатьМежремонтныйПериодПриПланированииПриИзменении(Элемент)
	МодифицированностьРегМер = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыходныеДокументы

&НаКлиенте
Процедура ВыходныеДокументыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
	
		ТекДанныеРегМероприятий = Элементы.РегламентныеМероприятия.ТекущиеДанные;
		Если ТекДанныеРегМероприятий <> Неопределено Тогда
		    ТекСтрока = Элемент.ТекущиеДанные;
			ТекСтрока.ВидМероприятия = ТекДанныеРегМероприятий.ВидМероприятия;
		    ТекСтрока.СписокОбъектов = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеДокументыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекДанные = Элемент.ТекущиеДанные;
	ТекущДанные = Элементы.РегламентныеМероприятия.ТекущиеДанные;
	Если НЕ ОтменаРедактирования И НЕ ТекДанные = Неопределено И НЕ ТекущДанные = Неопределено Тогда		
		МодифицированностьВыхДок = Истина;
		
		Если НоваяСтрока Тогда 
			ТекДанные.ВидМероприятия = ТекущДанные.ВидМероприятия;
			ТекДанные.СписокОбъектов = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Ссылка, СсылкаНаЭлемент);
			Элементы.ВыходныеДокументы.ОтборСтрок = Новый ФиксированнаяСтруктура("ВидМероприятия, СписокОбъектов", ТекущДанные.ВидМероприятия, ТекущДанные.СписокОбъектов);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеДокументыПослеУдаления(Элемент)
	МодифицированностьВыхДок = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеДокументыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекДанные = Элементы.РегламентныеМероприятия.ТекущиеДанные;
	Если ТекДанные = Неопределено Или Не ЗначениеЗаполнено(ТекДанные.ВидМероприятия) Тогда
		ТекстСообщения = НСтр("ru = 'Не указан вид мероприятия!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПредстоящиеМероприятия

&НаКлиенте
Процедура ПредстоящиеМероприятияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(,Элемент.ТекущиеДанные.Регистратор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыполненныеМероприятия

&НаКлиенте
Процедура ВыполненныеМероприятияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(,Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Перерассчитать(Команда)
	
	СпособРасчетаПродолжительностиПриИзменении(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьЗавершение(Результат, ДополнительныеПараметры)Экспорт
	Если ТипЗнч(Результат) = Тип("Число") Тогда
		МодифицированностьРегМер = Истина;
		Модифицированность = Истина;
		Элементы.РегламентныеМероприятия.ТекущиеДанные.Продолжительность = Результат;
		ОбновитьОтображениеПродолжительности();
	КонецЕсли;
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТолькоПроведенныеАкты(Команда)
	
	Кнопка = Элементы.ВыполненныеМероприятияОтображатьТолькоПроведенныеАкты;
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	УстановитьПараметрыЗапросовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТолькоПроведенныеГрафикиРМ(Команда)
	
	Кнопка = Элементы.ПредстоящиеМероприятияОтображатьТолькоПроведенныеГрафикиРМ;
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	УстановитьПараметрыЗапросовНаСервере();
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПодборОР(Команда)	
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, ИспользоватьВариантУчетаИерархииЭлементов, МножественныйВыбор", Ложь, Истина, Истина);
	ОткрытьФорму("Справочник.торо_ОбъектыРемонта.ФормаВыбора", ПараметрыФормы, Элементы.СписокОбъектов, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПерерассчитатьНаСервере(ИндивидОбслуживаниеОР, МассивОР, ТекНормРемонт, ТекВидМероприятия, КолОР)
	
	Если ИндивидОбслуживаниеОР Тогда
		ТаблицаОбъектов = Новый ТаблицаЗначений;
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("СправочникСсылка.торо_ОбъектыРемонта"));
		ТипКолонки = Новый ОписаниеТипов(МассивТипов);
		ТаблицаОбъектов.Колонки.Добавить("ОбъектРемонта", ТипКолонки);
		Для каждого Элем Из МассивОР Цикл
		
			ТаблицаОбъектов.Добавить().ОбъектРемонта = Элем;
		
		КонецЦикла; 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаОбъектов.ОбъектРемонта
		|ПОМЕСТИТЬ ТаблицаОбъектов
		|ИЗ
		|	&ТаблицаОбъектов КАК ТаблицаОбъектов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_ВерсииТехКартСрезПоследних.ТехКарта
		|ПОМЕСТИТЬ СтандартныйРемонт
		|ИЗ
		|	РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(&ДатаСреза, ИдентификаторТехКарты = &НормРемонт) КАК торо_ВерсииТехКартСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА торо_НормативныеРемонтыОборудования.НормативныйРемонт ЕСТЬ NULL 
		|			ТОГДА &НормРемонт
		|		ИНАЧЕ торо_НормативныеРемонтыОборудования.НормативныйРемонт
		|	КОНЕЦ КАК НормативныйРемонт,
		|	ТаблицаОбъектов.ОбъектРемонта,
		|	ЕСТЬNULL(торо_ВерсииТехКартСрезПоследних.ТехКарта, СтандартныйРемонт.ТехКарта) КАК ТехКарта
		|ПОМЕСТИТЬ НормРем
		|ИЗ
		|	ТаблицаОбъектов КАК ТаблицаОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(&ДатаСреза) КАК торо_ВерсииТехКартСрезПоследних
		|			ПО торо_НормативныеРемонтыОборудования.НормативныйРемонт = торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты
		|		ПО ТаблицаОбъектов.ОбъектРемонта = торо_НормативныеРемонтыОборудования.ОбъектРемонта
		|			И (торо_НормативныеРемонтыОборудования.ВидРемонта = &ВидРемонта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтандартныйРемонт КАК СтандартныйРемонт
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(торо_ТехКарты.НормаВремени) КАК НормаВремени
		|ИЗ
		|	НормРем КАК НормРем
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты КАК торо_ТехКарты
		|		ПО НормРем.ТехКарта = торо_ТехКарты.Ссылка";
		
		Запрос.УстановитьПараметр("ТаблицаОбъектов", ТаблицаОбъектов);
		Запрос.УстановитьПараметр("НормРемонт", ТекНормРемонт);
		Запрос.УстановитьПараметр("ВидРемонта", ТекВидМероприятия);
		Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДата());
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.НормаВремени;
		КонецЕсли;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(торо_ТехКарты.НормаВремени) КАК НормаВремени
		|ИЗ
		|	РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(&ДатаСреза, ИдентификаторТехКарты = &ТехКарта) КАК торо_ВерсииТехКартСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты КАК торо_ТехКарты
		|		ПО торо_ВерсииТехКартСрезПоследних.ТехКарта = торо_ТехКарты.Ссылка";
		
		Запрос.УстановитьПараметр("ТехКарта", ТекНормРемонт);
		Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДата());
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Возврат ВыборкаДетальныеЗаписи.НормаВремени * КолОР;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьОтображениеПродолжительности()
	
	Если Элементы.РегламентныеМероприятия.ТекущиеДанные = Неопределено Тогда
		Элементы.Перерассчитать.Доступность = Ложь;
		Элементы.СпособРасчетаПродолжительности.Доступность = Ложь;
		Элементы.НормаВремени.Заголовок = НСтр("ru = 'Не выбран вид мероприятия'");
	Иначе
		Элементы.Перерассчитать.Доступность = Истина И ПравоРедактирования;
		Элементы.СпособРасчетаПродолжительности.Доступность = Истина;
		СтроковоеПредставлениеНормы = торо_ОбщегоНазначенияКлиентСервер.СформироватьЗаголовокПоПродолжительности(Элементы.РегламентныеМероприятия.ТекущиеДанные.Продолжительность);
		Элементы.НормаВремени.Заголовок	= СтроковоеПредставлениеНормы;
		
		Если Элементы.РегламентныеМероприятия.ТекущиеДанные.Продолжительность = 0 Тогда
			Элементы.РегламентныеМероприятия.ТекущиеДанные.СпособРасчетаПродолжительности = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеГрафикаРемРабНаСервере()
	
	Возврат Константы.торо_ГрафикРемонтныхРабот.Получить();
	
КонецФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура СообщитьОВозможномИзмененииПродолжительностиРемонта()
	
	Если Объект.ИндивидуальноеОбслуживаниеОР <> флИндОбслужНачальный Тогда
		ТекстСообщения = НСтр("ru = 'Изменен флаг ""Индивидуальное обслуживание ОР"", перерассчитайте продолжительность ремонтов!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если флСписокОбъектовМодифицированность Тогда
		ТекстСообщения = НСтр("ru = 'Изменен состав списка объектов ремонта, перерассчитайте продолжительность ремонтов!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если НЕ Объект.ИндивидуальноеОбслуживаниеОР И СписокМодифицированныхМероприятий.Количество() Тогда
		
		Для каждого Элем Из СписокМодифицированныхМероприятий Цикл
			ШаблонСообщения = НСтр("ru = 'Изменен состав списка объектов ремонта мероприятия ""%1"", перерассчитайте продолжительность ремонтов!'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Элем.Значение);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоэффициентыРемонтныхОсобенностей()
	
	Перем МассивКоэффициентов;
	
	РассчитываемыеКоэффициенты.Очистить();
	УстанавливаемыеКоэффициенты.Очистить();
	
	МассивКоэффициентов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних.ВидКоэффициента КАК Ссылка,
	|	торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних.ЗначениеКоэффициента,
	|	торо_ВидыКоэффициентовРемонтныхОсобенностей.Рассчитываемый,
	|	торо_ВидыКоэффициентовРемонтныхОсобенностей.ЗависитОтНаработки,
	|	торо_ВидыКоэффициентовРемонтныхОсобенностей.СчитатьВозрастОтДатыИзготовления,
	|	торо_ВидыКоэффициентовРемонтныхОсобенностей.УчитыватьГрафикРаботыОборудования,
	|	торо_ВидыКоэффициентовРемонтныхОсобенностей.УчитыватьПростоиОборудования,
	|	торо_ВидыКоэффициентовРемонтныхОсобенностей.ПараметрНаработки
	|ИЗ
	|	РегистрСведений.торо_ЗначенияКоэффициентовРемонтныхОсобенностей.СрезПоследних(&ТекущаяДата, ОбъектРемонта = &ОбъектРемонта И Использование) КАК торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ВидыКоэффициентовРемонтныхОсобенностей КАК торо_ВидыКоэффициентовРемонтныхОсобенностей
	|		ПО торо_ЗначенияКоэффициентовРемонтныхОсобенностейСрезПоследних.ВидКоэффициента = торо_ВидыКоэффициентовРемонтныхОсобенностей.Ссылка
	|ГДЕ
	|	НЕ торо_ВидыКоэффициентовРемонтныхОсобенностей.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ОбъектРемонта", Объект.Ссылка);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Рассчитываемый Тогда
					
			НС = РассчитываемыеКоэффициенты.Добавить();
			НС.ВидКоэффициента = Выборка.Ссылка;
			НС.ЗначениеКоэффициента = Выборка.ЗначениеКоэффициента;
			НС.Коэффициент = Выборка.ЗначениеКоэффициента.Коэффициент;
			
			МассивКоэффициентов.Добавить(НС.Коэффициент);			
		Иначе
			НС = УстанавливаемыеКоэффициенты.Добавить();
			НС.ВидКоэффициента = Выборка.Ссылка;
			НС.ЗначениеКоэффициента = Выборка.ЗначениеКоэффициента;
			НС.Коэффициент = Выборка.ЗначениеКоэффициента.Коэффициент;
			
			МассивКоэффициентов.Добавить(НС.Коэффициент);
		КонецЕсли;
	КонецЦикла;
	
	ИтогКоэф = 1;
	Для каждого Элем Из МассивКоэффициентов Цикл
		
		ИтогКоэф = ИтогКоэф * Элем;
		
	КонецЦикла; 
	
	Если НЕ МассивКоэффициентов.Количество() = 0 Тогда
		ИтоговыйКоэффициент = ИтогКоэф;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыЗапросовНаСервере()
	
	Кнопка = Элементы.ВыполненныеМероприятияОтображатьТолькоПроведенныеАкты;
	ВыполненныеМероприятия.Параметры.УстановитьЗначениеПараметра("ОтображатьТолькоПроведенные", Кнопка.Пометка);
	ВыполненныеМероприятия.Параметры.УстановитьЗначениеПараметра("СписокОбъектов", Объект.Ссылка);
	
	Кнопка = Элементы.ПредстоящиеМероприятияОтображатьТолькоПроведенныеГрафикиРМ;
	ПредстоящиеМероприятия.Параметры.УстановитьЗначениеПараметра("ОтображатьТолькоПроведенные", Кнопка.Пометка);
	ПредстоящиеМероприятия.Параметры.УстановитьЗначениеПараметра("СписокОбъектов", Объект.Ссылка);
	
	Кнопка = Элементы.ВыполненныеМероприятияОтображатьТолькоЗавершенные;
	ВыполненныеМероприятия.Параметры.УстановитьЗначениеПараметра("ОтображатьТолькоЗавершенные", Кнопка.Пометка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДоступныхВыходныхДокументов()
	СтруктураФО = Новый Структура();
	СтруктураФО.Вставить("Предписание", ПолучитьФункциональнуюОпцию("торо_ИспользоватьВнешниеОснованияДляРабот"));
	СтруктураФО.Вставить("ВыявленныйДефект", ПолучитьФункциональнуюОпцию("торо_УчетВыявленныхДефектовОборудования"));
	СтруктураФО.Вставить("УчетКонтролируемыхПоказателей", ПолучитьФункциональнуюОпцию("торо_УчетКонтролируемыхПоказателей"));
	СтруктураФО.Вставить("УчетНаработкиОборудования", ПолучитьФункциональнуюОпцию("торо_УчетНаработкиОборудования"));
	СтруктураФО.Вставить("СостояниеОР", ПолучитьФункциональнуюОпцию("торо_УчетСостоянияОборудования"));
	
	Для каждого КлючИЗначение Из СтруктураФО Цикл
	    Если КлючИЗначение.Значение Тогда
			СписокДоступныхВыходныхДокументов.Добавить(Перечисления.торо_ВидыВыходныхДокументов[КлючИЗначение.Ключ]);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", СписокДоступныхВыходныхДокументов.ВыгрузитьЗначения());
	МассивПараметров = Новый Массив();
	МассивПараметров.Добавить(ПараметрВыбора);
	Элементы.ВыходныеДокументыВыходнойДокумент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
КонецПроцедуры

&НаСервере
Процедура ПерезаполнениеПриКопировании()
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Для Каждого Элемент Из РегламентныеМероприятия Цикл
			Элемент.СписокОбъектов = СсылкаНаЭлемент;
		КонецЦикла;
		Для Каждого Элемент Из СписокОбъектов Цикл
			Элемент.СписокОбъектов = СсылкаНаЭлемент;
		КонецЦикла;
		КоличествоОР = СписокОбъектов.Количество();
		Для Каждого Элемент Из ПредшествующиеМероприятия Цикл
			Элемент.СписокОбъектов = СсылкаНаЭлемент;
		КонецЦикла;
		Для Каждого Элемент Из ВыходныеДокументы Цикл
			Элемент.СписокОбъектов = СсылкаНаЭлемент;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТолькоЗавершенные(Команда)
	Кнопка = Элементы.ВыполненныеМероприятияОтображатьТолькоЗавершенные;
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	УстановитьПараметрыЗапросовНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьНомераСтрок()
	Сч = 1;
	Для Каждого ТекЗначение Из СписокОбъектов Цикл
		ТекЗначение.НомерПоПорядку = Сч;
		Сч = Сч + 1;
	КонецЦикла;
КонецПроцедуры

СписокОбъектовДобавляется = Ложь;
РегламентноеМероприятиеДобавляется = Ложь;
ВыходныеДокументыДобавляется = Ложь;
#КонецОбласти

