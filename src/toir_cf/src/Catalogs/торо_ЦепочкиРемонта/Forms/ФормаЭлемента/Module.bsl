#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьПересечениеВидовРемонтаВОбъектахРемонта(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПоследовательностьРемонтов

&НаКлиенте
Процедура ПоследовательностьРемонтовВидРемонтаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Для Каждого СтрокаРемонта Из Объект.ПоследовательностьРемонтов Цикл
		Если ВыбранноеЗначение = СтрокаРемонта.ВидРемонта Тогда
			СтандартнаяОбработка = Ложь;
			ТекстСообщения = НСтр("ru = 'Один вид ремонта нельзя добавить несколько раз!'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоследовательностьРемонтовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование = Истина И ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидРемонта) Тогда
		ТекстСообщения = НСтр("ru = 'Один вид ремонта нельзя добавить несколько раз!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьПересечениеВидовРемонтаВОбъектахРемонта(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	торо_РемонтныеЦиклыОборудования.ВидЦепочки,
	|	торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов
	|ПОМЕСТИТЬ торо_РемонтныеЦиклыОборудованияЦепочки
	|ИЗ
	|	РегистрСведений.торо_РемонтныеЦиклыОборудования КАК торо_РемонтныеЦиклыОборудования
	|ГДЕ
	|	торо_РемонтныеЦиклыОборудования.ВидЦепочки = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Ссылка КАК Цепочка,
	|	торо_ЦепочкиРемонтаПоследовательностьРемонтов.ВидРемонта
	|ПОМЕСТИТЬ ПоследовательностьРабот
	|ИЗ
	|	&ПоследовательностьРемонтов КАК торо_ЦепочкиРемонтаПоследовательностьРемонтов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_РемонтныеЦиклыОборудования.ВидЦепочки,
	|	торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов,
	|	ПоследовательностьРабот.ВидРемонта
	|ПОМЕСТИТЬ РемЦиклыСВидамиРемонтов
	|ИЗ
	|	торо_РемонтныеЦиклыОборудованияЦепочки КАК торо_РемонтныеЦиклыОборудования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоследовательностьРабот КАК ПоследовательностьРабот
	|		ПО торо_РемонтныеЦиклыОборудования.ВидЦепочки = ПоследовательностьРабот.Цепочка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РемЦиклыСВидамиРемонтов.ГруппаОбъектовРемонтов,
	|	РемЦиклыСВидамиРемонтов.ВидРемонта,
	|	РемЦиклыСВидамиРемонтов.ВидЦепочки
	|ПОМЕСТИТЬ РемонтныеЦиклыОР
	|ИЗ
	|	РемЦиклыСВидамиРемонтов КАК РемЦиклыСВидамиРемонтов
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(РемЦиклыСВидамиРемонтов.ГруппаОбъектовРемонтов) = ТИП(Справочник.торо_ОбъектыРемонта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РемЦиклыСВидамиРемонтов.ГруппаОбъектовРемонтов,
	|	РемЦиклыСВидамиРемонтов.ВидРемонта,
	|	РемЦиклыСВидамиРемонтов.ВидЦепочки
	|ПОМЕСТИТЬ РемонтныеЦиклыРГ
	|ИЗ
	|	РемЦиклыСВидамиРемонтов КАК РемЦиклыСВидамиРемонтов
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(РемЦиклыСВидамиРемонтов.ГруппаОбъектовРемонтов) = ТИП(Справочник.торо_ТиповыеОР)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_РемонтныеЦиклыОборудованияЦепочки.ГруппаОбъектовРемонтов,
	|	торо_РемонтныеЦиклыОборудования.ВидЦепочки
	|ПОМЕСТИТЬ РемонтныеЦиклыОбъектовИГрупп
	|ИЗ
	|	РегистрСведений.торо_РемонтныеЦиклыОборудования КАК торо_РемонтныеЦиклыОборудования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ торо_РемонтныеЦиклыОборудованияЦепочки КАК торо_РемонтныеЦиклыОборудованияЦепочки
	|		ПО торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов = торо_РемонтныеЦиклыОборудованияЦепочки.ГруппаОбъектовРемонтов
	|ГДЕ
	|	торо_РемонтныеЦиклыОборудования.ВидЦепочки <> &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_НормативныеРемонтыОборудования.ВидРемонта,
	|	торо_РемонтныеЦиклыОборудованияЦепочки.ГруппаОбъектовРемонтов
	|ПОМЕСТИТЬ НормативныеРемонтыОРИРГ
	|ИЗ
	|	РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ торо_РемонтныеЦиклыОборудованияЦепочки КАК торо_РемонтныеЦиклыОборудованияЦепочки
	|		ПО торо_НормативныеРемонтыОборудования.ОбъектРемонта = торо_РемонтныеЦиклыОборудованияЦепочки.ГруппаОбъектовРемонтов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ТиповыеОРНормативныеРемонты.ВидРемонта,
	|	торо_РемонтныеЦиклыОборудованияЦепочки.ГруппаОбъектовРемонтов
	|ИЗ
	|	Справочник.торо_ТиповыеОР.НормативныеРемонты КАК торо_ТиповыеОРНормативныеРемонты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ торо_РемонтныеЦиклыОборудованияЦепочки КАК торо_РемонтныеЦиклыОборудованияЦепочки
	|		ПО торо_ТиповыеОРНормативныеРемонты.Ссылка = торо_РемонтныеЦиклыОборудованияЦепочки.ГруппаОбъектовРемонтов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РемонтныеЦиклыОбъектовИГрупп.ГруппаОбъектовРемонтов,
	|	РемонтныеЦиклыОбъектовИГрупп.ВидЦепочки,
	|	торо_ЦепочкиРемонтаПоследовательностьРемонтов.ВидРемонта
	|ПОМЕСТИТЬ РемЦиклыОРИРГСВидамиРемонтов
	|ИЗ
	|	РемонтныеЦиклыОбъектовИГрупп КАК РемонтныеЦиклыОбъектовИГрупп
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ЦепочкиРемонта.ПоследовательностьРемонтов КАК торо_ЦепочкиРемонтаПоследовательностьРемонтов
	|		ПО РемонтныеЦиклыОбъектовИГрупп.ВидЦепочки = торо_ЦепочкиРемонтаПоследовательностьРемонтов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НормативныеРемонтыОРИРГ.ВидРемонта,
	|	НормативныеРемонтыОРИРГ.ГруппаОбъектовРемонтов
	|ПОМЕСТИТЬ ПересечениеВРЦепочкиСНРОР
	|ИЗ
	|	РемонтныеЦиклыОР КАК РемонтныеЦиклыОР
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НормативныеРемонтыОРИРГ КАК НормативныеРемонтыОРИРГ
	|		ПО РемонтныеЦиклыОР.ГруппаОбъектовРемонтов = НормативныеРемонтыОРИРГ.ГруппаОбъектовРемонтов
	|			И РемонтныеЦиклыОР.ВидРемонта = НормативныеРемонтыОРИРГ.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НормативныеРемонтыОРИРГ.ВидРемонта,
	|	НормативныеРемонтыОРИРГ.ГруппаОбъектовРемонтов
	|ПОМЕСТИТЬ ПересечениеВРЦепочкиСНРРГ
	|ИЗ
	|	РемонтныеЦиклыРГ КАК РемонтныеЦиклыРГ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НормативныеРемонтыОРИРГ КАК НормативныеРемонтыОРИРГ
	|		ПО РемонтныеЦиклыРГ.ГруппаОбъектовРемонтов = НормативныеРемонтыОРИРГ.ГруппаОбъектовРемонтов
	|			И РемонтныеЦиклыРГ.ВидРемонта = НормативныеРемонтыОРИРГ.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РемЦиклыОРИРГСВидамиРемонтов.ВидЦепочки,
	|	РемонтныеЦиклыОР.ВидРемонта,
	|	РемонтныеЦиклыОР.ГруппаОбъектовРемонтов
	|ПОМЕСТИТЬ ПересечениеРемонтныхЦикловОР
	|ИЗ
	|	РемЦиклыОРИРГСВидамиРемонтов КАК РемЦиклыОРИРГСВидамиРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РемонтныеЦиклыОР КАК РемонтныеЦиклыОР
	|		ПО РемЦиклыОРИРГСВидамиРемонтов.ГруппаОбъектовРемонтов = РемонтныеЦиклыОР.ГруппаОбъектовРемонтов
	|			И (РемонтныеЦиклыОР.ВидРемонта = РемЦиклыОРИРГСВидамиРемонтов.ВидРемонта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РемЦиклыОРИРГСВидамиРемонтов.ВидЦепочки,
	|	РемонтныеЦиклыРГ.ГруппаОбъектовРемонтов,
	|	РемонтныеЦиклыРГ.ВидРемонта
	|ПОМЕСТИТЬ ПересечениеРемЦикловРГ
	|ИЗ
	|	РемонтныеЦиклыРГ КАК РемонтныеЦиклыРГ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РемЦиклыОРИРГСВидамиРемонтов КАК РемЦиклыОРИРГСВидамиРемонтов
	|		ПО РемонтныеЦиклыРГ.ГруппаОбъектовРемонтов = РемЦиклыОРИРГСВидамиРемонтов.ГруппаОбъектовРемонтов
	|			И РемонтныеЦиклыРГ.ВидРемонта = РемЦиклыОРИРГСВидамиРемонтов.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_НормативныеРемонтыОборудования.ОбъектРемонта КАК ГруппаОбъектовРемонтов,
	|	торо_НормативныеРемонтыОборудования.ВидРемонта
	|ПОМЕСТИТЬ ПересечениеЦепочекРГиНормативныхРемонтовОР
	|ИЗ
	|	РемонтныеЦиклыРГ КАК РемонтныеЦиклыРГ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	|		ПО РемонтныеЦиклыРГ.ГруппаОбъектовРемонтов = торо_НормативныеРемонтыОборудования.ОбъектРемонта.ТиповойОР
	|			И РемонтныеЦиклыРГ.ВидРемонта = торо_НормативныеРемонтыОборудования.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_РемонтныеЦиклыОборудования.ВидЦепочки,
	|	торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов,
	|	торо_ЦепочкиРемонтаПоследовательностьРемонтов.ВидРемонта
	|ПОМЕСТИТЬ РемонтныеЦиклыОРПодчинРГизРЦРГ
	|ИЗ
	|	РегистрСведений.торо_РемонтныеЦиклыОборудования КАК торо_РемонтныеЦиклыОборудования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ЦепочкиРемонта.ПоследовательностьРемонтов КАК торо_ЦепочкиРемонтаПоследовательностьРемонтов
	|		ПО торо_РемонтныеЦиклыОборудования.ВидЦепочки = торо_ЦепочкиРемонтаПоследовательностьРемонтов.Ссылка
	|ГДЕ
	|	торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов.ТиповойОР В
	|			(ВЫБРАТЬ
	|				РемонтныеЦиклыРГ.ГруппаОбъектовРемонтов
	|			ИЗ
	|				РемонтныеЦиклыРГ КАК РемонтныеЦиклыРГ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РемонтныеЦиклыОРПодчинРГизРЦРГ.ВидЦепочки,
	|	РемонтныеЦиклыОРПодчинРГизРЦРГ.ГруппаОбъектовРемонтов,
	|	РемонтныеЦиклыРГ.ВидРемонта
	|ПОМЕСТИТЬ ПересечениеРЦРГсРЦПодчиненныхОР
	|ИЗ
	|	РемонтныеЦиклыРГ КАК РемонтныеЦиклыРГ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РемонтныеЦиклыОРПодчинРГизРЦРГ КАК РемонтныеЦиклыОРПодчинРГизРЦРГ
	|		ПО РемонтныеЦиклыРГ.ВидРемонта = РемонтныеЦиклыОРПодчинРГизРЦРГ.ВидРемонта
	|			И (РемонтныеЦиклыОРПодчинРГизРЦРГ.ГруппаОбъектовРемонтов.ТиповойОР = РемонтныеЦиклыРГ.ГруппаОбъектовРемонтов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	NULL КАК ВидЦепочки,
	|	ПересечениеВРЦепочкиСНРОР.ГруппаОбъектовРемонтов,
	|	ПересечениеВРЦепочкиСНРОР.ВидРемонта
	|ИЗ
	|	ПересечениеВРЦепочкиСНРОР КАК ПересечениеВРЦепочкиСНРОР
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ПересечениеВРЦепочкиСНРРГ.ГруппаОбъектовРемонтов,
	|	ПересечениеВРЦепочкиСНРРГ.ВидРемонта
	|ИЗ
	|	ПересечениеВРЦепочкиСНРРГ КАК ПересечениеВРЦепочкиСНРРГ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПересечениеРемонтныхЦикловОР.ВидЦепочки,
	|	ПересечениеРемонтныхЦикловОР.ГруппаОбъектовРемонтов,
	|	ПересечениеРемонтныхЦикловОР.ВидРемонта
	|ИЗ
	|	ПересечениеРемонтныхЦикловОР КАК ПересечениеРемонтныхЦикловОР
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПересечениеРемЦикловРГ.ВидЦепочки,
	|	ПересечениеРемЦикловРГ.ГруппаОбъектовРемонтов,
	|	ПересечениеРемЦикловРГ.ВидРемонта
	|ИЗ
	|	ПересечениеРемЦикловРГ КАК ПересечениеРемЦикловРГ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ПересечениеЦепочекРГиНормативныхРемонтовОР.ГруппаОбъектовРемонтов,
	|	ПересечениеЦепочекРГиНормативныхРемонтовОР.ВидРемонта
	|ИЗ
	|	ПересечениеЦепочекРГиНормативныхРемонтовОР КАК ПересечениеЦепочекРГиНормативныхРемонтовОР
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПересечениеРЦРГсРЦПодчиненныхОР.ВидЦепочки,
	|	ПересечениеРЦРГсРЦПодчиненныхОР.ГруппаОбъектовРемонтов,
	|	ПересечениеРЦРГсРЦПодчиненныхОР.ВидРемонта
	|ИЗ
	|	ПересечениеРЦРГсРЦПодчиненныхОР КАК ПересечениеРЦРГсРЦПодчиненныхОР";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ПоследовательностьРемонтов", Объект.ПоследовательностьРемонтов.Выгрузить());
	
	ТаблицаПересекающихсяВР = Запрос.Выполнить().Выгрузить();
	Если ТаблицаПересекающихсяВР.Количество()<>0 Тогда
		Для Каждого Строка Из ТаблицаПересекающихсяВР Цикл
			Если ЗначениеЗаполнено(Строка.ВидЦепочки) Тогда
				Если Строка.ВидЦепочки <> Объект.Ссылка Тогда
					ШаблонСообщения = НСтр("ru = 'Невозможно изменить состав цепочки, поскольку для ОР ""%1"" вид ремонта ""%2"" будет входить в состав цепочки ""%3"" и цепочки ""%4""!'");
					ТекстСообщения = СтрШаблон(ШаблонСообщения, Строка.ГруппаОбъектовРемонтов, Строка.ВидРемонта, Объект.Ссылка, Строка.ВидЦепочки);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
				КонецЕсли;
			Иначе	
				ШаблонСообщения = НСтр("ru = 'Невозможно изменить состав цепочки, поскольку для ОР ""%1"" вид ремонта ""%2"" уже является нормативным ремонтом, и текущая цепочка входит в состав данного объекта!'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Строка.ГруппаОбъектовРемонтов, Строка.ВидРемонта);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти