#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	МассивСтрок = СоставКомиссии.НайтиСтроки(Новый Структура("ЧленКомиссии", Председатель));
	
	Если МассивСтрок.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Сотрудник не может быть председателем и членом комиссии одновременно'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Председатель",, Отказ);
	КонецЕсли;
	
	ВспомТЗ = СоставКомиссии.Выгрузить();
	ВспомТЗ.Колонки.Добавить("Количество");
	ВспомТЗ.ЗаполнитьЗначения(1, "Количество");
	ВспомТЗ.Свернуть("ЧленКомиссии", "Количество");
	
	Для каждого Строка Из ВспомТЗ Цикл
		Если Строка.Количество > 1 Тогда
			
			МассивСтрок = СоставКомиссии.НайтиСтроки(Новый Структура("ЧленКомиссии", Строка.ЧленКомиссии));
			НомераСтрок = "";
			
			Для каждого СтрокаДляНомера Из МассивСтрок Цикл
				НомераСтрок = НомераСтрок + ", " + СтрокаДляНомера.НомерСтроки;
			КонецЦикла;
			
			НомераСтрок = Сред(НомераСтрок, 3, СтрДлина(НомераСтрок));
			
			ШаблонСообщения = НСтр("ru = 'Сотрудник %1 указан в составе членов комиссии в строках %2'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Строка.ЧленКомиссии, НомераСтрок);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#КонецЕсли