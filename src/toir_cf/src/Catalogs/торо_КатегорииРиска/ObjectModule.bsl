#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЗначениеЗаполнено(ДатаПлановаяКрайняя_Период) Тогда
		СтроковоеПредставлениеПериода = Строка(ДатаПлановаяКрайняя_КоличествоПериодов) + ВРег(Лев(Строка(ДатаПлановаяКрайняя_Период), 1));
	Иначе
		СтроковоеПредставлениеПериода = "";
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	# Область ТекстЗапроса
	Запрос.Текст = "ВЫБРАТЬ
	|	торо_КатегорииРиска.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.торо_КатегорииРиска КАК торо_КатегорииРиска
	|ГДЕ
	|	торо_КатегорииРиска.КатегорияВероятности = &КатегорияВероятности
	|	И торо_КатегорииРиска.КатегорияТяжестиПоследствий = &КатегорияТяжестиПоследствий
	|	И НЕ торо_КатегорииРиска.Ссылка = &Ссылка";
	# КонецОбласти
    Запрос.УстановитьПараметр("КатегорияВероятности"       , КатегорияВероятности);
    Запрос.УстановитьПараметр("КатегорияТяжестиПоследствий", КатегорияТяжестиПоследствий);
    Запрос.УстановитьПараметр("Ссылка"                     , Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ШаблонСообщения = НСтр("ru = 'Для пары категории вероятности <%1> и категории тяжести последствий <%2> уже имеются категории риска'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, КатегорияВероятности, КатегорияТяжестиПоследствий);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		Если Не ПометкаУдаления Тогда
			Отказ = Истина;
		КонецЕсли; 
		
	КонецЕсли; 
	 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ДатаНачала_КоличествоПериодов) Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаНачала_Период");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДатаОкончания_КоличествоПериодов) Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаОкончания_Период");
	КонецЕсли; 

	Если ЗначениеЗаполнено(ДатаПлановаяКрайняя_КоличествоПериодов) Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаПлановаяКрайняя_Период");
	КонецЕсли; 
 
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Цвет = Новый ХранилищеЗначения(ОбъектКопирования.Цвет.Получить());
	
КонецПроцедуры

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#КонецЕсли 


