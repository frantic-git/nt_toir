#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючКОРПДоступен = Истина;
	Если НЕ слкМенеджерЗащитыСервер.СерияЗащитыИнициализирована("28DD", Истина) Тогда 
		КлючКОРПДоступен = Ложь;
		Если слкМенеджерЗащитыСервер.СерияЗащитыИнициализирована("5DD5", Истина) Тогда 
			НаборИмен = торо_СЛКСервер.ПолучитьНаборКонстантБлокируемыхВПРОФ();
			Для Каждого ИмяКонстанты Из НаборИмен Цикл
				Элемент = Элементы.Найти(ИмяКонстанты);
				Если Элемент <> Неопределено Тогда 
					Элемент.ТолькоПросмотр = Истина; 
					Элемент.Доступность = Ложь;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ЭтаФорма.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДосутпностьЭлементов();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура торо_ИспользоватьТиповыеОРПриИзменении(Элемент)
	
	Элементы.торо_ИспользоватьКартинкиТиповыхОбъектов.Доступность = Объект.торо_ИспользоватьТиповыеОР;
	
	Если НЕ Объект.торо_ИспользоватьТиповыеОР Тогда 
		Объект.торо_ИспользоватьКартинкиТиповыхОбъектов = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьГарантииПриИзменении(Элемент)
	
	Элементы.торо_ЗапретитьРемонтГарантийногоОборудованияХозспособом.Доступность = Объект.торо_ИспользоватьГарантии;
	
	Если НЕ Объект.торо_ИспользоватьГарантии Тогда 
		Объект.торо_ЗапретитьРемонтГарантийногоОборудованияХозспособом = Ложь;		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьФункциональныеМестаПриИзменении(Элемент)

	Если НЕ Объект.торо_ИспользоватьФункциональныеМеста Тогда
		ЕстьФМ = ПроверитьНаличиеФМ();
		Если ЕстьФМ Тогда 
			Объект.торо_ИспользоватьФункциональныеМеста = Истина;
			ТекстСообщения = НСтр("ru = 'Нельзя изменить настройку, пока в системе есть объекты с типом функциональное место!'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура торо_УчетКонтролируемыхПоказателейПриИзменении(Элемент)
	
	Элементы.торо_ИспользоватьРемонтыПоСостоянию.Доступность = Объект.торо_УчетКонтролируемыхПоказателей;
	Элементы.торо_ИспользоватьСтандартныеОповещенияОПревышенииПоказателей.Доступность = Объект.торо_УчетКонтролируемыхПоказателей;
	
	Если НЕ Объект.торо_УчетКонтролируемыхПоказателей Тогда 
		Объект.торо_ИспользоватьРемонтыПоСостоянию = Ложь;
		Объект.торо_ИспользоватьСтандартныеОповещенияОПревышенииПоказателей = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура торо_УчетСостоянияОборудованияПриИзменении(Элемент)
	
	Элементы.торо_УчетПусковОборудования.Доступность = Объект.торо_УчетСостоянияОборудования;
	Элементы.торо_НапоминатьЗафиксироватьПростой.Доступность = Объект.торо_УчетСостоянияОборудования;
	
	Если НЕ Объект.торо_УчетСостоянияОборудования Тогда 
		Объект.торо_УчетПусковОборудования = Ложь;
		Объект.торо_НапоминатьЗафиксироватьПростой = Ложь;
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура торо_УчетВыявленныхДефектовОборудованияПриИзменении(Элемент)
	
	Элементы.торо_ИспользоватьУпрощеннуюФормуДефекта.Доступность = Объект.торо_УчетВыявленныхДефектовОборудования;
	Элементы.торо_НазначениеНаРемонтСУчетомРисков.Доступность = Объект.торо_УчетВыявленныхДефектовОборудования;
	Элементы.торо_ИспользоватьАнализКоренныхПричин.Доступность = Объект.торо_УчетВыявленныхДефектовОборудования;
	
	Если НЕ Объект.торо_УчетВыявленныхДефектовОборудования Тогда 
		Объект.торо_ИспользоватьУпрощеннуюФормуДефекта = Ложь;
		Объект.торо_НазначениеНаРемонтСУчетомРисков = Ложь;
		Объект.торо_ИспользоватьАнализКоренныхПричин = Ложь;
		Объект.торо_ИспользоватьВремяУчетаРецидивностиДефектов = Ложь;
		
		Элементы.торо_ВремяУчетаРецидивностиДефектов.Доступность = Ложь;
		Элементы.торо_ИспользоватьВремяУчетаРецидивностиДефектов.Доступность = Ложь;
	Иначе
		Элементы.торо_ВремяУчетаРецидивностиДефектов.Доступность = Ложь;
		Элементы.торо_ИспользоватьВремяУчетаРецидивностиДефектов.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьППРПриИзменении(Элемент)
	 	
	Элементы.торо_ИспользоватьФоновыйРасчетППР.Доступность = Объект.торо_ИспользоватьППР;
	
	Если НЕ Объект.торо_ИспользоватьППР Тогда 
		Объект.торо_ИспользоватьФоновыйРасчетППР = Ложь;
		Объект.торо_НеПересчитыватьППРВФонеПослеОшибки = Ложь;
		Элементы.торо_НеПересчитыватьППРВФонеПослеОшибки.Доступность = Объект.торо_ИспользоватьФоновыйРасчетППР;
	КонецЕсли;
	
	ПроверитьВключитьУчетСостояний();
			
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьФоновыйРасчетППРПриИзменении(Элемент)
	
	Элементы.торо_НеПересчитыватьППРВФонеПослеОшибки.Доступность = Объект.торо_ИспользоватьФоновыйРасчетППР;
	Если НЕ Объект.торо_ИспользоватьФоновыйРасчетППР Тогда
		Объект.торо_НеПересчитыватьППРВФонеПослеОшибки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользованиеЛимитовРемонтовПриИзменении(Элемент)
	
	Элементы.торо_ИспользоватьОценкиРМ.Доступность = Объект.торо_ИспользоватьЛимитыРемонтов;
	
	Если НЕ Объект.торо_ИспользоватьЛимитыРемонтов Тогда 
		Объект.торо_ИспользоватьОценкиРМ = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьНарядыНаВыполнениеРаботПриИзменении(Элемент)
	
	Элементы.торо_ИспользоватьНарядДопускНаРаботыПовышеннойОпасностиОбязательно.Доступность = Объект.торо_ИспользоватьНарядыНаВыполнениеРабот;
	
	Если НЕ Объект.торо_ИспользоватьНарядыНаВыполнениеРабот Тогда 
		Объект.торо_ИспользоватьНарядДопускНаРаботыПовышеннойОпасностиОбязательно = Ложь;
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура торо_ПоказыватьПодсистемуKPIПриИзменении(Элемент)
	Элементы.торо_АвтоматическиОбновлятьПоставляемуюМодельПоказателейKPI.Доступность = Объект.торо_ПоказыватьПодсистемуKPI;
	Если Не Объект.торо_ПоказыватьПодсистемуKPI Тогда
		Объект.торо_АвтоматическиОбновлятьПоставляемуюМодельПоказателейKPI = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСинхронизациюДанныхПриИзменении(Элемент)
	
	Если НЕ Объект.ИспользоватьСинхронизациюДанных Тогда
		Объект.торо_ИспользоватьСинхронизациюСERP = Ложь;
	КонецЕсли; 
	
	Если НЕ Объект.торо_РасширенныйРежимМТО ИЛИ НЕ Объект.ИспользоватьСинхронизациюДанных Тогда
		Объект.торо_ИнтеграцияЗаказовСERP = Ложь;
	КонецЕсли;
	
	Элементы.торо_ИспользоватьСинхронизациюСERP.Доступность = Объект.ИспользоватьСинхронизациюДанных И КлючКОРПДоступен;
	Элементы.торо_ИнтеграцияЗаказовСERP.Доступность = Объект.торо_РасширенныйРежимМТО И Объект.ИспользоватьСинхронизациюДанных;
	
КонецПроцедуры

&НаКлиенте
Процедура торо_РасширенныйРежимМТОПриИзменении(Элемент)
	
	Если НЕ Объект.торо_РасширенныйРежимМТО ИЛИ НЕ Объект.ИспользоватьСинхронизациюДанных Тогда
		Объект.торо_ИнтеграцияЗаказовСERP = Ложь;
	КонецЕсли;

	Элементы.торо_ИнтеграцияЗаказовСERP.Доступность = Объект.торо_РасширенныйРежимМТО И Объект.ИспользоватьСинхронизациюДанных;

КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьВремяУчетаРецидивностиДефектовПриИзменении(Элемент)
	Элементы.торо_ВремяУчетаРецидивностиДефектов.Доступность = Объект.торо_ИспользоватьВремяУчетаРецидивностиДефектов;
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьОбщийПланРаботПриИзменении(Элемент)
	Если Не Объект.торо_ИспользоватьОбщийПланРабот Тогда
		Элементы.Группа13.Доступность = Ложь;
		Объект.торо_ИспользоватьСкользящееПланирование = Ложь;
	Иначе
		Элементы.Группа13.Доступность = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьСметыРемонтаПриИзменении(Элемент)
	Элементы.торо_РазрешитьВводНесколькихСметПоРемонту.Доступность = Объект.торо_ИспользоватьСметыРемонта;
	
	Если Не Объект.торо_ИспользоватьСметыРемонта Тогда
		Объект.торо_РазрешитьВводНесколькихСметПоРемонту = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура торо_УчитыватьСтатистикуПотребленияПриИзменении(Элемент)
	
	Если Объект.торо_УчитыватьСтатистикуПотребления = Истина И Объект.торо_ПериодРасчетаСтатистикиПотребления = 0 Тогда
		Объект.торо_ПериодРасчетаСтатистикиПотребления = 5;
	КонецЕсли;
	
	Элементы.торо_ПериодРасчетаСтатистикиПотребления.Доступность = Объект.торо_УчитыватьСтатистикуПотребления;
КонецПроцедуры

&НаКлиенте
Процедура торо_ИспользоватьНастраиваемыеПредставленияПриИзменении(Элемент)   
	
	Если НЕ Объект.торо_ИспользоватьНастраиваемыеПредставления Тогда
		Объект.торо_РазрешитьПользовательскуюНастройкуПредставлений = Ложь;
	КонецЕсли;
	
	Элементы.торо_РазрешитьПользовательскуюНастройкуПредставлений.Доступность = Объект.торо_ИспользоватьНастраиваемыеПредставления;
	
КонецПроцедуры

&НаКлиенте
Процедура торо_УчетНаработкиОборудованияПриИзменении(Элемент)

	ПроверитьВключитьУчетСостояний();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура УстановитьДосутпностьЭлементов()
	
	Элементы.торо_ЗапретитьРемонтГарантийногоОборудованияХозспособом.Доступность            = Объект.торо_ИспользоватьГарантии;
	Элементы.торо_ИспользоватьКартинкиТиповыхОбъектов.Доступность                           = Объект.торо_ИспользоватьТиповыеОР;
	Элементы.торо_ИспользоватьРемонтыПоСостоянию.Доступность                                = Объект.торо_УчетКонтролируемыхПоказателей;
	Элементы.торо_ИспользоватьСтандартныеОповещенияОПревышенииПоказателей.Доступность       = Объект.торо_УчетКонтролируемыхПоказателей;
	Элементы.торо_ИспользоватьУпрощеннуюФормуДефекта.Доступность                            = Объект.торо_УчетВыявленныхДефектовОборудования;
	Элементы.торо_НазначениеНаРемонтСУчетомРисков.Доступность 								= Объект.торо_УчетВыявленныхДефектовОборудования;
	Элементы.торо_ИспользоватьАнализКоренныхПричин.Доступность 								= Объект.торо_УчетВыявленныхДефектовОборудования;
	Элементы.торо_ИспользоватьФоновыйРасчетППР.Доступность                                  = Объект.торо_ИспользоватьППР;
	Элементы.торо_НеПересчитыватьППРВФонеПослеОшибки.Доступность 							= Объект.торо_ИспользоватьФоновыйРасчетППР;
	Элементы.торо_ИспользоватьОценкиРМ.Доступность                                          = Объект.торо_ИспользоватьЛимитыРемонтов;
	Элементы.торо_ИспользоватьНарядДопускНаРаботыПовышеннойОпасностиОбязательно.Доступность = Объект.торо_ИспользоватьНарядыНаВыполнениеРабот;
	Элементы.торо_АвтоматическиОбновлятьПоставляемуюМодельПоказателейKPI.Доступность	    = Объект.торо_ПоказыватьПодсистемуKPI;
	Элементы.торо_ПериодРасчетаСтатистикиПотребления.Доступность							= Объект.торо_УчитыватьСтатистикуПотребления;
	Элементы.торо_ИспользоватьСинхронизациюСERP.Доступность                                 = Объект.ИспользоватьСинхронизациюДанных;
	Элементы.торо_ИнтеграцияЗаказовСERP.Доступность                                         = Объект.торо_РасширенныйРежимМТО И Объект.ИспользоватьСинхронизациюДанных;
	Элементы.торо_ИспользоватьВремяУчетаРецидивностиДефектов.Доступность                    = Объект.торо_УчетНаработкиОборудования;
	Элементы.торо_ВремяУчетаРецидивностиДефектов.Доступность                                = Объект.торо_ИспользоватьВремяУчетаРецидивностиДефектов;
	Элементы.торо_РазрешитьВводНесколькихСметПоРемонту.Доступность                          = Объект.торо_ИспользоватьСметыРемонта;
	Элементы.торо_РазрешитьПользовательскуюНастройкуПредставлений.Доступность               = Объект.торо_ИспользоватьНастраиваемыеПредставления;
	
	Если Не Объект.торо_ИспользоватьОбщийПланРабот Тогда
		Элементы.Группа13.Доступность = Ложь;
		Объект.торо_ИспользоватьСкользящееПланирование = Ложь;
	Иначе
		Элементы.Группа13.Доступность = Истина;
	КонецЕсли;	
	
	УстановитьВидимостьДоступностьУчетСостояний();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВключитьУчетСостояний()
	
	Если Объект.торо_УчетНаработкиОборудования И Объект.торо_ИспользоватьППР Тогда
		Объект.торо_УчетСостоянияОборудования = Истина;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьУчетСостояний();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьУчетСостояний()
	
	УчетСостоянийОбязателен = Объект.торо_УчетНаработкиОборудования И Объект.торо_ИспользоватьППР;
	Элементы.торо_УчетСостоянияОборудования.Доступность = НЕ УчетСостоянийОбязателен;
	Элементы.торо_УчетПусковОборудования.Доступность = Объект.торо_УчетСостоянияОборудования;
	Элементы.торо_НапоминатьЗафиксироватьПростой.Доступность = Объект.торо_УчетСостоянияОборудования;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьНаличиеФМ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ОбъектыРемонта.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	               |ГДЕ
	               |	торо_ОбъектыРемонта.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.торо_ТипыОбъектовRCM.ФункциональноеМесто)
	               |	И НЕ торо_ОбъектыРемонта.ПометкаУдаления
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_ТиповыеОР.Ссылка
	               |ИЗ
	               |	Справочник.торо_ТиповыеОР КАК торо_ТиповыеОР
	               |ГДЕ
	               |	торо_ТиповыеОР.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.торо_ТипыОбъектовRCM.ФункциональноеМесто)
	               |	И НЕ торо_ТиповыеОР.ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти