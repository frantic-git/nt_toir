#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Истина);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Выбрать(Команда)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		СтруктураВозврата = ПолучитьСтруктуруВозвратаНаСервере(ТекущиеДанные.Ссылка);
		Оповестить("ОБРАБОТКА_ВЫБОРА_СЦЕНАРИЯ", СтруктураВозврата);
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Функция ПолучитьСтруктуруВозвратаНаСервере(Ссылка)
	
	СтруктураКонстант = Новый Структура;
	МетаданныеСправочника = Метаданные.Справочники.торо_ПредопределенныеСценарииРаботыСистемы;
	
	Для каждого Реквизит из МетаданныеСправочника.Реквизиты Цикл
		Если Реквизит.Имя = "Описание" Тогда
			Продолжить;
		КонецЕсли;
		СтруктураКонстант.Вставить(Реквизит.Имя, Неопределено);
	КонецЦикла;
		
	Структура = Новый Структура();
	Структура.Вставить("Ссылка", Ссылка);
	Структура.Вставить("Предопределенный", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ *
	|ИЗ
	|	Справочник.торо_ПредопределенныеСценарииРаботыСистемы КАК торо_ПредопределенныеСценарииРаботыСистемы
	|ГДЕ
	|	торо_ПредопределенныеСценарииРаботыСистемы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(СтруктураКонстант, Выборка);

		Структура.Предопределенный = Выборка.Предопределенный;
		Структура.Вставить("МассивСтруктурЗначенийКонстант", СтруктураКонстант);
		
		ТЗВводНаОсновании = Выборка.ВводНаОсновании.Выгрузить();
		
		Если ТЗВводНаОсновании.Количество() > 0 Тогда
			
			Соответствие = Новый Соответствие;
			
			Для каждого Строка Из ТЗВводНаОсновании Цикл
				Соответствие.Вставить(Строка.ПараДокументов,Строка.Значение);
			КонецЦикла;
			
			Структура.Вставить("СоответствиеВводНаОсновании", Соответствие);
			
		КонецЕсли; 
		
	КонецЕсли;
	
	Возврат Структура;

КонецФункции
#КонецОбласти
 
