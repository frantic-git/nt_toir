#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ID_ПараллельнойОперации", ID_ПараллельнойОперации) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ID_ПараллельнойОперации) Тогда
		ID_ПараллельнойОперации = Строка(Новый УникальныйИдентификатор());
	КонецЕсли; 
	
	Параметры.Свойство("ID_Операции", ID_Операции);
	
	Для каждого Строка Из Параметры.СписокТехОпераций Цикл
		НоваяСтрока = СписокТехОпераций.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Операция = Строка.ТехОперация;
		
		Если ЗначениеЗаполнено(Строка.ID_ПараллельнойОперации) Тогда
			НоваяСтрока.Выбран = Истина;
		КонецЕсли; 
		
		торо_ТехнологическиеКартыКлиентСервер.ЗаполнитьИндексКартинкиОперации(НоваяСтрока);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Завершить(Команда)
	
	СоответствиеРезультата = Новый Соответствие();
    ЕстьВыбранныеДляТекущей = Ложь;
	
	Для каждого Строка Из СписокТехОпераций Цикл
		Если Не Строка.Выбран Тогда
			Продолжить;
		КонецЕсли;
		
		Структура = Новый Структура("ID_Операции, ID_ПараллельнойОперации");
		ЗаполнитьЗначенияСвойств(Структура, Строка);
		
		Если Строка.ID_ПараллельнойОперации = ID_ПараллельнойОперации Тогда
			ЕстьВыбранныеДляТекущей = Истина;
		КонецЕсли; 
		
		СоответствиеРезультата[Строка.ID_Операции] = Структура;
	КонецЦикла;  
	
	Если ЕстьВыбранныеДляТекущей Тогда
		Структура = Новый Структура("ID_Операции, ID_ПараллельнойОперации");
		
		ЗаполнитьЗначенияСвойств(Структура, ЭтаФорма);
		СоответствиеРезультата[ЭтаФорма.ID_Операции] = Структура;
	КонецЕсли; 
	
	СтруктураРезультата = Новый Структура("ПодборПараллельных, СоответствиеРезультата", Истина, СоответствиеРезультата);
	ОповеститьОВыборе(СтруктураРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокТехОперацийВыбранПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокТехОпераций.ТекущиеДанные;
	
	Если Не ТекущиеДанные = Неопределено Тогда
		Если ТекущиеДанные.Выбран Тогда
			ТекущиеДанные.ID_ПараллельнойОперации = ID_ПараллельнойОперации;
		Иначе
			ТекущиеДанные.ID_ПараллельнойОперации = "";
		КонецЕсли; 
		
		торо_ТехнологическиеКартыКлиентСервер.ЗаполнитьИндексКартинкиОперации(ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти