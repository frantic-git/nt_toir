#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСоставНаправления

&НаКлиенте
Процедура СоставНаправленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НайденныеСтроки = СоставНаправления.НайтиСтроки(Новый Структура("Направление", ВыбранноеЗначение));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НС = СоставНаправления.Добавить();	
	НС.Направление = ВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставНаправленияПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НЕ ОтменаРедактирования Тогда
		ТекДанные = Элементы.СоставНаправления.ТекущиеДанные;
		НайденныеСтроки = СоставНаправления.НайтиСтроки(Новый Структура("Направление", ТекДанные.Направление));
		Дубль = Ложь;
		ИдТекСтроки = ТекДанные.ПолучитьИдентификатор();
		Для каждого НайденаяСтрока из НайденныеСтроки Цикл
			Если НайденаяСтрока.ПолучитьИдентификатор() <> ИдТекСтроки Тогда
				Дубль = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Дубль > 0 Тогда
			ТекстСообщения = НСтр("ru='Это направление уже добавлено'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СложитьИзНаименований(Команда)
	
	СложитьИзНаименованийНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИВремяСоздания(Команда)
	
	ДатаИВремяСозданияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	НомерСтроки = 1;
	СписокНаправлений = Новый СписокЗначений;
	Для Каждого Стр Из СоставНаправления Цикл
		Если НЕ ЗначениеЗаполнено(Стр.Направление) Тогда
			ШаблонСообщения = НСтр("ru='В строке %1 не заполнено направление.'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НомерСтроки);
			Поле = СтрШаблон("СоставНаправления[%1].Направление", СоставНаправления.Индекс(Стр));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,Поле);
			Возврат;
		КонецЕсли;
		
		СписокНаправлений.Добавить(Стр.Направление);	
	   	НомерСтроки = НомерСтроки + 1;
	КонецЦикла; 
		
	ЗначенияЗаполнения = Новый Структура("Наименование, СписокНаправлений", Объект.Наименование, СписокНаправлений);
	ЗначенияЗаполнения.Вставить("НаименованиеЯзык1", Объект.НаименованиеЯзык1);
	ЗначенияЗаполнения.Вставить("НаименованиеЯзык2", Объект.НаименованиеЯзык2);
	
	ПараметрыОткрытия = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.торо_НаправленияОбъектовРемонтныхРабот.Форма.ФормаЭлемента", ПараметрыОткрытия);
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе", Ложь);
	ОткрытьФорму("Справочник.торо_НаправленияОбъектовРемонтныхРабот.ФормаВыбора", ПараметрыФормы, Элементы.СоставНаправления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СложитьИзНаименованийНаСервере()

	Объект.Наименование = СоставитьНаименованиеПоПервымСловам("Наименование");
	
	Если МультиязычностьСервер.ИспользуетсяПервыйДополнительныйЯзык() Тогда
		Объект.НаименованиеЯзык1 = СоставитьНаименованиеПоПервымСловам("НаименованиеЯзык1");
	КонецЕсли;

	Если МультиязычностьСервер.ИспользуетсяВторойДополнительныйЯзык() Тогда
		Объект.НаименованиеЯзык2 = СоставитьНаименованиеПоПервымСловам("НаименованиеЯзык2");
	КонецЕсли;
	
	ПереставитьНаименованияСУчетомТекущегоЯзыка();
	
КонецПроцедуры
	
&НаСервере
Функция СоставитьНаименованиеПоПервымСловам(ИмяРеквизита)
	
	Наименование = "";
	
	Первый = Истина;
	
	Для каждого СтрНаправление Из СоставНаправления Цикл
		НаименованиеЧасти = СтрНаправление.Направление[ИмяРеквизита];
		МассивСлов = СтрРазделить(НаименованиеЧасти, " ,", Ложь);
		
		Если МассивСлов.Количество() >= 1 Тогда
			ПервоеСлово = МассивСлов[0];
			Наименование = ?(Первый = Истина, ПервоеСлово, Наименование + "_" + ПервоеСлово);
			Первый = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СокрЛП(Наименование);
	
КонецФункции

&НаСервере
Процедура ДатаИВремяСозданияНаСервере()
	
	СтрокаДляШаблона = "ru='Составлено %1'; en='Created %1'";
	Объект.Наименование = СоставитьНаименованиеПоДатеСоздания(НСтр(СтрокаДляШаблона, ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Если МультиязычностьСервер.ИспользуетсяПервыйДополнительныйЯзык() Тогда
		Объект.НаименованиеЯзык1 = СоставитьНаименованиеПоДатеСоздания(НСтр(СтрокаДляШаблона, МультиязычностьСервер.КодПервогоДополнительногоЯзыкаИнформационнойБазы()));
	КонецЕсли;
	
	Если МультиязычностьСервер.ИспользуетсяВторойДополнительныйЯзык() Тогда
		Объект.НаименованиеЯзык2 = СоставитьНаименованиеПоДатеСоздания(НСтр(СтрокаДляШаблона, МультиязычностьСервер.КодВторогоДополнительногоЯзыкаИнформационнойБазы()));
	КонецЕсли;

	ПереставитьНаименованияСУчетомТекущегоЯзыка();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СоставитьНаименованиеПоДатеСоздания(ШаблонНаименования)
	
	ДатаСтрокой = Строка(ТекущаяДата());
	
	Если Не ПустаяСтрока(ШаблонНаименования) Тогда
		Возврат СтрШаблон(ШаблонНаименования, ДатаСтрокой);
	Иначе
		Возврат ДатаСтрокой;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПереставитьНаименованияСУчетомТекущегоЯзыка()
	
	торо_МультиязычностьСервер.ПереставитьЗначенияРеквизитаСУчетомЯызка(Объект, "Наименование");
	
КонецПроцедуры

#КонецОбласти




















