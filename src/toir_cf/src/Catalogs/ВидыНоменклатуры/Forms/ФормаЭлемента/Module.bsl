#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ФОИспользоватьДопРеквизиты = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения");	
	ФОИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры");
	ФОИспользоватьСерии						 = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");  
	
	Если ФОИспользоватьДопРеквизиты
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьДополнительныеРеквизиты(НаборДопРеквизитовСведенийОбъекта(Объект,"Номенклатура"), "Номенклатура");  
		Если ФОИспользоватьХарактеристикиНоменклатуры Тогда
			ЗаполнитьДополнительныеРеквизиты(НаборДопРеквизитовСведенийОбъекта(Объект,"Характеристика"), "Характеристика");
		КонецЕсли;
	КонецЕсли; 
	
	Если Не ФОИспользоватьСерии Тогда
		Элементы.ГруппаСерии.Видимость = Ложь;
	Иначе 
		ИспользованиеСерий = Объект.НастройкаИспользованияСерий;
		Если Объект.ИспользоватьСерии Тогда
			Элементы.НастройкаИспользованияСерий.Доступность = Истина; 
		ИначеЕсли НЕ ЗначениеЗаполнено(ИспользованиеСерий) Тогда 
			ИспользованиеСерий = "Не используются"; 
		КонецЕсли;
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	// СтандартныеПодсистемы.Свойства
	Контекст = Новый Структура();
	Контекст.Вставить("Объект",                     Объект);
	Контекст.Вставить("ИмяЭлементаДляРазмещения",   "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, Контекст);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПриСозданииНаСервереФормаВидаНоменклатуры(ЭтотОбъект, Объект.Ссылка, Элементы.ГруппаРаботаСНоменклатурой);
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаботаСНоменклатурой") Тогда
		Элементы.ДополнительныеРеквизитыСопоставлен.Видимость = Ложь;
		Элементы.ДополнительныеРеквизитыХарактеристикСопоставлен.Видимость = Ложь;
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

	ПравоРедактирование = ПравоДоступа("Редактирование", Метаданные.Справочники.ВидыНоменклатуры);
	
	Элементы.ДополнительныеРеквизитыНоменклатуры.Видимость   = ФОИспользоватьДопРеквизиты;
	Элементы.ДополнительныеРеквизитыХарактеристики.Видимость = ФОИспользоватьДопРеквизиты И ФОИспользоватьХарактеристикиНоменклатуры;    
	Элементы.Характеристики.Видимость = ФОИспользоватьХарактеристикиНоменклатуры;
	
	Элементы.ИспользованиеХарактеристик.Доступность = Объект.ИспользоватьХарактеристики;
	Элементы.ДополнительныеРеквизиты.ТолькоПросмотр = НЕ ПравоРедактирование;
	Элементы.ДополнительныеРеквизитыХарактеристик.ТолькоПросмотр = НЕ ПравоРедактирование;
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
	ОбработатьВидимостьСхемыОбеспечения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УстановитьДоступностьСерийХарактеристик();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ФОИспользоватьДопРеквизиты И ИмяСобытия = "Запись_НаборыДополнительныхРеквизитовИСведений" Тогда
		ЗаполнитьДополнительныеРеквизиты(НаборДопРеквизитовСведенийОбъекта(Объект,"Номенклатура"), "Номенклатура"); 
		ЗаполнитьДополнительныеРеквизиты(НаборДопРеквизитовСведенийОбъекта(Объект,"Характеристика"), "Характеристика");
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	Если ИмяСобытия = РаботаСНоменклатуройКлиент.ОписаниеОповещенийПодсистемы().ЗагрузкаКатегорий Тогда
		Если Не Модифицированность Тогда
			ПерезаполнитьИдентификаторыКатегорий();
		КонецЕсли;
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ФОИспользоватьДопРеквизиты Тогда
		
		ЗаписатьДопРеквизитыСведенияНабора(Отказ, ТекущийОбъект, "ДопРеквизит", "Номенклатура");
		
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
	РаботаСНоменклатурой.ПриЗаписиНаСервереФормаВидаНоменклатуры(ЭтотОбъект, ТекущийОбъект, Отказ);
	// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
   ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	УстановитьДоступностьСерийХарактеристик();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьХарактеристикиПриИзменении(Элемент)
	
	ИспользоватьХарактеристикиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСерииПриИзменении(Элемент)
	
	Элементы.НастройкаИспользованияСерий.Доступность = Объект.ИспользоватьСерии;
	
	Если Объект.ИспользоватьСерии Тогда
		Элементы.НастройкаИспользованияСерий.ТолькоПросмотр = Ложь;
		ИспользованиеСерий = ПредопределенноеЗначение("Перечисление.НастройкиИспользованияСерийНоменклатуры.ПустаяСсылка");
	Иначе
		Элементы.НастройкаИспользованияСерий.ТолькоПросмотр = Истина; 
		Объект.НастройкаИспользованияСерий = Неопределено;
		ИспользованиеСерий = "Не используются";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
    МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
 КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	ТипНоменклатурыПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИспользованияСерийПриИзменении(Элемент)
	Объект.НастройкаИспользованияСерий = ИспользованиеСерий;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеРеквизиты

&НаКлиенте
Процедура ДополнительныеРеквизитыПриИзменении(Элемент)
	
	УстановитьДоступностьКомандРедактированияДопРеквизитовСведений("ДопРеквизит", "Номенклатура");
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ДобавитьДопРеквизитСведение("ДопРеквизит", "Номенклатура");	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ДополнительныеРеквизиты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипВладельцаНабораЭлементов = "Номенклатура";
	ТипЭлемента                 = "ДопРеквизит";

	ПометитьТекущееСвойствоНаУдаление(
			ТекущиеДанные.ДополнительныйРеквизит,
			ТекущиеДанные.ИндексКартинки,
			ТипВладельцаНабораЭлементов,
			ТипЭлемента);
		
		Отказ = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДополнительныеРеквизиты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.ДополнительныйРеквизит);

	ПараметрыОписанияОповещения = Новый Структура("НаборСвойств, ТипВладельцаНабораЭлементов", Объект.НаборСвойств, "Номенклатура");
	
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
	ПараметрыФормы,Элементы.ДополнительныеРеквизиты,,,,Новый ОписаниеОповещения("ОбработкаРедактированияРеквизита",ЭтаФорма,ПараметрыОписанияОповещения));

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандРедактированияДопРеквизитовСведений("ДопРеквизит", "Номенклатура");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеРеквизитыХарактеристики

&НаКлиенте
Процедура ДополнительныеРеквизитыХарактеристикиПриИзменении(Элемент)
	
	УстановитьДоступностьКомандРедактированияДопРеквизитовСведений("ДопРеквизит", "Характеристика");
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыХарактеристикиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ДобавитьДопРеквизитСведение("ДопРеквизит", "Характеристика");	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыХарактеристикиПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ДополнительныеРеквизитыХарактеристик.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипВладельцаНабораЭлементов = "Характеристика";
	ТипЭлемента                 = "ДопРеквизит";

	ПометитьТекущееСвойствоНаУдаление(
			ТекущиеДанные.ДополнительныйРеквизит,
			ТекущиеДанные.ИндексКартинки,
			ТипВладельцаНабораЭлементов,
			ТипЭлемента);
		
		Отказ = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыХарактеристикиПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандРедактированияДопРеквизитовСведений("ДопРеквизит", "Характеристика");
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы
// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ПараметрыФормы = Новый Структура("Ссылка", Объект.Ссылка);
		ОткрытьФорму("Справочник.ВидыНоменклатуры.Форма.РазблокированиеРеквизитов", ПараметрыФормы,,,,, 
					Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект), 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
        ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаРедактированияРеквизита(Результат, ДопПараметры) Экспорт
	НаборСвойств                = ДопПараметры.НаборСвойств;
	ТипВладельцаНабораЭлементов = ДопПараметры.ТипВладельцаНабораЭлементов;
	ЗаполнитьДополнительныеРеквизиты(НаборСвойств, ТипВладельцаНабораЭлементов);	
КонецПроцедуры

// Процедура выполняет добавление дополнительного реквизита (сведения).
//
// Параметры:
//	ТипЭлемента (Строка) - Тип элемента который добавляется. 
//	Принимает значения: "ДопСведение", "ДопРеквизит".
//
//	ТипВладельцаНабораЭлементов (Строка) - Имя владельца набора элементов в который добавляется элемент. 
//	Принимает значения: "Номенклатура", "Характеристики", "Серии".
//
&НаКлиенте
Процедура ДобавитьДопРеквизитСведение(ТипЭлемента, ТипВладельцаНабораЭлементов, Вид = "")
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ТекстВопроса = Нстр("ru = 'Данные еще не записаны.
		|Добавление дополнительных свойств возможно только после записи элемента.
		|Записать элемент?'");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьДопРеквизитСведениеЗавершение", ЭтотОбъект, Новый Структура("Вид, ТипВладельцаНабораЭлементов, ТипЭлемента", Вид, ТипВладельцаНабораЭлементов, ТипЭлемента)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
        Возврат;
		
	КонецЕсли;
	
	ДобавитьДопРеквизитСведениеФрагмент(Вид, ТипВладельцаНабораЭлементов, ТипЭлемента);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДопРеквизитСведениеФрагмент(Знач Вид, Знач ТипВладельцаНабораЭлементов, Знач ТипЭлемента)
    
    Перем НаборДопРеквизитовСведений, ПараметрыФормы;
    
    ТекущийТипВладельцаНабораЭлементов = ТипВладельцаНабораЭлементов;
    ТекущийТипЭлемента                 = ТипЭлемента;
    
    НаборДопРеквизитовСведений = НаборДопРеквизитовСведенийОбъекта(Объект, ТипВладельцаНабораЭлементов);
	
	Если НЕ ЗначениеЗаполнено(НаборДопРеквизитовСведений) Тогда
        Возврат;
    КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НаборСвойств"             , НаборДопРеквизитовСведений);
	ПараметрыФормы.Вставить("ТекущийНаборСвойств"      , НаборДопРеквизитовСведений);
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", ТипЭлемента <> "ДопРеквизит");

	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
	ПараметрыФормы, ТаблицаФормыДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДопРеквизитСведениеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Вид                         = ДополнительныеПараметры.Вид;
    ТипВладельцаНабораЭлементов = ДополнительныеПараметры.ТипВладельцаНабораЭлементов;
    ТипЭлемента                 = ДополнительныеПараметры.ТипЭлемента;
       
    Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    Попытка
        ЭлементЗаписан = Записать();
    Исключение
        Возврат;
    КонецПопытки;
    
    Если Не ЭлементЗаписан Тогда
        Возврат;
    КонецЕсли;
    
    ДобавитьДопРеквизитСведениеФрагмент(Вид, ТипВладельцаНабораЭлементов, ТипЭлемента);

КонецПроцедуры

// Функция возвращает ссылку на таблицу набора доп.реквизитов / сведений объекта.
//
// Параметры:
//	ТипВладельцаНабораЭлементов (Строка) - Имя владельца набора элементов в который добавляется элемент. 
//	Принимает значения: "Номенклатура", "Характеристики", "Серии".
//
// Возвращаемое значение:
//	ДанныеФормыКоллекция.
//	Данные табличной части набора свойств.
//
&НаКлиентеНаСервереБезКонтекста
Функция НаборДопРеквизитовСведенийОбъекта(ОбъектНоменклатура, ТипВладельцаНабораЭлементов)
	
	Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
		Возврат ОбъектНоменклатура.НаборСвойств;
	ИначеЕсли ТипВладельцаНабораЭлементов = "Характеристика" Тогда
		Возврат ОбъектНоменклатура.НаборСвойствХарактеристик;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает коллекцию - таблицу доп. ревизитов (сведений), 
// относящуюся к нужному набору (набору номенклатуры, характеристик или серий).
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма,
//	ТипЭлемента - Строка - Тип элементов, коллекцию которых надо получить, 
// принимает значения: "ДопСведение", "ДопРеквизит".
//	ТипВладельцаНабораЭлементов - Строка - Имя владельца набора элементов, 
// принимает значения: "Номенклатура", "Характеристики", "Серии".
//
// Возвращаемое значение:
//	ДанныеФормыКоллекция - коллекция доп.реквизитов (сведений) нужного набора.
//
&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаДопРеквизитов(Форма, ТипЭлемента, ТипВладельцаНабораЭлементов)
	
	Если ТипЭлемента = "ДопРеквизит" Тогда
		
		Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
			Возврат Форма.ДополнительныеРеквизиты;
		ИначеЕсли ТипВладельцаНабораЭлементов = "Характеристика" Тогда
			Возврат Форма.ДополнительныеРеквизитыХарактеристик;
		КонецЕсли;
				
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает таблицу формы - таблицу доп. реквизитов (сведений), 
// относящуюся к нужному набору (номенклатуры, характеристик или серий).
//
// Параметры:
//	ТипЭлемента - Строка - тип элементов таблицу формы которых надо получить, принимает значения: "ДопСведение", "ДопРеквизит",
//	ТипВладельцаНабораЭлементов - Строка - Имя владельца набора элементов, таблицу формы которых надо получить, 
// принимает значения: "Номенклатура", "Характеристики", "Серии".
//
// Возвращаемое значение:
//	ТаблицаФормы - ЭлементФормы - таблица доп.реквизитов (сведений) нужного набора.
//
&НаКлиенте
Функция ТаблицаФормыДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов)
	Если ТипЭлемента = "ДопРеквизит" Тогда
		
		Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
			Возврат Элементы.ДополнительныеРеквизиты;
		ИначеЕсли ТипВладельцаНабораЭлементов = "Характеристика" Тогда
			Возврат Элементы.ДополнительныеРеквизитыХарактеристик;
		КонецЕсли;
				
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	
КонецФункции


// Возвращает имя элемента - таблицы доп. ревизитов (сведений),
// относящуюся к нужному набору (набору номенклатуры, характеристик или серий).
//
// Параметры:
//	ТипЭлемента - Строка - Тип элемента, имя элемента формы коллекции которых надо получить, принимает значения: "ДопСведение", "ДопРеквизит".
//	ТипВладельцаНабораЭлементов - Строка - Имя владельца набора элементов, принимает значения: "Номенклатура", "Характеристики", "Серии".
//
// Возвращаемое значение:
//	Строка - Имя элемента коллекции доп.реквизитов (сведений) нужного набора.
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТаблицыДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов)
	
	Если ТипЭлемента = "ДопРеквизит" Тогда
		
		Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
			Возврат "ДополнительныеРеквизиты";
		ИначеЕсли ТипВладельцаНабораЭлементов = "Характеристика" Тогда  
			Возврат "ДополнительныеРеквизитыХарактеристики";
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизиты(НаборСвойств, ТипВладельцаНабораЭлементов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК ДополнительныйРеквизит,
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Заголовок КАК ПредставлениеРеквизита,
	               |	ВЫБОР
	               |		КОГДА НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.ПометкаУдаления
	               |			ТОГДА 4
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК ИндексКартинки,
	               |	ЛОЖЬ КАК ОбщееСвойство,
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.НомерСтроки КАК НомерСтрокиДляСортировки
	               |ИЗ
	               |	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	               |ГДЕ
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство,
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Заголовок,
	               |	ВЫБОР
	               |		КОГДА НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.ПометкаУдаления
	               |			ТОГДА 4
	               |		ИНАЧЕ 1
	               |	КОНЕЦ,
	               |	ИСТИНА,
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.НомерСтроки
	               |ИЗ
	               |	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	               |ГДЕ
	               |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = &ОбщиеРеквизиты";
	               // Если нужно получить еще и общие реквизиты для ОР или типового ор, то это можно сделать в запросе и объединить.
				   
	Запрос.УстановитьПараметр("Ссылка", НаборСвойств);
				   
	Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
		Запрос.УстановитьПараметр("ОбщиеРеквизиты", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура_Общие"));
	ИначеЕсли ТипВладельцаНабораЭлементов = "Характеристика" Тогда
		Запрос.УстановитьПараметр("ОбщиеРеквизиты", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_ХарактеристикиНоменклатуры_Общие"));
	КонецЕсли; 
	
	
	ТЧРезультат = Запрос.Выполнить().Выгрузить();
	
	ТЧРезультат.Сортировать("ОбщееСвойство Убыв, НомерСтрокиДляСортировки Возр");
	
	ТаблицаДопРеквизитов = ТаблицаДопРеквизитов(ЭтаФорма, "ДопРеквизит", ТипВладельцаНабораЭлементов);
	
	СопоставленныеРеквизиты = РаботаСНоменклатурой.СопоставленныеРеквизитыПоВидамНоменклатуры(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
		
	ТаблицаДопРеквизитов.Очистить();
	
	Для каждого Строка Из ТЧРезультат Цикл
		НС = ТаблицаДопРеквизитов.Добавить();
		ПредставлениеРеквизита = Строка.ПредставлениеРеквизита + ?(Строка.ОбщееСвойство, НСтр("ru = ' (Общий)'"), "");
		ЗаполнитьЗначенияСвойств(НС, Строка,,"ПредставлениеРеквизита");                                               
		НС.ПредставлениеРеквизита = ПредставлениеРеквизита;
		НС.Сопоставлен = СопоставленныеРеквизиты.Найти(Строка.ДополнительныйРеквизит, "Реквизит") <> Неопределено;
	КонецЦикла; 
 
		
КонецПроцедуры
&НаСервере
Процедура ПометитьТекущееСвойствоНаУдаление(Свойство, ИндексКартинки, ТипВладельцаНабораЭлементов, ТипЭлемента)
	
	Если Свойство.НаборСвойств = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура_Общие")
		ИЛИ Свойство.НаборСвойств = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_ХарактеристикиНоменклатуры_Общие") Тогда
		Возврат;
	КонецЕсли; 
	НачатьТранзакцию();
	
	ОбъектСвойства = Свойство.ПолучитьОбъект();
	ОбъектСвойства.ПометкаУдаления = НЕ ОбъектСвойства.ПометкаУдаления;
	ОбъектСвойства.Записать();
	
	НаборСвойствДопРеквизитовСведений = НаборДопРеквизитовСведенийОбъекта(Объект, ТипВладельцаНабораЭлементов);
	Если НЕ ЗначениеЗаполнено(НаборСвойствДопРеквизитовСведений) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектНаборСвойств = НаборСвойствДопРеквизитовСведений.ПолучитьОбъект();
	
	Если ТипЭлемента = "ДопРеквизит" Тогда
		ТЧДополнительныеРеквизитыСведения = ОбъектНаборСвойств.ДополнительныеРеквизиты;
	ИначеЕсли ТипЭлемента = "ДопСведение" Тогда
		ТЧДополнительныеРеквизитыСведения = ОбъектНаборСвойств.ДополнительныеСведения;
	КонецЕсли;
	
	Для Каждого ЭлементСтр Из ТЧДополнительныеРеквизитыСведения Цикл
		Если ЭлементСтр.Свойство = Свойство Тогда
			ЭлементСтр.ПометкаУдаления = ОбъектСвойства.ПометкаУдаления;
		КонецЕсли;
	КонецЦикла;
	
	ОбъектНаборСвойств.Записать();
	
	ЗафиксироватьТранзакцию();
	
	ИндексКартинки = ?(ОбъектСвойства.ПометкаУдаления, 4, 1);
	
КонецПроцедуры

// Процедура выполняет запись изменившихся наборов доп. реквизитов (сведений).
// Вызывается при записи текущего элемента на сервере.
//
// Параметры:
//	Отказ (Булево) - Признак отказа от записи. Значение по умолчанию: Ложь.
//
//	ТекущийОбъект (СправочникОбъект.ВидыНоменклатуры) - Записываемый объект - вид номенклатуры.
//
//	ТипЭлемента (Строка) - Тип элемента который удаляется. 
//	Принимает значения: "ДопСведение", "ДопРеквизит",
//	ТипВладельцаНабораЭлементов (Строка) - Имя владельца набора элементов из которого удаляется элемент. 
//	Принимает значения: "Номенклатура", "Характеристики", "Серии".
//
&НаСервере
Процедура ЗаписатьДопРеквизитыСведенияНабора(Отказ, ТекущийОбъект, ТипЭлемента, ТипВладельцаНабораЭлементов)
	
	НаборСвойствДопРеквизитовСведений = НаборДопРеквизитовСведенийОбъекта(ТекущийОбъект, ТипВладельцаНабораЭлементов);
	Если НЕ ЗначениеЗаполнено(НаборСвойствДопРеквизитовСведений) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектНаборСвойств = НаборСвойствДопРеквизитовСведений.ПолучитьОбъект();
	
	Если ТипЭлемента = "ДопРеквизит" Тогда
		ТЧДополнительныеРеквизитыСведения = ОбъектНаборСвойств.ДополнительныеРеквизиты;
	ИначеЕсли ТипЭлемента = "ДопСведение" Тогда
		ТЧДополнительныеРеквизитыСведения = ОбъектНаборСвойств.ДополнительныеСведения;
	КонецЕсли;
	
	ТаблицаДопРеквизитов = ТаблицаДопРеквизитов(ЭтаФорма, ТипЭлемента, ТипВладельцаНабораЭлементов);
	ТЧДополнительныеРеквизитыСведения.Очистить();
	
	Для каждого ЭлементСтр Из ТаблицаДопРеквизитов Цикл
		Если Не ЭлементСтр.ОбщееСвойство Тогда
			НовСтр = ТЧДополнительныеРеквизитыСведения.Добавить();
			НовСтр.Свойство = ЭлементСтр.ДополнительныйРеквизит;
			НовСтр.ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлементСтр.ДополнительныйРеквизит, "ПометкаУдаления");
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		ОбъектНаборСвойств.Записать();
	Исключение
		
		// Установить признак отказа записи.
		Отказ = Истина;
		
		// Добавить запись в журнал регистрации.
		ИмяСобытия = НСтр("ru = 'Дополнительные %1 %2. Не удалось обновить состав дополнительных %3 набора.'");
		
		ТипСведений = ?(ТипЭлемента = "ДопРеквизит", "реквизиты", "сведения");
		ТипОбъекта  = ?(ТипВладельцаНабораЭлементов = "Номенклатура", "номенклатуры", ?(ТипВладельцаНабораЭлементов = "Характеристика", "характеристик", "серий"));
		ТипСостава  = ?(ТипЭлемента = "ДопРеквизит", "реквизитов", "сведений");
		
		ИмяСобытия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ИмяСобытия, 
			ТипСведений, 
			ТипОбъекта, 
			ТипСостава);
		
		КомментарийЖурнала  = НСтр("ru = 'При обновлении табличной части дополнительных реквизитов набора свойств ""%1"" произошла ошибка: ""%2""'");
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		КомментарийЖурнала  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			КомментарийЖурнала, 
			НаборСвойствДопРеквизитовСведений, 
			ПредставлениеОшибки);
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка, 
			, 
			НаборСвойствДопРеквизитовСведений, 
			КомментарийЖурнала, 
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		
		// Вызвать исключение.
		ВыражениеИсключения = НСтр("ru = 'При записи дополнительных %1 %2 произошла ошибка (см. журнал регистрации).'");
		
		ТипСведений = ?(ТипЭлемента = "ДопРеквизит", "реквизитов", "сведений");
		ТипОбъекта = ?(ТипВладельцаНабораЭлементов = "Номенклатура", "номенклатуры", ?(ТипВладельцаНабораЭлементов = "Характеристики", "характеристик", "серий"));
		
		ВыражениеИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ВыражениеИсключения, 
			ТипСведений, 
			ТипОбъекта, 
			ТипСостава);
		
		ВызватьИсключение(ВыражениеИсключения);
		
	КонецПопытки;
	
КонецПроцедуры

// Процедура управляет доступностью команд редактирования таблиц доп. реквизитов (сведений).
//
// Параметры:
//	ТипЭлемента (Строка) - Тип элемента который удаляется.
//	Принимает значения: "ДопСведение", "ДопРеквизит".
//
//	ТипВладельцаНабораЭлементов (Строка) - Имя владельца набора элементов из которого удаляется элемент.
//	Принимает значения: "Номенклатура", "Характеристики", "Серии".
//
&НаКлиенте
Процедура УстановитьДоступностьКомандРедактированияДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов)
	
	ТаблицаДопРеквизитовСведений = ТаблицаФормыДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов);
	
	ТекущиеДанные = ТаблицаДопРеквизитовСведений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДопРеквизитов = ТаблицаДопРеквизитов(ЭтаФорма, ТипЭлемента, ТипВладельцаНабораЭлементов);
	КомандыРедактирования = КомандыРедактированияДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов);
	
	// Если активная строка - строка общего доп. реквизита (сведения), то сделать недоступными кнопки удаления, перемещения.
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		КомандыРедактирования.Удалить, 
		"Доступность", 
		Не ТекущиеДанные.ОбщееСвойство);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		КомандыРедактирования.ПереместитьВверх, 
		"Доступность", 
		Не ТекущиеДанные.ОбщееСвойство);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		КомандыРедактирования.ПереместитьВниз, 
		"Доступность", 
		Не ТекущиеДанные.ОбщееСвойство);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		КомандыРедактирования.КонтекстноеМенюУдалить, 
		"Доступность", 
		Не ТекущиеДанные.ОбщееСвойство);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		КомандыРедактирования.КонтекстноеМенюПереместитьВверх, 
		"Доступность", 
		Не ТекущиеДанные.ОбщееСвойство);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		КомандыРедактирования.КонтекстноеМенюПереместитьВниз,
		"Доступность",
		Не ТекущиеДанные.ОбщееСвойство);
	
	// Если активная строка - первая или последняя в списке, то сделать недоступными кнопки сдвига вверх или вниз.
	Если Не ТекущиеДанные.ОбщееСвойство Тогда
		
		ИндексСтроки = ТаблицаДопРеквизитов.Индекс(ТекущиеДанные);
		
		Если ИндексСтроки = 0 Тогда
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, 
				КомандыРедактирования.ПереместитьВверх, 
				"Доступность", 
				Ложь);
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, 
				КомандыРедактирования.КонтекстноеМенюПереместитьВверх, 
				"Доступность", 
				Ложь);
			
		КонецЕсли;
		
		Если ИндексСтроки = ТаблицаДопРеквизитов.Количество() - 1 Тогда
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, 
				КомандыРедактирования.ПереместитьВниз, 
				"Доступность", 
				Ложь);
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, 
				КомандыРедактирования.КонтекстноеМенюПереместитьВниз, 
				"Доступность", 
				Ложь);
			
		КонецЕсли;
		
		Если ИндексСтроки <> 0 Тогда
			
			ПредыдущаяСтрока = ТаблицаДопРеквизитов[ИндексСтроки - 1];
			
			Если ПредыдущаяСтрока.ОбщееСвойство Тогда
				
				// Если предыдущая строка является строкой общего реквизита, то сделать недоступной кнопку сдвига вверх.
				ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы, 
					КомандыРедактирования.ПереместитьВверх, 
					"Доступность", 
					Ложь);
				
				ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы, 
					КомандыРедактирования.КонтекстноеМенюПереместитьВверх, 
					"Доступность", 
					Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает структуру команд редактирования таблицы доп. реквизитов (сведений).
//
// Параметры:
//	ТипЭлемента (Строка) - Тип элемента, команды редактирования коллекции которых надо получить.
//	Принимает значения: "ДопСведение", "ДопРеквизит".
//
//	ТипВладельцаНабораЭлементов (Строка) - Имя владельца набора элементов, команды редактирования коллекции которых надо получить.
//	Принимает значения: "Номенклатура", "Характеристики", "Серии".
//
// Возвращаемое значение:
//	Структура.
//	Структура команд редактирования таблицы доп. реквизитов (сведений). Ключ структуры - строка - имя команды,
//	а значение - строка - имя элемента-команды редактирования.
//
&НаКлиенте
Функция КомандыРедактированияДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов)
	
	СтруктураКоманд = Новый Структура;
	СтруктураКоманд.Вставить("Удалить");
	СтруктураКоманд.Вставить("ПереместитьВверх");
	СтруктураКоманд.Вставить("ПереместитьВниз");
	
	СтруктураКоманд.Вставить("КонтекстноеМенюУдалить");
	СтруктураКоманд.Вставить("КонтекстноеМенюПереместитьВверх");
	СтруктураКоманд.Вставить("КонтекстноеМенюПереместитьВниз");
	
	ИмяТаблицыДопРеквизитовСведений = ИмяТаблицыДопРеквизитовСведений(ТипЭлемента, ТипВладельцаНабораЭлементов);
	
	СтруктураКоманд.Удалить                         = ИмяТаблицыДопРеквизитовСведений + "Удалить";
	СтруктураКоманд.ПереместитьВверх                = ИмяТаблицыДопРеквизитовСведений + "ПереместитьВверх";
	СтруктураКоманд.ПереместитьВниз                 = ИмяТаблицыДопРеквизитовСведений + "ПереместитьВниз";
	
	СтруктураКоманд.КонтекстноеМенюУдалить          = ИмяТаблицыДопРеквизитовСведений + "КонтекстноеМенюУдалить";
	СтруктураКоманд.КонтекстноеМенюПереместитьВверх = ИмяТаблицыДопРеквизитовСведений + "КонтекстноеМенюПереместитьВверх";
	СтруктураКоманд.КонтекстноеМенюПереместитьВниз  = ИмяТаблицыДопРеквизитовСведений + "КонтекстноеМенюПереместитьВниз";
	
	Возврат СтруктураКоманд;
	
КонецФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
      УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ИспользоватьХарактеристикиПриИзмененииНаСервере()
	
	Элементы.ИспользованиеХарактеристик.Доступность = Объект.ИспользоватьХарактеристики;
	
	Если Не Объект.ИспользоватьХарактеристики Тогда
		
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать");
			
	Иначе
		
		Объект.ИспользованиеХарактеристик = ПредопределенноеЗначение("Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыХарактеристикиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРеквизитыХарактеристикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДополнительныеРеквизитыХарактеристик.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.ДополнительныйРеквизит);

	ПараметрыОписанияОповещения = Новый Структура("НаборСвойств, ТипВладельцаНабораЭлементов", Объект.НаборСвойствХарактеристик, "Характеристика");
	
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
	ПараметрыФормы,Элементы.ДополнительныеРеквизитыХарактеристик,,,,Новый ОписаниеОповещения("ОбработкаРедактированияРеквизита",ЭтаФорма,ПараметрыОписанияОповещения));

КонецПроцедуры

Процедура УстановитьДоступностьСерийХарактеристик()

	Если Элементы.Найти("ФормаСправочникСерииНоменклатурыСерии") <> Неопределено Тогда 
		Элементы.Форматоро_Перейти.ПодчиненныеЭлементы.ФормаСправочникСерииНоменклатурыСерии.Доступность = Объект.ИспользоватьСерии;
	КонецЕсли;
	Если Элементы.Найти("ФормаСправочникХарактеристикиНоменклатурыОткрытьПоЗначению") <> Неопределено Тогда
		Элементы.Форматоро_Перейти.ПодчиненныеЭлементы.ФормаСправочникХарактеристикиНоменклатурыОткрытьПоЗначению.Доступность = Объект.ИспользоватьХарактеристики;
	КонецЕсли;
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.РаботаСНоменклатурой
&НаКлиенте
Процедура Подключаемый_НажатиеРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеГиперссылки(ЭтотОбъект, Элемент, СтандартнаяОбработка,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаНавигационнойСсылкиРаботаСНоменклатурой(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеГиперссылки(ЭтотОбъект, Элемент, СтандартнаяОбработка,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыбораРаботаСНоменклатурой(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.ВыбратьОбъектСервиса(ЭтотОбъект, Элемент, СтандартнаяОбработка, Ложь,
		Новый ОписаниеОповещения("ЗакрытиеФормыВыбораОбъектаСервиса", ЭтотОбъект, Новый Структура));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОчисткаРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НажатиеОчиститьКатегорию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеРежимОбновленияРаботаСНоменклатурой(Элемент)
	
	РаботаСНоменклатуройКлиент.НажатиеРежимОбновления(ЭтотОбъект, ПодготовитьДанныеФормы(),
		Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект));
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьРаботаСНоменклатурой(Элемент, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.ОткрытьКарточкуОбъектаСервиса(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыВыбораОбъектаСервиса(ДанныеОбъекта, ДополнительныеПараметры) Экспорт 
	
	РаботаСНоменклатуройКлиент.ОбработкаОповещенияЗакрытиеФормыВыбора(
		ДанныеОбъекта, ДополнительныеПараметры, ПодготовитьДанныеФормы(),
			Новый ОписаниеОповещения("ЗакрытиеФормыЗаполненияОбъекта", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыЗаполненияОбъекта(ПараметрыЗакрытияФормы, ДополнительныеПараметры) Экспорт 
	
	ЗаполнитьРеквизитыФормы(ПараметрыЗакрытияФормы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(ПараметрыЗакрытияФормы)
	
	РаботаСНоменклатурой.ЗаполнитьРеквизитыФормы(ЭтотОбъект, ПараметрыЗакрытияФормы);
	Если ФОИспользоватьДопРеквизиты
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьДополнительныеРеквизиты(НаборДопРеквизитовСведенийОбъекта(Объект,"Номенклатура"), "Номенклатура");  
		Если ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры") Тогда
			ЗаполнитьДополнительныеРеквизиты(НаборДопРеквизитовСведенийОбъекта(Объект,"Характеристика"), "Характеристика");
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеФормы()
	
	Возврат РаботаСНоменклатурой.ПодготовитьДанныеДляИнтерактивногоЗаполнения(ЭтотОбъект);
			
КонецФункции

&НаСервере
Процедура ПерезаполнитьИдентификаторыКатегорий()
	
	РаботаСНоменклатурой.ПерезаполнитьВстроенныеРеквизиты(ЭтотОбъект);
		
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.РаботаСНоменклатурой

&НаСервере
Процедура ТипНоменклатурыПриИзмененииНаСервере()
	ОбработатьВидимостьСхемыОбеспечения();
КонецПроцедуры

&НаСервере
Процедура ОбработатьВидимостьСхемыОбеспечения()
	
	ЕстьПравоЧтения = ПравоДоступа("Чтение", Метаданные.Справочники.СхемыОбеспечения);
	Если ЕстьПравоЧтения
		И (Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
			Или Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара) Тогда
	    Элементы.СхемаОбеспечения.Видимость = Истина;
	Иначе
		Элементы.СхемаОбеспечения.Видимость = Ложь;
		Объект.СхемаОбеспечения = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
