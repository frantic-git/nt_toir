#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);

	Период = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если Период.Использование И ЗначениеЗаполнено(Период.Значение) Тогда 
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Дата", Период.Значение);
	Иначе
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Дата", ТекущаяДата());
	КонецЕсли;
		
	Если ТекИерархия.СтроитсяАвтоматически Тогда
		ЗапросВТОбъектов = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
			|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + "  КАК ОбъектИерархии,
			|	торо_ОбъектыРемонта.ДатаВводаВЭксплуатацию,
			|	торо_ОбъектыРемонта.СрокПолезногоИспользования,
			|	ВЫБОР
			|		КОГДА торо_ОбъектыРемонта.СрокПолезногоИспользования ЕСТЬ NULL 
			|				ИЛИ торо_ОбъектыРемонта.СрокПолезногоИспользования = 0
			|			ТОГДА 0
			|		ИНАЧЕ РАЗНОСТЬДАТ(торо_ОбъектыРемонта.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ) / торо_ОбъектыРемонта.СрокПолезногоИспользования * 100
			|	КОНЕЦ КАК ПроцентИспользования
			|ПОМЕСТИТЬ ВТ_ОР
			|ИЗ
			|	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
			|ГДЕ НЕ торо_ОбъектыРемонта.ЭтоГруппа;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|";

			
		Если Метаданные.Справочники[ТекИерархия.ТипРеквизитаОР].Иерархический Тогда
			
			СхемаКомпоновкиДанных.НаборыДанных.Иерархия.Запрос = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СправочникСсылка.Ссылка КАК ОбъектИерархии,
			|	СправочникСсылка.Родитель КАК РодительИерархии
			|ИЗ
			|	Справочник." + ТекИерархия.ТипРеквизитаОР + " КАК СправочникСсылка
			|ГДЕ
			|	СправочникСсылка.Ссылка В (&Элемент)";
			
			СхемаКомпоновкиДанных.НаборыДанных.Контроль.Запрос = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СправочникСсылка.Ссылка КАК ЭлементКонтроль,
			|	СправочникСсылка.Родитель КАК РодительКонтроль
			|ИЗ
			|	Справочник." + ТекИерархия.ТипРеквизитаОР + " КАК СправочникСсылка
			|ГДЕ
			|	СправочникСсылка.Родитель В(&Родитель)";
			
		Иначе
			
			СхемаКомпоновкиДанных.НаборыДанных.Иерархия.Запрос = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СправочникСсылка.Ссылка КАК ОбъектИерархии,
			|	Значение(Справочник." + ТекИерархия.ТипРеквизитаОР + ".ПустаяСсылка) КАК РодительИерархии
			|ИЗ
			|	Справочник." + ТекИерархия.ТипРеквизитаОР + " КАК СправочникСсылка"; 
			
			СхемаКомпоновкиДанных.НаборыДанных.Контроль.Запрос = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СправочникСсылка.Ссылка КАК ЭлементКонтроль,
			|	Значение(Справочник." + ТекИерархия.ТипРеквизитаОР + ".ПустаяСсылка) КАК РодительКонтроль
			|ИЗ
			|	Справочник." + ТекИерархия.ТипРеквизитаОР + " КАК СправочникСсылка"; 
						
		КонецЕсли;
		ПолеИерархия = СхемаКомпоновкиДанных.НаборыДанных.Объекты.Поля.Найти("ОбъектИерархии");
		ПолеИерархия.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+ТекИерархия.ТипРеквизитаОР);
		
		Структура = КомпоновщикНастроек.Настройки.Структура;
		Если Структура.Количество() > 0 И ТипЗнч(Структура[0]) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			ЭлементыГруппировки = Структура[0].ПоляГруппировки.Элементы;
			Для каждого Элемент из ЭлементыГруппировки Цикл
				Если Элемент.Поле = Новый ПолеКомпоновкиДанных("ОбъектИерархии") Тогда
					Элемент.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	Иначе

		Структура = КомпоновщикНастроек.Настройки.Структура;
		Если Структура.Количество() > 0 И ТипЗнч(Структура[0]) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			ЭлементыГруппировки = Структура[0].ПоляГруппировки.Элементы;
			Для каждого Элемент из ЭлементыГруппировки Цикл
				Если Элемент.Поле = Новый ПолеКомпоновкиДанных("ОбъектИерархии") Тогда
					Элемент.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ТекИерархия.ИзменяетсяДокументами Тогда
			ЗапросВТОбъектов = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Документами.ОбъектИерархии КАК ОбъектРемонта,
			|	Документами.ОбъектИерархии КАК ОбъектИерархии,
			|	Документами.РодительИерархии КАК РодительИерархии,
			|	торо_ОбъектыРемонта.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
			|	торо_ОбъектыРемонта.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
			|	ВЫБОР
			|		КОГДА торо_ОбъектыРемонта.СрокПолезногоИспользования ЕСТЬ NULL
			|				ИЛИ торо_ОбъектыРемонта.СрокПолезногоИспользования = 0
			|			ТОГДА 0
			|		ИНАЧЕ РАЗНОСТЬДАТ(торо_ОбъектыРемонта.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ) / торо_ОбъектыРемонта.СрокПолезногоИспользования * 100
			|	КОНЕЦ КАК ПроцентИспользования
			|ПОМЕСТИТЬ ВТ_ОР
			|ИЗ
			|	РегистрСведений.торо_РасположениеОРВСтруктуреИерархии.СрезПоследних(&Дата, СтруктураИерархии = &ИерархияТип) КАК Документами
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
			|		ПО Документами.ОбъектИерархии = торо_ОбъектыРемонта.Ссылка
			|ГДЕ
			|	НЕ Документами.Удален
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			СхемаКомпоновкиДанных.НаборыДанных.Иерархия.Запрос = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Документами.ОбъектИерархии КАК ОбъектИерархии,
			|	Документами.РодительИерархии КАК РодительИерархии
			|ИЗ
			|	РегистрСведений.торо_РасположениеОРВСтруктуреИерархии.СрезПоследних(&Дата, СтруктураИерархии = &ИерархияТип) КАК Документами
			|ГДЕ
			|	Документами.ОбъектИерархии В(&Элемент)
			|	И НЕ Документами.Удален";
			
			СхемаКомпоновкиДанных.НаборыДанных.Контроль.Запрос = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Документами.ОбъектИерархии КАК ЭлементКонтроль,
			|	Документами.РодительИерархии КАК РодительКонтроль
			|ИЗ
			|	РегистрСведений.торо_РасположениеОРВСтруктуреИерархии.СрезПоследних(&Дата, СтруктураИерархии = &ИерархияТип) КАК Документами
			|ГДЕ
			|	Документами.РодительИерархии В(&Родитель)
			|	И НЕ Документами.Удален";
			
		Иначе
			ЗапросВТОбъектов = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БезДокументов.ОбъектИерархии КАК ОбъектРемонта,
			|	БезДокументов.ОбъектИерархии КАК ОбъектИерархии,
			|	БезДокументов.РодительИерархии КАК РодительИерархии,
			|	торо_ОбъектыРемонта.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
			|	торо_ОбъектыРемонта.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
			|	ВЫБОР
			|		КОГДА торо_ОбъектыРемонта.СрокПолезногоИспользования ЕСТЬ NULL
			|				ИЛИ торо_ОбъектыРемонта.СрокПолезногоИспользования = 0
			|			ТОГДА 0
			|		ИНАЧЕ РАЗНОСТЬДАТ(торо_ОбъектыРемонта.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ) / торо_ОбъектыРемонта.СрокПолезногоИспользования * 100
			|	КОНЕЦ КАК ПроцентИспользования
			|ПОМЕСТИТЬ ВТ_ОР
			|ИЗ
			|	РегистрСведений.торо_ИерархическиеСтруктурыОР КАК БезДокументов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
			|		ПО БезДокументов.ОбъектИерархии = торо_ОбъектыРемонта.Ссылка
			|ГДЕ
			|	БезДокументов.СтруктураИерархии = &ИерархияТип
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			СхемаКомпоновкиДанных.НаборыДанных.Иерархия.Запрос = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	БезДокументов.ОбъектИерархии КАК ОбъектИерархии,
				|	БезДокументов.РодительИерархии КАК РодительИерархии
				|ИЗ
				|	РегистрСведений.торо_ИерархическиеСтруктурыОР КАК БезДокументов
				|ГДЕ
				|	БезДокументов.СтруктураИерархии = &ИерархияТип
				|	И БезДокументов.ОбъектИерархии В(&Элемент)";
				
			СхемаКомпоновкиДанных.НаборыДанных.Контроль.Запрос = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	БезДокументов.ОбъектИерархии КАК ЭлементКонтроль,
				|	БезДокументов.РодительИерархии КАК РодительКонтроль
				|ИЗ
				|	РегистрСведений.торо_ИерархическиеСтруктурыОР КАК БезДокументов
				|ГДЕ
				|	БезДокументов.СтруктураИерархии = &ИерархияТип
				|	И БезДокументов.РодительИерархии В(&Родитель)";
				
		КонецЕсли;
		ПолеИерархия = СхемаКомпоновкиДанных.НаборыДанных.Объекты.Поля.Найти("ОбъектИерархии");
		ПолеИерархия.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.торо_ОбъектыРемонта");
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("торо_ИспользоватьДокументыПринятияИСписанияОборудования") Тогда 
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ЗапросВТОбъектов + "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		                   |	торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
		                   |	торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних.СтатусОР КАК СтатусОР,
		                   |	торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних.Регистратор.ДатаСписания КАК ДатаСписания
		                   |ПОМЕСТИТЬ СтатусыВУчете
		                   |ИЗ
		                   |	РегистрСведений.торо_СтатусыОбъектовРемонтаВУчете.СрезПоследних(&Дата, ) КАК торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних
		                   |
		                   |ИНДЕКСИРОВАТЬ ПО
		                   |	ОбъектРемонта
		                   |;
		                   |
		                   |////////////////////////////////////////////////////////////////////////////////
		                   |ВЫБРАТЬ
		                   |	ВТ_ОР.ОбъектРемонта КАК ОбъектРемонта,
		                   |	ВТ_ОР.ОбъектИерархии КАК ОбъектИерархии,
		                   |	ВТ_ОР.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		                   |	ВТ_ОР.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		                   |	ВЫБОР
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 0
		                   |				И ВТ_ОР.ДатаВводаВЭксплуатацию <> ДАТАВРЕМЯ(1, 1, 1)
		                   |			ТОГДА ВТ_ОР.ПроцентИспользования
		                   |		ИНАЧЕ 0
		                   |	КОНЕЦ КАК ПроцентИспользования,
		                   |	ВЫБОР
		                   |		КОГДА ВТ_ОР.СрокПолезногоИспользования ЕСТЬ NULL
		                   |				ИЛИ ВТ_ОР.СрокПолезногоИспользования = 0
		                   |				ИЛИ ВТ_ОР.ДатаВводаВЭксплуатацию = ДАТАВРЕМЯ(1, 1, 1)
		                   |			ТОГДА 0
		                   |		КОГДА РАЗНОСТЬДАТ(ВТ_ОР.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ) <= 0
		                   |			ТОГДА ВТ_ОР.СрокПолезногоИспользования
		                   |		ИНАЧЕ ВТ_ОР.СрокПолезногоИспользования - РАЗНОСТЬДАТ(ВТ_ОР.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ)
		                   |	КОНЕЦ КАК ОстаточныйСрокПолезногоИспользования,
		                   |	ВЫБОР
		                   |		КОГДА ВТ_ОР.ПроцентИспользования <= 20
		                   |			ТОГДА ""Износ менее 20%""
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 20
		                   |				И ВТ_ОР.ПроцентИспользования <= 40
		                   |			ТОГДА ""Износ от 20% до 40%""
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 40
		                   |				И ВТ_ОР.ПроцентИспользования <= 60
		                   |			ТОГДА ""Износ от 40% до 60%""
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 60
		                   |				И ВТ_ОР.ПроцентИспользования <= 80
		                   |			ТОГДА ""Износ от 60% до 80%""
		                   |		ИНАЧЕ ""Износ более 80%""
		                   |	КОНЕЦ КАК ИзносОбъектаРемонта,
		                   |	ЕСТЬNULL(СтатусыВУчете.СтатусОР, ЗНАЧЕНИЕ(Перечисление.торо_СтатусыОРВУчете.НеПринятоКУчету)) КАК СтатусВУчете,
		                   |	СтатусыВУчете.ДатаСписания КАК ДатаСписания
		                   |ИЗ
		                   |	ВТ_ОР КАК ВТ_ОР
		                   |		ЛЕВОЕ СОЕДИНЕНИЕ СтатусыВУчете КАК СтатусыВУчете
		                   |		ПО ВТ_ОР.ОбъектРемонта = СтатусыВУчете.ОбъектРемонта";
	Иначе
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ЗапросВТОбъектов + "ВЫБРАТЬ
		                   |	ВТ_ОР.ОбъектРемонта КАК ОбъектРемонта,
		                   |	ВТ_ОР.ОбъектИерархии КАК ОбъектИерархии,
		                   |	ВТ_ОР.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		                   |	ВТ_ОР.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		                   |	ВЫБОР
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 0
		                   |				И ВТ_ОР.ДатаВводаВЭксплуатацию <> ДАТАВРЕМЯ(1, 1, 1)
		                   |			ТОГДА ВТ_ОР.ПроцентИспользования
		                   |		ИНАЧЕ 0
		                   |	КОНЕЦ КАК ПроцентИспользования,
		                   |	ВЫБОР
		                   |		КОГДА ВТ_ОР.СрокПолезногоИспользования ЕСТЬ NULL
		                   |				ИЛИ ВТ_ОР.СрокПолезногоИспользования = 0
		                   |				ИЛИ ВТ_ОР.ДатаВводаВЭксплуатацию = ДАТАВРЕМЯ(1, 1, 1)
		                   |			ТОГДА 0
		                   |		КОГДА РАЗНОСТЬДАТ(ВТ_ОР.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ) <= 0
		                   |			ТОГДА ВТ_ОР.СрокПолезногоИспользования
		                   |		ИНАЧЕ ВТ_ОР.СрокПолезногоИспользования - РАЗНОСТЬДАТ(ВТ_ОР.ДатаВводаВЭксплуатацию, &Дата, МЕСЯЦ)
		                   |	КОНЕЦ КАК ОстаточныйСрокПолезногоИспользования,
		                   |	ВЫБОР
		                   |		КОГДА ВТ_ОР.ПроцентИспользования <= 20
		                   |			ТОГДА ""Износ менее 20%""
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 20
		                   |				И ВТ_ОР.ПроцентИспользования <= 40
		                   |			ТОГДА ""Износ от 20% до 40%""
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 40
		                   |				И ВТ_ОР.ПроцентИспользования <= 60
		                   |			ТОГДА ""Износ от 40% до 60%""
		                   |		КОГДА ВТ_ОР.ПроцентИспользования > 60
		                   |				И ВТ_ОР.ПроцентИспользования <= 80
		                   |			ТОГДА ""Износ от 60% до 80%""
		                   |		ИНАЧЕ ""Износ более 80%""
		                   |	КОНЕЦ КАК ИзносОбъектаРемонта
		                   |ИЗ
		                   |	ВТ_ОР КАК ВТ_ОР";
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ФункцииОтчетовКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.ВыводитьСуммуВыделенныхЯчеек = Ложь;
	Настройки.События.ПослеЗаполненияПанелиБыстрыхНастроек = Истина;
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();
КонецПроцедуры

Процедура ПослеЗаполненияПанелиБыстрыхНастроек(Форма, ПараметрыЗаполнения) Экспорт
	
	ПараметрыФормы = Форма.Параметры;
	
	ЭлементыНастроек = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	
	Параметр = Неопределено;
	
	Для каждого Элемент Из ЭлементыНастроек Цикл
		Если ТипЗнч(Элемент) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Если Строка(Элемент.Параметр) = "ИерархияТип" Тогда
				Параметр = Элемент;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Параметр <> Неопределено
		И ПараметрыФормы.Свойство("ИерархияТип") 
		И ЗначениеЗаполнено(ПараметрыФормы.ИерархияТип) Тогда
		
		Параметр.Значение = ПараметрыФормы.ИерархияТип;
		Параметр.Использование = Истина;
			
	КонецЕсли;
	
	Если Не Параметр = Неопределено
		И Не ЗначениеЗаполнено(Параметр.Значение) Тогда
		
		ТекСтруктураИерархии = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиТОиР",
		"ОсновнаяСтруктураИерархии",
		Справочники.торо_СтруктурыОР.ПустаяСсылка());
		Если не ТекСтруктураИерархии = Неопределено Тогда
			Параметр.Значение = ТекСтруктураИерархии;
			параметр.Использование = Истина;
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецЕсли