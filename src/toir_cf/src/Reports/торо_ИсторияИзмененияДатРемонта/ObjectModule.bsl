#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "ДатаОкончанияОтчета");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ 	
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	ВТ_Данные.IDРемонта КАК IDРемонта,
		|	ВТ_Данные.ДатаИзменения КАК ДатаИзменения,
		|	ВТ_Данные.Регистратор КАК Регистратор,
		|	ВТ_Данные.ДатаНачалаПлан КАК ДатаНачалаПлан,
		|	ВТ_Данные.ДатаОкончанияПлан КАК ДатаОкончанияПлан,
		|	ВТ_Данные.Период КАК Период,
		|	ВТ_Данные.ВидРемонта КАК ВидРемонта,
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ ВТ_Данные КАК ВТ_Данные		
		|   ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|   ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка		   
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ 	
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.IDРемонта,
		|	ВТ_Данные.ДатаИзменения,
		|	ВТ_Данные.Регистратор,
		|	ВТ_Данные.ДатаНачалаПлан,
		|	ВТ_Данные.ДатаОкончанияПлан,
		|	ВТ_Данные.Период,
		|	ВТ_Данные.ВидРемонта,
		|	ЗНАЧЕНИЕ (Справочник." + ТекИерархия.ТипРеквизитаОР + ".ПустаяСсылка)
		|ИЗ ВТ_Данные КАК ВТ_Данные		
		|ГДЕ НЕ ВТ_Данные.ОбъектРемонта ССЫЛКА Справочник.торо_ОбъектыРемонта
		|";
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
			
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ 	
	    |	ВТ_Данные.ОбъектРемонта КАК ОбъектРемонта,
	    |	ВТ_Данные.IDРемонта КАК IDРемонта,
	    |	ВТ_Данные.ДатаИзменения КАК ДатаИзменения,
	    |	ВТ_Данные.Регистратор КАК Регистратор,
	    |	ВТ_Данные.ДатаНачалаПлан КАК ДатаНачалаПлан,
	    |	ВТ_Данные.ДатаОкончанияПлан КАК ДатаОкончанияПлан,
	    |	ВТ_Данные.Период КАК Период,
	    |	ВТ_Данные.ВидРемонта КАК ВидРемонта,
	    |	ВТ_Данные.ОбъектРемонта КАК ОбъектИерархии
	    |ИЗ ВТ_Данные КАК ВТ_Данные
		|";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ФункцииОтчетовКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
	Настройки.События.ПриЗагрузкеВариантаНаСервере 				   = Истина;
	Настройки.События.ПередЗаполнениемПанелиБыстрыхНастроек 	   = Истина;
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();
	
КонецПроцедуры

Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ПользовательскиеНастройкиЭлементы = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	НастройкиСтруктура = КомпоновщикНастроек.Настройки.Структура;
	ПрименитьПараметрыКНастройкам(ПользовательскиеНастройкиЭлементы, НастройкиСтруктура, Форма, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПриЗагрузкеВариантаНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ПользовательскиеНастройкиЭлементы = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	НастройкиСтруктура = КомпоновщикНастроек.Настройки.Структура;
	ПрименитьПараметрыКНастройкам(ПользовательскиеНастройкиЭлементы, НастройкиСтруктура, Форма, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПередЗаполнениемПанелиБыстрыхНастроек (Форма, ПараметрыЗаполнения) Экспорт
	
	КомпоновщикНастроекТекущий = Форма.Отчет.КомпоновщикНастроек;
	ПользовательскиеНастройкиЭлементы = КомпоновщикНастроекТекущий.ПользовательскиеНастройки.Элементы;
	НастройкиСтруктура = КомпоновщикНастроекТекущий.Настройки.Структура;
	ПрименитьПараметрыКНастройкам(ПользовательскиеНастройкиЭлементы, НастройкиСтруктура, Форма, КомпоновщикНастроекТекущий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСодержательнуюЧастьЗапроса()
	
	ЗапросТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ОбщиеДанныеПоРемонтам.IDРемонта КАК IDРемонта,
	|	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	|	ВЫБОР
	|		КОГДА НЕ торо_ОбщиеДанныеПоРемонтам.ВидРемонта = ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)
	|			ТОГДА торо_ОбщиеДанныеПоРемонтам.ВидРемонта
	|		КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ВыявленныйДефект)
	|			ТОГДА торо_ВидРемонтаПриВводеНаОснованииВыявленныхДефектов.Значение
	|		ИНАЧЕ торо_ВидРемонтаПриВводеНаОснованииВнешнихОснований.Значение
	|	КОНЕЦ КАК ВидРемонта,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачала,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания КАК ДатаОкончания,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТ_ОтборПоАктуальнымДатам
	|ИЗ
	|	РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	|		ПО торо_АктуальныеПлановыеДатыРемонтов.IDРемонта = торо_ОбщиеДанныеПоРемонтам.IDРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ВыявленныеДефекты КАК торо_ВыявленныеДефекты
	|		ПО торо_АктуальныеПлановыеДатыРемонтов.ДокументОснование = торо_ВыявленныеДефекты.Ссылка,
	|	Константа.торо_ВидРемонтаПриВводеНаОснованииВыявленныхДефектов КАК торо_ВидРемонтаПриВводеНаОснованииВыявленныхДефектов,
	|	Константа.торо_ВидРемонтаПриВводеНаОснованииВнешнихОснований КАК торо_ВидРемонтаПриВводеНаОснованииВнешнихОснований
	|ГДЕ
	|	НЕ торо_ОбщиеДанныеПоРемонтам.Отменен
	|	И НЕ торо_ОбщиеДанныеПоРемонтам.Замещен
	|	И ВЫБОР
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ВыявленныйДефект)
	|				ТОГДА &РасчетВД
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ВнешнееОснованиеДляРабот)
	|				ТОГДА &РасчетВО
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ПланГрафикППР)
	|				ТОГДА &РасчетППР
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ГрафикРегламентныхМероприятий)
	|				ТОГДА &РасчетРМ
	|		КОНЕЦ
	|{ГДЕ
	|	(торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала <= &ДатаОкончанияОтчета
	|			И торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания >= &ДатаНачалаОтчета),
	|	торо_АктуальныеПлановыеДатыРемонтов.IDРемонта,
	|	торо_АктуальныеПлановыеДатыРемонтов.ОбъектРемонта.*,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки.*}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ОтборПоАктуальнымДатам.IDРемонта КАК IDРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ОбъектРемонта КАК ОбъектРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ВидРемонта КАК ВидРемонта,
	|	торо_ВыявленныеДефектыДокумент.Дата КАК ДатаИзменения,
	|	торо_ВыявленныеДефекты.Регистратор КАК Регистратор,
	|	торо_ВыявленныеДефекты.Период КАК ДатаНачалаПлан,
	|	торо_ВыявленныеДефекты.Период КАК ДатаОкончанияПлан,
	|	торо_ВыявленныеДефекты.Период КАК Период
	|ПОМЕСТИТЬ ВТ_ЗависятОтПараметровОтчета
	|ИЗ
	|	РегистрСведений.торо_ВыявленныеДефекты КАК торо_ВыявленныеДефекты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ВыявленныеДефекты КАК торо_ВыявленныеДефектыДокумент
	|		ПО торо_ВыявленныеДефекты.Регистратор = торо_ВыявленныеДефектыДокумент.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтборПоАктуальнымДатам КАК ВТ_ОтборПоАктуальнымДатам
	|		ПО торо_ВыявленныеДефекты.Регистратор = ВТ_ОтборПоАктуальнымДатам.ДокументНачалаЦепочки
	|			И торо_ВыявленныеДефекты.ID = ВТ_ОтборПоАктуальнымДатам.IDРемонта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОтборПоАктуальнымДатам.IDРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ОбъектРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ВидРемонта,
	|	торо_ВнешниеОснованияДляРабот.Период,
	|	торо_ВнешниеОснованияДляРабот.Регистратор,
	|	торо_ВнешниеОснованияДляРабот.ПлановаяДатаРемонта,
	|	торо_ВнешниеОснованияДляРабот.ПлановаяДатаРемонта,
	|	торо_ВнешниеОснованияДляРабот.Период
	|ИЗ
	|	РегистрСведений.торо_ВнешниеОснованияДляРабот КАК торо_ВнешниеОснованияДляРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтборПоАктуальнымДатам КАК ВТ_ОтборПоАктуальнымДатам
	|		ПО торо_ВнешниеОснованияДляРабот.Регистратор = ВТ_ОтборПоАктуальнымДатам.ДокументНачалаЦепочки
	|			И торо_ВнешниеОснованияДляРабот.ID = ВТ_ОтборПоАктуальнымДатам.IDРемонта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОтборПоАктуальнымДатам.IDРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ОбъектРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ВидРемонта,
	|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка.Дата,
	|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка,
	|	торо_ПланГрафикРемонтаПланРемонтов.ДатаНач,
	|	торо_ПланГрафикРемонтаПланРемонтов.ДатаКон,
	|	торо_ПланГрафикРемонтаПланРемонтов.Ссылка.Дата
	|ИЗ
	|	РегистрСведений.торо_ВсеДокументыПоРемонтам КАК торо_ВсеДокументыПоРемонтам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтборПоАктуальнымДатам КАК ВТ_ОтборПоАктуальнымДатам
	|		ПО торо_ВсеДокументыПоРемонтам.IDРемонта = ВТ_ОтборПоАктуальнымДатам.IDРемонта
	|			И (торо_ВсеДокументыПоРемонтам.ДокументПоРемонту ССЫЛКА Документ.торо_ПланГрафикРемонта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ПланГрафикРемонта.ПланРемонтов КАК торо_ПланГрафикРемонтаПланРемонтов
	|		ПО торо_ВсеДокументыПоРемонтам.IDРемонта = торо_ПланГрафикРемонтаПланРемонтов.ID
	|			И ((ВЫРАЗИТЬ(торо_ВсеДокументыПоРемонтам.ДокументПоРемонту КАК Документ.торо_ПланГрафикРемонта)) = торо_ПланГрафикРемонтаПланРемонтов.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОтборПоАктуальнымДатам.IDРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ОбъектРемонта,
	|	ВТ_ОтборПоАктуальнымДатам.ВидРемонта,
	|	торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.Ссылка.Дата,
	|	торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.Ссылка,
	|	торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.ДатаНач,
	|	торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.ДатаКон,
	|	торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.Ссылка.Дата
	|ИЗ
	|	РегистрСведений.торо_ВсеДокументыПоРемонтам КАК торо_ВсеДокументыПоРемонтам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтборПоАктуальнымДатам КАК ВТ_ОтборПоАктуальнымДатам
	|		ПО торо_ВсеДокументыПоРемонтам.IDРемонта = ВТ_ОтборПоАктуальнымДатам.IDРемонта
	|			И (торо_ВсеДокументыПоРемонтам.ДокументПоРемонту ССЫЛКА Документ.торо_ГрафикРегламентныхМероприятийТОиР)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ГрафикРегламентныхМероприятийТОиР.ПланРемонтов КАК торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов
	|		ПО торо_ВсеДокументыПоРемонтам.IDРемонта = торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.ID
	|			И ((ВЫРАЗИТЬ(торо_ВсеДокументыПоРемонтам.ДокументПоРемонту КАК Документ.торо_ГрафикРегламентныхМероприятийТОиР)) = торо_ГрафикРегламентныхМероприятийТОиРПланРемонтов.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ЗависятОтПараметровОтчета.IDРемонта КАК IDРемонта,
	|	ВТ_ЗависятОтПараметровОтчета.ОбъектРемонта КАК ОбъектРемонта,
	|	ВТ_ЗависятОтПараметровОтчета.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВТ_РазличныеИДРемонта
	|ИЗ
	|	ВТ_ЗависятОтПараметровОтчета КАК ВТ_ЗависятОтПараметровОтчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ОстановочныеРемонтыСрезПоследних.IDЗависимого КАК IDРемонта
	|ПОМЕСТИТЬ ВТ_НеОтвязанныеОстановочные
	|ИЗ
	|	РегистрСведений.торо_ОстановочныеРемонты.СрезПоследних(
	|			,
	|			IDЗависимого В
	|				(ВЫБРАТЬ
	|					ВТ_ЗависятОтПараметровОтчета.IDРемонта КАК IDРемонта
	|				ИЗ
	|					ВТ_ЗависятОтПараметровОтчета КАК ВТ_ЗависятОтПараметровОтчета)) КАК торо_ОстановочныеРемонтыСрезПоследних
	|ГДЕ
	|	НЕ торо_ОстановочныеРемонтыСрезПоследних.Отвязан
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ЗаявкиПоРемонтамСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.IDРемонта КАК IDРемонта,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.Период КАК ДатаИзменения,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.Регистратор КАК Регистратор,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.ДатаНачала КАК ДатаНачалаПлан,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.ДатаОкончания КАК ДатаОкончанияПлан,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.Период КАК Период,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.торо_ЗаявкиПоРемонтам.СрезПоследних(
	|			,
	|			IDРемонта В
	|				(ВЫБРАТЬ
	|					ВТ_РазличныеИДРемонта.IDРемонта
	|				ИЗ
	|					ВТ_РазличныеИДРемонта КАК ВТ_РазличныеИДРемонта)) КАК торо_ЗаявкиПоРемонтамСрезПоследних
	|ГДЕ
	|	НЕ торо_ЗаявкиПоРемонтамСрезПоследних.Отменен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_РазличныеИДРемонта.ОбъектРемонта,
	|	ВТ_РазличныеИДРемонта.IDРемонта,
	|	торо_ОстановочныеРемонты.Период,
	|	торо_ОстановочныеРемонты.Регистратор,
	|	торо_ОстановочныеРемонты.ДатаНачалаРемонта,
	|	торо_ОстановочныеРемонты.ДатаОкончанияРемонта,
	|	торо_ОстановочныеРемонты.Период,
	|	ВТ_РазличныеИДРемонта.ВидРемонта
	|ИЗ
	|	ВТ_РазличныеИДРемонта КАК ВТ_РазличныеИДРемонта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОстановочныеРемонты КАК торо_ОстановочныеРемонты
	|		ПО ВТ_РазличныеИДРемонта.IDРемонта = торо_ОстановочныеРемонты.IDЗависимого
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НеОтвязанныеОстановочные КАК ВТ_НеОтвязанныеОстановочные
	|		ПО ВТ_РазличныеИДРемонта.IDРемонта = ВТ_НеОтвязанныеОстановочные.IDРемонта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ПлановыеРемонтныеРаботы.ОбъектРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботы.ID,
	|	торо_ЗакрытиеЗаявокИРемонтов.Дата,
	|	торо_ЗакрытиеЗаявокИРемонтов.Ссылка,
	|	торо_ПлановыеРемонтныеРаботы.ДатаНачалаРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботы.ДатаОкончанияРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботы.Период,
	|	торо_ПлановыеРемонтныеРаботы.ВидРемонтныхРабот
	|ИЗ
	|	РегистрСведений.торо_ПлановыеРемонтныеРаботы КАК торо_ПлановыеРемонтныеРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РазличныеИДРемонта КАК ВТ_ЗависятОтПараметровОтчета
	|		ПО торо_ПлановыеРемонтныеРаботы.ID = ВТ_ЗависятОтПараметровОтчета.IDРемонта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗакрытиеЗаявокИРемонтов КАК торо_ЗакрытиеЗаявокИРемонтов
	|		ПО торо_ПлановыеРемонтныеРаботы.Регистратор = торо_ЗакрытиеЗаявокИРемонтов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ЗависятОтПараметровОтчета.ОбъектРемонта,
	|	ВТ_ЗависятОтПараметровОтчета.IDРемонта,
	|	торо_СкользящийПланРабот.Период,
	|	торо_ПланРаботПодразделения.Ссылка,
	|	торо_СкользящийПланРабот.ДатаНачалаРемонта,
	|	торо_СкользящийПланРабот.ДатаОкончанияРемонта,
	|	торо_СкользящийПланРабот.Период,
	|	ВТ_ЗависятОтПараметровОтчета.ВидРемонта
	|ИЗ
	|	ВТ_РазличныеИДРемонта КАК ВТ_ЗависятОтПараметровОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СкользящийПланРабот КАК торо_СкользящийПланРабот
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ПланРаботПодразделения КАК торо_ПланРаботПодразделения
	|			ПО торо_СкользящийПланРабот.Регистратор = торо_ПланРаботПодразделения.Ссылка
	|		ПО ВТ_ЗависятОтПараметровОтчета.IDРемонта = торо_СкользящийПланРабот.ID_Ремонта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ЗависятОтПараметровОтчета.ОбъектРемонта,
	|	ВТ_ЗависятОтПараметровОтчета.IDРемонта,
	|	торо_СкользящийПланРегламентныхМероприятий.Период,
	|	торо_ПланРаботПодразделения.Ссылка,
	|	торо_СкользящийПланРегламентныхМероприятий.ДатаНачалаРемонта,
	|	торо_СкользящийПланРегламентныхМероприятий.ДатаОкончанияРемонта,
	|	торо_СкользящийПланРегламентныхМероприятий.Период,
	|	ВТ_ЗависятОтПараметровОтчета.ВидРемонта
	|ИЗ
	|	ВТ_РазличныеИДРемонта КАК ВТ_ЗависятОтПараметровОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СкользящийПланРегламентныхМероприятий КАК торо_СкользящийПланРегламентныхМероприятий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ПланРаботПодразделения КАК торо_ПланРаботПодразделения
	|			ПО торо_СкользящийПланРегламентныхМероприятий.Регистратор = торо_ПланРаботПодразделения.Ссылка
	|		ПО ВТ_ЗависятОтПараметровОтчета.IDРемонта = торо_СкользящийПланРегламентныхМероприятий.ID_Ремонта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ЗависятОтПараметровОтчета.ОбъектРемонта,
	|	ВТ_ЗависятОтПараметровОтчета.IDРемонта,
	|	ВТ_ЗависятОтПараметровОтчета.ДатаИзменения,
	|	ВТ_ЗависятОтПараметровОтчета.Регистратор,
	|	ВТ_ЗависятОтПараметровОтчета.ДатаНачалаПлан,
	|	ВТ_ЗависятОтПараметровОтчета.ДатаОкончанияПлан,
	|	ВТ_ЗависятОтПараметровОтчета.Период,
	|	ВТ_ЗависятОтПараметровОтчета.ВидРемонта
	|ИЗ
	|	ВТ_ЗависятОтПараметровОтчета КАК ВТ_ЗависятОтПараметровОтчета"
	
	+ "
	|;	
	|
	|//////////////////////////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("МассивID"));
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));
	Возврат Параметры;
	
КонецФункции

Процедура ПрименитьПараметрыКНастройкам(ПользовательскиеНастройкиЭлементы, НастройкиСтруктура, Форма, КомпоновщикНастроек) 
	
	ПараметрыВыполнения = Форма.Параметры;
	
	Для каждого Элемент Из ПользовательскиеНастройкиЭлементы Цикл
		Если ТипЗнч(Элемент) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			
			Если Строка(Элемент.Параметр) = "ПериодОтчета" Тогда
				Если ПараметрыВыполнения.Свойство("ИспользоватьОтборПоПериоду") Тогда
					Элемент.Использование = ПараметрыВыполнения.ИспользоватьОтборПоПериоду;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли