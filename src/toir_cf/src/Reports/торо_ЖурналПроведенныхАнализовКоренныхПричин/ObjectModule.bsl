#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	СодержательнаяЧастьЗапроса = ПолучитьСодержательнуюЧастьЗапроса();
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "КонецПериода");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		СодержательнаяЧастьЗапроса +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Данные.Организация,
		|	ВТ_Данные.Подразделение,
		|	ВТ_Данные.Ответственный,
		|	ВТ_Данные.ДатаДокумента,
		|	ВТ_Данные.Документ,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ДатаСобытия,
		|	ВТ_Данные.ВыявленныйДефект,
		|	ВТ_Данные.Комиссия,
		|	ВТ_Данные.Статус,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ДатаЗавершения,
		|	ВТ_Данные.Количество,
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
		|";
		
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		СодержательнаяЧастьЗапроса +        
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Данные.Организация,
		|	ВТ_Данные.Подразделение,
		|	ВТ_Данные.Ответственный,
		|	ВТ_Данные.ДатаДокумента,
		|	ВТ_Данные.Документ,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ДатаСобытия,
		|	ВТ_Данные.ВыявленныйДефект,
		|	ВТ_Данные.Комиссия,
		|	ВТ_Данные.Статус,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ДатаЗавершения,
		|	ВТ_Данные.Количество,
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_АнализКоренныхПричин.Организация КАК Организация,
	|	торо_АнализКоренныхПричин.Подразделение КАК Подразделение,
	|	торо_АнализКоренныхПричин.Ответственный КАК Ответственный,
	|	торо_АнализКоренныхПричин.Дата КАК ДатаДокумента,
	|	торо_АнализКоренныхПричин.Ссылка КАК Документ,
	|	торо_АнализКоренныхПричин.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_АнализКоренныхПричин.ДатаСобытия КАК ДатаСобытия,
	|	торо_АнализКоренныхПричин.ДокументОснование КАК ВыявленныйДефект,
	|	торо_АнализКоренныхПричин.Комиссия КАК Комиссия,
	|	торо_АнализКоренныхПричин.Статус КАК Статус,
	|	торо_АнализКоренныхПричин.ДатаЗавершенияАнализа КАК ДатаЗавершения,
	|	1 КАК Количество,
	|	торо_АнализКоренныхПричин.ОбъектРемонта КАК ОбъектИерархии
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	Документ.торо_АнализКоренныхПричин КАК торо_АнализКоренныхПричин
	|ГДЕ
	|	торо_АнализКоренныхПричин.ПометкаУдаления = ЛОЖЬ
	|{ГДЕ
	|	(торо_АнализКоренныхПричин.Дата >= &НачалоПериода),
	|	(торо_АнализКоренныхПричин.Дата <= &КонецПериода)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта"

	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли