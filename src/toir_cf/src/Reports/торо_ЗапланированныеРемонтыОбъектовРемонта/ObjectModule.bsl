#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "ДатаОкончания");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	Данные.ВидРемонта КАК ВидРемонта,
		|	Данные.Документ КАК Документ,
		|	Данные.Подразделение КАК Подразделение,
		|	Данные.ДатаПланирования КАК ДатаПланирования,
		|	Данные.КоличествоПериодов КАК КоличествоПериодов,
		|	Данные.ПериодичностьДетализации КАК ПериодичностьДетализации,		
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ
		|	Данные КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
		
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	Данные.ВидРемонта КАК ВидРемонта,
		|	Данные.Документ КАК Документ,
		|	Данные.Подразделение КАК Подразделение,
		|	Данные.ДатаПланирования КАК ДатаПланирования,
		|	Данные.КоличествоПериодов КАК КоличествоПериодов,
		|	Данные.ПериодичностьДетализации КАК ПериодичностьДетализации,		
		|	торо_ОбъектыРемонта.Ссылка КАК ОбъектИерархии
		|ИЗ
		|	Данные КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
	|	торо_ОбъектыРемонта.Подразделение КАК Подразделение,
	|	торо_НормативныеРемонтыОборудования.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВТ_ОР
	|ИЗ
	|	РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|		ПО торо_НормативныеРемонтыОборудования.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	|ГДЕ
	|	торо_ОбъектыРемонта.НеУчаствуетВПланировании = ЛОЖЬ
	|	И торо_НормативныеРемонтыОборудования.НеУчаствуетВПланировании = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ПланГрафикРемонтаОбъектыРемонта.ОбъектРемонтныхРабот КАК ОбъектРемонта,
	|	торо_ПланГрафикРемонтаОбъектыРемонта.ВидРемонтныхРабот КАК ВидРемонта,
	|	торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка КАК Документ,
	|	торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка.ДатаПланирования КАК ДатаПланирования,
	|	торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка.КоличествоПериодов КАК КоличествоПериодов,
	|	торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка.ПериодичностьДетализации КАК ПериодичностьДетализации
	|ПОМЕСТИТЬ ВТ_ППР
	|ИЗ
	|	Документ.торо_ПланГрафикРемонта.ОбъектыРемонта КАК торо_ПланГрафикРемонтаОбъектыРемонта
	|ГДЕ
	|	торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка.Проведен = ИСТИНА
	|{ГДЕ
	|	(торо_ПланГрафикРемонтаОбъектыРемонта.Ссылка.ДатаПланирования МЕЖДУ &ДатаНачала И &ДатаОкончания)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ВТ_ОР.ОбъектРемонта, ВТ_ППР.ОбъектРемонта) КАК ОбъектРемонта,
	|	ЕСТЬNULL(ВТ_ОР.ВидРемонта, ВТ_ППР.ВидРемонта) КАК ВидРемонта,
	|	ВТ_ППР.Документ КАК Документ,
	|	ВТ_ОР.Подразделение КАК Подразделение,
	|	ВТ_ППР.ДатаПланирования КАК ДатаПланирования,
	|	ВТ_ППР.КоличествоПериодов КАК КоличествоПериодов,
	|	ВТ_ППР.ПериодичностьДетализации КАК ПериодичностьДетализации
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	ВТ_ОР КАК ВТ_ОР
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ППР КАК ВТ_ППР
	|		ПО ВТ_ОР.ОбъектРемонта = ВТ_ППР.ОбъектРемонта
	|			И ВТ_ОР.ВидРемонта = ВТ_ППР.ВидРемонта"
	
	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#КонецЕсли
