#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	СодержательнаяЧастьЗапроса = ПолучитьСодержательнуюЧастьЗапроса();
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "КонецПериода");
		
	Если ТекИерархия.СтроитсяАвтоматически Тогда
		
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		СодержательнаяЧастьЗапроса +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Данные.КореннаяПричина,
		|	ВТ_Данные.НепосредственнаяПричина,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ВыявленныйДефект,
		|	ВТ_Данные.ДатаСобытия,
		|	ВТ_Данные.ОтказавшийЭлемент,
		|	ВТ_Данные.ОписаниеДефекта,
		|	ВТ_Данные.КритичностьДефекта,
		|	ВТ_Данные.Количество,
		|	ВТ_Данные.ПериодДефекта,
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
		|";
		
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		СодержательнаяЧастьЗапроса +        
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Данные.КореннаяПричина,
		|	ВТ_Данные.НепосредственнаяПричина,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ВыявленныйДефект,
		|	ВТ_Данные.ДатаСобытия,
		|	ВТ_Данные.ОтказавшийЭлемент,
		|	ВТ_Данные.ОписаниеДефекта,
		|	ВТ_Данные.КритичностьДефекта,
		|	ВТ_Данные.Количество,
		|	ВТ_Данные.ПериодДефекта,
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_КоренныеПричиныДефектов.КореннаяПричина КАК КореннаяПричина,
	|	торо_КоренныеПричиныДефектов.НепосредственнаяПричина КАК НепосредственнаяПричина,
	|	торо_КоренныеПричиныДефектов.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_КоренныеПричиныДефектов.Регистратор.ДокументОснование КАК ВыявленныйДефект,
	|	торо_КоренныеПричиныДефектов.ДатаСобытия КАК ДатаСобытия,
	|	торо_ВыявленныеДефекты.ОтказавшийЭлемент КАК ОтказавшийЭлемент,
	|	торо_ВыявленныеДефекты.ДефектОписание КАК ОписаниеДефекта,
	|	торо_ВыявленныеДефекты.КритичностьДефекта КАК КритичностьДефекта,
	|	1 КАК Количество,
	|	торо_КоренныеПричиныДефектов.ОбъектРемонта КАК ОбъектИерархии,
	|		ВЫБОР &Периодичность
	|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|		ТОГДА НАЧАЛОПЕРИОДА(торо_КоренныеПричиныДефектов.ДатаСобытия, МЕСЯЦ)
	|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|		ТОГДА НАЧАЛОПЕРИОДА(торо_КоренныеПричиныДефектов.ДатаСобытия, ГОД)
	|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|		ТОГДА НАЧАЛОПЕРИОДА(торо_КоренныеПричиныДефектов.ДатаСобытия, КВАРТАЛ)
	|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|		ТОГДА НАЧАЛОПЕРИОДА(торо_КоренныеПричиныДефектов.ДатаСобытия, НЕДЕЛЯ)
	|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|		ТОГДА НАЧАЛОПЕРИОДА(торо_КоренныеПричиныДефектов.ДатаСобытия, ДЕНЬ)	
	|   КОНЕЦ КАК ПериодДефекта
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.торо_КоренныеПричиныДефектов КАК торо_КоренныеПричиныДефектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВыявленныеДефекты КАК торо_ВыявленныеДефекты
	|		ПО торо_КоренныеПричиныДефектов.ИДРемонта = торо_ВыявленныеДефекты.ID
	|{ГДЕ
	|	(торо_КоренныеПричиныДефектов.ДатаСобытия >= &НачалоПериода),
	|	(торо_КоренныеПричиныДефектов.ДатаСобытия <= &КонецПериода)}
	|
	|СГРУППИРОВАТЬ ПО
	|	МЕСЯЦ(торо_КоренныеПричиныДефектов.ДатаСобытия),
	|	ГОД(торо_КоренныеПричиныДефектов.ДатаСобытия),
	|	торо_КоренныеПричиныДефектов.КореннаяПричина,
	|	торо_КоренныеПричиныДефектов.НепосредственнаяПричина,
	|	торо_КоренныеПричиныДефектов.ОбъектРемонта,
	|	торо_КоренныеПричиныДефектов.Регистратор.ДокументОснование,
	|	торо_КоренныеПричиныДефектов.ДатаСобытия,
	|	торо_ВыявленныеДефекты.ОтказавшийЭлемент,
	|	торо_ВыявленныеДефекты.ДефектОписание,
	|	торо_ВыявленныеДефекты.КритичностьДефекта,
	|	торо_КоренныеПричиныДефектов.ОбъектРемонта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта"

	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли