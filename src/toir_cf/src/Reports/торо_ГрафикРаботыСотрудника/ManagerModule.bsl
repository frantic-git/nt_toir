#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ГрафикРаботыСотрудника") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
			"ГрафикРаботыСотрудника", 
			НСтр("ru = 'График работы сотрудника'"), 
			СформироватьПечатнуюФормуГрафикРаботыСотрудника(МассивОбъектов, ПараметрыПечати),
			,
			"Отчет.торо_ГрафикРаботыСотрудника.ПФ_MXL_ГрафикРаботыСотрудника");
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуГрафикРаботыСотрудника(МассивОбъектов, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ГрафикРаботыСотрудника";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.Автомасштаб = Истина;
	ТабличныйДокумент.Защита = Ложь;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.торо_ГрафикРаботыСотрудника.ПФ_MXL_ГрафикРаботыСотрудника");
	
	Год = ПараметрыПечати.Год;
	Сотрудники = ПараметрыПечати.Сотрудники;
	
	НачалоПериода = НачалоГода(Год);
	КонецПериода = КонецГода(Год);
	
	КадровыеДанныеСотрудников = КадровыеДанныеСотрудниковЗаПериод(Сотрудники, НачалоПериода, КонецПериода);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	КалендарныеГрафики.СоздатьВТРасписанияРаботыНаПериод(
		МенеджерВременныхТаблиц, 
		КадровыеДанныеСотрудников.ВыгрузитьКолонку("ГрафикРаботы"), 
		НачалоПериода, 
		КонецПериода);
			
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КадровыеДанныеСотрудников", КадровыеДанныеСотрудников);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст = ТекстЗапросаДляГрафикаРаботСотрудников();
		
	РезультатЗапроса = Запрос.Выполнить(); 

	ВыборкаСотрудники = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЭтоПервыйСотрудник = Истина;
	Пока ВыборкаСотрудники.Следующий() Цикл
		
		Если ЭтоПервыйСотрудник Тогда
			ЭтоПервыйСотрудник = Ложь;
		Иначе	
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Область "Заголовок".
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.Заголовок = СтрШаблон(
			НСтр("ru = 'График работы %1 на %2 год'"), 
			ПолучитьСклоненияСтроки(ВыборкаСотрудники.ФИО,, "ПД=Родительный")[0], 
			Формат(Год, "ДФ=гггг"));
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		// Область "Шапка".
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ВыборкаКвартал = ВыборкаСотрудники.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКвартал.Следующий() Цикл
			
			// Область "Квартал".
			ОбластьКвартал = Макет.ПолучитьОбласть("Квартал");
			ЗаполнитьЗначенияСвойств(ОбластьКвартал.Параметры, ВыборкаКвартал);
			ОбластьКвартал.Параметры.Квартал = Формат(ВыборкаКвартал.Дата, "ДФ='к ""квартал""'");
			ОбластьКвартал.Параметры.РабочихДней = ВыборкаКвартал.РабочихДней - ВыборкаКвартал.КоличествоСдвигов;
			ТабличныйДокумент.Вывести(ОбластьКвартал);
			
			ВыборкаМесяц = ВыборкаКвартал.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаМесяц.Следующий() Цикл
				
				// Область "Месяц".
				ОбластьМесяц = Макет.ПолучитьОбласть("Месяц");
				
				ЗаполнитьЗначенияСвойств(ОбластьМесяц.Параметры, ВыборкаМесяц);
				ОбластьМесяц.Параметры.Месяц = Формат(ВыборкаМесяц.Дата, "ДФ=ММММ");
				
				Выборка = ВыборкаМесяц.Выбрать();
				Пока Выборка.Следующий() Цикл
					ИмяЯчейки = "День" + Выборка.День;
					
					Если Выборка.РабочихЧасов = 0 Тогда 
						Состояние = ?(Выборка.СостояниеСотрудника = Неопределено, "В", Выборка.СостояниеСотрудника);
						ОбластьМесяц.Параметры[ИмяЯчейки] = Состояние;
					Иначе
						ОбластьМесяц.Параметры[ИмяЯчейки] = Выборка.РабочихЧасов;
					КонецЕсли;
					
					Если Выборка.Уволен Тогда
						ОбластьМесяц.Области[ИмяЯчейки].ЦветФона = WebЦвета.ТемноСерый;
						ОбластьМесяц.Параметры[ИмяЯчейки] = "У";
					ИначеЕсли Выборка.РабочихЧасовПоКалендарю = 0 Тогда
						ОбластьМесяц.Области[ИмяЯчейки].ЦветФона = WebЦвета.Розовый;
					КонецЕсли;
					
					ОбластьМесяц.Параметры.РабочихДней = ВыборкаМесяц.РабочихДней - ВыборкаМесяц.КоличествоСдвигов;
					
				КонецЦикла;
				
				ДнейВМесяце = День(КонецМесяца(ВыборкаМесяц.Дата));
				Для День = ДнейВМесяце + 1 По 31 Цикл
					ИмяЯчейки = "День" + День;
					ОбластьМесяц.Области[ИмяЯчейки].ЦветФона = WebЦвета.ТемноСерый;
					ОбластьМесяц.Параметры[ИмяЯчейки] = "Х";
				КонецЦикла;	
				
				ТабличныйДокумент.Вывести(ОбластьМесяц);
				
			КонецЦикла;
		КонецЦикла;
		
		// Область "Итого".
		ОбластьИтого = Макет.ПолучитьОбласть("Итого");
		ЗаполнитьЗначенияСвойств(ОбластьИтого.Параметры, ВыборкаСотрудники);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		// Область "Подпись".
		ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
		ТабличныйДокумент.Вывести(ОбластьПодпись);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции	

Функция ТекстЗапросаДляГрафикаРаботСотрудников()
	
	Возврат "ВЫБРАТЬ
	        |	КадровыеДанныеСотрудников.НачалоПериода КАК НачалоПериода,
	        |	КадровыеДанныеСотрудников.КонецПериода КАК КонецПериода,
	        |	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	        |	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	        |	КадровыеДанныеСотрудников.ГрафикРаботы КАК ГрафикРаботы,
	        |	КадровыеДанныеСотрудников.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь,
	        |	КадровыеДанныеСотрудников.Организация КАК Организация,
	        |	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	        |	КадровыеДанныеСотрудников.ВидСобытия КАК ВидСобытия
	        |ПОМЕСТИТЬ ВТКадровыеДанныеЗаПериод
	        |ИЗ
	        |	&КадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ПроизводственныйКалендарь,
	        |	НачалоПериода,
	        |	КонецПериода
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТРасписанияРаботы.ГрафикРаботы КАК ГрафикРаботы,
	        |	ВТРасписанияРаботы.ДатаГрафика КАК ДатаГрафика,
	        |	ВЫБОР
	        |		КОГДА ВТРасписанияРаботы.ЭтоСдвиг
	        |			ТОГДА 1
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК ЭтоСдвиг,
	        |	СУММА(РАЗНОСТЬДАТ(ВТРасписанияРаботы.ВремяНачала, ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА) / 3600) КАК РабочихЧасов
	        |ПОМЕСТИТЬ втВремяПоГрафику
	        |ИЗ
	        |	ВТРасписанияРаботы КАК ВТРасписанияРаботы
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТРасписанияРаботы.ГрафикРаботы,
	        |	ВТРасписанияРаботы.ДатаГрафика,
	        |	ВТРасписанияРаботы.ЭтоСдвиг
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ГрафикРаботы,
	        |	ДатаГрафика
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДанныеПроизводственногоКалендаря.Дата КАК Дата,
	        |	ВТКадровыеДанныеЗаПериод.Сотрудник КАК Сотрудник,
	        |	ФизическиеЛица.ФИО КАК ФИО,
	        |	ВЫБОР
	        |		КОГДА ВТКадровыеДанныеЗаПериод.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	        |			ТОГДА ИСТИНА
	        |		ИНАЧЕ ЛОЖЬ
	        |	КОНЕЦ КАК Уволен,
	        |	ВЫБОР
	        |		КОГДА ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	        |			ТОГДА 8
	        |		КОГДА ДанныеПроизводственногоКалендаря.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	        |			ТОГДА 7
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК РабочихЧасовПоКалендарю,
	        |	ВЫБОР
	        |		КОГДА ДанныеПроизводственногоКалендаря.ВидДня В (ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный))
	        |			ТОГДА 1
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК РабочихДнейПоКалендарю,
	        |	ВЫБОР
	        |		КОГДА ДанныеПроизводственногоКалендаря.ВидДня В (ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный))
	        |			ТОГДА 0
	        |		ИНАЧЕ 1
	        |	КОНЕЦ КАК НерабочихДнейПоКалендарю,
	        |	ВТКадровыеДанныеЗаПериод.Организация КАК Организация,
	        |	ВТКадровыеДанныеЗаПериод.Подразделение КАК Подразделение,
	        |	ВТКадровыеДанныеЗаПериод.ГрафикРаботы КАК ГрафикРаботы
	        |ПОМЕСТИТЬ ВТКадровыеДанные
	        |ИЗ
	        |	ВТКадровыеДанныеЗаПериод КАК ВТКадровыеДанныеЗаПериод
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	        |		ПО ВТКадровыеДанныеЗаПериод.НачалоПериода <= ДанныеПроизводственногоКалендаря.Дата
	        |			И ВТКадровыеДанныеЗаПериод.КонецПериода >= ДанныеПроизводственногоКалендаря.Дата
	        |			И ВТКадровыеДанныеЗаПериод.ПроизводственныйКалендарь = ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	        |		ПО ВТКадровыеДанныеЗаПериод.ФизическоеЛицо = ФизическиеЛица.Ссылка
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Сотрудник,
	        |	Организация,
	        |	Подразделение,
	        |	Дата
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.Организация КАК Организация,
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.Подразделение КАК Подразделение,
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.ДатаГрафика КАК ДатаГрафика,
	        |	СУММА(ВЫРАЗИТЬ(торо_СостояниеРаботыСотрудниковСрезПоследних.ВремяРаботы / 3600 КАК ЧИСЛО(15, 2))) КАК РабочихЧасов,
	        |	МАКСИМУМ(торо_СостояниеРаботыСотрудниковСрезПоследних.СостояниеСотрудника.Сокращение) КАК СостояниеСотрудника
	        |ПОМЕСТИТЬ ВТСостоянияРаботыСотрудников
	        |ИЗ
	        |	РегистрСведений.торо_СостояниеРаботыСотрудников.СрезПоследних(
	        |			,
	        |			(Организация, Подразделение, Сотрудник) В
	        |					(ВЫБРАТЬ
	        |						ВТКадровыеДанные.Организация КАК Организация,
	        |						ВТКадровыеДанные.Подразделение КАК Подразделение,
	        |						ВТКадровыеДанные.Сотрудник КАК Сотрудник
	        |					ИЗ
	        |						ВТКадровыеДанные КАК ВТКадровыеДанные)
	        |				И (ДатаГрафика МЕЖДУ &НачалоПериода И &КонецПериода)) КАК торо_СостояниеРаботыСотрудниковСрезПоследних
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.Организация,
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.Подразделение,
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.Сотрудник,
	        |	торо_СостояниеРаботыСотрудниковСрезПоследних.ДатаГрафика
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	Организация,
	        |	Подразделение,
	        |	Сотрудник,
	        |	ДатаГрафика
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	ВТКадровыеДанные.Дата КАК Дата,
	        |	КВАРТАЛ(ВТКадровыеДанные.Дата) КАК Квартал,
	        |	МЕСЯЦ(ВТКадровыеДанные.Дата) КАК Месяц,
	        |	ДЕНЬ(ВТКадровыеДанные.Дата) КАК День,
	        |	ВТКадровыеДанные.Сотрудник КАК Сотрудник,
	        |	ВТКадровыеДанные.ФИО КАК ФИО,
	        |	ВТКадровыеДанные.Уволен КАК Уволен,
	        |	ЕСТЬNULL(ВТСостоянияРаботыСотрудников.СостояниеСотрудника, НЕОПРЕДЕЛЕНО) КАК СостояниеСотрудника,
	        |	ВЫБОР
	        |		КОГДА ВТСостоянияРаботыСотрудников.РабочихЧасов ЕСТЬ NULL
	        |			ТОГДА ЕСТЬNULL(втВремяПоГрафику.РабочихЧасов, 0)
	        |		ИНАЧЕ ВТСостоянияРаботыСотрудников.РабочихЧасов
	        |	КОНЕЦ КАК РабочихЧасов,
	        |	ВЫБОР
	        |		КОГДА ВТСостоянияРаботыСотрудников.РабочихЧасов ЕСТЬ NULL
	        |				И ЕСТЬNULL(втВремяПоГрафику.РабочихЧасов, 0) > 0
	        |			ТОГДА 1
	        |		КОГДА ВТСостоянияРаботыСотрудников.РабочихЧасов > 0
	        |			ТОГДА 1
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК РабочихДней,
	        |	ВТКадровыеДанные.РабочихЧасовПоКалендарю КАК РабочихЧасовПоКалендарю,
	        |	ВТКадровыеДанные.РабочихДнейПоКалендарю КАК РабочихДнейПоКалендарю,
	        |	ВТКадровыеДанные.НерабочихДнейПоКалендарю КАК НерабочихДнейПоКалендарю,
	        |	ЕСТЬNULL(втВремяПоГрафику.ЭтоСдвиг, 0) КАК КоличествоСдвигов
	        |ИЗ
	        |	ВТКадровыеДанные КАК ВТКадровыеДанные
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТСостоянияРаботыСотрудников КАК ВТСостоянияРаботыСотрудников
	        |		ПО ВТКадровыеДанные.Организация = ВТСостоянияРаботыСотрудников.Организация
	        |			И ВТКадровыеДанные.Подразделение = ВТСостоянияРаботыСотрудников.Подразделение
	        |			И ВТКадровыеДанные.Сотрудник = ВТСостоянияРаботыСотрудников.Сотрудник
	        |			И ВТКадровыеДанные.Дата = ВТСостоянияРаботыСотрудников.ДатаГрафика
	        |		ЛЕВОЕ СОЕДИНЕНИЕ втВремяПоГрафику КАК втВремяПоГрафику
	        |		ПО ВТКадровыеДанные.Дата = втВремяПоГрафику.ДатаГрафика
	        |			И ВТКадровыеДанные.ГрафикРаботы = втВремяПоГрафику.ГрафикРаботы
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	Квартал,
	        |	Месяц,
	        |	День
	        |ИТОГИ
	        |	МИНИМУМ(Дата),
	        |	МАКСИМУМ(ФИО),
	        |	СУММА(РабочихЧасов),
	        |	СУММА(РабочихДней),
	        |	СУММА(РабочихЧасовПоКалендарю),
	        |	СУММА(РабочихДнейПоКалендарю),
	        |	СУММА(НерабочихДнейПоКалендарю),
	        |	СУММА(КоличествоСдвигов)
	        |ПО
	        |	Сотрудник,
	        |	Квартал,
	        |	Месяц";
	
КонецФункции

Функция КадровыеДанныеСотрудниковЗаПериод(Сотрудники, НачалоПериода, КонецПериода)
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	СоздатьВТДатыИзмененияКадровыхДанныхСотрудников(Запрос.МенеджерВременныхТаблиц, Сотрудники, НачалоПериода, КонецПериода);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДатыИзмененияКадровыхДанных.ДатаИзменения КАК НачалоПериода,
	|	МИНИМУМ(ЕСТЬNULL(ДатыИзмененияКадровыхДанных1.ДатаИзменения, &КонецПериода)) КАК КонецПериода,
	|	ДатыИзмененияКадровыхДанных.Сотрудник КАК Сотрудник,
	|	ДатыИзмененияКадровыхДанных.ГрафикРаботы КАК ГрафикРаботы,
	|	ДатыИзмененияКадровыхДанных.Организация КАК Организация,
	|	ДатыИзмененияКадровыхДанных.Подразделение КАК Подразделение,
	|	ДатыИзмененияКадровыхДанных.ВидСобытия КАК ВидСобытия
	|ПОМЕСТИТЬ ВТПериодыИзмененияКадровыхДанных
	|ИЗ
	|	ВТДатыИзмененияКадровыхДанныхСотрудников КАК ДатыИзмененияКадровыхДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыИзмененияКадровыхДанныхСотрудников КАК ДатыИзмененияКадровыхДанных1
	|		ПО ДатыИзмененияКадровыхДанных.ДатаИзменения < ДатыИзмененияКадровыхДанных1.ДатаИзменения
	|			И ДатыИзмененияКадровыхДанных.Сотрудник = ДатыИзмененияКадровыхДанных1.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыИзмененияКадровыхДанных.ДатаИзменения,
	|	ДатыИзмененияКадровыхДанных.Сотрудник,
	|	ДатыИзмененияКадровыхДанных.ГрафикРаботы,
	|	ДатыИзмененияКадровыхДанных.Организация,
	|	ДатыИзмененияКадровыхДанных.Подразделение,
	|	ДатыИзмененияКадровыхДанных.ВидСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПериодыИзмененияКадровыхДанных.НачалоПериода КАК НачалоПериода,
	|	ВТПериодыИзмененияКадровыхДанных.КонецПериода КАК КонецПериода,
	|	ВТПериодыИзмененияКадровыхДанных.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТПериодыИзмененияКадровыхДанных.ГрафикРаботы КАК ГрафикРаботы,
	|	Календари.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь,
	|	ВТПериодыИзмененияКадровыхДанных.Организация КАК Организация,
	|	ВТПериодыИзмененияКадровыхДанных.Подразделение КАК Подразделение,
	|	ВТПериодыИзмененияКадровыхДанных.ВидСобытия КАК ВидСобытия
	|ИЗ
	|	ВТПериодыИзмененияКадровыхДанных КАК ВТПериодыИзмененияКадровыхДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ВТПериодыИзмененияКадровыхДанных.Сотрудник = Сотрудники.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Календари КАК Календари
	|		ПО ВТПериодыИзмененияКадровыхДанных.ГрафикРаботы = Календари.Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
		
КонецФункции

Процедура СоздатьВТДатыИзмененияГрафиковРаботыСотрудников(МенеджерВременныхТаблиц, Сотрудники, НачалоПериода, КонецПериода) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрафикРаботыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	|	ГрафикРаботыСотрудниковСрезПоследних.ГрафикРаботы КАК ГрафикРаботы,
	|	ГрафикРаботыСотрудниковСрезПоследних.ДействуетДо КАК ДействуетДо,
	|	ГрафикРаботыСотрудниковСрезПоследних.ГрафикРаботыПоОкончании КАК ГрафикРаботыПоОкончании
	|ПОМЕСТИТЬ ВТГрафикиНаНачалоПериода
	|ИЗ
	|	РегистрСведений.ГрафикРаботыСотрудников.СрезПоследних(&НачалоПериода, Сотрудник В (&Сотрудники)) КАК ГрафикРаботыСотрудниковСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДействуетДо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрафикРаботыСотрудников.Период КАК Период,
	|	ГрафикРаботыСотрудников.Сотрудник КАК Сотрудник,
	|	ГрафикРаботыСотрудников.ГрафикРаботы КАК ГрафикРаботы,
	|	ГрафикРаботыСотрудников.ДействуетДо КАК ДействуетДо,
	|	ГрафикРаботыСотрудников.ГрафикРаботыПоОкончании КАК ГрафикРаботыПоОкончании
	|ПОМЕСТИТЬ ВТГрафикиВПериоде
	|ИЗ
	|	РегистрСведений.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников
	|ГДЕ
	|	ГрафикРаботыСотрудников.Активность
	|	И ГрафикРаботыСотрудников.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ГрафикРаботыСотрудников.Сотрудник В(&Сотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДействуетДо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоПериода КАК ДатаИзменения,
	|	втГрафикиНаНачалоПериода.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА втГрафикиНаНачалоПериода.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втГрафикиНаНачалоПериода.ГрафикРаботы
	|		КОГДА втГрафикиНаНачалоПериода.ДействуетДо > &НачалоПериода
	|			ТОГДА втГрафикиНаНачалоПериода.ГрафикРаботы
	|		ИНАЧЕ втГрафикиНаНачалоПериода.ГрафикРаботыПоОкончании
	|	КОНЕЦ КАК ГрафикРаботы
	|ПОМЕСТИТЬ ВТДатыИзмененияГрафиковРаботыСотрудников
	|ИЗ
	|	ВТГрафикиНаНачалоПериода КАК втГрафикиНаНачалоПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрафикиНаНачалоПериода.ДействуетДо,
	|	втГрафикиНаНачалоПериода.Сотрудник,
	|	втГрафикиНаНачалоПериода.ГрафикРаботыПоОкончании
	|ИЗ
	|	ВТГрафикиНаНачалоПериода КАК втГрафикиНаНачалоПериода
	|ГДЕ
	|	втГрафикиНаНачалоПериода.ДействуетДо МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрафикиВПериоде.Период,
	|	втГрафикиВПериоде.Сотрудник,
	|	втГрафикиВПериоде.ГрафикРаботы
	|ИЗ
	|	ВТГрафикиВПериоде КАК втГрафикиВПериоде
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втГрафикиВПериоде.ДействуетДо,
	|	втГрафикиВПериоде.Сотрудник,
	|	втГрафикиВПериоде.ГрафикРаботыПоОкончании
	|ИЗ
	|	ВТГрафикиВПериоде КАК втГрафикиВПериоде
	|ГДЕ
	|	втГрафикиВПериоде.ДействуетДо МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТГрафикиНаНачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТГрафикиВПериоде";
	
	Запрос.Выполнить();
	
КонецПроцедуры	

Процедура СоздатьВТДатыИзмененияВКадровойИсторииСотрудников(МенеджерВременныхТаблиц, Сотрудники, НачалоПериода, КонецПериода) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	|	КадроваяИсторияСотрудниковСрезПоследних.Организация КАК Организация,
	|	КадроваяИсторияСотрудниковСрезПоследних.Подразделение КАК Подразделение,
	|	КадроваяИсторияСотрудниковСрезПоследних.ВидСобытия КАК ВидСобытия,
	|	КадроваяИсторияСотрудниковСрезПоследних.ДействуетДо КАК ДействуетДо,
	|	КадроваяИсторияСотрудниковСрезПоследних.ОрганизацияПоОкончании КАК ОрганизацияПоОкончании,
	|	КадроваяИсторияСотрудниковСрезПоследних.ПодразделениеПоОкончании КАК ПодразделениеПоОкончании,
	|	КадроваяИсторияСотрудниковСрезПоследних.ВидСобытияПоОкончании КАК ВидСобытияПоОкончании
	|ПОМЕСТИТЬ ВТКадроваяИсторияНаНачалоПериода
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&НачалоПериода, Сотрудник В (&Сотрудники)) КАК КадроваяИсторияСотрудниковСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДействуетДо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	КадроваяИсторияСотрудников.Период КАК Период,
	|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
	|	КадроваяИсторияСотрудников.Организация КАК Организация,
	|	КадроваяИсторияСотрудников.Подразделение КАК Подразделение,
	|	КадроваяИсторияСотрудников.ВидСобытия КАК ВидСобытия,
	|	КадроваяИсторияСотрудников.ДействуетДо КАК ДействуетДо,
	|	КадроваяИсторияСотрудников.ОрганизацияПоОкончании КАК ОрганизацияПоОкончании,
	|	КадроваяИсторияСотрудников.ПодразделениеПоОкончании КАК ПодразделениеПоОкончании,
	|	КадроваяИсторияСотрудников.ВидСобытияПоОкончании КАК ВидСобытияПоОкончании
	|ПОМЕСТИТЬ ВТКадроваяИсторияВПериоде
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	|ГДЕ
	|	КадроваяИсторияСотрудников.Активность
	|	И КадроваяИсторияСотрудников.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И КадроваяИсторияСотрудников.Сотрудник В(&Сотрудники)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДействуетДо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоПериода КАК ДатаИзменения,
	|	втКадроваяИсторияНаНачалоПериода.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА втКадроваяИсторияНаНачалоПериода.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втКадроваяИсторияНаНачалоПериода.Организация
	|		КОГДА втКадроваяИсторияНаНачалоПериода.ДействуетДо > &НачалоПериода
	|			ТОГДА втКадроваяИсторияНаНачалоПериода.Организация
	|		ИНАЧЕ втКадроваяИсторияНаНачалоПериода.ОрганизацияПоОкончании
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА втКадроваяИсторияНаНачалоПериода.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втКадроваяИсторияНаНачалоПериода.Подразделение
	|		КОГДА втКадроваяИсторияНаНачалоПериода.ДействуетДо > &НачалоПериода
	|			ТОГДА втКадроваяИсторияНаНачалоПериода.Подразделение
	|		ИНАЧЕ втКадроваяИсторияНаНачалоПериода.ПодразделениеПоОкончании
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА втКадроваяИсторияНаНачалоПериода.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА втКадроваяИсторияНаНачалоПериода.ВидСобытия
	|		КОГДА втКадроваяИсторияНаНачалоПериода.ДействуетДо > &НачалоПериода
	|			ТОГДА втКадроваяИсторияНаНачалоПериода.ВидСобытия
	|		ИНАЧЕ втКадроваяИсторияНаНачалоПериода.ВидСобытияПоОкончании
	|	КОНЕЦ КАК ВидСобытия
	|ПОМЕСТИТЬ ВТДатыИзмененияВКадровойИсторииСотрудников
	|ИЗ
	|	втКадроваяИсторияНаНачалоПериода КАК втКадроваяИсторияНаНачалоПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втКадроваяИсторияНаНачалоПериода.ДействуетДо,
	|	втКадроваяИсторияНаНачалоПериода.Сотрудник,
	|	втКадроваяИсторияНаНачалоПериода.ОрганизацияПоОкончании,
	|	втКадроваяИсторияНаНачалоПериода.ПодразделениеПоОкончании,
	|	втКадроваяИсторияНаНачалоПериода.ВидСобытияПоОкончании
	|ИЗ
	|	втКадроваяИсторияНаНачалоПериода КАК втКадроваяИсторияНаНачалоПериода
	|ГДЕ
	|	втКадроваяИсторияНаНачалоПериода.ДействуетДо МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втКадроваяИсторияВПериоде.Период,
	|	втКадроваяИсторияВПериоде.Сотрудник,
	|	втКадроваяИсторияВПериоде.Организация,
	|	втКадроваяИсторияВПериоде.Подразделение,
	|	втКадроваяИсторияВПериоде.ВидСобытия
	|ИЗ
	|	втКадроваяИсторияВПериоде КАК втКадроваяИсторияВПериоде
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втКадроваяИсторияВПериоде.ДействуетДо,
	|	втКадроваяИсторияВПериоде.Сотрудник,
	|	втКадроваяИсторияВПериоде.ОрганизацияПоОкончании,
	|	втКадроваяИсторияВПериоде.ПодразделениеПоОкончании,
	|	втКадроваяИсторияВПериоде.ВидСобытияПоОкончании
	|ИЗ
	|	втКадроваяИсторияВПериоде КАК втКадроваяИсторияВПериоде
	|ГДЕ
	|	втКадроваяИсторияВПериоде.ДействуетДо МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ДатаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадроваяИсторияНаНачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадроваяИсторияВПериоде";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТДатыИзмененияКадровыхДанныхСотрудников(МенеджерВременныхТаблиц, Сотрудники, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	СоздатьВТДатыИзмененияГрафиковРаботыСотрудников(МенеджерВременныхТаблиц, Сотрудники, НачалоПериода, КонецПериода);
	СоздатьВТДатыИзмененияВКадровойИсторииСотрудников(МенеджерВременныхТаблиц, Сотрудники, НачалоПериода, КонецПериода);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.Сотрудник КАК Сотрудник,
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.ДатаИзменения КАК ДатаИзменения,
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.ГрафикРаботы КАК ГрафикРаботы,
	|	МАКСИМУМ(ВТДатыИзмененияВКадровойИсторииСотрудников.ДатаИзменения) КАК ДатаСреза
	|ПОМЕСТИТЬ ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории
	|ИЗ
	|	ВТДатыИзмененияГрафиковРаботыСотрудников КАК ВТДатыИзмененияГрафиковРаботыСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыИзмененияВКадровойИсторииСотрудников КАК ВТДатыИзмененияВКадровойИсторииСотрудников
	|		ПО ВТДатыИзмененияГрафиковРаботыСотрудников.ДатаИзменения >= ВТДатыИзмененияВКадровойИсторииСотрудников.ДатаИзменения
	|			И ВТДатыИзмененияГрафиковРаботыСотрудников.Сотрудник = ВТДатыИзмененияВКадровойИсторииСотрудников.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.Сотрудник,
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.ДатаИзменения,
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.ГрафикРаботы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ДатаСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Сотрудник КАК Сотрудник,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.ДатаИзменения КАК ДатаИзменения,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Организация КАК Организация,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Подразделение КАК Подразделение,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.ВидСобытия КАК ВидСобытия,
	|	МАКСИМУМ(ВТДатыИзмененияГрафиковРаботыСотрудников.ДатаИзменения) КАК ДатаСреза
	|ПОМЕСТИТЬ ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков
	|ИЗ
	|	ВТДатыИзмененияВКадровойИсторииСотрудников КАК ВТДатыИзмененияВКадровойИсторииСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыИзмененияГрафиковРаботыСотрудников КАК ВТДатыИзмененияГрафиковРаботыСотрудников
	|		ПО ВТДатыИзмененияВКадровойИсторииСотрудников.ДатаИзменения >= ВТДатыИзмененияГрафиковРаботыСотрудников.ДатаИзменения
	|			И ВТДатыИзмененияВКадровойИсторииСотрудников.Сотрудник = ВТДатыИзмененияГрафиковРаботыСотрудников.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Сотрудник,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.ДатаИзменения,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Организация,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Подразделение,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.ВидСобытия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ДатаСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории.ДатаИзменения КАК ДатаИзменения,
	|	ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории.Сотрудник КАК Сотрудник,
	|	ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Организация КАК Организация,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.Подразделение КАК Подразделение,
	|	ВТДатыИзмененияВКадровойИсторииСотрудников.ВидСобытия КАК ВидСобытия
	|ПОМЕСТИТЬ ВТДатыИзмененияКадровыхДанныхСотрудников
	|ИЗ
	|	ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории КАК ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыИзмененияВКадровойИсторииСотрудников КАК ВТДатыИзмененияВКадровойИсторииСотрудников
	|		ПО ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории.ДатаСреза = ВТДатыИзмененияВКадровойИсторииСотрудников.ДатаИзменения
	|			И ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории.Сотрудник = ВТДатыИзмененияВКадровойИсторииСотрудников.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.ДатаИзменения,
	|	ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.Сотрудник,
	|	ВТДатыИзмененияГрафиковРаботыСотрудников.ГрафикРаботы,
	|	ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.Организация,
	|	ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.Подразделение,
	|	ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.ВидСобытия
	|ИЗ
	|	ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков КАК ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыИзмененияГрафиковРаботыСотрудников КАК ВТДатыИзмененияГрафиковРаботыСотрудников
	|		ПО ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.ДатаСреза = ВТДатыИзмененияГрафиковРаботыСотрудников.ДатаИзменения
	|			И ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков.Сотрудник = ВТДатыИзмененияГрафиковРаботыСотрудников.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДатыИзмененияГрафиковСДатойСрезаКадровойИстории
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДатыИзмененияВКадровойИсторииСДатойСрезаГрафиков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДатыИзмененияГрафиковРаботыСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДатыИзмененияВКадровойИсторииСотрудников";
	
	Запрос.Выполнить();
	
КонецПроцедуры	

// Настройки размещения в панели отчетов. (См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов).
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//       Может использоваться для получения настроек варианта этого отчета при помощи функции ВариантыОтчетов.ОписаниеВарианта().
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//       уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//       См. "Свойства для изменения" процедуры ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	НастройкиОтчета.ОпределитьНастройкиФормы = Ложь;
	
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина);
 
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Основной");
	ОписаниеВарианта.ВидимостьПоУмолчанию = Ложь;
	ОписаниеВарианта.Размещение.Удалить(Метаданные.Подсистемы.торо_УправлениеПерсоналом.Подсистемы.торо_Сотрудники);
КонецПроцедуры

#КонецОбласти

#КонецЕсли