
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// При печати из документа "Плановая доступность персонала".
	Если Параметры.Свойство("ПлановаяДоступность") Тогда
		
		РеквизитыПлановойДоступности = торо_ОбщегоНазначенияВызовСервера.ЗначенияРеквизитовОбъекта(
			Параметры.ПлановаяДоступность, "НачалоПланирования, РабочееВремяСотрудников");
			
		ДобавленныеСотрудники = Новый Массив;	
			
		Выборка = РеквизитыПлановойДоступности.РабочееВремяСотрудников.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ДобавленныеСотрудники.Найти(Выборка.Сотрудник) = Неопределено Тогда
				НоваяСтрока = Сотрудники.Добавить();
				НоваяСтрока.Печатать = Истина;
				НоваяСтрока.Сотрудник = Выборка.Сотрудник;
				
				ДобавленныеСотрудники.Добавить(Выборка.Сотрудник);
			КонецЕсли;
		КонецЦикла;	 
		
		Год = НачалоГода(РеквизитыПлановойДоступности.НачалоПланирования);
	КонецЕсли;
	
	// При печати из обработки "Управление плановой доступностью персонала".
	Если Параметры.Свойство("Год") Тогда
		Год = Параметры.Год;
	КонецЕсли;
	
	Если Параметры.Свойство("Сотрудники") Тогда 
		
		Для Каждого Сотрудник Из Параметры.Сотрудники Цикл
			НоваяСтрока = Сотрудники.Добавить();
			НоваяСтрока.Печатать = Истина;
			НоваяСтрока.Сотрудник = Сотрудник;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Год) ИЛИ Сотрудники.Количество() > 0 Тогда
		// Значит эти реквизиты заполнились из параметров.
		Настройки.Удалить("Год");
		Настройки.Удалить("Сотрудники");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если НоваяСтрока И Не Копирование Тогда
		ТекущиеДанные.Печатать = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого Значение Из ВыбранноеЗначение Цикл
		НоваяСтрока = Сотрудники.Добавить();
		НоваяСтрока.Печатать = Истина;
		НоваяСтрока.Сотрудник = Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьСнятьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьСнятьФлажки(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаВыбора", ПараметрыФормы, Элементы.Сотрудники,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиНаПечать(Команда)
	
	Если Не ЗначениеЗаполнено(Год) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Год не заполнен. Печать невозможна.'"));
		Возврат;
	КонецЕсли;	
	
	МассивСотрудников = Новый Массив;
	Для Каждого Строка Из Сотрудники Цикл
		Если Не Строка.Печатать Или Не ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСотрудников.Добавить(Строка.Сотрудник);
	КонецЦикла;	
	
	Если МассивСотрудников.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано ни одного сотрудника. Печать невозможна.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("Год", Год);
	ПараметрыПечати.Вставить("Сотрудники", МассивСотрудников);
		
	торо_Печать.НапечататьДокумент(
		"Отчет.торо_ГрафикРаботыСотрудника", 
		"ГрафикРаботыСотрудника", 
		ПредопределенноеЗначение("Документ.торо_ПлановаяДоступностьПерсонала.ПустаяСсылка"), 
		ПараметрыПечати);

	ЭтотОбъект.Закрыть();	
		
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура УстановитьСнятьФлажки(Значение)
	
	Для Каждого Строка Из Сотрудники Цикл
		Строка.Печатать = Значение;	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти