#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекущаяИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекущаяИерархия, "ДатаОкончания", "НаборДанных1");
	торо_ОтчетыСервер.УстановитьТипГруппировкиОбъектаИерархии(КомпоновщикНастроек, ТекущаяИерархия);
	
	Элементы = КомпоновщикНастроек.Настройки.Выбор.Элементы;
	Для Каждого Элемент Из Элементы Цикл
		Если Элемент.Поле = Новый ПолеКомпоновкиДанных("СреднееВремяРаботы") Тогда
			Элемент.Использование = Ложь;
		КонецЕсли
	КонецЦикла;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ДанныеОтчета = ДанныеОтчета(НастройкиОсновнойСхемы, ТекущаяИерархия);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("НаборДанных", ДанныеОтчета);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);	
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

	// Никаких настроек не требуется.
	
КонецПроцедуры

Функция ДанныеОтчета(НастройкиОсновнойСхемы, ТекущаяИерархия)
	
	ПериодОтчета = НастройкиОсновнойСхемы.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета"));
	
	ДатаНачала = ПериодОтчета.Значение.ДатаНачала;
	ДатаОкончания = ПериодОтчета.Значение.ДатаОкончания; 
		
	ТекущаяДата = ТекущаяДатаСеанса();
	Если ДатаОкончания > ТекущаяДата Тогда
		ДатаОкончания = ТекущаяДата;
	КонецЕсли;
	
	ПредварительныеДанные = ПредварительныеДанныеСПользовательскимиОтборами(ТекущаяИерархия, ДатаНачала, ДатаОкончания);
	
	МинДатаОбнаружения = ДатаОкончания;
	Если ПредварительныеДанные.Количество() > 0 Тогда
		МинДатаОбнаружения = ПредварительныеДанные[0].ДатаОбнаруженияПредыдущийДефект;
	КонецЕсли;
	
	ПлановыеГрафикиРабот = ПредварительныеДанные.ВыгрузитьКолонку("ПлановыйГрафикРаботы");
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	КалендарныеГрафики.СоздатьВТРасписанияРаботыНаПериод(МенеджерВременныхТаблиц, ПлановыеГрафикиРабот, МинДатаОбнаружения, ДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = ТекстЗапросаДанныхОтчета();
	
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	Запрос.УстановитьПараметр("МинДатаОбнаружения", МинДатаОбнаружения);
	Запрос.УстановитьПараметр("СписокРемонтов", ПредварительныеДанные);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаДанныхОтчета() 
	
	Возврат "ВЫБРАТЬ
	        |	ВТ_СписокРемонтов.ID КАК ID,
	        |	ВТ_СписокРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_СписокРемонтов.ВидРемонта КАК ВидРемонта,
	        |	ВТ_СписокРемонтов.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_СписокРемонтов.ДатаУстранения КАК ДатаУстранения,
	        |	ВТ_СписокРемонтов.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_СписокРемонтов.ВидРемонтаПредыдущийДефект КАК ВидРемонтаПредыдущийДефект,
	        |	ВТ_СписокРемонтов.ПлановыйГрафикРаботы КАК ПлановыйГрафикРаботы,
	        |	ВТ_СписокРемонтов.Организация КАК Организация,
	        |	ВТ_СписокРемонтов.Подразделение КАК Подразделение,
	        |	ВТ_СписокРемонтов.ОбъектИерархии КАК ОбъектИерархии
	        |ПОМЕСТИТЬ ВТ_ПодготовленнаяТаблицаРемонтов
	        |ИЗ
	        |	&СписокРемонтов КАК ВТ_СписокРемонтов
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта,
	        |	ID
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID КАК ID,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	СУММА(торо_ВремяНахожденияВСостоянии.ВремяНахожденияВСостоянии) КАК ВремяНахожденияВСостоянии
	        |ПОМЕСТИТЬ ВТ_ПолныеИнтервалыРаботы
	        |ИЗ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_ВремяНахожденияВСостоянии КАК торо_ВремяНахожденияВСостоянии
	        |		ПО ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта = торо_ВремяНахожденияВСостоянии.ОбъектРемонта
	        |			И ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект <= торо_ВремяНахожденияВСостоянии.Период
	        |			И ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения >= торо_ВремяНахожденияВСостоянии.ДатаИзмененияСостояния
	        |			И (торо_ВремяНахожденияВСостоянии.ВидЭксплуатации.ТипЭксплуатации = ЗНАЧЕНИЕ(Перечисление.торо_ТипЭксплуатации.Эксплуатация))
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта,
	        |	ID
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	торо_ТекущееСостояниеОРСрезПоследних.Период КАК Период,
	        |	торо_ТекущееСостояниеОРСрезПоследних.ОбъектРемонта КАК ОбъектРемонта
	        |ПОМЕСТИТЬ ВТ_ОРВЭксплуатацииНаКонецПериода
	        |ИЗ
	        |	РегистрСведений.торо_ТекущееСостояниеОР.СрезПоследних(
	        |			&КонецПериода,
	        |			ОбъектРемонта В
	        |				(ВЫБРАТЬ
	        |					ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта
	        |				ИЗ
	        |					ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов)) КАК торо_ТекущееСостояниеОРСрезПоследних
	        |ГДЕ
	        |	торо_ТекущееСостояниеОРСрезПоследних.ВидЭксплуатации.ТипЭксплуатации = ЗНАЧЕНИЕ(Перечисление.торо_ТипЭксплуатации.Эксплуатация)
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID КАК ID,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ПлановыйГрафикРаботы КАК ПлановыйГрафикРаботы,
	        |	ВЫБОР
	        |		КОГДА ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект > торо_ВремяНахожденияВСостоянии.Период
	        |			ТОГДА ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект
	        |		ИНАЧЕ торо_ВремяНахожденияВСостоянии.Период
	        |	КОНЕЦ КАК ДатаНачала,
	        |	ВЫБОР
	        |		КОГДА ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения < торо_ВремяНахожденияВСостоянии.ДатаИзмененияСостояния
	        |			ТОГДА ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения
	        |		ИНАЧЕ торо_ВремяНахожденияВСостоянии.ДатаИзмененияСостояния
	        |	КОНЕЦ КАК ДатаОкончания
	        |ПОМЕСТИТЬ ВТ_ИнтервалыДляРасчетаПоГрафику
	        |ИЗ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_ВремяНахожденияВСостоянии КАК торо_ВремяНахожденияВСостоянии
	        |		ПО ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта = торо_ВремяНахожденияВСостоянии.ОбъектРемонта
	        |			И (торо_ВремяНахожденияВСостоянии.Период < ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект
	        |					И торо_ВремяНахожденияВСостоянии.ДатаИзмененияСостояния > ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект
	        |				ИЛИ торо_ВремяНахожденияВСостоянии.Период < ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения
	        |					И торо_ВремяНахожденияВСостоянии.ДатаИзмененияСостояния > ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения)
	        |			И (торо_ВремяНахожденияВСостоянии.ВидЭксплуатации.ТипЭксплуатации = ЗНАЧЕНИЕ(Перечисление.торо_ТипЭксплуатации.Эксплуатация))
	        |
	        |ОБЪЕДИНИТЬ
	        |
	        |ВЫБРАТЬ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ПлановыйГрафикРаботы,
	        |	ВЫБОР
	        |		КОГДА ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект > ВТ_ОРВЭксплуатацииНаКонецПериода.Период
	        |			ТОГДА ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект
	        |		ИНАЧЕ ВТ_ОРВЭксплуатацииНаКонецПериода.Период
	        |	КОНЕЦ,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения
	        |ИЗ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРВЭксплуатацииНаКонецПериода КАК ВТ_ОРВЭксплуатацииНаКонецПериода
	        |		ПО ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта = ВТ_ОРВЭксплуатацииНаКонецПериода.ОбъектРемонта
	        |			И ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения > ВТ_ОРВЭксплуатацииНаКонецПериода.Период
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ПлановыйГрафикРаботы,
	        |	ДатаНачала,
	        |	ДатаОкончания
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТРасписанияРаботы.ГрафикРаботы КАК ГрафикРаботы,
	        |	ВТРасписанияРаботы.ДатаГрафика КАК ДатаГрафика,
	        |	ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА)) КАК ДатаНачала,
	        |	ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА)) КАК ДатаОкончания
	        |ПОМЕСТИТЬ РасписанияОбъектовРемонтов
	        |ИЗ
	        |	ВТРасписанияРаботы КАК ВТРасписанияРаботы
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ГрафикРаботы,
	        |	ДатаНачала,
	        |	ДатаОкончания
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ID КАК ID,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы КАК ПлановыйГрафикРаботы,
	        |	РасписанияОбъектовРемонтов.ДатаНачала КАК ДатаНачала,
	        |	РасписанияОбъектовРемонтов.ДатаОкончания КАК ДатаОкончания
	        |ПОМЕСТИТЬ ВТ_Пересечения
	        |ИЗ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику КАК ВТ_ИнтервалыДляРасчетаПоГрафику
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасписанияОбъектовРемонтов КАК РасписанияОбъектовРемонтов
	        |		ПО ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы = РасписанияОбъектовРемонтов.ГрафикРаботы
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала <= РасписанияОбъектовРемонтов.ДатаНачала
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания >= РасписанияОбъектовРемонтов.ДатаОкончания
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ID,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ОбъектРемонта,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаружения,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала,
	        |	РасписанияОбъектовРемонтов.ДатаОкончания
	        |ИЗ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику КАК ВТ_ИнтервалыДляРасчетаПоГрафику
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасписанияОбъектовРемонтов КАК РасписанияОбъектовРемонтов
	        |		ПО ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы = РасписанияОбъектовРемонтов.ГрафикРаботы
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала < РасписанияОбъектовРемонтов.ДатаОкончания
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала > РасписанияОбъектовРемонтов.ДатаНачала
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания >= РасписанияОбъектовРемонтов.ДатаОкончания
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ID,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ОбъектРемонта,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаружения,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы,
	        |	РасписанияОбъектовРемонтов.ДатаНачала,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания
	        |ИЗ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику КАК ВТ_ИнтервалыДляРасчетаПоГрафику
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасписанияОбъектовРемонтов КАК РасписанияОбъектовРемонтов
	        |		ПО ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы = РасписанияОбъектовРемонтов.ГрафикРаботы
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания < РасписанияОбъектовРемонтов.ДатаОкончания
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания > РасписанияОбъектовРемонтов.ДатаНачала
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала <= РасписанияОбъектовРемонтов.ДатаНачала
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ID,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ОбъектРемонта,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаружения,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала,
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания
	        |ИЗ
	        |	ВТ_ИнтервалыДляРасчетаПоГрафику КАК ВТ_ИнтервалыДляРасчетаПоГрафику
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасписанияОбъектовРемонтов КАК РасписанияОбъектовРемонтов
	        |		ПО ВТ_ИнтервалыДляРасчетаПоГрафику.ПлановыйГрафикРаботы = РасписанияОбъектовРемонтов.ГрафикРаботы
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаНачала > РасписанияОбъектовРемонтов.ДатаНачала
	        |			И ВТ_ИнтервалыДляРасчетаПоГрафику.ДатаОкончания < РасписанияОбъектовРемонтов.ДатаОкончания
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ПолныеИнтервалыРаботы.ID КАК ID,
	        |	ВТ_ПолныеИнтервалыРаботы.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_ПолныеИнтервалыРаботы.ВремяНахожденияВСостоянии КАК ФактВремяРаботы
	        |ПОМЕСТИТЬ ВТ_ФактВремяРаботы
	        |ИЗ
	        |	ВТ_ПолныеИнтервалыРаботы КАК ВТ_ПолныеИнтервалыРаботы
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	ВТ_Пересечения.ID,
	        |	ВТ_Пересечения.ОбъектРемонта,
	        |	РАЗНОСТЬДАТ(ВТ_Пересечения.ДатаНачала, ВТ_Пересечения.ДатаОкончания, СЕКУНДА)
	        |ИЗ
	        |	ВТ_Пересечения КАК ВТ_Пересечения
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ФактВремяРаботы.ID КАК ID,
	        |	ВТ_ФактВремяРаботы.ОбъектРемонта КАК ОбъектРемонта,
	        |	СУММА(ВТ_ФактВремяРаботы.ФактВремяРаботы) КАК ФактВремяРаботы
	        |ПОМЕСТИТЬ ВТ_ФактВремяРаботыСвернутое
	        |ИЗ
	        |	ВТ_ФактВремяРаботы КАК ВТ_ФактВремяРаботы
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ФактВремяРаботы.ID,
	        |	ВТ_ФактВремяРаботы.ОбъектРемонта
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта,
	        |	ID
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ФактВремяРаботыСвернутое.ОбъектРемонта КАК ОбъектРемонта,
	        |	СУММА(ВТ_ФактВремяРаботыСвернутое.ФактВремяРаботы) КАК ВремяРаботыОР
	        |ПОМЕСТИТЬ ВТ_ВремяРаботыПоОР
	        |ИЗ
	        |	ВТ_ФактВремяРаботыСвернутое КАК ВТ_ФактВремяРаботыСвернутое
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ФактВремяРаботыСвернутое.ОбъектРемонта
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID КАК ID,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ВидРемонта КАК ВидРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаУстранения КАК ДатаУстранения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ВидРемонтаПредыдущийДефект КАК ВидРемонтаПредыдущийДефект,
	        |	ЕСТЬNULL(ВТ_ФактВремяРаботы.ФактВремяРаботы, 0) КАК ФактВремяРаботы,
	        |	ЕСТЬNULL(ВТ_ВремяРаботыПоОР.ВремяРаботыОР, 0) КАК ВремяРаботыОР,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.Организация КАК Организация,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.Подразделение КАК Подразделение,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектИерархии КАК ОбъектИерархии
	        |ПОМЕСТИТЬ ВТ_ВремяРаботы
	        |ИЗ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФактВремяРаботыСвернутое КАК ВТ_ФактВремяРаботы
	        |		ПО ВТ_ПодготовленнаяТаблицаРемонтов.ID = ВТ_ФактВремяРаботы.ID
	        |			И ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта = ВТ_ФактВремяРаботы.ОбъектРемонта
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВремяРаботыПоОР КАК ВТ_ВремяРаботыПоОР
	        |		ПО ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта = ВТ_ВремяРаботыПоОР.ОбъектРемонта
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ID
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	        |	торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних.Показатель КАК Показатель
	        |ПОМЕСТИТЬ ВТ_АктуальныеПоказателиНаработки
	        |ИЗ
	        |	РегистрСведений.торо_ПараметрыНаработкиОбъектовРемонта.СрезПоследних(
	        |			&КонецПериода,
	        |			ОбъектРемонта В
	        |				(ВЫБРАТЬ
	        |					ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта КАК ОбъектРемонта
	        |				ИЗ
	        |					ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов)) КАК торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта,
	        |	Показатель
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	торо_НаработкаОбъектовРемонта.ОбъектРемонта КАК ОбъектРемонта,
	        |	торо_НаработкаОбъектовРемонта.Показатель КАК Показатель,
	        |	торо_НаработкаОбъектовРемонта.Наработка КАК Наработка,
	        |	ВЫБОР
	        |		КОГДА торо_НаработкаОбъектовРемонта.ДатаНач = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА торо_НаработкаОбъектовРемонта.ОбъектРемонта.ДатаВводаВЭксплуатацию
	        |		ИНАЧЕ торо_НаработкаОбъектовРемонта.ДатаНач
	        |	КОНЕЦ КАК ДатаНачала,
	        |	торо_НаработкаОбъектовРемонта.ДатаКон КАК ДатаОкончания
	        |ПОМЕСТИТЬ ТабНаработки
	        |ИЗ
	        |	РегистрНакопления.торо_НаработкаОбъектовРемонта КАК торо_НаработкаОбъектовРемонта
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АктуальныеПоказателиНаработки КАК ВТ_АктуальныеПоказателиНаработки
	        |		ПО торо_НаработкаОбъектовРемонта.ОбъектРемонта = ВТ_АктуальныеПоказателиНаработки.ОбъектРемонта
	        |			И торо_НаработкаОбъектовРемонта.Показатель = ВТ_АктуальныеПоказателиНаработки.Показатель
	        |			И (торо_НаработкаОбъектовРемонта.ДатаКон > &МинДатаОбнаружения)
	        |			И (торо_НаработкаОбъектовРемонта.ДатаНач < &КонецПериода)
	        |ГДЕ
	        |	торо_НаработкаОбъектовРемонта.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	        |	И торо_НаработкаОбъектовРемонта.Наработка <> 0
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТабНаработки.Показатель КАК Показатель,
	        |	ТабНаработки.ОбъектРемонта КАК ОбъектРемонта,
	        |	ТабНаработки.Наработка КАК Наработка,
	        |	ТабНаработки.ДатаНачала КАК ДатаНачала,
	        |	ТабНаработки.ДатаОкончания КАК ДатаОкончания,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID КАК ID
	        |ПОМЕСТИТЬ ПериодыФактичНаработки
	        |ИЗ
	        |	ТабНаработки КАК ТабНаработки
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов
	        |		ПО ТабНаработки.ОбъектРемонта = ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта
	        |ГДЕ
	        |	(ТабНаработки.ДатаНачала МЕЖДУ ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект И ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения
	        |			ИЛИ ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект > ТабНаработки.ДатаНачала
	        |				И ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект < ТабНаработки.ДатаОкончания)
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ПериодыФактичНаработки.Показатель КАК Показатель,
	        |	ПериодыФактичНаработки.ОбъектРемонта КАК ОбъектРемонта,
	        |	ПериодыФактичНаработки.Наработка КАК Наработка,
	        |	ВЫБОР
	        |		КОГДА ПериодыФактичНаработки.ДатаНачала МЕЖДУ ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект И ПериодыФактичНаработки.ДатаОбнаружения
	        |				И ПериодыФактичНаработки.ДатаОкончания = ПериодыФактичНаработки.ДатаНачала
	        |			ТОГДА 1
	        |		КОГДА ПериодыФактичНаработки.ДатаНачала МЕЖДУ ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект И ПериодыФактичНаработки.ДатаОбнаружения
	        |				И ПериодыФактичНаработки.ДатаОкончания >= ПериодыФактичНаработки.ДатаОбнаружения
	        |			ТОГДА РАЗНОСТЬДАТ(ПериодыФактичНаработки.ДатаНачала, ПериодыФактичНаработки.ДатаОбнаружения, СЕКУНДА)
	        |		КОГДА ПериодыФактичНаработки.ДатаНачала МЕЖДУ ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект И ПериодыФактичНаработки.ДатаОбнаружения
	        |				И ПериодыФактичНаработки.ДатаОкончания < ПериодыФактичНаработки.ДатаОбнаружения
	        |			ТОГДА РАЗНОСТЬДАТ(ПериодыФактичНаработки.ДатаНачала, ПериодыФактичНаработки.ДатаОкончания, СЕКУНДА)
	        |		КОГДА ПериодыФактичНаработки.ДатаНачала < ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект
	        |				И ПериодыФактичНаработки.ДатаОкончания >= ПериодыФактичНаработки.ДатаОбнаружения
	        |			ТОГДА РАЗНОСТЬДАТ(ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект, ПериодыФактичНаработки.ДатаОбнаружения, СЕКУНДА)
	        |		КОГДА ПериодыФактичНаработки.ДатаНачала < ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект
	        |				И ПериодыФактичНаработки.ДатаОкончания < ПериодыФактичНаработки.ДатаОбнаружения
	        |			ТОГДА РАЗНОСТЬДАТ(ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект, ПериодыФактичНаработки.ДатаОкончания, СЕКУНДА)
	        |	КОНЕЦ КАК Секунды,
	        |	ВЫБОР
	        |		КОГДА ПериодыФактичНаработки.ДатаНачала МЕЖДУ ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект И ПериодыФактичНаработки.ДатаОбнаружения
	        |				И ПериодыФактичНаработки.ДатаОкончания = ПериодыФактичНаработки.ДатаНачала
	        |			ТОГДА 1
	        |		КОГДА ПериодыФактичНаработки.ДатаОкончания > ПериодыФактичНаработки.ДатаОбнаруженияПредыдущийДефект
	        |			ТОГДА РАЗНОСТЬДАТ(ПериодыФактичНаработки.ДатаНачала, ПериодыФактичНаработки.ДатаОкончания, СЕКУНДА)
	        |		ИНАЧЕ NULL
	        |	КОНЕЦ КАК СекундыКонецНачало,
	        |	ПериодыФактичНаработки.ID КАК ID
	        |ПОМЕСТИТЬ ТабСекунды
	        |ИЗ
	        |	ПериодыФактичНаработки КАК ПериодыФактичНаработки
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ТабСекунды.Показатель КАК Показатель,
	        |	СУММА(ТабСекунды.Наработка * ТабСекунды.Секунды / ТабСекунды.СекундыКонецНачало) КАК ФактНаработка,
	        |	ТабСекунды.ID КАК ID,
	        |	ТабСекунды.ОбъектРемонта КАК ОбъектРемонта
	        |ПОМЕСТИТЬ ФактическаяНаработка
	        |ИЗ
	        |	ТабСекунды КАК ТабСекунды
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ТабСекунды.Показатель,
	        |	ТабСекунды.ОбъектРемонта,
	        |	ТабСекунды.ID
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта,
	        |	ID
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ID КАК ID,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ВидРемонта КАК ВидРемонта,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаУстранения КАК ДатаУстранения,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ВидРемонтаПредыдущийДефект КАК ВидРемонтаПредыдущийДефект,
	        |	ФактическаяНаработка.Показатель КАК Показатель,
	        |	ФактическаяНаработка.ФактНаработка КАК Наработка,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.Организация КАК Организация,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.Подразделение КАК Подразделение,
	        |	ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектИерархии КАК ОбъектИерархии
	        |ПОМЕСТИТЬ ВТ_Наработка
	        |ИЗ
	        |	ВТ_ПодготовленнаяТаблицаРемонтов КАК ВТ_ПодготовленнаяТаблицаРемонтов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФактическаяНаработка КАК ФактическаяНаработка
	        |		ПО ВТ_ПодготовленнаяТаблицаРемонтов.ID = ФактическаяНаработка.ID
	        |			И ВТ_ПодготовленнаяТаблицаРемонтов.ОбъектРемонта = ФактическаяНаработка.ОбъектРемонта
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ID
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ЕСТЬNULL(ВТ_Наработка.ID, ВТ_ВремяРаботы.ID) КАК ID,
	        |	ЕСТЬNULL(ВТ_Наработка.ОбъектРемонта, ВТ_ВремяРаботы.ОбъектРемонта) КАК ОбъектРемонта,
	        |	ЕСТЬNULL(ВТ_Наработка.ОбъектИерархии, ВТ_ВремяРаботы.ОбъектИерархии) КАК ОбъектИерархии,
	        |	ЕСТЬNULL(ВТ_Наработка.Организация, ВТ_ВремяРаботы.Организация) КАК Организация,
	        |	ЕСТЬNULL(ВТ_Наработка.Подразделение, ВТ_ВремяРаботы.Подразделение) КАК Подразделение,
	        |	ЕСТЬNULL(ВТ_Наработка.ВидРемонта, ВТ_ВремяРаботы.ВидРемонта) КАК ВидРемонта,
	        |	ЕСТЬNULL(ВТ_Наработка.ДатаОбнаружения, ВТ_ВремяРаботы.ДатаОбнаружения) КАК ДатаОбнаружения,
	        |	ЕСТЬNULL(ВТ_Наработка.ДатаУстранения, ВТ_ВремяРаботы.ДатаУстранения) КАК ДатаУстранения,
	        |	ЕСТЬNULL(ВТ_Наработка.ДатаОбнаруженияПредыдущийДефект, ВТ_ВремяРаботы.ДатаОбнаруженияПредыдущийДефект) КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ЕСТЬNULL(ВТ_Наработка.ВидРемонтаПредыдущийДефект, ВТ_ВремяРаботы.ВидРемонтаПредыдущийДефект) КАК ВидРемонтаПредыдущийДефект,
	        |	ЕСТЬNULL(ВТ_Наработка.Показатель, ЗНАЧЕНИЕ(Справочник.ПараметрыВыработкиОС.ПустаяСсылка)) КАК Показатель,
	        |	ЕСТЬNULL(ВТ_Наработка.Наработка, 0) КАК Наработка,
	        |	ЕСТЬNULL(ВТ_ВремяРаботы.ФактВремяРаботы, 0) КАК ФактВремяРаботы,
	        |	ЕСТЬNULL(ВТ_ВремяРаботы.ВремяРаботыОР, 0) КАК ВремяРаботыОР
	        |ПОМЕСТИТЬ ВТ_Итог
	        |ИЗ
	        |	ВТ_Наработка КАК ВТ_Наработка
	        |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВремяРаботы КАК ВТ_ВремяРаботы
	        |		ПО ВТ_Наработка.ID = ВТ_ВремяРаботы.ID
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ОбъектРемонта
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	ВТ_Итог.Организация КАК Организация,
	        |	ВТ_Итог.Подразделение КАК Подразделение,
	        |	ВТ_Итог.ID КАК ID,
	        |	ВТ_Итог.ОбъектРемонта КАК ОбъектРемонта,
	        |	ВТ_Итог.ВидРемонта КАК ВидРемонта,
	        |	ВТ_Итог.ДатаОбнаружения КАК ДатаОбнаружения,
	        |	ВТ_Итог.ДатаУстранения КАК ДатаУстранения,
	        |	ВТ_Итог.ДатаОбнаруженияПредыдущийДефект КАК ДатаОбнаруженияПредыдущийДефект,
	        |	ВТ_Итог.ВидРемонтаПредыдущийДефект КАК ВидРемонтаПредыдущийДефект,
	        |	ВТ_Итог.Показатель КАК Показатель,
	        |	ВТ_Итог.Наработка КАК Наработка,
	        |	ВТ_Итог.ФактВремяРаботы КАК ФактВремяРаботы,
	        |	ВТ_Итог.ВремяРаботыОР КАК ВремяРаботыОР,
	        |	ВТ_Итог.ОбъектИерархии КАК ОбъектИерархии
	        |ИЗ
	        |	ВТ_Итог КАК ВТ_Итог
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	        |		ПО ВТ_Итог.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
	
	
КонецФункции

Функция ПредварительныеДанныеСПользовательскимиОтборами(ТекущаяИерархия, ДатаНачала, ДатаОкончания)
	
	СхемаКомпоновкиОтбор = ПолучитьМакет("СхемаКомпоновкиОтбор");
	
	ПараметрыИсходнойСхемы = СхемаКомпоновкиДанных.Параметры;
	Для каждого ПараметрИсходнойСхемы из ПараметрыИсходнойСхемы Цикл
		
		ПараметрСхемыОтбора = СхемаКомпоновкиОтбор.Параметры.Найти(ПараметрИсходнойСхемы.Имя);
		Если ПараметрСхемыОтбора = Неопределено Тогда
			ПараметрСхемыОтбора = СхемаКомпоновкиОтбор.Параметры.Добавить();
			ПараметрСхемыОтбора.Имя = ПараметрИсходнойСхемы.Имя;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрСхемыОтбора, ПараметрыИсходнойСхемы);
	КонецЦикла;
	
	ТекущаяИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	ПараметрСхемыОтбора = СхемаКомпоновкиОтбор.Параметры.Найти("ИерархияТип");
	Если ПараметрСхемыОтбора <> Неопределено Тогда
		ПараметрСхемыОтбора.Значение = ТекущаяИерархия;                                        
	КонецЕсли;
	
	КомпоновщикСхемыОтбора = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикСхемыОтбора.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиОтбор));
	КомпоновщикСхемыОтбора.ЗагрузитьНастройки(СхемаКомпоновкиОтбор.НастройкиПоУмолчанию);
	
	торо_ОтчетыСервер.УстановитьПолеОбъектИерархии(
		СхемаКомпоновкиОтбор.НаборыДанных.Данные.Запрос,
		"ВТ_ДатыПоследнихРемонтовМаксимальные.ОбъектРемонта",
		ТекущаяИерархия);
	
	СхемаКомпоновкиОтбор.НаборыДанных.Иерархия.Запрос = СхемаКомпоновкиДанных.НаборыДанных.Иерархия.Запрос;
	СхемаКомпоновкиОтбор.НаборыДанных.Контроль.Запрос = СхемаКомпоновкиДанных.НаборыДанных.Контроль.Запрос;
	
	НастройкиИсходнойСхемы = КомпоновщикНастроек.ПолучитьНастройки(); 	
	Для каждого ПараметрИсходнойСхемы из НастройкиИсходнойСхемы.ПараметрыДанных.Элементы Цикл
		
		ПараметрСхемыОтбора = КомпоновщикСхемыОтбора.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрИсходнойСхемы.Параметр);
		Если ПараметрСхемыОтбора = Неопределено Тогда
			Продолжить;
		КонецЕсли;   
		
		КомпоновщикСхемыОтбора.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(ПараметрИсходнойСхемы.Параметр, ПараметрИсходнойСхемы.Значение);	
		ПараметрСхемыОтбора.Использование = ПараметрИсходнойСхемы.Использование; 
	КонецЦикла;
			
	Для каждого ЭлементОтбораИсходнойСхемы из НастройкиИсходнойСхемы.Отбор.Элементы Цикл
		
		ТипОтбораЭлемент = ТипЗнч(ЭлементОтбораИсходнойСхемы) = Тип("ЭлементОтбораКомпоновкиДанных");
		
		Если ТипОтбораЭлемент Тогда
			ПолеНеДоступноДляОтбора = КомпоновщикСхемыОтбора.Настройки.ДоступныеПоляОтбора.НайтиПоле(ЭлементОтбораИсходнойСхемы.ЛевоеЗначение) = Неопределено;
			Если ПолеНеДоступноДляОтбора Тогда
				Продолжить;
			КонецЕсли; 
		КонецЕсли;	
		
		НовыйОтбор = КомпоновщикСхемыОтбора.Настройки.Отбор.Элементы.Добавить(ТипЗнч(ЭлементОтбораИсходнойСхемы));
		ЗаполнитьЗначенияСвойств(НовыйОтбор, ЭлементОтбораИсходнойСхемы);
		Если ТипОтбораЭлемент Тогда
			Если ТипЗнч(ЭлементОтбораИсходнойСхемы.ПравоеЗначение) = Тип("СписокЗначений") Тогда
				НовыйОтбор.ПравоеЗначение.ЗагрузитьЗначения(ЭлементОтбораИсходнойСхемы.ПравоеЗначение.ВыгрузитьЗначения());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Настройки = КомпоновщикСхемыОтбора.ПолучитьНастройки();
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиОтбор, Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПредварительныеДанные = Новый ТаблицаЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ПредварительныеДанные);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат ПредварительныеДанные;
	
КонецФункции
#КонецОбласти

#КонецЕсли