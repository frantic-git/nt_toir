#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "ОкончаниеПериода");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	ВТ_Данные.Подразделение КАК Подразделение,
		|	ВТ_Данные.ДатаНачала КАК ДатаНачала,
		|	ВТ_Данные.Регистратор КАК Регистратор,
		|	ВТ_Данные.Ответственный КАК Ответственный,
		|	ВТ_Данные.ДатаОкончания КАК ДатаОкончания,
		|	ВТ_Данные.ПричинаЗакрытия КАК ПричинаЗакрытия,
		|	ВТ_Данные.ВидРемонта КАК ВидРемонта,
		|	ВТ_Данные.Отменен КАК Отменен,
		|	ВТ_Данные.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
		|	ВТ_Данные.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
		|	ВТ_Данные.ОбъектРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные";
		

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	ВТ_Данные.Подразделение КАК Подразделение,
		|	ВТ_Данные.ДатаНачала КАК ДатаНачала,
		|	ВТ_Данные.Регистратор КАК Регистратор,
		|	ВТ_Данные.Ответственный КАК Ответственный,
		|	ВТ_Данные.ДатаОкончания КАК ДатаОкончания,
		|	ВТ_Данные.ПричинаЗакрытия КАК ПричинаЗакрытия,
		|	ВТ_Данные.ВидРемонта КАК ВидРемонта,
		|	ВТ_Данные.Отменен КАК Отменен,
		|	ВТ_Данные.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
		|	ВТ_Данные.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ОтмененныеРемонты.ОбъектРемонта КАК ОбъектРемонта,
	|	ВЫБОР
	|		КОГДА торо_ОтмененныеРемонты.ОбъектРемонта.ВнешнийОбъект
	|			ТОГДА торо_ОтмененныеРемонты.ОбъектРемонта.Контрагент
	|		ИНАЧЕ торо_ОтмененныеРемонты.ОбъектРемонта.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	торо_ОтмененныеРемонты.ДатаНачала КАК ДатаНачала,
	|	торо_ОтмененныеРемонты.Регистратор КАК Регистратор,
	|	торо_ОтмененныеРемонты.Регистратор.Ответственный КАК Ответственный,
	|	торо_ОтмененныеРемонты.ДатаОкончания КАК ДатаОкончания,
	|	торо_ОтмененныеРемонты.ПричинаЗакрытия КАК ПричинаЗакрытия,
	|	торо_ОтмененныеРемонты.ВидРемонта КАК ВидРемонта,
	|	торо_ОтмененныеРемонты.Отменен КАК Отменен,
	|	торо_ОтмененныеРемонты.ID КАК ID
	|ПОМЕСТИТЬ ВТ_ОтмененныеРемонты
	|ИЗ
	|	РегистрСведений.торо_ОтмененныеРемонты КАК торо_ОтмененныеРемонты
	|{ГДЕ
	|	(торо_ОтмененныеРемонты.ДатаНачала МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|			ИЛИ торо_ОтмененныеРемонты.ДатаОкончания МЕЖДУ &НачалоПериода И &ОкончаниеПериода)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID КАК ID,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Регистратор КАК Регистратор,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.Отменен КАК Отменен
	|ПОМЕСТИТЬ ВТ_ПлановыеРемонтныеРаботы
	|ИЗ
	|	РегистрСведений.торо_ПлановыеРемонтныеРаботы.СрезПоследних(
	|			,
	|			ID В
	|				(ВЫБРАТЬ
	|					ВТ_ОтмененныеРемонты.ID КАК ID
	|				ИЗ
	|					ВТ_ОтмененныеРемонты КАК ВТ_ОтмененныеРемонты)) КАК торо_ПлановыеРемонтныеРаботыСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
	|	ВЫБОР
	|		КОГДА торо_ОтмененныеРемонты.ОбъектРемонта.ВнешнийОбъект
	|			ТОГДА торо_ОтмененныеРемонты.ОбъектРемонта.Контрагент
	|		ИНАЧЕ торо_ОтмененныеРемонты.ОбъектРемонта.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	торо_ОтмененныеРемонты.ДатаНачала КАК ДатаНачала,
	|	торо_ОтмененныеРемонты.Регистратор КАК Регистратор,
	|	торо_ОтмененныеРемонты.Регистратор.Ответственный КАК Ответственный,
	|	торо_ОтмененныеРемонты.ДатаОкончания КАК ДатаОкончания,
	|	торо_ОтмененныеРемонты.ПричинаЗакрытия КАК ПричинаЗакрытия,
	|	торо_ОтмененныеРемонты.ВидРемонта КАК ВидРемонта,
	|	торо_ОтмененныеРемонты.Отменен КАК Отменен,
	|	ВЫБОР
	|		КОГДА торо_ОтмененныеРемонты.Отменен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ИНАЧЕ торо_ПлановыеРемонтныеРаботыСрезПоследних.ДатаНачалаРемонтныхРабот
	|	КОНЕЦ КАК ДатаНачалаРемонтныхРабот,
	|	ВЫБОР
	|		КОГДА торо_ОтмененныеРемонты.Отменен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ИНАЧЕ торо_ПлановыеРемонтныеРаботыСрезПоследних.ДатаОкончанияРемонтныхРабот
	|	КОНЕЦ КАК ДатаОкончанияРемонтныхРабот
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	ВТ_ОтмененныеРемонты КАК торо_ОтмененныеРемонты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПлановыеРемонтныеРаботы КАК торо_ПлановыеРемонтныеРаботыСрезПоследних
	|		ПО торо_ОтмененныеРемонты.ID = торо_ПлановыеРемонтныеРаботыСрезПоследних.ID
	|			И (торо_ОтмененныеРемонты.Регистратор = торо_ПлановыеРемонтныеРаботыСрезПоследних.Регистратор
	|					И торо_ОтмененныеРемонты.Отменен
	|				ИЛИ НЕ торо_ПлановыеРемонтныеРаботыСрезПоследних.Отменен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|		ПО торо_ОтмененныеРемонты.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка"
		
	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#КонецЕсли