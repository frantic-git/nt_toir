#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "КонецПериода");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	ВТ_Данные.ВидРемонта КАК ВидРемонта,
		|	ВТ_Данные.ДатаНачала КАК ДатаНачала,
		|	ВТ_Данные.ДатаОкончания КАК ДатаОкончания,
		|	ВТ_Данные.Операция КАК Операция,
		|	ВТ_Данные.ID_Операции КАК ID_Операции,
		|	ВТ_Данные.ПроцентПлан КАК ПроцентПлан,
		|	ВТ_Данные.ПроцентФакт КАК ПроцентФакт,
		|	ВТ_Данные.Регистратор КАК Регистратор,
		|	ВТ_Данные.РегистраторПлан КАК РегистраторПлан,
		|	ВТ_Данные.РегистраторФакт КАК РегистраторФакт,
		|	ВТ_Данные.ОбъектРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|{ВЫБРАТЬ
		|	ОбъектРемонта.*,
		|	ВидРемонта.*,
		|	ДатаНачала,
		|	ДатаОкончания,
		|	Операция.*,
		|	ID_Операции,
		|	ПроцентПлан,
		|	ПроцентФакт,
		|	Регистратор,
		|	РегистраторПлан,
		|	РегистраторФакт,
		|	ОбъектИерархии}
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные";
		
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	ВТ_Данные.ВидРемонта КАК ВидРемонта,
		|	ВТ_Данные.ДатаНачала КАК ДатаНачала,
		|	ВТ_Данные.ДатаОкончания КАК ДатаОкончания,
		|	ВТ_Данные.Операция КАК Операция,
		|	ВТ_Данные.ID_Операции КАК ID_Операции,
		|	ВТ_Данные.ПроцентПлан КАК ПроцентПлан,
		|	ВТ_Данные.ПроцентФакт КАК ПроцентФакт,
		|	ВТ_Данные.Регистратор КАК Регистратор,
		|	ВТ_Данные.РегистраторПлан КАК РегистраторПлан,
		|	ВТ_Данные.РегистраторФакт КАК РегистраторФакт,		
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектИерархии
		|{ВЫБРАТЬ
		|	ОбъектРемонта.*,
		|	ВидРемонта.*,
		|	ДатаНачала,
		|	ДатаОкончания,
		|	Операция.*,
		|	ID_Операции,
		|	ПроцентПлан,
		|	ПроцентФакт,
		|	Регистратор,
		|	РегистраторПлан,
		|	РегистраторФакт,
		|	ОбъектИерархии}
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	торо_ПроцентВыполненныхРабот.Регистратор КАК Регистратор,
	|	торо_ПроцентВыполненныхРабот.IDРемонта КАК IDРемонта,
	|	торо_ПроцентВыполненныхРабот.IDОперации КАК IDОперации,
	|	торо_ПроцентВыполненныхРабот.ВидДокумента КАК ВидДокумента,
	|	торо_ПроцентВыполненныхРабот.Операция КАК Операция,
	|	торо_ПроцентВыполненныхРабот.IDРодителя КАК IDРодителя,
	|	торо_ПроцентВыполненныхРабот.Процент КАК Процент,
	|	ВЫБОР
	|		КОГДА торо_ПроцентВыполненныхРабот.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.торо_ВидДокумента.ПлановыеЗатраты)
	|			ТОГДА 1
	|		КОГДА торо_ПроцентВыполненныхРабот.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.торо_ВидДокумента.ЗаявкаНаРемонт)
	|			ТОГДА 2
	|		КОГДА торо_ПроцентВыполненныхРабот.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.торо_ВидДокумента.НарядНаВыполнениеРемонтныхРабот)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК ТипДокументаЧислом
	|ПОМЕСТИТЬ ВТ_План
	|ИЗ
	|	РегистрНакопления.торо_ПроцентВыполненныхРабот КАК торо_ПроцентВыполненныхРабот
	|ГДЕ
	|	торо_ПроцентВыполненныхРабот.ВидДокумента В(&СписокПлана)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта,
	|	IDОперации,
	|	IDРодителя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_План.IDРемонта КАК IDРемонта,
	|	ВТ_План.IDОперации КАК IDОперации,
	|	ВТ_План.Операция КАК Операция,
	|	ВТ_План.IDРодителя КАК IDРодителя,
	|	ВТ_План.Процент КАК Процент,
	|	МИНИМУМ(ВТ_План.ТипДокументаЧислом) КАК ТипДокументаЧислом
	|ПОМЕСТИТЬ ВТ_ПланГруппировка
	|ИЗ
	|	ВТ_План КАК ВТ_План
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_План.IDРемонта,
	|	ВТ_План.IDОперации,
	|	ВТ_План.Операция,
	|	ВТ_План.IDРодителя,
	|	ВТ_План.Процент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПланГруппировка.IDРемонта КАК IDРемонта,
	|	ВТ_ПланГруппировка.IDОперации КАК IDОперации,
	|	ВТ_ПланГруппировка.Операция КАК Операция,
	|	ВТ_ПланГруппировка.IDРодителя КАК IDРодителя,
	|	ВТ_ПланГруппировка.Процент КАК Процент,
	|	ВТ_ПланГруппировка.ТипДокументаЧислом КАК ТипДокументаЧислом,
	|	ВТ_План.Регистратор КАК Регистратор,
	|	ВТ_План.ВидДокумента КАК ВидДокумента
	|ПОМЕСТИТЬ ВТ_ПланОтфильтрованный
	|ИЗ
	|	ВТ_ПланГруппировка КАК ВТ_ПланГруппировка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_План КАК ВТ_План
	|		ПО ВТ_ПланГруппировка.IDРемонта = ВТ_План.IDРемонта
	|			И ВТ_ПланГруппировка.IDОперации = ВТ_План.IDОперации
	|			И ВТ_ПланГруппировка.Операция = ВТ_План.Операция
	|			И ВТ_ПланГруппировка.IDРодителя = ВТ_План.IDРодителя
	|			И ВТ_ПланГруппировка.Процент = ВТ_План.Процент
	|			И ВТ_ПланГруппировка.ТипДокументаЧислом = ВТ_План.ТипДокументаЧислом
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ПроцентВыполненныхРабот.Регистратор КАК Регистратор,
	|	торо_ПроцентВыполненныхРабот.IDРемонта КАК IDРемонта,
	|	торо_ПроцентВыполненныхРабот.IDОперации КАК IDОперации,
	|	торо_ПроцентВыполненныхРабот.ВидДокумента КАК ВидДокумента,
	|	торо_ПроцентВыполненныхРабот.IDРодителя КАК IDРодителя,
	|	торо_ПроцентВыполненныхРабот.Операция КАК Операция,
	|	-торо_ПроцентВыполненныхРабот.Процент КАК Процент
	|ПОМЕСТИТЬ ВТ_Факт
	|ИЗ
	|	РегистрНакопления.торо_ПроцентВыполненныхРабот КАК торо_ПроцентВыполненныхРабот
	|ГДЕ
	|	торо_ПроцентВыполненныхРабот.ВидДокумента В(&СписокФакта)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта,
	|	IDОперации,
	|	IDРодителя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Факт.Регистратор КАК Регистратор,
	|	ВТ_Факт.IDРемонта КАК IDРемонта,
	|	ВТ_Факт.IDОперации КАК IDОперации,
	|	ВТ_Факт.ВидДокумента КАК ВидДокумента,
	|	ВТ_Факт.IDРодителя КАК IDРодителя,
	|	ВТ_Факт.Операция КАК Операция,
	|	ВТ_Факт.Процент КАК Процент,
	|	ЕСТЬNULL(торо_ЗакрытиеЗаявокИРемонтовЗакрываемыеЗаявки.Заявка, 0) КАК Заявка
	|ПОМЕСТИТЬ ВТ_ФактЗакрытыеСметы
	|ИЗ
	|	ВТ_Факт КАК ВТ_Факт
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ЗакрытиеЗаявокИРемонтов.ЗакрываемыеЗаявки КАК торо_ЗакрытиеЗаявокИРемонтовЗакрываемыеЗаявки
	|		ПО ВТ_Факт.Регистратор = торо_ЗакрытиеЗаявокИРемонтовЗакрываемыеЗаявки.Ссылка
	|			И ВТ_Факт.IDРемонта = торо_ЗакрытиеЗаявокИРемонтовЗакрываемыеЗаявки.ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_ПланОтфильтрованный.IDРемонта, ВТ_ФактЗакрытыеСметы.IDРемонта) КАК IDРемонта,
	|	ЕСТЬNULL(ВТ_ПланОтфильтрованный.IDОперации, ВТ_ФактЗакрытыеСметы.IDОперации) КАК IDОперации,
	|	ЕСТЬNULL(ВТ_ПланОтфильтрованный.Операция, ВТ_ФактЗакрытыеСметы.Операция) КАК Операция,
	|	ЕСТЬNULL(ВТ_ПланОтфильтрованный.IDРодителя, ВТ_ФактЗакрытыеСметы.IDРодителя) КАК IDРодителя,
	|	ЕСТЬNULL(ВТ_ПланОтфильтрованный.Регистратор, 0) КАК РегистраторПлан,
	|	ЕСТЬNULL(ВТ_ФактЗакрытыеСметы.Регистратор, 0) КАК РегистраторФакт,
	|	ЕСТЬNULL(ВТ_ПланОтфильтрованный.Процент, 0) КАК ПроцентПлан,
	|	ЕСТЬNULL(ВТ_ФактЗакрытыеСметы.Процент, 0) КАК ПроцентФакт
	|ПОМЕСТИТЬ ВТ_ПланФакт
	|ИЗ
	|	ВТ_ПланОтфильтрованный КАК ВТ_ПланОтфильтрованный
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ФактЗакрытыеСметы КАК ВТ_ФактЗакрытыеСметы
	|		ПО ВТ_ПланОтфильтрованный.IDРемонта = ВТ_ФактЗакрытыеСметы.IDРемонта
	|			И (ВТ_ФактЗакрытыеСметы.IDОперации = ВТ_ПланОтфильтрованный.IDОперации)
	|			И ВТ_ПланОтфильтрованный.IDРодителя = ВТ_ФактЗакрытыеСметы.IDРодителя
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ВТ_ФактЗакрытыеСметы.Регистратор) <> ТИП(Документ.торо_ЗакрытиеЗаявокИРемонтов)
	|	И НЕ ВТ_ПланОтфильтрованный.Регистратор В
	|				(ВЫБРАТЬ
	|					ВТ_ФактЗакрытыеСметы.Заявка
	|				ИЗ
	|					ВТ_ФактЗакрытыеСметы КАК ВТ_ФактЗакрытыеСметы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	IDРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ОбщиеДанныеПоРемонтам.ВидРемонта КАК ВидРемонта,
	|	торо_ОбщиеДанныеПоРемонтам.IDРемонта КАК ID_Ремонта,
	|	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектИерархии,
	|	торо_ОбщиеДанныеПоРемонтам.ДокументНачалаЦепочки КАК Регистратор,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала) КАК ДатаНачала,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаОкончания, торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания) КАК ДатаОкончания,
	|	ВТ_ПланФакт.Операция КАК Операция,
	|	ВТ_ПланФакт.IDОперации КАК ID_Операции,
	|	ВТ_ПланФакт.ПроцентПлан КАК ПроцентПлан,
	|	ВТ_ПланФакт.ПроцентФакт КАК ПроцентФакт,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВТ_ПланФакт.РегистраторПлан) = ТИП(Документ.торо_ПлановыеЗатраты)
	|			ТОГДА ВТ_ПланФакт.РегистраторПлан.ДокументПлана
	|		ИНАЧЕ ВТ_ПланФакт.РегистраторПлан
	|	КОНЕЦ КАК РегистраторПлан,
	|	ВТ_ПланФакт.РегистраторФакт КАК РегистраторФакт
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	ВТ_ПланФакт КАК ВТ_ПланФакт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	|		ПО ВТ_ПланФакт.IDРемонта = торо_ОбщиеДанныеПоРемонтам.IDРемонта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_ПланФакт.IDРемонта = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СвернутыеФактическиеДатыРемонтов КАК торо_СвернутыеФактическиеДатыРемонтов
	|		ПО ВТ_ПланФакт.IDРемонта = торо_СвернутыеФактическиеДатыРемонтов.IDРемонта
	|ГДЕ
	|	ВЫБОР
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ВыявленныйДефект)
	|				ТОГДА &ОтборПоВД
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ВнешнееОснованиеДляРабот)
	|				ТОГДА &ОтборПоВО
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ПланГрафикППР)
	|				ТОГДА &ОтборПоППР
	|			КОГДА торо_ОбщиеДанныеПоРемонтам.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ГрафикРегламентныхМероприятий)
	|				ТОГДА &ОтборПоРМ
	|		КОНЕЦ
	|{ГДЕ
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала) >= &НачалоПериода,
	|	ЕСТЬNULL(торо_СвернутыеФактическиеДатыРемонтов.ДатаНачала, торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала) <= &КонецПериода}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#КонецЕсли
