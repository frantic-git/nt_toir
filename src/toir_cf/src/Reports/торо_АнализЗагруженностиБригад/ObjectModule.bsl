
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ТабДанныхДляСКД = ПолучитьДанныеДляСКД(НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);
	
	ВнешниеНаборы = Новый Структура("ВнешнийНабор", ТабДанныхДляСКД);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

	// Никаких настроек не требуется.
	
КонецПроцедуры

Функция ПолучитьДанныеДляСКД(НастройкиОсновнойСхемы)
	
    МенеджерВТ = ПолучитьОсновныеДанныеДляОтчета(НастройкиОсновнойСхемы);
	ПериодОтчета = ПолучитьПериодОтчета(НастройкиОсновнойСхемы);
	Графики = ПолучитьГрафикиДляРасчета(МенеджерВТ);
	ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(МенеджерВТ, Графики, ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВТРасписанияРаботы.ДатаГрафика КАК ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДатыСтатусаВРаботеВРамкахПериода.ДатаНачала МЕЖДУ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА)) И ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА))
	|			ТОГДА ДатыСтатусаВРаботеВРамкахПериода.ДатаНачала
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА))
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ДатыСтатусаВРаботеВРамкахПериода.ДатаОкончания МЕЖДУ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА)) И ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА))
	|			ТОГДА ДатыСтатусаВРаботеВРамкахПериода.ДатаОкончания
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА))
	|	КОНЕЦ КАК ДатаОкончания,
	|	ДатыСтатусаВРаботеВРамкахПериода.Бригада КАК Бригада,
	|	ДатыСтатусаВРаботеВРамкахПериода.ИДРемонта КАК ИДРемонта,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ИнтервалыБригадыВРаботе
	|ИЗ
	|	ДатыСтатусаВРаботеВРамкахПериода КАК ДатыСтатусаВРаботеВРамкахПериода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК ВТРасписанияРаботы
	|		ПО ДатыСтатусаВРаботеВРамкахПериода.ГрафикРаботы = ВТРасписанияРаботы.ГрафикРаботы
	|			И (ВТРасписанияРаботы.ДатаГрафика МЕЖДУ НАЧАЛОПЕРИОДА(ДатыСтатусаВРаботеВРамкахПериода.ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(ДатыСтатусаВРаботеВРамкахПериода.ДатаОкончания, ДЕНЬ))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|			ПО ДатыСтатусаВРаботеВРамкахПериода.ИДРемонта = ВидыИОбъектыРемонтов.ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВРаботе.ДатаГрафика КАК ДатаГрафика,
	|	МИНИМУМ(ИнтервалыБригадыВРаботе.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ИнтервалыБригадыВРаботе.ДатаОкончания) КАК ДатаОкончания,
	|	ИнтервалыБригадыВРаботе.Бригада КАК Бригада,
	|	ИнтервалыБригадыВРаботе.ОбъектРемонта КАК ОбъектРемонта,
	|	ИнтервалыБригадыВРаботе.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ИнтервалыБригадыВРаботеПоВР
	|ИЗ
	|	ИнтервалыБригадыВРаботе КАК ИнтервалыБригадыВРаботе
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВРаботе.ДатаГрафика,
	|	ИнтервалыБригадыВРаботе.Бригада,
	|	ИнтервалыБригадыВРаботе.ОбъектРемонта,
	|	ИнтервалыБригадыВРаботе.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВРаботеПоВР.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ИнтервалыБригадыВРаботеПоВР.ДатаНачала, ИнтервалыБригадыВРаботеПоВР.ДатаОкончания, СЕКУНДА)) КАК ВремяВРаботеПоВР,
	|	ИнтервалыБригадыВРаботеПоВР.Бригада КАК Бригада,
	|	ИнтервалыБригадыВРаботеПоВР.ОбъектРемонта КАК ОбъектРемонта,
	|	ИнтервалыБригадыВРаботеПоВР.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВремяБригадыВРаботеПоВР
	|ИЗ
	|	ИнтервалыБригадыВРаботеПоВР КАК ИнтервалыБригадыВРаботеПоВР
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВРаботеПоВР.ДатаГрафика,
	|	ИнтервалыБригадыВРаботеПоВР.Бригада,
	|	ИнтервалыБригадыВРаботеПоВР.ОбъектРемонта,
	|	ИнтервалыБригадыВРаботеПоВР.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВРаботе.ДатаГрафика КАК ДатаГрафика,
	|	МИНИМУМ(ИнтервалыБригадыВРаботе.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ИнтервалыБригадыВРаботе.ДатаОкончания) КАК ДатаОкончания,
	|	ИнтервалыБригадыВРаботе.Бригада КАК Бригада,
	|	ИнтервалыБригадыВРаботе.ОбъектРемонта КАК ОбъектРемонта
	|ПОМЕСТИТЬ ИнтервалыБригадыВРаботеПоОР
	|ИЗ
	|	ИнтервалыБригадыВРаботе КАК ИнтервалыБригадыВРаботе
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВРаботе.ДатаГрафика,
	|	ИнтервалыБригадыВРаботе.Бригада,
	|	ИнтервалыБригадыВРаботе.ОбъектРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВРаботеПоОР.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ИнтервалыБригадыВРаботеПоОР.ДатаНачала, ИнтервалыБригадыВРаботеПоОР.ДатаОкончания, СЕКУНДА)) КАК ВремяВРаботеПоОР,
	|	ИнтервалыБригадыВРаботеПоОР.Бригада КАК Бригада,
	|	ИнтервалыБригадыВРаботеПоОР.ОбъектРемонта КАК ОбъектРемонта
	|ПОМЕСТИТЬ ВремяБригадыВРаботеПоОР
	|ИЗ
	|	ИнтервалыБригадыВРаботеПоОР КАК ИнтервалыБригадыВРаботеПоОР
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВРаботеПоОР.ДатаГрафика,
	|	ИнтервалыБригадыВРаботеПоОР.Бригада,
	|	ИнтервалыБригадыВРаботеПоОР.ОбъектРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВРаботе.ДатаГрафика КАК ДатаГрафика,
	|	МИНИМУМ(ИнтервалыБригадыВРаботе.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ИнтервалыБригадыВРаботе.ДатаОкончания) КАК ДатаОкончания,
	|	ИнтервалыБригадыВРаботе.Бригада КАК Бригада
	|ПОМЕСТИТЬ ИнтервалыБригадыВРаботеПоБригаде
	|ИЗ
	|	ИнтервалыБригадыВРаботе КАК ИнтервалыБригадыВРаботе
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВРаботе.ДатаГрафика,
	|	ИнтервалыБригадыВРаботе.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВРаботеПоБригаде.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ИнтервалыБригадыВРаботеПоБригаде.ДатаНачала, ИнтервалыБригадыВРаботеПоБригаде.ДатаОкончания, СЕКУНДА)) КАК ВремяВРаботе,
	|	ИнтервалыБригадыВРаботеПоБригаде.Бригада КАК Бригада
	|ПОМЕСТИТЬ ВремяБригадыВРаботе
	|ИЗ
	|	ИнтервалыБригадыВРаботеПоБригаде КАК ИнтервалыБригадыВРаботеПоБригаде
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВРаботеПоБригаде.ДатаГрафика,
	|	ИнтервалыБригадыВРаботеПоБригаде.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРасписанияРаботы.ДатаГрафика КАК ДатаГрафика,
	|	ВЫБОР
	|		КОГДА ДатыСтатусаВПутиВРамкахПериода.ДатаНачала МЕЖДУ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА)) И ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА))
	|			ТОГДА ДатыСтатусаВПутиВРамкахПериода.ДатаНачала
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА))
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ДатыСтатусаВПутиВРамкахПериода.ДатаОкончания МЕЖДУ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяНачала, СЕКУНДА)) И ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА))
	|			ТОГДА ДатыСтатусаВПутиВРамкахПериода.ДатаОкончания
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВТРасписанияРаботы.ДатаГрафика, СЕКУНДА, РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА))
	|	КОНЕЦ КАК ДатаОкончания,
	|	ДатыСтатусаВПутиВРамкахПериода.Бригада КАК Бригада,
	|	ДатыСтатусаВПутиВРамкахПериода.ИДРемонта КАК ИДРемонта,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ИнтервалыБригадыВПути
	|ИЗ
	|	ДатыСтатусаВПутиВРамкахПериода КАК ДатыСтатусаВПутиВРамкахПериода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК ВТРасписанияРаботы
	|		ПО ДатыСтатусаВПутиВРамкахПериода.ГрафикРаботы = ВТРасписанияРаботы.ГрафикРаботы
	|			И (ВТРасписанияРаботы.ДатаГрафика МЕЖДУ НАЧАЛОПЕРИОДА(ДатыСтатусаВПутиВРамкахПериода.ДатаНачала, ДЕНЬ) И КОНЕЦПЕРИОДА(ДатыСтатусаВПутиВРамкахПериода.ДатаОкончания, ДЕНЬ))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|			ПО ДатыСтатусаВПутиВРамкахПериода.ИДРемонта = ВидыИОбъектыРемонтов.ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВПути.ДатаГрафика КАК ДатаГрафика,
	|	МИНИМУМ(ИнтервалыБригадыВПути.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ИнтервалыБригадыВПути.ДатаОкончания) КАК ДатаОкончания,
	|	ИнтервалыБригадыВПути.Бригада КАК Бригада,
	|	ИнтервалыБригадыВПути.ОбъектРемонта КАК ОбъектРемонта,
	|	ИнтервалыБригадыВПути.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ИнтервалыБригадыВПутиПоВР
	|ИЗ
	|	ИнтервалыБригадыВПути КАК ИнтервалыБригадыВПути
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВПути.ДатаГрафика,
	|	ИнтервалыБригадыВПути.Бригада,
	|	ИнтервалыБригадыВПути.ОбъектРемонта,
	|	ИнтервалыБригадыВПути.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВПутиПоВР.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ИнтервалыБригадыВПутиПоВР.ДатаНачала, ИнтервалыБригадыВПутиПоВР.ДатаОкончания, СЕКУНДА)) КАК ВремяВПутиПоВР,
	|	ИнтервалыБригадыВПутиПоВР.Бригада КАК Бригада,
	|	ИнтервалыБригадыВПутиПоВР.ОбъектРемонта КАК ОбъектРемонта,
	|	ИнтервалыБригадыВПутиПоВР.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВремяБригадыВПутиПоВР
	|ИЗ
	|	ИнтервалыБригадыВПутиПоВР КАК ИнтервалыБригадыВПутиПоВР
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВПутиПоВР.ДатаГрафика,
	|	ИнтервалыБригадыВПутиПоВР.Бригада,
	|	ИнтервалыБригадыВПутиПоВР.ОбъектРемонта,
	|	ИнтервалыБригадыВПутиПоВР.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВПути.ДатаГрафика КАК ДатаГрафика,
	|	МИНИМУМ(ИнтервалыБригадыВПути.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ИнтервалыБригадыВПути.ДатаОкончания) КАК ДатаОкончания,
	|	ИнтервалыБригадыВПути.Бригада КАК Бригада,
	|	ИнтервалыБригадыВПути.ОбъектРемонта КАК ОбъектРемонта
	|ПОМЕСТИТЬ ИнтервалыБригадыВПутиПоОР
	|ИЗ
	|	ИнтервалыБригадыВПути КАК ИнтервалыБригадыВПути
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВПути.ДатаГрафика,
	|	ИнтервалыБригадыВПути.Бригада,
	|	ИнтервалыБригадыВПути.ОбъектРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВПутиПоОР.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ИнтервалыБригадыВПутиПоОР.ДатаНачала, ИнтервалыБригадыВПутиПоОР.ДатаОкончания, СЕКУНДА)) КАК ВремяВПутиПоОР,
	|	ИнтервалыБригадыВПутиПоОР.Бригада КАК Бригада,
	|	ИнтервалыБригадыВПутиПоОР.ОбъектРемонта КАК ОбъектРемонта
	|ПОМЕСТИТЬ ВремяБригадыВПутиПоОР
	|ИЗ
	|	ИнтервалыБригадыВПутиПоОР КАК ИнтервалыБригадыВПутиПоОР
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВПутиПоОР.ДатаГрафика,
	|	ИнтервалыБригадыВПутиПоОР.Бригада,
	|	ИнтервалыБригадыВПутиПоОР.ОбъектРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВПути.ДатаГрафика КАК ДатаГрафика,
	|	МИНИМУМ(ИнтервалыБригадыВПути.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ИнтервалыБригадыВПути.ДатаОкончания) КАК ДатаОкончания,
	|	ИнтервалыБригадыВПути.Бригада КАК Бригада
	|ПОМЕСТИТЬ ИнтервалыБригадыВПутиПоБригаде
	|ИЗ
	|	ИнтервалыБригадыВПути КАК ИнтервалыБригадыВПути
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВПути.ДатаГрафика,
	|	ИнтервалыБригадыВПути.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнтервалыБригадыВПутиПоБригаде.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ИнтервалыБригадыВПутиПоБригаде.ДатаНачала, ИнтервалыБригадыВПутиПоБригаде.ДатаОкончания, СЕКУНДА)) КАК ВремяВПути,
	|	ИнтервалыБригадыВПутиПоБригаде.Бригада КАК Бригада
	|ПОМЕСТИТЬ ВремяБригадыВПути
	|ИЗ
	|	ИнтервалыБригадыВПутиПоБригаде КАК ИнтервалыБригадыВПутиПоБригаде
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнтервалыБригадыВПутиПоБригаде.ДатаГрафика,
	|	ИнтервалыБригадыВПутиПоБригаде.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРасписанияРаботы.ДатаГрафика КАК ДатаГрафика,
	|	СУММА(РАЗНОСТЬДАТ(ВТРасписанияРаботы.ВремяНачала, ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА)) КАК ФондВремени,
	|	втДанныеБригад.Организация КАК Организация,
	|	втДанныеБригад.Подразделение КАК Подразделение,
	|	втДанныеБригад.Бригада КАК Бригада
	|ПОМЕСТИТЬ ВТ_ВремяБригадыПлан
	|ИЗ
	|	ВТРасписанияРаботы КАК ВТРасписанияРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеБригад КАК втДанныеБригад
	|		ПО ВТРасписанияРаботы.ГрафикРаботы = втДанныеБригад.ГрафикРаботы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРасписанияРаботы.ДатаГрафика,
	|	втДанныеБригад.Организация,
	|	втДанныеБригад.Подразделение,
	|	втДанныеБригад.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ВремяБригадыПлан.ДатаГрафика КАК ДатаГрафика,
	|	ВТ_ВремяБригадыПлан.ФондВремени КАК ФондВремени,
	|	ВТ_ВремяБригадыПлан.Организация КАК Организация,
	|	ВТ_ВремяБригадыПлан.Подразделение КАК Подразделение,
	|	ВТ_ВремяБригадыПлан.Бригада КАК Бригада,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВремяБригадыПлан
	|ИЗ
	|	ВТ_ВремяБригадыПлан КАК ВТ_ВремяБригадыПлан,
	|	ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяБригадыВПутиПоВР.Бригада КАК Бригада,
	|	ВремяБригадыВПутиПоВР.ВидРемонта КАК ВидРемонта,
	|	ВремяБригадыВПутиПоВР.ОбъектРемонта КАК ОбъектРемонта
	|ПОМЕСТИТЬ ВТ_БригадыСРемонтами
	|ИЗ
	|	ВремяБригадыВПутиПоВР КАК ВремяБригадыВПутиПоВР,
	|	ВремяБригадыВРаботеПоВР КАК ВремяБригадыВРаботеПоВР
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВремяБригадыВРаботеПоВР.Бригада,
	|	ВремяБригадыВРаботеПоВР.ВидРемонта,
	|	ВремяБригадыВРаботеПоВР.ОбъектРемонта
	|ИЗ
	|	ВремяБригадыВРаботеПоВР КАК ВремяБригадыВРаботеПоВР
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Бригада,
	|	ОбъектРемонта,
	|	ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяБригадыВРаботеПоВР.ДатаГрафика КАК ДатаГрафика,
	|	ВремяБригадыВРаботеПоВР.Бригада КАК Бригада,
	|	ВремяБригадыВРаботеПоВР.ОбъектРемонта КАК ОбъектРемонта,
	|	ВремяБригадыВРаботеПоВР.ВидРемонта КАК ВидРемонта,
	|	ВремяБригадыВРаботеПоВР.ВремяВРаботеПоВР КАК ВремяВРаботеПоВР,
	|	ВремяБригадыВРаботеПоОР.ВремяВРаботеПоОР КАК ВремяВРаботеПоОР,
	|	ВремяБригадыВРаботе.ВремяВРаботе КАК ВремяВРаботе
	|ПОМЕСТИТЬ ВТ_ИтоговаяВРаботе
	|ИЗ
	|	ВремяБригадыВРаботеПоВР КАК ВремяБригадыВРаботеПоВР
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремяБригадыВРаботеПоОР КАК ВремяБригадыВРаботеПоОР
	|		ПО ВремяБригадыВРаботеПоВР.ДатаГрафика = ВремяБригадыВРаботеПоОР.ДатаГрафика
	|			И ВремяБригадыВРаботеПоВР.Бригада = ВремяБригадыВРаботеПоОР.Бригада
	|			И ВремяБригадыВРаботеПоВР.ОбъектРемонта = ВремяБригадыВРаботеПоОР.ОбъектРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремяБригадыВРаботе КАК ВремяБригадыВРаботе
	|		ПО ВремяБригадыВРаботеПоВР.ДатаГрафика = ВремяБригадыВРаботе.ДатаГрафика
	|			И ВремяБригадыВРаботеПоВР.Бригада = ВремяБригадыВРаботе.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяБригадыВПутиПоВР.ДатаГрафика КАК ДатаГрафика,
	|	ВремяБригадыВПутиПоВР.Бригада КАК Бригада,
	|	ВремяБригадыВПутиПоВР.ОбъектРемонта КАК ОбъектРемонта,
	|	ВремяБригадыВПутиПоВР.ВидРемонта КАК ВидРемонта,
	|	ВремяБригадыВПутиПоВР.ВремяВПутиПоВР КАК ВремяВПутиПоВР,
	|	ВремяБригадыВПутиПоОР.ВремяВПутиПоОР КАК ВремяВПутиПоОР,
	|	ВремяБригадыВПути.ВремяВПути КАК ВремяВПути
	|ПОМЕСТИТЬ ВТ_ИтоговаяВПути
	|ИЗ
	|	ВремяБригадыВПутиПоВР КАК ВремяБригадыВПутиПоВР
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремяБригадыВПутиПоОР КАК ВремяБригадыВПутиПоОР
	|		ПО ВремяБригадыВПутиПоВР.ДатаГрафика = ВремяБригадыВПутиПоОР.ДатаГрафика
	|			И ВремяБригадыВПутиПоВР.Бригада = ВремяБригадыВПутиПоОР.Бригада
	|			И ВремяБригадыВПутиПоВР.ОбъектРемонта = ВремяБригадыВПутиПоОР.ОбъектРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремяБригадыВПути КАК ВремяБригадыВПути
	|		ПО ВремяБригадыВПутиПоВР.ДатаГрафика = ВремяБригадыВПути.ДатаГрафика
	|			И ВремяБригадыВПутиПоВР.Бригада = ВремяБригадыВПути.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремяБригадыПлан.ДатаГрафика КАК ДатаГрафика,
	|	ВремяБригадыПлан.Бригада КАК Бригада,
	|	ВремяБригадыПлан.Организация КАК Организация,
	|	ВремяБригадыПлан.Подразделение КАК Подразделение,
	|	ВремяБригадыПлан.ОбъектРемонта КАК ОбъектРемонта,
	|	ВремяБригадыПлан.ВидРемонта КАК ВидРемонта,
	|	ЕСТЬNULL(ВремяБригадыПлан.ФондВремени, 0) КАК ФондВремени,
	|	ЕСТЬNULL(ВТ_ИтоговаяВРаботе.ВремяВРаботеПоВР, 0) КАК ВремяВРаботеПоВР,
	|	ЕСТЬNULL(ВТ_ИтоговаяВРаботе.ВремяВРаботеПоОР, 0) КАК ВремяВРаботеПоОР,
	|	ЕСТЬNULL(ВТ_ИтоговаяВРаботе.ВремяВРаботе, 0) КАК ВремяВРаботе,
	|	ЕСТЬNULL(ВТ_ИтоговаяВПути.ВремяВПутиПоВР, 0) КАК ВремяВПутиПоВР,
	|	ЕСТЬNULL(ВТ_ИтоговаяВПути.ВремяВПутиПоОР, 0) КАК ВремяВПутиПоОР,
	|	ЕСТЬNULL(ВТ_ИтоговаяВПути.ВремяВПути, 0) КАК ВремяВПути
	|ПОМЕСТИТЬ ВТ_Итоговая
	|ИЗ
	|	ВремяБригадыПлан КАК ВремяБригадыПлан
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтоговаяВРаботе КАК ВТ_ИтоговаяВРаботе
	|		ПО ВремяБригадыПлан.ДатаГрафика = ВТ_ИтоговаяВРаботе.ДатаГрафика
	|			И ВремяБригадыПлан.Бригада = ВТ_ИтоговаяВРаботе.Бригада
	|			И ВремяБригадыПлан.ОбъектРемонта = ВТ_ИтоговаяВРаботе.ОбъектРемонта
	|			И ВремяБригадыПлан.ВидРемонта = ВТ_ИтоговаяВРаботе.ВидРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтоговаяВПути КАК ВТ_ИтоговаяВПути
	|		ПО ВремяБригадыПлан.ДатаГрафика = ВТ_ИтоговаяВПути.ДатаГрафика
	|			И ВремяБригадыПлан.Бригада = ВТ_ИтоговаяВПути.Бригада
	|			И ВремяБригадыПлан.ОбъектРемонта = ВТ_ИтоговаяВПути.ОбъектРемонта
	|			И ВремяБригадыПлан.ВидРемонта = ВТ_ИтоговаяВПути.ВидРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итоговая.ДатаГрафика КАК Период,
	|	ВТ_Итоговая.Бригада КАК Бригада,  
	|	ВТ_Итоговая.Организация КАК Организация,
	|	ВТ_Итоговая.Подразделение КАК Подразделение,
	|	ВТ_Итоговая.ОбъектРемонта КАК ОбъектРемонта,
	|	ВТ_Итоговая.ВидРемонта КАК ВидРемонта,
	|	ВТ_Итоговая.ФондВремени КАК ФондВремени,
	|	ВТ_Итоговая.ВремяВРаботеПоВР КАК ВремяВРаботеПоВР,
	|	ВТ_Итоговая.ВремяВРаботеПоОР КАК ВремяВРаботеПоОР,
	|	ВТ_Итоговая.ВремяВРаботе КАК ВремяВРаботе,
	|	ВТ_Итоговая.ВремяВПутиПоВР КАК ВремяВПутиПоВР,
	|	ВТ_Итоговая.ВремяВПутиПоОР КАК ВремяВПутиПоОР,
	|	ВТ_Итоговая.ВремяВПути КАК ВремяВПутиБригада
	|ИЗ
	|	ВТ_Итоговая КАК ВТ_Итоговая
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_БригадыСРемонтами КАК ВТ_БригадыСРемонтами
	|		ПО ВТ_Итоговая.Бригада = ВТ_БригадыСРемонтами.Бригада
	|			И ВТ_Итоговая.ОбъектРемонта = ВТ_БригадыСРемонтами.ОбъектРемонта
	|			И ВТ_Итоговая.ВидРемонта = ВТ_БригадыСРемонтами.ВидРемонта");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ГрафикиРаботыРасч = Запрос.Выполнить().Выгрузить();
	
	МенеджерВТ.Закрыть();
	Возврат ГрафикиРаботыРасч;
	
КонецФункции

Функция ПолучитьОсновныеДанныеДляОтчета(НастройкиОсновнойСхемы)
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц();
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.ИДРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Исполнитель КАК Исполнитель,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Бригада,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Период КАК Период,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_СтатусВРаботе
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтовВЗаявкахПоБригадам КАК торо_СтатусыРемонтовВЗаявкахПоБригадам
	|ГДЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус = ЗНАЧЕНИЕ(Перечисление.торо_СтатусыРемонтов.ВРаботе)
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.Период <= &ДатаОкончания
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя ССЫЛКА Справочник.торо_РемонтныеБригады
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.ИДРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Исполнитель КАК Исполнитель,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Бригада,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Период КАК Период,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_СтатусВПути
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтовВЗаявкахПоБригадам КАК торо_СтатусыРемонтовВЗаявкахПоБригадам
	|ГДЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус = ЗНАЧЕНИЕ(Перечисление.торо_СтатусыРемонтов.ВПути)
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.Период <= &ДатаОкончания
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя ССЫЛКА Справочник.торо_РемонтныеБригады
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.ИДРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Исполнитель КАК Исполнитель,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Бригада,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Период КАК Период,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_СтатусВыполнен
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтовВЗаявкахПоБригадам КАК торо_СтатусыРемонтовВЗаявкахПоБригадам
	|ГДЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус = ЗНАЧЕНИЕ(Перечисление.торо_СтатусыРемонтов.Выполнен)
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.Период <= &ДатаОкончания
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя ССЫЛКА Справочник.торо_РемонтныеБригады
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.ИДРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Исполнитель КАК Исполнитель,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Бригада,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Период КАК Период,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_СтатусПриостановлен
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтовВЗаявкахПоБригадам КАК торо_СтатусыРемонтовВЗаявкахПоБригадам
	|ГДЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус = ЗНАЧЕНИЕ(Перечисление.торо_СтатусыРемонтов.Приостановлен)
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.Период <= &ДатаОкончания
	|	И торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя ССЫЛКА Справочник.торо_РемонтныеБригады
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Бригада, 
	|	ВЫРАЗИТЬ(торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Справочник.торо_РемонтныеБригады).Подразделение КАК Подразделение,
	|	ВЫРАЗИТЬ(торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК Справочник.торо_РемонтныеБригады).РесурсыРемонтнойБригады КАК РесурсыРемонтнойБригады
	|ПОМЕСТИТЬ втБригады
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтовВЗаявкахПоБригадам КАК торо_СтатусыРемонтовВЗаявкахПоБригадам
	|ГДЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя ССЫЛКА Справочник.торо_РемонтныеБригады
	|	И НЕ торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя ЕСТЬ NULL
	|	И НЕ торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя = ЗНАЧЕНИЕ(Справочник.торо_РемонтныеБригады.ПустаяСсылка)
	|	И НЕ торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя = НЕОПРЕДЕЛЕНО
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СоставРемонтныхБригадСрезПоследних.РемонтнаяБригада КАК Бригада,
	|	торо_СоставРемонтныхБригадСрезПоследних.Исполнитель КАК Бригадир
	|ПОМЕСТИТЬ втБригадирыБригад
	|ИЗ
	|	РегистрСведений.торо_СоставРемонтныхБригад.СрезПоследних(
	|			&ДатаНачала,
	|			РемонтнаяБригада В
	|				(ВЫБРАТЬ
	|					втБригады.Бригада
	|				ИЗ
	|					втБригады КАК втБригады)) КАК торо_СоставРемонтныхБригадСрезПоследних
	|ГДЕ
	|	торо_СоставРемонтныхБригадСрезПоследних.Бригадир
	|	И НЕ торо_СоставРемонтныхБригадСрезПоследних.ИсключенИзБригады
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Бригадир
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Бригадир,
	|	КадроваяИсторияСотрудниковСрезПоследних.Организация КАК Организация
	|ПОМЕСТИТЬ втОрганизацииБригадиров
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(
	|			&ДатаНачала,
	|			Сотрудник В
	|				(ВЫБРАТЬ
	|					втБригадирыБригад.Бригадир
	|				ИЗ
	|					втБригадирыБригад КАК втБригадирыБригад)) КАК КадроваяИсторияСотрудниковСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Бригадир
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втБригады.Бригада КАК Бригада,
	|	торо_РесурсыРемонтнойБригады.ГрафикРаботы КАК ГрафикРаботы,
	|	втОрганизацииБригадиров.Организация КАК Организация,
	|	втБригады.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ втДанныеБригад
	|ИЗ
	|	втБригады КАК втБригады
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_РесурсыРемонтнойБригады КАК торо_РесурсыРемонтнойБригады
	|		ПО втБригады.РесурсыРемонтнойБригады = торо_РесурсыРемонтнойБригады.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втБригадирыБригад КАК втБригадирыБригад
	|			ЛЕВОЕ СОЕДИНЕНИЕ втОрганизацииБригадиров КАК втОрганизацииБригадиров
	|			ПО втБригадирыБригад.Бригадир = втОрганизацииБригадиров.Бригадир
	|		ПО втБригады.Бригада = втБригадирыБригад.Бригада
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Бригада,
	|	ГрафикРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтатусВПути.ИДРемонта КАК ИДРемонта,
	|	ВТ_СтатусВПути.Бригада КАК Бригада,
	|	ВЫБОР
	|		КОГДА ВТ_СтатусВПути.Период < &ДатаНачала
	|			ТОГДА &ДатаНачала
	|		ИНАЧЕ ВТ_СтатусВПути.Период
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СтатусВРаботе.Период ЕСТЬ NULL
	|			ТОГДА ВТ_СтатусВРаботе.Период
	|		КОГДА НЕ ВТ_СтатусПриостановлен.Период ЕСТЬ NULL
	|			ТОГДА ВТ_СтатусПриостановлен.Период
	|		КОГДА НЕ ВТ_СтатусВыполнен.Период ЕСТЬ NULL
	|			ТОГДА ВТ_СтатусВыполнен.Период
	|		ИНАЧЕ &ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания,
	|	втДанныеБригад.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ ДатыСтатусаВПутиВРамкахПериода
	|ИЗ
	|	ВТ_СтатусВПути КАК ВТ_СтатусВПути
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусВРаботе КАК ВТ_СтатусВРаботе
	|		ПО ВТ_СтатусВПути.ИДРемонта = ВТ_СтатусВРаботе.ИДРемонта
	|			И ВТ_СтатусВПути.Бригада = ВТ_СтатусВРаботе.Бригада
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусВыполнен КАК ВТ_СтатусВыполнен
	|		ПО ВТ_СтатусВПути.ИДРемонта = ВТ_СтатусВыполнен.ИДРемонта
	|			И ВТ_СтатусВПути.Бригада = ВТ_СтатусВыполнен.Бригада
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусПриостановлен КАК ВТ_СтатусПриостановлен
	|		ПО ВТ_СтатусВПути.ИДРемонта = ВТ_СтатусПриостановлен.ИДРемонта
	|			И ВТ_СтатусВПути.Бригада = ВТ_СтатусПриостановлен.Бригада
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеБригад КАК втДанныеБригад
	|		ПО ВТ_СтатусВПути.Бригада = втДанныеБригад.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтатусВРаботе.ИДРемонта КАК ИДРемонта,
	|	ВТ_СтатусВРаботе.Бригада КАК Бригада,
	|	ВЫБОР
	|		КОГДА ВТ_СтатусВРаботе.Период < &ДатаНачала
	|			ТОГДА &ДатаНачала
	|		ИНАЧЕ ВТ_СтатусВРаботе.Период
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА НЕ ВТ_СтатусПриостановлен.Период ЕСТЬ NULL
	|			ТОГДА ВТ_СтатусПриостановлен.Период
	|		КОГДА НЕ ВТ_СтатусВыполнен.Период ЕСТЬ NULL
	|			ТОГДА ВТ_СтатусВыполнен.Период
	|		ИНАЧЕ &ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания,
	|	втДанныеБригад.ГрафикРаботы КАК ГрафикРаботы
	|ПОМЕСТИТЬ ДатыСтатусаВРаботеВРамкахПериода
	|ИЗ
	|	ВТ_СтатусВРаботе КАК ВТ_СтатусВРаботе
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусВыполнен КАК ВТ_СтатусВыполнен
	|		ПО ВТ_СтатусВРаботе.ИДРемонта = ВТ_СтатусВыполнен.ИДРемонта
	|			И ВТ_СтатусВРаботе.Бригада = ВТ_СтатусВыполнен.Бригада
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусПриостановлен КАК ВТ_СтатусПриостановлен
	|		ПО ВТ_СтатусВРаботе.ИДРемонта = ВТ_СтатусПриостановлен.ИДРемонта
	|			И ВТ_СтатусВРаботе.Бригада = ВТ_СтатусПриостановлен.Бригада
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеБригад КАК втДанныеБригад
	|		ПО ВТ_СтатусВРаботе.Бригада = втДанныеБригад.Бригада
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ОбщиеДанныеПоРемонтам.IDРемонта КАК ID,
	|	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ОбщиеДанныеПоРемонтам.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВидыИОбъектыРемонтов
	|ИЗ
	|	РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	|ГДЕ
	|	торо_ОбщиеДанныеПоРемонтам.IDРемонта В
	|			(ВЫБРАТЬ
	|				ДатыСтатусаВРаботеВРамкахПериода.ИДРемонта
	|			ИЗ
	|				ДатыСтатусаВРаботеВРамкахПериода КАК ДатыСтатусаВРаботеВРамкахПериода
	|		
	|			ОБЪЕДИНИТЬ
	|		
	|			ВЫБРАТЬ
	|				ДатыСтатусаВПутиВРамкахПериода.ИДРемонта
	|			ИЗ
	|				ДатыСтатусаВПутиВРамкахПериода КАК ДатыСтатусаВПутиВРамкахПериода)");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ПериодОтчета = ПолучитьПериодОтчета(НастройкиОсновнойСхемы);
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПериодОтчета.ДатаОкончания);

	Запрос.Выполнить();
	Возврат МенеджерВТ;
	
КонецФункции

Функция ПолучитьГрафикиДляРасчета(МенеджерВТ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втДанныеБригад.ГрафикРаботы
	|ИЗ
	|	втДанныеБригад КАК втДанныеБригад");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГрафикРаботы");
	
КонецФункции

Функция ПолучитьПериодОтчета(НастройкиОсновнойСхемы)
	
	ПараметрПериод = НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПериодОтчета").Значение;
	ПериодОтчета = Новый Структура("ДатаНачала, ДатаОкончания", ПараметрПериод.ДатаНачала, ПараметрПериод.ДатаОкончания);
	Возврат ПериодОтчета;
	
КонецФункции

#КонецОбласти

#КонецЕсли