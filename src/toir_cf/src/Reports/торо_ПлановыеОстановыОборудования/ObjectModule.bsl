#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "ДатаОкончания");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	Данные.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	Данные.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
		|	Данные.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
		|	Данные.ТехКарта КАК ТехКарта,
		|	Данные.НормативныйРемонт КАК НормативныйРемонт,
		|	Данные.ВидПростоя КАК ВидПростоя,
		|	Данные.ВремяПростоя КАК ВремяПростоя,
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ
		|	Данные КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО Данные.ОбъектРемонтныхРабот = торо_ОбъектыРемонта.Ссылка";
		
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
		|	Данные.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	Данные.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
		|	Данные.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
		|	Данные.ТехКарта КАК ТехКарта,
		|	Данные.НормативныйРемонт КАК НормативныйРемонт,
		|	Данные.ВидПростоя КАК ВидПростоя,
		|	Данные.ВремяПростоя КАК ВремяПростоя,
		|	торо_ОбъектыРемонта.Ссылка КАК ОбъектИерархии
		|ИЗ
		|	Данные КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО Данные.ОбъектРемонтныхРабот = торо_ОбъектыРемонта.Ссылка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_АктуальныеПлановыеДатыРемонтов.IDРемонта КАК ID,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачалаРемонтныхРабот,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания КАК ДатаОкончанияРемонтныхРабот,
	|	торо_АктуальныеПлановыеДатыРемонтов.ОбъектРемонта КАК ОбъектРемонтныхРабот
	|ПОМЕСТИТЬ ВТ_РемонтыПоОтбору
	|ИЗ
	|	РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|ГДЕ
	|	торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки ССЫЛКА Документ.торо_ПланГрафикРемонта
	|{ГДЕ
	|	(торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИЛИ торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания МЕЖДУ &ДатаНачала И &ДатаОкончания)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ID КАК ID,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	торо_ПлановыеРемонтныеРаботыСрезПоследних.ВидРемонтныхРабот КАК ВидРемонтныхРабот
	|ПОМЕСТИТЬ ВТ_ВидыРемонта
	|ИЗ
	|	РегистрСведений.торо_ПлановыеРемонтныеРаботы.СрезПоследних(
	|			,
	|			ID В
	|				(ВЫБРАТЬ
	|					ВТ_РемонтыПоОтбору.ID КАК ID
	|				ИЗ
	|					ВТ_РемонтыПоОтбору КАК ВТ_РемонтыПоОтбору)) КАК торо_ПлановыеРемонтныеРаботыСрезПоследних
	|ГДЕ
	|	НЕ торо_ПлановыеРемонтныеРаботыСрезПоследних.Отменен
	|	И НЕ торо_ПлановыеРемонтныеРаботыСрезПоследних.Замещен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_РемонтыПоОтбору.ID КАК ID,
	|	ВТ_РемонтыПоОтбору.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	|	ВТ_РемонтыПоОтбору.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
	|	ВТ_РемонтыПоОтбору.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ВТ_ВидыРемонта.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	торо_НормативныеРемонтыОборудования.НормативныйРемонт КАК НормативныйРемонт
	|ПОМЕСТИТЬ ВТ_Нормативы
	|ИЗ
	|	ВТ_РемонтыПоОтбору КАК ВТ_РемонтыПоОтбору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВидыРемонта КАК ВТ_ВидыРемонта
	|		ПО ВТ_РемонтыПоОтбору.ID = ВТ_ВидыРемонта.ID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	|		ПО (ВТ_ВидыРемонта.ОбъектРемонтныхРабот = торо_НормативныеРемонтыОборудования.ОбъектРемонта)
	|			И (ВТ_ВидыРемонта.ВидРемонтныхРабот = торо_НормативныеРемонтыОборудования.ВидРемонта)
	|			И (торо_НормативныеРемонтыОборудования.Приоритет)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НормативныйРемонт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ВерсииТехКартСрезПоследних.ИдентификаторТехКарты КАК ИдентификаторТехКарты,
	|	торо_ВерсииТехКартСрезПоследних.ТехКарта КАК ТехКарта
	|ПОМЕСТИТЬ ВТ_ВерсииТехКарт
	|ИЗ
	|	РегистрСведений.торо_ВерсииТехКарт.СрезПоследних(
	|			,
	|			ИдентификаторТехКарты В
	|				(ВЫБРАТЬ
	|					ВТ_Нормативы.НормативныйРемонт КАК НормативныйРемонт
	|				ИЗ
	|					ВТ_Нормативы КАК ВТ_Нормативы)) КАК торо_ВерсииТехКартСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторТехКарты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Нормативы.ID КАК ID,
	|	ВТ_Нормативы.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	|	ВТ_Нормативы.ДатаОкончанияРемонтныхРабот КАК ДатаОкончанияРемонтныхРабот,
	|	ВТ_Нормативы.ОбъектРемонтныхРабот КАК ОбъектРемонтныхРабот,
	|	ВТ_Нормативы.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ВТ_Нормативы.НормативныйРемонт КАК НормативныйРемонт,
	|	ВТ_ВерсииТехКарт.ТехКарта КАК ТехКарта,
	|	ВТ_ВерсииТехКарт.ТехКарта.ВидПростоя КАК ВидПростоя,
	|	ВТ_ВерсииТехКарт.ТехКарта.ВремяПростоя КАК ВремяПростоя
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	ВТ_Нормативы КАК ВТ_Нормативы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВерсииТехКарт КАК ВТ_ВерсииТехКарт
	|		ПО ВТ_Нормативы.НормативныйРемонт = ВТ_ВерсииТехКарт.ИдентификаторТехКарты
	|ГДЕ
	|	ВТ_ВерсииТехКарт.ТехКарта.ВыполнениеРемонтаТребуетОстановаОборудования
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонтныхРабот"
	
	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#КонецЕсли
