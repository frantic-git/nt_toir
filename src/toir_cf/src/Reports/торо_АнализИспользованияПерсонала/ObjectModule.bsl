#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ТабДанныхДляСКД = ПолучитьДанныеДляСКД(НастройкиОсновнойСхемы);
	НастроитьДополнениеПоПериоду(НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);
	
	ВнешниеНаборы = Новый Структура("ВнешнийНабор", ТабДанныхДляСКД);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

	// Никаких настроек не требуется.
	
КонецПроцедуры

Функция ПолучитьДанныеДляСКД(НастройкиОсновнойСхемы)
	
	ПараметрыОтчетаСтруктура = ПолучитьПараметрыОтчета(НастройкиОсновнойСхемы);
	
	МенеджерВТ = ПолучитьОсновныеДанныеДляОтчета(НастройкиОсновнойСхемы, ПараметрыОтчетаСтруктура);
	ПериодОтчета = ПолучитьПериодОтчетаРеальный(МенеджерВТ);
	Графики = ПолучитьГрафикиДляРасчета(МенеджерВТ);
	ГрафикиРаботы.СоздатьВТРасписанияРаботыНаПериод(МенеджерВТ, Графики, ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания);
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВЫБОР &Периодичность
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, МЕСЯЦ)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ГОД)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, КВАРТАЛ)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, НЕДЕЛЯ)
	                      |	КОНЕЦ КАК ДатаГрафика,
	                      |	СУММА(РАЗНОСТЬДАТ(ВТРасписанияРаботы.ВремяНачала, ВТРасписанияРаботы.ВремяОкончания, СЕКУНДА)) КАК ВремяВСекундах,
	                      |	ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.Сотрудник КАК Сотрудник
	                      |ПОМЕСТИТЬ ПлановоеВремяРаботыСотрудников
	                      |ИЗ
	                      |	ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета КАК ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК ВТРасписанияРаботы
	                      |		ПО ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.ГрафикРаботы = ВТРасписанияРаботы.ГрафикРаботы
	                      |			И (ВТРасписанияРаботы.ДатаГрафика МЕЖДУ ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.ДатаНачала И ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.ДатаОкончания)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВЫБОР &Периодичность
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ДЕНЬ)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, МЕСЯЦ)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, ГОД)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, КВАРТАЛ)
	                      |		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	                      |			ТОГДА НАЧАЛОПЕРИОДА(ВТРасписанияРаботы.ДатаГрафика, НЕДЕЛЯ)
	                      |	КОНЕЦ,
	                      |	ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета.Сотрудник
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ВидыИОбъектыРемонтаПлан.ОбъектРемонта КАК ОбъектРемонта,
	                      |	ВидыИОбъектыРемонтаПлан.ВидРемонта КАК ВидРемонта
	                      |ПОМЕСТИТЬ ВТ_ОбъектыИВидыРемонтаПлан
	                      |ИЗ
	                      |	ВидыИОбъектыРемонтаПлан КАК ВидыИОбъектыРемонтаПлан
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ОбъектРемонта КАК ОбъектРемонта,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ВидРемонта КАК ВидРемонта,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.Сотрудник КАК Сотрудник
	                      |ПОМЕСТИТЬ ВТ_СотрудникиСРемонтами
	                      |ИЗ
	                      |	ФактЗатратыВРазрезеОбъектовИВидов КАК ФактЗатратыВРазрезеОбъектовИВидов
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	ОбъектРемонта,
	                      |	ВидРемонта,
	                      |	Сотрудник
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ПлановоеВремяРаботыСотрудников.ДатаГрафика КАК ДатаГрафика,
	                      |	ПлановоеВремяРаботыСотрудников.ВремяВСекундах КАК ВремяРаботыПлан,
	                      |	ПлановоеВремяРаботыСотрудников.Сотрудник КАК Сотрудник,
	                      |	ВТ_ОбъектыИВидыРемонтаПлан.ОбъектРемонта КАК ОбъектРемонта,
	                      |	ВТ_ОбъектыИВидыРемонтаПлан.ВидРемонта КАК ВидРемонта
	                      |ПОМЕСТИТЬ ВТ_ПлановоеВремя
	                      |ИЗ
	                      |	ПлановоеВремяРаботыСотрудников КАК ПлановоеВремяРаботыСотрудников,
	                      |	ВТ_ОбъектыИВидыРемонтаПлан КАК ВТ_ОбъектыИВидыРемонтаПлан
	                      |ГДЕ
	                      |	ПлановоеВремяРаботыСотрудников.Сотрудник В
	                      |			(ВЫБРАТЬ
	                      |				ВТ_СотрудникиСРемонтами.Сотрудник КАК Сотрудник
	                      |			ИЗ
	                      |				ВТ_СотрудникиСРемонтами КАК ВТ_СотрудникиСРемонтами)
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	ОбъектРемонта,
	                      |	ВидРемонта,
	                      |	ДатаГрафика
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ОбъектРемонта КАК ОбъектРемонта,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ВидРемонта КАК ВидРемонта,
	                      |	СУММА(ФактЗатратыВРазрезеОбъектовИВидов.ВремяРаботы) КАК ВремяРаботыФакт,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.Сотрудник КАК Сотрудник,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.Период КАК Период,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ДокументФакт КАК ДокументФакт
	                      |ПОМЕСТИТЬ ВТ_ФактСвернуто
	                      |ИЗ
	                      |	ФактЗатратыВРазрезеОбъектовИВидов КАК ФактЗатратыВРазрезеОбъектовИВидов
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ОбъектРемонта,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ВидРемонта,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.Сотрудник,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.Период,
	                      |	ФактЗатратыВРазрезеОбъектовИВидов.ДокументФакт
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	ОбъектРемонта,
	                      |	ВидРемонта,
	                      |	Период
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЕСТЬNULL(ВТ_ПлановоеВремя.Сотрудник, ВТ_ФактСвернуто.Сотрудник) КАК Сотрудник,
	                      |	ЕСТЬNULL(ВТ_ПлановоеВремя.ДатаГрафика, ВТ_ФактСвернуто.Период) КАК Период,
	                      |	МАКСИМУМ(ВТ_ПлановоеВремя.ВремяРаботыПлан) КАК ВремяРаботыПлан,
	                      |	ЕСТЬNULL(ВТ_ФактСвернуто.ОбъектРемонта, ВТ_ПлановоеВремя.ОбъектРемонта) КАК ОбъектРемонта,
	                      |	ЕСТЬNULL(ВТ_ФактСвернуто.ВидРемонта, ВТ_ПлановоеВремя.ВидРемонта) КАК ВидРемонта,
	                      |	МАКСИМУМ(ВТ_ФактСвернуто.ВремяРаботыФакт) КАК ВремяРаботыФакт,
	                      |	ВТ_ФактСвернуто.ДокументФакт КАК ДокументФакт
	                      |ПОМЕСТИТЬ ВТ_Итоговая
	                      |ИЗ
	                      |	ВТ_ПлановоеВремя КАК ВТ_ПлановоеВремя
	                      |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ФактСвернуто КАК ВТ_ФактСвернуто
	                      |		ПО ВТ_ПлановоеВремя.Сотрудник = ВТ_ФактСвернуто.Сотрудник
	                      |			И ВТ_ПлановоеВремя.ОбъектРемонта = ВТ_ФактСвернуто.ОбъектРемонта
	                      |			И ВТ_ПлановоеВремя.ДатаГрафика = ВТ_ФактСвернуто.Период
	                      |			И ВТ_ПлановоеВремя.ВидРемонта = ВТ_ФактСвернуто.ВидРемонта
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ЕСТЬNULL(ВТ_ПлановоеВремя.Сотрудник, ВТ_ФактСвернуто.Сотрудник),
	                      |	ЕСТЬNULL(ВТ_ПлановоеВремя.ДатаГрафика, ВТ_ФактСвернуто.Период),
	                      |	ЕСТЬNULL(ВТ_ФактСвернуто.ОбъектРемонта, ВТ_ПлановоеВремя.ОбъектРемонта),
	                      |	ЕСТЬNULL(ВТ_ФактСвернуто.ВидРемонта, ВТ_ПлановоеВремя.ВидРемонта),
	                      |	ВТ_ФактСвернуто.ДокументФакт
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	ОбъектРемонта,
	                      |	ВидРемонта,
	                      |	Сотрудник
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Итоговая.Сотрудник КАК Сотрудник,
	                      |	ВТ_Итоговая.Период КАК Период,
	                      |	ВТ_Итоговая.ВремяРаботыПлан КАК ВремяРаботыПлан,
	                      |	ВТ_Итоговая.ОбъектРемонта КАК ОбъектРемонта,
	                      |	ВТ_Итоговая.ВидРемонта КАК ВидРемонта,
	                      |	ВТ_Итоговая.ВремяРаботыФакт КАК ВремяРаботыФакт,
	                      |	ВТ_Итоговая.ДокументФакт КАК ДокументФакт,
	                      |	МАКСИМУМ(КадроваяИстория.Период) КАК ПериодКадровойИстории
	                      |ПОМЕСТИТЬ ВТ_Итоги
	                      |ИЗ
	                      |	ВТ_Итоговая КАК ВТ_Итоговая
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СотрудникиСРемонтами КАК ВТ_СотрудникиСРемонтами
	                      |		ПО ВТ_Итоговая.Сотрудник = ВТ_СотрудникиСРемонтами.Сотрудник
	                      |			И ВТ_Итоговая.ОбъектРемонта = ВТ_СотрудникиСРемонтами.ОбъектРемонта
	                      |			И ВТ_Итоговая.ВидРемонта = ВТ_СотрудникиСРемонтами.ВидРемонта
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КадроваяИстория КАК КадроваяИстория
	                      |		ПО ВТ_Итоговая.Сотрудник = КадроваяИстория.Сотрудник
	                      |			И ВТ_Итоговая.Период >= КадроваяИстория.Период
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_Итоговая.Сотрудник,
	                      |	ВТ_Итоговая.Период,
	                      |	ВТ_Итоговая.ВремяРаботыПлан,
	                      |	ВТ_Итоговая.ОбъектРемонта,
	                      |	ВТ_Итоговая.ВидРемонта,
	                      |	ВТ_Итоговая.ВремяРаботыФакт,
	                      |	ВТ_Итоговая.ДокументФакт
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Итоги.Сотрудник КАК Сотрудник,
	                      |	ВТ_Итоги.Период КАК Период,
	                      |	ВТ_Итоги.ВремяРаботыПлан КАК ВремяРаботыПлан,
	                      |	ВТ_Итоги.ОбъектРемонта КАК ОбъектРемонта,
	                      |	ВТ_Итоги.ВидРемонта КАК ВидРемонта,
	                      |	ВТ_Итоги.ВремяРаботыФакт КАК ВремяРаботыФакт,
	                      |	ВТ_Итоги.ДокументФакт КАК ДокументФакт,
	                      |	КадроваяИстория.Организация КАК Организация,
	                      |	КадроваяИстория.Подразделение КАК Подразделение
	                      |ИЗ
	                      |	ВТ_Итоги КАК ВТ_Итоги
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КадроваяИстория КАК КадроваяИстория
	                      |		ПО ВТ_Итоги.Сотрудник = КадроваяИстория.Сотрудник
	                      |			И ВТ_Итоги.ПериодКадровойИстории = КадроваяИстория.Период");

	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("Периодичность", ПараметрыОтчетаСтруктура.Периодичность);
	
	ГрафикиРаботыРасч = Запрос.Выполнить().Выгрузить();
	
	МенеджерВТ.Закрыть();
	Возврат ГрафикиРаботыРасч;
	
КонецФункции

Функция ПолучитьОсновныеДанныеДляОтчета(НастройкиОсновнойСхемы, ПараметрыОтчетаСтруктура)
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Квалификация КАК Квалификация,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Сотрудник КАК Сотрудник,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.ВремяРаботы КАК ВремяРаботы,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Период КАК Период,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Регистратор КАК ДокументФакт
	|ПОМЕСТИТЬ ФактЗатраты
	|ИЗ
	|	РегистрНакопления.торо_ФактическиеТрудовыеЗатратыРемонтныхРабот КАК торо_ФактическиеТрудовыеЗатратыРемонтныхРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|			ПО торо_АктуальныеПлановыеДатыРемонтов.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	|		ПО торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.РемонтыОборудования_ID = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|			И (ВЫБОР
	|				КОГДА &ПараметрПериодИспользуется
	|					ТОГДА торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|			И (&ПоППР
	|					И торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки ССЫЛКА Документ.торо_ПланГрафикРемонта
	|				ИЛИ &ПоПредписаниям
	|					И торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки ССЫЛКА Документ.торо_ВнешнееОснованиеДляРабот
	|				ИЛИ &ПоВыявленнымДефектам
	|					И торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки ССЫЛКА Документ.торо_ВыявленныеДефекты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.РемонтыОборудования_ID,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Квалификация,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Сотрудник,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.ВремяРаботы,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Период,
	|	торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Регистратор
	|ИЗ
	|	РегистрНакопления.торо_ФактическиеТрудовыеЗатратыРемонтныхРабот КАК торо_ФактическиеТрудовыеЗатратыРемонтныхРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.РемонтыОборудования_ID = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|			И (ВЫБОР
	|				КОГДА &ПараметрПериодИспользуется
	|					ТОГДА торо_ФактическиеТрудовыеЗатратыРемонтныхРабот.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|			И (&ПоРегламентнымМероприятиям)
	|			И (торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки ССЫЛКА Документ.торо_ГрафикРегламентныхМероприятийТОиР)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РемонтыОборудования_ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(ВЫБОР
	|				КОГДА &ПараметрПериодИспользуется
	|					ТОГДА &ДатаНачала
	|				ИНАЧЕ ФактЗатраты.Период
	|			КОНЕЦ), &ДатаНачала) КАК ДатаНачала,
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|				КОГДА &ПараметрПериодИспользуется
	|					ТОГДА &ДатаОкончания
	|				ИНАЧЕ ФактЗатраты.Период
	|			КОНЕЦ), &ДатаОкончания) КАК ДатаОкончания
	|ПОМЕСТИТЬ ПериодОтчета
	|ИЗ
	|	ФактЗатраты КАК ФактЗатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА КадроваяИсторияСотрудников.Регистратор ССЫЛКА Документ.Увольнение
	|					И КадроваяИсторияСотрудников.Период <= ПериодОтчета.ДатаОкончания
	|				ТОГДА КадроваяИсторияСотрудников.Период
	|			ИНАЧЕ ПериодОтчета.ДатаОкончания
	|		КОНЕЦ) КАК ДатаОкончанияПериодаОтчетаИлиУвольнения,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА КадроваяИсторияСотрудников.Период <= ПериодОтчета.ДатаНачала
	|				ТОГДА КадроваяИсторияСотрудников.Период
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ) КАК ДатаПервогоДокументаГрафикаРаботы
	|ПОМЕСТИТЬ ДатыДляПолученияГрафиков
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников,
	|	ПериодОтчета КАК ПериодОтчета
	|
	|СГРУППИРОВАТЬ ПО
	|	КадроваяИсторияСотрудников.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГрафикРаботыСотрудников.Сотрудник КАК Сотрудник,
	|	ГрафикРаботыСотрудников.ГрафикРаботы КАК ГрафикРаботы,
	|	ГрафикРаботыСотрудников.Период КАК Период
	|ПОМЕСТИТЬ ГрафикиПоСотрудникам
	|ИЗ
	|	РегистрСведений.ГрафикРаботыСотрудников КАК ГрафикРаботыСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыДляПолученияГрафиков КАК ДатыДляПолученияГрафиков
	|			ПОЛНОЕ СОЕДИНЕНИЕ ПериодОтчета КАК ПериодОтчета
	|			ПО (ИСТИНА)
	|		ПО ГрафикРаботыСотрудников.Сотрудник = ДатыДляПолученияГрафиков.Сотрудник
	|			И (ГрафикРаботыСотрудников.Период МЕЖДУ НАЧАЛОПЕРИОДА(ВЫБОР
	|					КОГДА ДатыДляПолученияГрафиков.ДатаПервогоДокументаГрафикаРаботы = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ПериодОтчета.ДатаНачала
	|					ИНАЧЕ ДатыДляПолученияГрафиков.ДатаПервогоДокументаГрафикаРаботы
	|				КОНЕЦ, ДЕНЬ) И ДатыДляПолученияГрафиков.ДатаОкончанияПериодаОтчетаИлиУвольнения)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикиПоСотрудникам.Сотрудник КАК Сотрудник,
	|	ГрафикиПоСотрудникам.ГрафикРаботы КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ГрафикиПоСотрудникам.Период < ПериодОтчета.ДатаНачала
	|			ТОГДА ПериодОтчета.ДатаНачала
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ГрафикиПоСотрудникам.Период, ДЕНЬ)
	|	КОНЕЦ КАК ДатаНачала,
	|	МАКСИМУМ(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ЕСТЬNULL(ГрафикиПоСотрудникам1.Период, ДатыДляПолученияГрафиков.ДатаОкончанияПериодаОтчетаИлиУвольнения), ДЕНЬ), СЕКУНДА, 1)) КАК ДатаОкончания
	|ПОМЕСТИТЬ ДатыДействияГрафиковПоСотрудникамВРамкахПериодаОтчета
	|ИЗ
	|	ГрафикиПоСотрудникам КАК ГрафикиПоСотрудникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикиПоСотрудникам КАК ГрафикиПоСотрудникам1
	|		ПО ГрафикиПоСотрудникам.Сотрудник = ГрафикиПоСотрудникам1.Сотрудник
	|			И ГрафикиПоСотрудникам.Период < ГрафикиПоСотрудникам1.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыДляПолученияГрафиков КАК ДатыДляПолученияГрафиков
	|		ПО ГрафикиПоСотрудникам.Сотрудник = ДатыДляПолученияГрафиков.Сотрудник,
	|	ПериодОтчета КАК ПериодОтчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикиПоСотрудникам.Сотрудник,
	|	ГрафикиПоСотрудникам.ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ГрафикиПоСотрудникам.Период < ПериодОтчета.ДатаНачала
	|			ТОГДА ПериодОтчета.ДатаНачала
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ГрафикиПоСотрудникам.Период, ДЕНЬ)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ОбщиеДанныеПоРемонтам.IDРемонта КАК ID,
	|	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ОбщиеДанныеПоРемонтам.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВидыИОбъектыРемонтов
	|ИЗ
	|	РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФактЗатраты КАК ФактЗатраты
	|		ПО торо_ОбщиеДанныеПоРемонтам.IDРемонта = ФактЗатраты.РемонтыОборудования_ID
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВидыИОбъектыРемонтов.ID КАК ID,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВидыИОбъектыРемонтаПлан
	|ИЗ
	|	ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|ГДЕ
	|	ВидыИОбъектыРемонтов.ОбъектРемонта ССЫЛКА Справочник.торо_ОбъектыРемонта
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВидыИОбъектыРемонтов.ID,
	|	торо_ЗапланированныеМероприятияОбъектов.ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта
	|ИЗ
	|	ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ЗапланированныеМероприятияОбъектов КАК торо_ЗапланированныеМероприятияОбъектов
	|		ПО ВидыИОбъектыРемонтов.ID = торо_ЗапланированныеМероприятияОбъектов.ID
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВидыИОбъектыРемонтов.ID КАК ID,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВидыИОбъектыРемонтаФакт
	|ИЗ
	|	ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|ГДЕ
	|	ВидыИОбъектыРемонтов.ОбъектРемонта ССЫЛКА Справочник.торо_ОбъектыРемонта
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВидыИОбъектыРемонтов.ID,
	|	торо_ВыполненныеМероприятияОбъектов.ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта
	|ИЗ
	|	ВидыИОбъектыРемонтов КАК ВидыИОбъектыРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВыполненныеМероприятияОбъектов КАК торо_ВыполненныеМероприятияОбъектов
	|		ПО ВидыИОбъектыРемонтов.ID = торо_ВыполненныеМероприятияОбъектов.ID
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, НЕДЕЛЯ)
	|	КОНЕЦ КАК Период,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	|	ВидыИОбъектыРемонтов.ВидРемонта КАК ВидРемонта,
	|	ФактЗатраты.Сотрудник КАК Сотрудник,
	|	СУММА(ФактЗатраты.ВремяРаботы) КАК ВремяРаботы,
	|	ФактЗатраты.ДокументФакт КАК ДокументФакт
	|ПОМЕСТИТЬ ФактЗатратыВРазрезеОбъектовИВидов
	|ИЗ
	|	ФактЗатраты КАК ФактЗатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыИОбъектыРемонтаФакт КАК ВидыИОбъектыРемонтов
	|		ПО ФактЗатраты.РемонтыОборудования_ID = ВидыИОбъектыРемонтов.ID
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактЗатраты.Сотрудник,
	|	ВидыИОбъектыРемонтов.ВидРемонта,
	|	ВидыИОбъектыРемонтов.ОбъектРемонта,
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ФактЗатраты.Период, НЕДЕЛЯ)
	|	КОНЕЦ,
	|	ФактЗатраты.ДокументФакт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадроваяИсторияСотрудников.Организация КАК Организация,
	|	КадроваяИсторияСотрудников.Подразделение КАК Подразделение,
	|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(&ДатаНачала, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(&ДатаНачала, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(&ДатаНачала, НЕДЕЛЯ)
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ КадроваяИстория
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&ДатаНачала, &ПараметрПериодИспользуется) КАК КадроваяИсторияСотрудников
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КадроваяИсторияСотрудников.Организация,
	|	КадроваяИсторияСотрудников.Подразделение,
	|	КадроваяИсторияСотрудников.Сотрудник,
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА НАЧАЛОПЕРИОДА(КадроваяИсторияСотрудников.Период, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(КадроваяИсторияСотрудников.Период, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(КадроваяИсторияСотрудников.Период, ГОД)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(КадроваяИсторияСотрудников.Период, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(КадроваяИсторияСотрудников.Период, НЕДЕЛЯ)
	|	КОНЕЦ
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ПараметрПериодИспользуется
	|				ТОГДА КадроваяИсторияСотрудников.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	
	КоллекцияПараметров = НастройкиОсновнойСхемы.ПараметрыДанных.Элементы;
	ПараметрПериод = КоллекцияПараметров.Найти("ПериодОтчета");
	ИспользованиеПериода = ПараметрПериод.Использование;
	
	Запрос.УстановитьПараметр("ПараметрПериодИспользуется", ИспользованиеПериода);
	Запрос.УстановитьПараметр("ДатаНачала", ?(ИспользованиеПериода, ПараметрПериод.Значение.ДатаНачала, Дата(1,1,1)));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(ИспользованиеПериода, ПараметрПериод.Значение.ДатаОкончания, Дата(1,1,1)));
	Запрос.УстановитьПараметр("Периодичность",ПараметрыОтчетаСтруктура.Периодичность);
	Запрос.УстановитьПараметр("ПоРегламентнымМероприятиям",ПараметрыОтчетаСтруктура.ПоРегламентнымМероприятиям);
	Запрос.УстановитьПараметр("ПоППР",ПараметрыОтчетаСтруктура.ПоППР);
	Запрос.УстановитьПараметр("ПоПредписаниям",ПараметрыОтчетаСтруктура.ПоПредписаниям);
	Запрос.УстановитьПараметр("ПоВыявленнымДефектам",ПараметрыОтчетаСтруктура.ПоВыявленнымДефектам);
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ПараметрыОтчетаСтруктура);
		
	Запрос.Выполнить();
	
	Возврат МенеджерВТ;
	
КонецФункции

Функция ПолучитьПериодОтчетаРеальный(МенеджерВТ)
	
	СтруктураВозврата = Новый Структура("ДатаНачала, ДатаОкончания");
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПериодОтчета.ДатаНачала,
	|	ПериодОтчета.ДатаОкончания
	|ИЗ
	|	ПериодОтчета КАК ПериодОтчета");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	Иначе
		СтруктураВозврата.ДатаНачала = Дата(1, 1, 1);
		СтруктураВозврата.ДатаОкончания = Дата(1, 1, 1);
	КонецЕсли;
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПолучитьГрафикиДляРасчета(МенеджерВТ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГрафикиПоСотрудникам.ГрафикРаботы
	|ИЗ
	|	ГрафикиПоСотрудникам КАК ГрафикиПоСотрудникам");
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГрафикРаботы");
	
КонецФункции

Функция ПолучитьПараметрыОтчета(НастройкиОсновнойСхемы)
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Периодичность", НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("Периодичность").Значение);
	ПараметрыОтчета.Вставить("ПоРегламентнымМероприятиям",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоРегламентнымМероприятиям").Значение);
	ПараметрыОтчета.Вставить("ПоППР",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоППР").Значение);
	ПараметрыОтчета.Вставить("ПоПредписаниям",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоПредписаниям").Значение);
	ПараметрыОтчета.Вставить("ПоВыявленнымДефектам",НастройкиОсновнойСхемы.ПараметрыДанных.Элементы.Найти("ПоВыявленнымДефектам").Значение);
	Возврат ПараметрыОтчета;
	
КонецФункции

Процедура НастроитьДополнениеПоПериоду(НастройкиСхемы)
	
	ПолеГруппировки = ПолучитьПолеГруппировкиКомпоновкиДанныхДляНастройкиДополнения(НастройкиСхемы, "Период");
	ПараметрПериодичностьОтчета = НастройкиСхемы.ПараметрыДанных.Элементы.Найти("Периодичность");
	ТипДополненияПериодаПеречисление = ПараметрПериодичностьОтчета.Значение;
	Если ПолеГруппировки <> Неопределено И ПараметрПериодичностьОтчета <> Неопределено Тогда
		ТипДополненияПериодаПеречисление = ПараметрПериодичностьОтчета.Значение;
		УстановитьТипДополненияПериодаДляПоляГруппировки(ПолеГруппировки, НастройкиСхемы,ТипДополненияПериодаПеречисление);
		УстановитьДатыНачалаИОкончанияДополнения(ПолеГруппировки, НастройкиСхемы,ТипДополненияПериодаПеречисление);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПолеГруппировкиКомпоновкиДанныхДляНастройкиДополнения(НастройкиСхемы, ИмяГруппировки)
	
	Для Каждого ЭлементНастройки Из НастройкиСхемы.Структура Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Для Каждого Колонка Из ЭлементНастройки.Колонки Цикл
				Если Колонка.Имя = ИмяГруппировки Тогда
					Возврат Колонка.ПоляГруппировки.Элементы[0];
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Процедура УстановитьТипДополненияПериодаДляПоляГруппировки(ПолеГруппировкиКомпоновкиДанных, НастройкиСхемы, ТипДополненияПериодаПеречисление)

	Если ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Месяц Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Месяц;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.День Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.День;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Год Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Год;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Квартал Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Квартал;
	ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Неделя Тогда
		ПолеГруппировкиКомпоновкиДанных.ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Неделя;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДатыНачалаИОкончанияДополнения(ПолеГруппировкиКомпоновкиДанных, НастройкиСхемы,ТипДополненияПериодаПеречисление)

	ПараметрПериодОтчета = НастройкиСхемы.ПараметрыДанных.Элементы.Найти("ПериодОтчета");
	
	Если ПараметрПериодОтчета <> Неопределено И ПараметрПериодОтчета.Использование Тогда
		
		Если ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Месяц Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоМесяца(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецМесяца(ПараметрПериодОтчета.Значение.ДатаОкончания);
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.День Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = ПараметрПериодОтчета.Значение.ДатаНачала;
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = ПараметрПериодОтчета.Значение.ДатаОкончания;
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Год Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоГода(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецГода(ПараметрПериодОтчета.Значение.ДатаОкончания);
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Квартал Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоКвартала(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецКвартала(ПараметрПериодОтчета.Значение.ДатаОкончания);
		ИначеЕсли ТипДополненияПериодаПеречисление = Перечисления.Периодичность.Неделя Тогда
			ПолеГруппировкиКомпоновкиДанных.НачалоПериода = НачалоНедели(ПараметрПериодОтчета.Значение.ДатаНачала);
			ПолеГруппировкиКомпоновкиДанных.КонецПериода = КонецНедели(ПараметрПериодОтчета.Значение.ДатаОкончания);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли