#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "ДатаОкончания");
	
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.РегистраторПредписания КАК РегистраторПредписания,
		|	Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	Данные.Описание КАК Описание,
		|	Данные.ПлановаяДатаРемонта КАК ПлановаяДатаРемонта,
		|	Данные.Обработано КАК Обработано,
		|	Данные.РегистраторОбработки КАК РегистраторОбработки,
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии,
		|	Данные.ГарантийныйРемонт КАК ГарантийныйРемонт,
		|	Данные.Исполнитель КАК Исполнитель
		|ИЗ
		|	Данные КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
	
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		ПолучитьСодержательнуюЧастьЗапроса()+
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Данные.РегистраторПредписания КАК РегистраторПредписания,
		|	Данные.ОбъектРемонта КАК ОбъектРемонта,
		|	Данные.Описание КАК Описание,
		|	Данные.ПлановаяДатаРемонта КАК ПлановаяДатаРемонта,
		|	Данные.Обработано КАК Обработано,
		|	Данные.РегистраторОбработки КАК РегистраторОбработки,
		|	торо_ОбъектыРемонта.Ссылка КАК ОбъектИерархии,
		|	Данные.ГарантийныйРемонт КАК ГарантийныйРемонт,
		|	Данные.Исполнитель КАК Исполнитель
		|ИЗ
		|	Данные КАК Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ВнешниеОснованияДляРаботСрезПервых.Регистратор КАК РегистраторПредписания,
	|	торо_ВнешниеОснованияДляРаботСрезПервых.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ВнешниеОснованияДляРаботСрезПервых.Описание КАК Описание,
	|	торо_ВнешниеОснованияДляРаботСрезПервых.ПлановаяДатаРемонта КАК ПлановаяДатаРемонта,
	|	ЕСТЬNULL(торо_ВнешниеОснованияДляРабот.Обработано, ЛОЖЬ) КАК Обработано,
	|	торо_ВнешниеОснованияДляРабот.Регистратор КАК РегистраторОбработки,
	|	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.ГарантийныйРемонт КАК ГарантийныйРемонт,
	|	торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.Исполнитель КАК Исполнитель
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	РегистрСведений.торо_ВнешниеОснованияДляРабот.СрезПервых({(&Дата)}, {(Период МЕЖДУ &ДатаНачала И &ДатаОкончания) КАК Поле2}) КАК торо_ВнешниеОснованияДляРаботСрезПервых
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВнешниеОснованияДляРабот КАК торо_ВнешниеОснованияДляРабот
	|		ПО торо_ВнешниеОснованияДляРаботСрезПервых.ID = торо_ВнешниеОснованияДляРабот.ID
	|			И (торо_ВнешниеОснованияДляРабот.Обработано)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ВнешнееОснованиеДляРабот.ОбследованноеОборудование КАК торо_ВнешнееОснованиеДляРаботОбследованноеОборудование
	|		ПО торо_ВнешниеОснованияДляРаботСрезПервых.ID = торо_ВнешнееОснованиеДляРаботОбследованноеОборудование.ID
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта"
		
	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

#КонецОбласти

#КонецЕсли
