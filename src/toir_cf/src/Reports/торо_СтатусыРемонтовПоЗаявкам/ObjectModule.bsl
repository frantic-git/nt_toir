#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекИерархия = торо_ОтчетыСервер.ПолучитьЗначениеСтруктурыИерархии(КомпоновщикНастроек);
	
	КлючВарианта = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.КлючВарианта;
	КлючПредопределенногоВарианта = ПолучитьНазваниеПредопределенногоВариантаНастроек(КлючВарианта);
	
	СодержательнаяЧастьЗапроса = "";
	Если КлючПредопределенногоВарианта = "ПоБригадам" ИЛИ КлючПредопределенногоВарианта = "ТаблицаПоБригадамСИерархией" Тогда
		СодержательнаяЧастьЗапроса = ПолучитьСодержательнуюЧастьЗапросаПоБригадам();
	Иначе
		СодержательнаяЧастьЗапроса = ПолучитьСодержательнуюЧастьЗапроса();
	КонецЕсли;
	
	торо_ОтчетыСервер.УстановитьЗапросыНаборовДанныхИерархииОР(СхемаКомпоновкиДанных, ТекИерархия, "КонецПериода");
		
	Если ТекИерархия.СтроитсяАвтоматически Тогда
				
		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос =
		СодержательнаяЧастьЗапроса +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Данные.ЗаявкаНаРемонт,
		|	ВТ_Данные.ИДРемонта,
		|	ВТ_Данные.Статус,
		|	ВТ_Данные.КомментарийКСтатусу,
		|	ВТ_Данные.Пользователь,
		|	ВТ_Данные.Исполнитель,
		|	ВТ_Данные.УточнениеИсполнителя,
		|	ВТ_Данные.ДатаИзмененияСтатуса,
		|	ВТ_Данные.ТекущийСтатус,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ВидРемонтныхРабот,
		|	ВТ_Данные.ДатаНачала,
		|	ВТ_Данные.ДатаОкончания,
		|	торо_ОбъектыРемонта." + ТекИерархия.РеквизитОР + " КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
		|";
		
		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.Иерархия);
		
	Иначе

		торо_ОтчетыКлиентСервер.УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, "ОбъектИерархии", ТипГруппировкиКомпоновкиДанных.ТолькоИерархия);

		СхемаКомпоновкиДанных.НаборыДанных.Объекты.Запрос = 
		СодержательнаяЧастьЗапроса +        
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_Данные.ЗаявкаНаРемонт,
		|	ВТ_Данные.ИДРемонта,
		|	ВТ_Данные.Статус,
		|	ВТ_Данные.КомментарийКСтатусу,
		|	ВТ_Данные.Пользователь,
		|	ВТ_Данные.Исполнитель,
		|	ВТ_Данные.УточнениеИсполнителя,
		|	ВТ_Данные.ДатаИзмененияСтатуса,
		|	ВТ_Данные.ТекущийСтатус,
		|	ВТ_Данные.ОбъектРемонта,
		|	ВТ_Данные.ВидРемонтныхРабот,
		|	ВТ_Данные.ДатаНачала,
		|	ВТ_Данные.ДатаОкончания,
		|	ВТ_Данные.ОбъектРемонта КАК ОбъектИерархии
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|		ПО ВТ_Данные.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ЗагрузитьНастройкиПриИзмененииПараметров = ЗагрузитьНастройкиПриИзмененииПараметров();

КонецПроцедуры

Функция ЗагрузитьНастройкиПриИзмененииПараметров()  
	
	Параметры = Новый Массив;
	Параметры.Добавить(Новый ПараметрКомпоновкиДанных("ИерархияТип"));	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапроса() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_ЗаявкиПоРемонтамСрезПоследних.IDРемонта КАК ИДРемонта,
	|	торо_ЗаявкиПоРемонтамСрезПоследних.ДокументЗаявка КАК ЗаявкаНаРемонт
	|ПОМЕСТИТЬ ВТ_ЗаявкиИРемонты
	|ИЗ
	|	РегистрСведений.торо_ЗаявкиПоРемонтам.СрезПоследних КАК торо_ЗаявкиПоРемонтамСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт КАК торо_ЗаявкаНаРемонт
	|		ПО торо_ЗаявкиПоРемонтамСрезПоследних.ДокументЗаявка = торо_ЗаявкаНаРемонт.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта,
	|	ЗаявкаНаРемонт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЗаявкиИРемонты.ИДРемонта КАК ИДРемонта,
	|	ВТ_ЗаявкиИРемонты.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт,
	|	торо_СтатусыРемонтов.Период КАК Период, 
	|	торо_СтатусыРемонтов.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_ИсторияСтатусов
	|ИЗ
	|	ВТ_ЗаявкиИРемонты КАК ВТ_ЗаявкиИРемонты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_СтатусыРемонтов КАК торо_СтатусыРемонтов
	|		ПО ВТ_ЗаявкиИРемонты.ИДРемонта = торо_СтатусыРемонтов.IDРемонта
	|{ГДЕ
	|	(торо_СтатусыРемонтов.Период >= &НачалоПериода),
	|	(торо_СтатусыРемонтов.Период <= &КонецПериода)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта,
	|	ЗаявкаНаРемонт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовСрезПоследних.IDРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовСрезПоследних.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_ТекущиеСтатусыРемонтов
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтов.СрезПоследних(
	|			&ПериодТекущихСтатусов {(&ПериодТекущихСтатусов)},
	|			IDРемонта В
	|				(ВЫБРАТЬ
	|					ВТ_ЗаявкиИРемонты.ИДРемонта КАК ИДРемонта
	|				ИЗ
	|					ВТ_ЗаявкиИРемонты КАК ВТ_ЗаявкиИРемонты)) КАК торо_СтатусыРемонтовСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ИсторияСтатусов.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт,
	|	ВТ_ИсторияСтатусов.ИДРемонта КАК ИДРемонта,
	|	ВТ_ИсторияСтатусов.Статус КАК Статус,
	|	"""" КАК КомментарийКСтатусу,
	|	НЕОПРЕДЕЛЕНО КАК Пользователь,
	|	НЕОПРЕДЕЛЕНО КАК Исполнитель,
	|	НЕОПРЕДЕЛЕНО КАК УточнениеИсполнителя,
	|	ВТ_ИсторияСтатусов.Период КАК ДатаИзмененияСтатуса,
	|	ВТ_ТекущиеСтатусыРемонтов.Статус КАК ТекущийСтатус,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачала,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	ВТ_ИсторияСтатусов КАК ВТ_ИсторияСтатусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущиеСтатусыРемонтов КАК ВТ_ТекущиеСтатусыРемонтов
	|		ПО ВТ_ИсторияСтатусов.ИДРемонта = ВТ_ТекущиеСтатусыРемонтов.ИДРемонта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|		ПО ВТ_ИсторияСтатусов.ИДРемонта = торо_ЗаявкаНаРемонтРемонтыОборудования.ID
	|			И (ВТ_ИсторияСтатусов.ЗаявкаНаРемонт = торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_ИсторияСтатусов.ИДРемонта = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыЗавершенные)
	|				ТОГДА &ПоказыватьЗавершенные = ИСТИНА
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыОтклоненные)
	|				ТОГДА &ПоказыватьОтклоненные = ИСТИНА
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыЗарегистрированные)
	|				ТОГДА &ПоказыватьЗарегистрированные = ИСТИНА
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыВРаботе)
	|				ТОГДА &ПоказыватьВРаботе = ИСТИНА
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта"

	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ПолучитьСодержательнуюЧастьЗапросаПоБригадам() 
	
	ЗапросТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Период КАК Период,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.ИДРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Статус КАК Статус,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.КомментарийКСтатусу КАК КомментарийКСтатусу,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Пользователь КАК Пользователь,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.Исполнитель КАК Исполнитель,
	|	торо_СтатусыРемонтовВЗаявкахПоБригадам.УточнениеИсполнителя КАК УточнениеИсполнителя
	|ПОМЕСТИТЬ ВТ_ИсторияСтатусов
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтовВЗаявкахПоБригадам КАК торо_СтатусыРемонтовВЗаявкахПоБригадам
	|{ГДЕ
	|	(торо_СтатусыРемонтовВЗаявкахПоБригадам.Период >= &НачалоПериода),
	|	(торо_СтатусыРемонтовВЗаявкахПоБригадам.Период <= &КонецПериода)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаявкаНаРемонт,
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыРемонтовСрезПоследних.IDРемонта КАК ИДРемонта,
	|	торо_СтатусыРемонтовСрезПоследних.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_ТекущиеСтатусыРемонтов
	|ИЗ
	|	РегистрСведений.торо_СтатусыРемонтов.СрезПоследних(
	|			&ПериодТекущихСтатусов {(&ПериодТекущихСтатусов)},
	|			(IDРемонта) В
	|				(ВЫБРАТЬ
	|					ВТ_ИсторияСтатусов.ИДРемонта КАК ИДРемонта
	|				ИЗ
	|					ВТ_ИсторияСтатусов КАК ВТ_ИсторияСтатусов)) КАК торо_СтатусыРемонтовСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИДРемонта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ИсторияСтатусов.ЗаявкаНаРемонт КАК ЗаявкаНаРемонт,
	|	ВТ_ИсторияСтатусов.ИДРемонта КАК ИДРемонта,
	|	ВТ_ИсторияСтатусов.Статус КАК Статус,
	|	ВТ_ИсторияСтатусов.КомментарийКСтатусу КАК КомментарийКСтатусу,
	|	ВТ_ИсторияСтатусов.Пользователь КАК Пользователь,
	|	ВТ_ИсторияСтатусов.Исполнитель КАК Исполнитель,
	|	ВТ_ИсторияСтатусов.УточнениеИсполнителя КАК УточнениеИсполнителя,
	|	ВТ_ИсторияСтатусов.Период КАК ДатаИзмененияСтатуса,
	|	ВТ_ТекущиеСтатусыРемонтов.Статус КАК ТекущийСтатус,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачала,
	|	торо_АктуальныеПлановыеДатыРемонтов.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	ВТ_ИсторияСтатусов КАК ВТ_ИсторияСтатусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущиеСтатусыРемонтов КАК ВТ_ТекущиеСтатусыРемонтов
	|		ПО ВТ_ИсторияСтатусов.ИДРемонта = ВТ_ТекущиеСтатусыРемонтов.ИДРемонта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|		ПО ВТ_ИсторияСтатусов.ИДРемонта = торо_ЗаявкаНаРемонтРемонтыОборудования.ID
	|			И (ВТ_ИсторияСтатусов.ЗаявкаНаРемонт = торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	|		ПО ВТ_ИсторияСтатусов.ИДРемонта = торо_АктуальныеПлановыеДатыРемонтов.IDРемонта
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыЗавершенные)
	|				ТОГДА &ПоказыватьЗавершенные = ИСТИНА
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыОтклоненные)
	|				ТОГДА &ПоказыватьОтклоненные = ИСТИНА
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыЗарегистрированные)
	|				ТОГДА &ПоказыватьЗарегистрированные = ИСТИНА
	|			КОГДА ВТ_ТекущиеСтатусыРемонтов.Статус В (&СтатусыВРаботе)
	|				ТОГДА &ПоказыватьВРаботе = ИСТИНА
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта"

	+ "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ПолучитьНазваниеПредопределенногоВариантаНастроек(КлючВарианта)
	
	МассивПредопределенныхВариантов = ПолучитьМассивПредопредленныхВариантовНастроекОтчета();
	Если МассивПредопределенныхВариантов.Найти(КлючВарианта) <> Неопределено Тогда
		Возврат КлючВарианта;
	КонецЕсли;
	
	КлючПредопределенногоВарианта = Неопределено;
	
	ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ЭтотОбъект.Метаданные());
	ВариантОтчета = ВариантыОтчетов.ВариантОтчета(ИдентификаторОтчета, КлючВарианта);
	
	Если ВариантОтчета <> Неопределено Тогда
		ТаблицаРодителейВарианта = торо_ОбщегоНазначения.ПолучитьТаблицуРодителейОбъекта(ВариантОтчета, Истина);
		Для каждого Строка из ТаблицаРодителейВарианта Цикл
			Если НЕ Строка.Родитель.Пользовательский Тогда
				КлючПредопределенногоВарианта = Строка.Родитель.КлючВарианта;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат КлючПредопределенногоВарианта;
	
КонецФункции

Функция ПолучитьМассивПредопредленныхВариантовНастроекОтчета()
	
	МассивПредопределенныхВариантов = Новый Массив;
	Для каждого Вариант из ЭтотОбъект.СхемаКомпоновкиДанных.ВариантыНастроек Цикл
   	МассивПредопределенныхВариантов.Добавить(Вариант.Имя);
	КонецЦикла;
	
	Возврат МассивПредопределенныхВариантов;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли