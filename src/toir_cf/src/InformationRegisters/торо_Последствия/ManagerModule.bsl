#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура выполняет заполнение регистра данными из макета "НачальноеЗаполнение"
Процедура ЗагрузитьИзКлассификатора() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ОбъектыВоздействия.Ссылка КАК Ссылка,
	               |	торо_ОбъектыВоздействия.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.торо_ОбъектыВоздействия КАК торо_ОбъектыВоздействия
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_КатегорииТяжестиПоследствий.Ссылка КАК Ссылка,
	               |	торо_КатегорииТяжестиПоследствий.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.торо_КатегорииТяжестиПоследствий КАК торо_КатегорииТяжестиПоследствий";
	
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой()
		ИЛИ РезультатЗапроса[1].Пустой() Тогда
		
		Если РезультатЗапроса[0].Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнен справочник <Объекты воздействия>'"); 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли; 
		
		Если РезультатЗапроса[1].Пустой()  Тогда
			ТекстСообщения = ТекстСообщения = НСтр("ru = 'Не заполнен справочник <Категории тяжести последствий>'"); 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли; 
		
		Возврат;
		
	КонецЕсли; 
	
	ОбъектыВоздействия = РезультатЗапроса[0].Выгрузить();
	ОбъектыВоздействия.Индексы.Добавить("Наименование");
	
	КатегорииТяжести = РезультатЗапроса[1].Выгрузить();
	КатегорииТяжести.Индексы.Добавить("Наименование");
	
	Макет = РегистрыСведений.торо_Последствия.ПолучитьМакет("НачальноеЗаполнение");
	
	ОбластьДанные = Макет.ПолучитьОбласть("ДанныеДляЗаполнения");
		
	ВысотаТаблицы = ОбластьДанные.ВысотаТаблицы;
	ШиринаТаблицы = ОбластьДанные.ШиринаТаблицы;
	
	СписокПроблемныхКолонок = Новый СписокЗначений;
	
	Для НомерСтроки = 2 По ВысотаТаблицы Цикл
		
		НаименованиеКатегории = СокрЛП(ОбластьДанные.Область(НомерСтроки, 1, НомерСтроки, 1).Текст);
		СтрокиСКатегорями = КатегорииТяжести.НайтиСтроки(Новый Структура("Наименование", НаименованиеКатегории));
		
		Если СтрокиСКатегорями.Количество() = 0 Тогда
			
			ШаблонСообщения = НСтр("ru = 'Для категории %1 из классификатора не найдено соответствия в базе'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НаименованиеКатегории);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
			Продолжить;
			
		КонецЕсли; 
		
		ТекущаяКатегорияТяжести = СтрокиСКатегорями[0].Ссылка;
		
		Для НомерКолонки = 2 По ШиринаТаблицы Цикл
			
			Если НЕ СписокПроблемныхКолонок.НайтиПоЗначению(НомерКолонки) = Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			
			НаименованиеВоздействия = СокрЛП(ОбластьДанные.Область(1, НомерКолонки, 1, НомерКолонки).Текст);
			СтрокиСВоздействием = ОбъектыВоздействия.НайтиСтроки(Новый Структура("Наименование", НаименованиеВоздействия));
			
			Если СтрокиСВоздействием.Количество() = 0 Тогда
				
				ШаблонСообщения = НСтр("ru = 'Для объекта воздействия %1 из классификатора не найдено соответствия в базе'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НаименованиеВоздействия);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
				Продолжить;
				
			КонецЕсли; 
			
			Попытка
				
				Запись = РегистрыСведений.торо_Последствия.СоздатьМенеджерЗаписи();
				Запись.КатегорияТяжести  = ТекущаяКатегорияТяжести;
				Запись.ОбъектВоздействия = СтрокиСВоздействием[0].Ссылка;
				Запись.Описание          = СокрЛП(ОбластьДанные.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).Текст);
				Запись.Записать();
				
			Исключение
				ШаблонСообщения = НСтр("ru = 'Для объекта воздействия %1 из классификатора не удалось заполнить последствия из классификатора Причина: %1'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ОписаниеОшибки());
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецПопытки;
			
		КонецЦикла; 
		
	КонецЦикла;  
	
	ТекстСообщения = НСтр("ru = 'Операция по заполнению последствий завершена.'"); 
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли