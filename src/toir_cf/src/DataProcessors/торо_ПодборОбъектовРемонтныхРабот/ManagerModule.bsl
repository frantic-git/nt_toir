
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СоответствиеОбъектовРемонтаВидамРемонтаИЦепочкам(ОбъектыРемонта, Параметры) Экспорт
	
	ИспользоватьПланированиеПоЖесткимРемЦиклам = ПолучитьФункциональнуюОпцию("торо_ИспользоватьПланированиеПоЖесткимРемЦиклам");
	
	ТолькоВидыРемонтовППР = Параметры.ТолькоВидыРемонтовППР;
	ПодбиратьЦепочкиРемонтов = ИспользоватьПланированиеПоЖесткимРемЦиклам И Параметры.ПодбиратьЦепочкиРемонтов;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_НормативныеРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_НормативныеРемонтыОборудования.ВидРемонта КАК ВидРемонта
	|ПОМЕСТИТЬ ВТ_НормативныеРемонты
	|ИЗ
	|	РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудованияУдаленные КАК торо_НормативныеРемонтыОборудованияУдаленные
	|		ПО торо_НормативныеРемонтыОборудования.ОбъектРемонта = торо_НормативныеРемонтыОборудованияУдаленные.ОбъектРемонта
	|			И торо_НормативныеРемонтыОборудования.ВидРемонта = торо_НормативныеРемонтыОборудованияУдаленные.ВидРемонта
	|			И торо_НормативныеРемонтыОборудования.ТиповойОР = торо_НормативныеРемонтыОборудованияУдаленные.ТиповойОР
	|ГДЕ
	|	&УсловныйОтбор
	|	И торо_НормативныеРемонтыОборудованияУдаленные.ОбъектРемонта ЕСТЬ NULL
	|	И торо_НормативныеРемонтыОборудования.ОбъектРемонта В(&ОбъектыРемонта)
	|	И торо_НормативныеРемонтыОборудования.Приоритет
	|	И НЕ торо_НормативныеРемонтыОборудования.НеУчаствуетВПланировании";

	Если ПодбиратьЦепочкиРемонтов Тогда
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_ОбъектыРемонта.Ссылка КАК Ссылка,
		|	торо_ОбъектыРемонта.ТиповойОР КАК ТиповойОР
		|ПОМЕСТИТЬ ВТ_ОбъектыРемонта
		|ИЗ
		|	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|ГДЕ
		|	торо_ОбъектыРемонта.Ссылка В(&ОбъектыРемонта)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов КАК ОбъектРемонта,
		|	торо_РемонтныеЦиклыОборудования.ВидЦепочки КАК ВидРемонта
		|ИЗ
		|	РегистрСведений.торо_РемонтныеЦиклыОборудования КАК торо_РемонтныеЦиклыОборудования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбъектыРемонта КАК ВТ_ОбъектыРемонта
		|		ПО торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов = ВТ_ОбъектыРемонта.Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_ОбъектыРемонта.Ссылка,
		|	торо_РемонтныеЦиклыОборудования.ВидЦепочки
		|ИЗ
		|	РегистрСведений.торо_РемонтныеЦиклыОборудования КАК торо_РемонтныеЦиклыОборудования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбъектыРемонта КАК ВТ_ОбъектыРемонта
		|		ПО торо_РемонтныеЦиклыОборудования.ГруппаОбъектовРемонтов = ВТ_ОбъектыРемонта.ТиповойОР
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_НормативныеРемонты.ОбъектРемонта,
		|	ВТ_НормативныеРемонты.ВидРемонта
		|ИЗ
		|	ВТ_НормативныеРемонты КАК ВТ_НормативныеРемонты
		|ИТОГИ ПО
		|	ОбъектРемонта";
			
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	ВТ_НормативныеРемонты.ОбъектРемонта,
		|	ВТ_НормативныеРемонты.ВидРемонта
		|ИЗ
		|	ВТ_НормативныеРемонты КАК ВТ_НормативныеРемонты
		|ИТОГИ ПО
		|	ОбъектРемонта";	
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Если ТолькоВидыРемонтовППР  Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловныйОтбор", "торо_НормативныеРемонтыОборудования.ВидРемонта.ПланГрафикППР");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловныйОтбор", "ИСТИНА");
	КонецЕсли;

	Запрос.УстановитьПараметр("ОбъектыРемонта", ОбъектыРемонта);
	
	СоответствиеОбъектовРемонта = Новый Соответствие; // Ключ - объект ремонта, Значение - Виды ремонта.
	
	ВыборкаОбъектРемонта = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбъектРемонта.Следующий() Цикл
		
		ВидыРемонта = Новый Массив;	
		
		Выборка = ВыборкаОбъектРемонта.Выбрать();
		Пока Выборка.Следующий() Цикл 
			ВидыРемонта.Добавить(Выборка.ВидРемонта);
		КонецЦикла;
		
		СоответствиеОбъектовРемонта.Вставить(ВыборкаОбъектРемонта.ОбъектРемонта, ВидыРемонта);
	КонецЦикла;
	
	Возврат СоответствиеОбъектовРемонта;
	
КонецФункции	

Функция СоответствиеСписковОбъектовРМВидамМероприятий(СпискиОбъектов, Параметры) Экспорт
	
	ТолькоГрафикиМероприятий = Параметры.ТолькоГрафикиМероприятий;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов КАК СписокОбъектов,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия КАК ВидМероприятия,
		|	торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия.ГрафикМероприятий КАК ВидМероприятияГрафикМероприятий
		|ИЗ
		|	РегистрСведений.торо_РегламентныеМероприятияИСпискиОбъектов КАК торо_РегламентныеМероприятияИСпискиОбъектов
		|ГДЕ
		|	&УсловныйОтбор
		|	И торо_РегламентныеМероприятияИСпискиОбъектов.СписокОбъектов В(&СпискиОбъектов)
		|	И торо_РегламентныеМероприятияИСпискиОбъектов.НеУчитыватьПродолжительность = ЛОЖЬ
		|ИТОГИ ПО
		|	СписокОбъектов";
	
	Запрос.УстановитьПараметр("СпискиОбъектов", СпискиОбъектов);
	
	Если ТолькоГрафикиМероприятий  Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловныйОтбор", "торо_РегламентныеМероприятияИСпискиОбъектов.ВидМероприятия.ГрафикМероприятий");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловныйОтбор", "ИСТИНА");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СоответствиеСписковОбъектов = Новый Соответствие; // Ключ - список объектов РМ, Значение - Виды мероприятий.
	
	ВыборкаСписокОбъектов = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСписокОбъектов.Следующий() Цикл
		
		ВидыМероприятий = Новый Массив;	
		
		Выборка = ВыборкаСписокОбъектов.Выбрать();
		Пока Выборка.Следующий() Цикл 
			ВидыМероприятий.Добавить(Выборка.ВидМероприятия);
		КонецЦикла;
		
		СоответствиеСписковОбъектов.Вставить(ВыборкаСписокОбъектов.СписокОбъектов, ВидыМероприятий);		
	КонецЦикла;
	
	Возврат СоответствиеСписковОбъектов;
	
КонецФункции	

#КонецОбласти

#КонецЕсли