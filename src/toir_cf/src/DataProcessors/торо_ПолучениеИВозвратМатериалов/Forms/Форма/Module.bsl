#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Подразделение) Тогда
		
		Подразделение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиТОиР",
		"ОсновноеПодразделение",
		Справочники.СтруктураПредприятия.ПустаяСсылка());
		
	КонецЕсли;
	Склад = Подразделение.Склад;
	
	Направление = " Возр";
	Сортировка = "Номенклатура";
	
	Период.Вариант = ВариантСтандартногоПериода.СледующийМесяц;
	ОбновитьДанные();
	
	УстановитьУсловноеОформление();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьДанныеОбработчикОжидания", 90, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеСкладПриИзменении(Элемент)
	ОбновитьДанные();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалы
&НаКлиенте
Процедура МатериалыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Ремонты", НСтр("ru = 'Ремонты, где требуется ""'")+ ДанныеСтроки.Номенклатура + """" + ?(ЗначениеЗаполнено(ДанныеСтроки.Характеристика), " """ + ДанныеСтроки.Характеристика + """", ""));
	СписокВыбора.Добавить("Заказы", НСтр("ru = 'Заказы, где присутствует ""'")+ ДанныеСтроки.Номенклатура + """" + ?(ЗначениеЗаполнено(ДанныеСтроки.Характеристика), " """ + ДанныеСтроки.Характеристика + """", ""));
	СписокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("МатериалыВыборЗавершение", ЭтотОбъект, Новый Структура("ДанныеСтроки", ДанныеСтроки)));
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыВыборЗавершение(ВыбранныйЭлемент, Параметры) Экспорт
	
	ДанныеСтроки = Параметры.ДанныеСтроки;
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Если ВыбранныйЭлемент.Значение = "Ремонты" Тогда
			
			НайденныеСтроки = Объект.Ремонты.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", ДанныеСтроки.Номенклатура, ДанныеСтроки.Характеристика));
			МассивСтрок = Новый Массив;
			Для Каждого Строка Из НайденныеСтроки Цикл
				СтруктураСтроки = Новый Структура("ДокументОснование, ДокументПлановыхДат, ОбъектРемонта, ВидРемонта, ДатаНачала, РемонтВыполняется, ЕстьДокументыЗаказа, ВЗаказеЕстьВсеПозиции, ID_Ремонта, ТребуемоеКоличество");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, Строка);
				МассивСтрок.Добавить(СтруктураСтроки);
			КонецЦикла;
			
			Форма = ОткрытьФорму("Обработка.торо_ПолучениеИВозвратМатериалов.Форма.СписокРемонтов", Новый Структура("МассивСтрок, Номенклатура, Характеристика", МассивСтрок, ДанныеСтроки.Номенклатура, ДанныеСтроки.Характеристика), Объект, Объект,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		ИначеЕсли ВыбранныйЭлемент.Значение = "Заказы" Тогда
			
			НайденныеСтроки = Объект.Заказы.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", ДанныеСтроки.Номенклатура, ДанныеСтроки.Характеристика));
			МассивСтрок = Новый Массив;
			Для Каждого Строка Из НайденныеСтроки Цикл
				СтруктураСтроки = Новый Структура("ДокументЗаказа, ЗаказанноеКоличество");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, Строка);
				МассивСтрок.Добавить(СтруктураСтроки);
			КонецЦикла;
			
			Форма = ОткрытьФорму("Обработка.торо_ПолучениеИВозвратМатериалов.Форма.СписокЗаказов", Новый Структура("МассивСтрок, Номенклатура, Характеристика", МассивСтрок, ДанныеСтроки.Номенклатура, ДанныеСтроки.Характеристика), Объект, Объект,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПереместитьИзлишкиНаДругойСклад(Команда)
	
	МассивТоваров = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.Материалы.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Материалы.ДанныеСтроки(ВыделеннаяСтрока);
		Если ДанныеСтроки.ПланируемыйИзлишекДефицит > 0 Тогда
			НоваяСтрока = Новый Структура("Номенклатура, Характеристика, КоличествоУпаковок");
			НоваяСтрока.Номенклатура = ДанныеСтроки.Номенклатура;
			НоваяСтрока.Характеристика = ДанныеСтроки.Характеристика;
			НоваяСтрока.КоличествоУпаковок = ДанныеСтроки.ПланируемыйИзлишекДефицит;
			МассивТоваров.Добавить(НоваяСтрока);
		КонецЕсли;
	КонецЦикла;	
	
	ЗначенияЗаполнения = Новый Структура("СкладОтправитель, Товары", Склад, МассивТоваров);
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ПеремещениеТоваров.ФормаОбъекта", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура УстранитьДефицитЗаказами(Команда)
	
	ДатаОтгрузки = Период.ДатаОкончания;
	
	СоответствиеID = Новый Соответствие;
	МассивСтрокНоменклатуры = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.Материалы.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.Материалы.ДанныеСтроки(ВыделеннаяСтрока);
		Если ДанныеСтроки.ПланируемыйИзлишекДефицит < 0 Тогда
			НС = Новый Структура("Номенклатура, Характеристика, КоличествоУпаковок, Количество"); 
			НС.Номенклатура = ДанныеСтроки.Номенклатура;
			НС.Характеристика = ДанныеСтроки.Характеристика;
			НС.КоличествоУпаковок = 0 - ДанныеСтроки.ПланируемыйИзлишекДефицит;
			НС.Количество = 0 - ДанныеСтроки.ПланируемыйИзлишекДефицит;
			
			НайденныеСтроки = Объект.Ремонты.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", ДанныеСтроки.Номенклатура, ДанныеСтроки.Характеристика));
			
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				ДатаОтгрузки = ?(НайденнаяСтрока.ДатаНачала < ДатаОтгрузки, НайденнаяСтрока.ДатаНачала, ДатаОтгрузки);
				Если (ТипЗнч(НайденнаяСтрока.ДокументОснование) = Тип("ДокументСсылка.торо_ВыявленныеДефекты")
					ИЛИ ТипЗнч(НайденнаяСтрока.ДокументОснование) = Тип("ДокументСсылка.торо_ВнешнееОснованиеДляРабот")) 
					И ТипЗнч(НайденнаяСтрока.ДокументПлановыхДат) = Тип("ДокументСсылка.торо_ЗаявкаНаРемонт")  Тогда 
					ДокументОснование = НайденнаяСтрока.ДокументПлановыхДат; 
				Иначе
					ДокументОснование = НайденнаяСтрока.ДокументОснование;
				КонецЕсли;
				
				СоответствиеID.Вставить(НайденнаяСтрока.ID_Ремонта, ДокументОснование);
			КонецЦикла;
			МассивСтрокНоменклатуры.Добавить(НС);
		КонецЕсли;
	КонецЦикла;
	ТекДата = ТекущаяДата();
	ДатаОтгрузки = ?(ТекДата >= ДатаОтгрузки, ТекДата, ДатаОтгрузки);
	
	ДатаДокумента = ?(ТекДата < ДатаОтгрузки, ТекДата, ДатаОтгрузки);
	Комментарий = "Документ сгенерирован обработкой ""Получение и возврат материалов""";
	ЗначенияЗаполнения = Новый Структура("Дата, МассивСтрокНоменклатуры, ДатаОтгрузки, Склад, Комментарий",
		ДатаДокумента, МассивСтрокНоменклатуры, ДатаОтгрузки, Склад, Комментарий);
	СтруктураПараметров = Новый Структура("СписокID, ЗначенияЗаполнения", СоответствиеID, ЗначенияЗаполнения);
	ОткрытьФорму("Документ.ЗаказНаВнутреннееПотребление.ФормаОбъекта", СтруктураПараметров, ,,,,Новый ОписаниеОповещения("ОбновитьДанныеУстраненДефицит", ЭтотОбъект));

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеУстраненДефицит(Результат, Параметры) Экспорт
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура СортироватьМатериалыПоВозрастанию(Команда)
	Направление = " Возр"; 
	Сортировка = Элементы.Материалы.ТекущийЭлемент.Имя;
	СортироватьМатериалы();
КонецПроцедуры

&НаКлиенте
Процедура СортироватьМатериалыПоУбыванию(Команда)
	Направление = " Убыв";
	Сортировка = Элементы.Материалы.ТекущийЭлемент.Имя;
	СортироватьМатериалы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДанныеОбработчикОжидания()
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанные()
	
	ТекСтрока = Элементы.Материалы.ТекущаяСтрока;
	МассивВыделенных = Новый Массив;
	
	Если ТекСтрока <> Неопределено Тогда
		ТекДанные = Объект.Материалы.НайтиПоИдентификатору(ТекСтрока);
		ВыделенныеСтроки = Элементы.Материалы.ВыделенныеСтроки;
		
		Для Каждого ИдВыделеннойСтроки Из ВыделенныеСтроки Цикл
			ДанныеВыделенной = Объект.Материалы.НайтиПоИдентификатору(ИдВыделеннойСтроки);
			МассивВыделенных.Добавить(Новый Структура("Номенклатура, Характеристика", ДанныеВыделенной.Номенклатура, ДанныеВыделенной.Характеристика));
		КонецЦикла;
		
		СтруктураПоиска = Новый Структура("Номенклатура, Характеристика", ТекДанные.Номенклатура, ТекДанные.Характеристика);
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	торо_ОбщиеДанныеПоРемонтам.IDРемонта КАК ID,
	               |	торо_ОбщиеДанныеПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_ОбщиеДанныеПоРемонтам.ВидРемонта КАК ВидРемонта,
	               |	торо_ОбщиеДанныеПоРемонтам.ЕстьЗаявка КАК ЕстьЗаявка,
	               |	ВЫБОР
	               |		КОГДА торо_ОбщиеДанныеПоРемонтам.ЕстьНаряд
	               |				ИЛИ торо_ОбщиеДанныеПоРемонтам.ЕстьАкт
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК РемонтВыполняется,
	               |	торо_ОбщиеДанныеПоРемонтам.Завершен КАК Завершен,
	               |	торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала КАК ДатаНачалаРемонтныхРабот,
	               |	торо_АктуальныеПлановыеДатыРемонтов.ДокументОснование КАК ПоследнийДокумент,
	               |	торо_АктуальныеПлановыеДатыРемонтов.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки,
	               |	торо_АктуальныеПлановыеДатыРемонтов.ВидДокументаНачалаЦепочки КАК ВидДокументаНачалаЦепочки
	               |ПОМЕСТИТЬ ВТ_ДокументыОснованияНачальная
	               |ИЗ
	               |	РегистрСведений.торо_АктуальныеПлановыеДатыРемонтов КАК торо_АктуальныеПлановыеДатыРемонтов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ОбщиеДанныеПоРемонтам КАК торо_ОбщиеДанныеПоРемонтам
	               |		ПО торо_АктуальныеПлановыеДатыРемонтов.IDРемонта = торо_ОбщиеДанныеПоРемонтам.IDРемонта
	               |ГДЕ
	               |	(НЕ &ПериодЗаполнен
	               |			ИЛИ торо_АктуальныеПлановыеДатыРемонтов.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания)
	               |	И НЕ торо_ОбщиеДанныеПоРемонтам.Отменен
	               |	И НЕ торо_ОбщиеДанныеПоРемонтам.Завершен
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID,
	               |	ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	торо_ЗаявкиПоРемонтамСрезПоследних.ДокументЗаявка КАК ДокументЗаявка,
	               |	торо_ЗаявкиПоРемонтамСрезПоследних.IDРемонта КАК IDРемонта
	               |ПОМЕСТИТЬ ВТ_НеОтмененныеЗаявки
	               |ИЗ
	               |	РегистрСведений.торо_ЗаявкиПоРемонтам.СрезПоследних(
	               |			,
	               |			IDРемонта В
	               |				(ВЫБРАТЬ
	               |					ВТ_ДокументыОснованияНачальная.ID КАК ID
	               |				ИЗ
	               |					ВТ_ДокументыОснованияНачальная КАК ВТ_ДокументыОснованияНачальная)) КАК торо_ЗаявкиПоРемонтамСрезПоследних
	               |ГДЕ
	               |	НЕ торо_ЗаявкиПоРемонтамСрезПоследних.Отменен
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	IDРемонта,
	               |	ДокументЗаявка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДокументыОснованияНачальная.ID КАК ID,
	               |	ВТ_ДокументыОснованияНачальная.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_ДокументыОснованияНачальная.ВидРемонта КАК ВидРемонта,
	               |	ВТ_ДокументыОснованияНачальная.ЕстьЗаявка КАК ЕстьЗаявка,
	               |	ВТ_ДокументыОснованияНачальная.РемонтВыполняется КАК РемонтВыполняется,
	               |	ВТ_ДокументыОснованияНачальная.Завершен КАК Завершен,
	               |	ВТ_ДокументыОснованияНачальная.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	               |	ЕСТЬNULL(торо_ПлановыеИсполнителиРемонтов.Исполнитель, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Исполнитель,
	               |	ЕСТЬNULL(торо_ИсполнителиВЗаявкахПоРемонтам.ДокументЗаявка, ВТ_ДокументыОснованияНачальная.ДокументНачалаЦепочки) КАК Регистратор,
	               |	ВТ_ДокументыОснованияНачальная.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	ВТ_ДокументыОснованияНачальная.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки,
	               |	ВТ_ДокументыОснованияНачальная.ВидДокументаНачалаЦепочки КАК ВидДокументаНачалаЦепочки
	               |ПОМЕСТИТЬ ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем
	               |ИЗ
	               |	ВТ_ДокументыОснованияНачальная КАК ВТ_ДокументыОснованияНачальная
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ПлановыеИсполнителиРемонтов КАК торо_ПлановыеИсполнителиРемонтов
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ИсполнителиВЗаявкахПоРемонтам КАК торо_ИсполнителиВЗаявкахПоРемонтам
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НеОтмененныеЗаявки КАК ВТ_НеОтмененныеЗаявки
	               |				ПО торо_ИсполнителиВЗаявкахПоРемонтам.IDРемонта = ВТ_НеОтмененныеЗаявки.IDРемонта
	               |					И торо_ИсполнителиВЗаявкахПоРемонтам.ДокументЗаявка = ВТ_НеОтмененныеЗаявки.ДокументЗаявка
	               |			ПО торо_ПлановыеИсполнителиРемонтов.IDРемонта = торо_ИсполнителиВЗаявкахПоРемонтам.IDРемонта
	               |				И торо_ПлановыеИсполнителиРемонтов.Исполнитель = торо_ИсполнителиВЗаявкахПоРемонтам.Исполнитель
	               |				И торо_ПлановыеИсполнителиРемонтов.УточнениеИсполнителя = торо_ИсполнителиВЗаявкахПоРемонтам.УточнениеИсполнителя
	               |		ПО ВТ_ДокументыОснованияНачальная.ID = торо_ПлановыеИсполнителиРемонтов.IDРемонта
	               |ГДЕ
	               |	(&НеОтбиратьПоПодразделению
	               |			ИЛИ ЕСТЬNULL(торо_ПлановыеИсполнителиРемонтов.Исполнитель, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) = &Подразделение)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID,
	               |	ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ID КАК ID,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ВидРемонта КАК ВидРемонта,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.РемонтВыполняется КАК РемонтВыполняется,
	               |	торо_НормативныеРемонтыОборудования.НормативныйРемонт КАК НормативныйРемонт,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.Регистратор КАК Регистратор,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки
	               |ПОМЕСТИТЬ ППР
	               |ИЗ
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем КАК ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормативныеРемонтыОборудования КАК торо_НормативныеРемонтыОборудования
	               |		ПО ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ОбъектРемонта = торо_НормативныеРемонтыОборудования.ОбъектРемонта
	               |			И ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ВидРемонта = торо_НормативныеРемонтыОборудования.ВидРемонта
	               |			И (торо_НормативныеРемонтыОборудования.Приоритет)
	               |ГДЕ
	               |	(НЕ ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ЕстьЗаявка
	               |			ИЛИ НЕ ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.Завершен)
	               |	И ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ВидДокументаНачалаЦепочки = ЗНАЧЕНИЕ(Перечисление.торо_ВидыДокументовНачалаЦепочкиРемонтов.ПланГрафикППР)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	НормативныйРемонт,
	               |	ДатаНачалаРемонтныхРабот
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ППР.ID КАК ID,
	               |	МАКСИМУМ(торо_ВерсииТехКарт.Период) КАК Период,
	               |	торо_ВерсииТехКарт.ИдентификаторТехКарты КАК ИдентификаторТехКарты
	               |ПОМЕСТИТЬ ВТ_ДатыДляСреза
	               |ИЗ
	               |	ППР КАК ППР
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
	               |		ПО ППР.НормативныйРемонт = торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |			И ППР.ДатаНачалаРемонтныхРабот >= торо_ВерсииТехКарт.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ППР.ID,
	               |	торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ППР.ДатаНачалаРемонтныхРабот КАК ДатаНачалаРемонтныхРабот,
	               |	ППР.ID КАК ID,
	               |	ППР.ОбъектРемонта КАК ОбъектРемонта,
	               |	ППР.ВидРемонта КАК ВидРемонта,
	               |	ППР.РемонтВыполняется КАК РемонтВыполняется,
	               |	торо_ВерсииТехКарт.ТехКарта КАК ТехКарта,
	               |	ППР.Регистратор КАК Регистратор,
	               |	ППР.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	ППР.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки
	               |ПОМЕСТИТЬ ВТ_ППРсТехКартами
	               |ИЗ
	               |	ППР КАК ППР
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДатыДляСреза КАК ВТ_ДатыДляСреза
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ВерсииТехКарт КАК торо_ВерсииТехКарт
	               |			ПО ВТ_ДатыДляСреза.Период = торо_ВерсииТехКарт.Период
	               |				И ВТ_ДатыДляСреза.ИдентификаторТехКарты = торо_ВерсииТехКарт.ИдентификаторТехКарты
	               |		ПО ППР.ID = ВТ_ДатыДляСреза.ID
	               |			И ППР.НормативныйРемонт = ВТ_ДатыДляСреза.ИдентификаторТехКарты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID,
	               |	Регистратор,
	               |	ТехКарта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ID КАК ID,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ДатаНачалаРемонтныхРабот КАК ДатаНачала,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.РемонтВыполняется КАК РемонтВыполняется,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ВидРемонта КАК ВидРемонта,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.Регистратор КАК Документ,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки
	               |ПОМЕСТИТЬ IDРемонтов
	               |ИЗ
	               |	ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем КАК ВТ_ПоследниеДокиПоРемонтамСУстановленнымИсполнителем
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ_ППРсТехКартами.ID,
	               |	ВТ_ППРсТехКартами.ДатаНачалаРемонтныхРабот,
	               |	ВТ_ППРсТехКартами.РемонтВыполняется,
	               |	ВТ_ППРсТехКартами.ОбъектРемонта,
	               |	ВТ_ППРсТехКартами.ВидРемонта,
	               |	ВТ_ППРсТехКартами.Регистратор,
	               |	ВТ_ППРсТехКартами.ПоследнийДокумент,
	               |	ВТ_ППРсТехКартами.ДокументНачалаЦепочки
	               |ИЗ
	               |	ВТ_ППРсТехКартами КАК ВТ_ППРсТехКартами
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID,
	               |	ОбъектРемонта,
	               |	ВидРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ППРсТехКартами.Регистратор КАК ДокументПлан,
	               |	ВТ_ППРсТехКартами.ID КАК ID_Ремонта,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура КАК Номенклатура,
	               |	торо_ТехКартыМатериальныеЗатраты.Количество КАК ТребуемоеКоличество,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА торо_ТехКартыМатериальныеЗатраты.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ХарактеристикаНоменклатуры,
	               |	IDРемонтов.ДатаНачала КАК ПлановаяДатаРемонта,
	               |	IDРемонтов.РемонтВыполняется КАК РемонтВыполняется,
	               |	IDРемонтов.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки
	               |ПОМЕСТИТЬ Требуемые
	               |ИЗ
	               |	ВТ_ППРсТехКартами КАК ВТ_ППРсТехКартами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.МатериальныеЗатраты КАК торо_ТехКартыМатериальныеЗатраты
	               |		ПО ВТ_ППРсТехКартами.ТехКарта = торо_ТехКартыМатериальныеЗатраты.Ссылка
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ IDРемонтов КАК IDРемонтов
	               |		ПО ВТ_ППРсТехКартами.ID = IDРемонтов.ID
	               |ГДЕ
	               |	НЕ торо_ТехКартыМатериальныеЗатраты.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ППРсТехКартами.Регистратор,
	               |	ВТ_ППРсТехКартами.ID,
	               |	торо_ТехКартыМатериальныеЗатраты.Номенклатура,
	               |	торо_ТехКартыМатериальныеЗатраты.Количество,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА торо_ТехКартыМатериальныеЗатраты.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ,
	               |	IDРемонтов.ДатаНачала,
	               |	IDРемонтов.РемонтВыполняется,
	               |	IDРемонтов.ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки
	               |ИЗ
	               |	ВТ_ППРсТехКартами КАК ВТ_ППРсТехКартами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_СоставИерархическихТехКартРемонтов КАК торо_СоставИерархическихТехКартРемонтов
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТехКарты.МатериальныеЗатраты КАК торо_ТехКартыМатериальныеЗатраты
	               |			ПО торо_СоставИерархическихТехКартРемонтов.Состав = торо_ТехКартыМатериальныеЗатраты.Ссылка
	               |		ПО ВТ_ППРсТехКартами.ТехКарта = торо_СоставИерархическихТехКартРемонтов.ТехКарта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ IDРемонтов КАК IDРемонтов
	               |		ПО ВТ_ППРсТехКартами.ID = IDРемонтов.ID
	               |ГДЕ
	               |	НЕ торо_ТехКартыМатериальныеЗатраты.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ППРсТехКартами.Регистратор,
	               |	ВТ_ППРсТехКартами.ID,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Номенклатура,
	               |	торо_НормыЗапчастейНаОбъектыРемонта.Количество,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА торо_НормыЗапчастейНаОбъектыРемонта.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ,
	               |	IDРемонтов.ДатаНачала,
	               |	IDРемонтов.РемонтВыполняется,
	               |	IDРемонтов.ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки
	               |ИЗ
	               |	ВТ_ППРсТехКартами КАК ВТ_ППРсТехКартами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_НормыЗапчастейНаОбъектыРемонта КАК торо_НормыЗапчастейНаОбъектыРемонта
	               |		ПО (торо_НормыЗапчастейНаОбъектыРемонта.Приоритет)
	               |			И ВТ_ППРсТехКартами.ОбъектРемонта = торо_НормыЗапчастейНаОбъектыРемонта.ОбъектРемонта
	               |			И ВТ_ППРсТехКартами.ВидРемонта = торо_НормыЗапчастейНаОбъектыРемонта.ВидРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ IDРемонтов КАК IDРемонтов
	               |		ПО ВТ_ППРсТехКартами.ID = IDРемонтов.ID
	               |ГДЕ
	               |	НЕ торо_НормыЗапчастейНаОбъектыРемонта.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.Регистратор,
	               |	торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.РемонтыОборудования_ID,
	               |	торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.Номенклатура,
	               |	торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.Количество,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.ХарактеристикаНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ,
	               |	IDРемонтов.ДатаНачала,
	               |	IDРемонтов.РемонтВыполняется,
	               |	IDРемонтов.ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки
	               |ИЗ
	               |	IDРемонтов КАК IDРемонтов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_ПлановыеМатериальныеЗатратыРемонтныхРабот КАК торо_ПлановыеМатериальныеЗатратыРемонтныхРабот
	               |		ПО IDРемонтов.ID = торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.РемонтыОборудования_ID
	               |ГДЕ
	               |	НЕ торо_ПлановыеМатериальныеЗатратыРемонтныхРабот.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	торо_ПлановыеЗатратыЗапчастей.Регистратор,
	               |	торо_ПлановыеЗатратыЗапчастей.РемонтыОборудования_ID,
	               |	торо_ПлановыеЗатратыЗапчастей.Номенклатура,
	               |	торо_ПлановыеЗатратыЗапчастей.Количество,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА торо_ПлановыеЗатратыЗапчастей.ХарактеристикаНоменклатуры
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ,
	               |	IDРемонтов.ДатаНачала,
	               |	IDРемонтов.РемонтВыполняется,
	               |	IDРемонтов.ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки
	               |ИЗ
	               |	IDРемонтов КАК IDРемонтов
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_ПлановыеЗатратыЗапчастей КАК торо_ПлановыеЗатратыЗапчастей
	               |		ПО (торо_ПлановыеЗатратыЗапчастей.РемонтыОборудования_ID = IDРемонтов.ID)
	               |ГДЕ
	               |	НЕ торо_ПлановыеЗатратыЗапчастей.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Требуемые.ДокументПлан КАК ДокументПлан,
	               |	Требуемые.ID_Ремонта КАК ID_Ремонта,
	               |	Требуемые.Номенклатура КАК Номенклатура,
	               |	СУММА(Требуемые.ТребуемоеКоличество) КАК ТребуемоеКоличество,
	               |	Требуемые.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	Требуемые.ПлановаяДатаРемонта КАК ПлановаяДатаРемонта,
	               |	Требуемые.РемонтВыполняется КАК РемонтВыполняется,
	               |	Требуемые.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	Требуемые.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки
	               |ПОМЕСТИТЬ ТребуемыеСумма
	               |ИЗ
	               |	Требуемые КАК Требуемые
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Требуемые.ДокументПлан,
	               |	Требуемые.ID_Ремонта,
	               |	Требуемые.Номенклатура,
	               |	Требуемые.ХарактеристикаНоменклатуры,
	               |	Требуемые.ПлановаяДатаРемонта,
	               |	Требуемые.РемонтВыполняется,
	               |	Требуемые.ПоследнийДокумент,
	               |	Требуемые.ДокументНачалаЦепочки
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID_Ремонта,
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыНаВнутреннееПотребление.ЗаказНаВнутреннееПотребление КАК ДокументЗаказа,
	               |	ЗаказыНаВнутреннееПотребление.Номенклатура КАК Номенклатура,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА ЗаказыНаВнутреннееПотребление.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ХарактеристикаНоменклатуры,
	               |	ЗаказыНаВнутреннееПотребление.КОформлениюОстаток КАК ЗаказанноеКоличество,
	               |	IDРемонтов.ID КАК ID_Ремонта,
	               |	ЗаказыНаВнутреннееПотребление.КодСтроки КАК КодСтроки
	               |ПОМЕСТИТЬ Заказанные
	               |ИЗ
	               |	РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки КАК ЗаказыНаВнутреннееПотребление
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ИнтеграцияДокументов КАК торо_ИнтеграцияДокументов
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ IDРемонтов КАК IDРемонтов
	               |			ПО (IDРемонтов.ID = торо_ИнтеграцияДокументов.ID)
	               |		ПО (торо_ИнтеграцияДокументов.ДокументЕРП = ЗаказыНаВнутреннееПотребление.ЗаказНаВнутреннееПотребление)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаВнутреннееПотребление КАК ДокументЗаказНаВнутреннееПотребление
	               |		ПО ЗаказыНаВнутреннееПотребление.ЗаказНаВнутреннееПотребление = ДокументЗаказНаВнутреннееПотребление.Ссылка
	               |ГДЕ
	               |	(&НеОтбиратьПоСкладу
	               |			ИЛИ ЗаказыНаВнутреннееПотребление.Склад = &Склад)
	               |	И (ВЫБОР
	               |					КОГДА &ПериодЗаполнен
	               |						ТОГДА ДокументЗаказНаВнутреннееПотребление.ДатаОтгрузки МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ
	               |				И IDРемонтов.ID ЕСТЬ NULL
	               |			ИЛИ НЕ IDРемонтов.ID ЕСТЬ NULL)
	               |	И НЕ ЗаказыНаВнутреннееПотребление.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID_Ремонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА ТоварыНаСкладахОстатки.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) КАК Количество
	               |ПОМЕСТИТЬ ИмеющиесяВсего
	               |ИЗ
	               |	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
	               |ГДЕ
	               |	НЕ ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТоварыНаСкладахОстатки.Номенклатура,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА ТоварыНаСкладахОстатки.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА ТоварыНаСкладахОстатки.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) КАК Количество
	               |ПОМЕСТИТЬ Имеющиеся
	               |ИЗ
	               |	РегистрНакопления.ТоварыНаСкладах.Остатки(
	               |			&ДатаОкончания,
	               |			&НеОтбиратьПоСкладу
	               |				ИЛИ Склад = &Склад) КАК ТоварыНаСкладахОстатки
	               |ГДЕ
	               |	НЕ ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (&Услуга)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТоварыНаСкладахОстатки.Номенклатура,
	               |	ВЫБОР
	               |		КОГДА &ФОИспользованиеХарактеристик
	               |			ТОГДА ТоварыНаСкладахОстатки.Характеристика
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	КОНЕЦ
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТребуемыеСумма.Номенклатура КАК Номенклатура,
	               |	ТребуемыеСумма.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ТребуемыеСумма.ТребуемоеКоличество) КАК ТребуемоеКоличество,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТребуемыеСумма.РемонтВыполняется
	               |				ТОГДА ТребуемыеСумма.ТребуемоеКоличество
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ТребуетсяПоВыполняемым
	               |ПОМЕСТИТЬ ТребуемыеСуммаГр
	               |ИЗ
	               |	ТребуемыеСумма КАК ТребуемыеСумма
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТребуемыеСумма.Номенклатура,
	               |	ТребуемыеСумма.ХарактеристикаНоменклатуры
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Заказанные.Номенклатура КАК Номенклатура,
	               |	Заказанные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	МАКСИМУМ(Заказанные.ЗаказанноеКоличество) КАК ЗаказанноеКоличество,
	               |	Заказанные.КодСтроки КАК КодСтроки,
	               |	Заказанные.ДокументЗаказа КАК ДокументЗаказа
	               |ПОМЕСТИТЬ ВТ_ЗаказанныеГруппировка
	               |ИЗ
	               |	Заказанные КАК Заказанные
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Заказанные.Номенклатура,
	               |	Заказанные.ХарактеристикаНоменклатуры,
	               |	Заказанные.КодСтроки,
	               |	Заказанные.ДокументЗаказа
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ЗаказанныеГруппировка.Номенклатура КАК Номенклатура,
	               |	ВТ_ЗаказанныеГруппировка.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ВТ_ЗаказанныеГруппировка.ЗаказанноеКоличество) КАК ЗаказанноеКоличество
	               |ПОМЕСТИТЬ ЗаказанныеГр
	               |ИЗ
	               |	ВТ_ЗаказанныеГруппировка КАК ВТ_ЗаказанныеГруппировка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ЗаказанныеГруппировка.Номенклатура,
	               |	ВТ_ЗаказанныеГруппировка.ХарактеристикаНоменклатуры
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТребуемыеСумма.ДокументПлан КАК Документ,
	               |	IDРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	               |	IDРемонтов.ВидРемонта КАК ВидРемонта,
	               |	ВЫБОР
	               |		КОГДА IDРемонтов.РемонтВыполняется
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК РемонтВыполняется,
	               |	IDРемонтов.ДатаНачала КАК ДатаНачала,
	               |	ВЫБОР
	               |		КОГДА СУММА(ВЫБОР
	               |					КОГДА Заказанные1.ДокументЗаказа ЕСТЬ NULL
	               |						ТОГДА 1
	               |					ИНАЧЕ 0
	               |				КОНЕЦ) > 0
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК ЕстьДокументыЗаказа,
	               |	ВЫБОР
	               |		КОГДА СУММА(ВЫБОР
	               |					КОГДА Заказанные.Номенклатура ЕСТЬ NULL
	               |						ТОГДА 1
	               |					ИНАЧЕ 0
	               |				КОНЕЦ) > 0
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК ВЗаказеЕстьВсеПозиции,
	               |	ТребуемыеСумма.ID_Ремонта КАК ID_Ремонта,
	               |	IDРемонтов.ПоследнийДокумент КАК ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки КАК ДокументНачалаЦепочки
	               |ПОМЕСТИТЬ НаличиеЗаказовПоРемонтам
	               |ИЗ
	               |	ТребуемыеСумма КАК ТребуемыеСумма
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Заказанные КАК Заказанные
	               |		ПО ТребуемыеСумма.ID_Ремонта = Заказанные.ID_Ремонта
	               |			И ТребуемыеСумма.Номенклатура = Заказанные.Номенклатура
	               |			И ТребуемыеСумма.ХарактеристикаНоменклатуры = Заказанные.ХарактеристикаНоменклатуры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ IDРемонтов КАК IDРемонтов
	               |		ПО ТребуемыеСумма.ID_Ремонта = IDРемонтов.ID
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Заказанные КАК Заказанные1
	               |		ПО ТребуемыеСумма.ID_Ремонта = Заказанные1.ID_Ремонта
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА IDРемонтов.РемонтВыполняется
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ,
	               |	ТребуемыеСумма.ДокументПлан,
	               |	IDРемонтов.ВидРемонта,
	               |	IDРемонтов.ОбъектРемонта,
	               |	IDРемонтов.ДатаНачала,
	               |	ТребуемыеСумма.ID_Ремонта,
	               |	IDРемонтов.ПоследнийДокумент,
	               |	IDРемонтов.ДокументНачалаЦепочки
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID_Ремонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТребуемыеСумма.ДокументПлан КАК Документ,
	               |	IDРемонтов.ОбъектРемонта КАК ОбъектРемонта,
	               |	IDРемонтов.ВидРемонта КАК ВидРемонта,
	               |	ВЫБОР
	               |		КОГДА IDРемонтов.РемонтВыполняется
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК РемонтВыполняется,
	               |	IDРемонтов.ДатаНачала КАК ДатаНачала,
	               |	ВЫБОР
	               |		КОГДА СУММА(ВЫБОР
	               |					КОГДА Заказанные.Номенклатура ЕСТЬ NULL
	               |						ТОГДА 1
	               |					ИНАЧЕ 0
	               |				КОНЕЦ) > 0
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК ВЗаказеЕстьВсеПозиции,
	               |	ТребуемыеСумма.ID_Ремонта КАК ID_Ремонта,
	               |	Заказанные.Номенклатура КАК Номенклатура,
	               |	Заказанные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ПОМЕСТИТЬ ВТ_НаличиеЗаказовПоНоменклатуре
	               |ИЗ
	               |	ТребуемыеСумма КАК ТребуемыеСумма
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Заказанные КАК Заказанные
	               |		ПО ТребуемыеСумма.ID_Ремонта = Заказанные.ID_Ремонта
	               |			И ТребуемыеСумма.Номенклатура = Заказанные.Номенклатура
	               |			И ТребуемыеСумма.ХарактеристикаНоменклатуры = Заказанные.ХарактеристикаНоменклатуры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ IDРемонтов КАК IDРемонтов
	               |		ПО ТребуемыеСумма.ID_Ремонта = IDРемонтов.ID
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА IDРемонтов.РемонтВыполняется
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ,
	               |	ТребуемыеСумма.ДокументПлан,
	               |	IDРемонтов.ВидРемонта,
	               |	IDРемонтов.ОбъектРемонта,
	               |	IDРемонтов.ДатаНачала,
	               |	ТребуемыеСумма.ID_Ремонта,
	               |	Заказанные.Номенклатура,
	               |	Заказанные.ХарактеристикаНоменклатуры
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ID_Ремонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТребуемыеСумма.Номенклатура КАК Номенклатура,
	               |	ТребуемыеСумма.ХарактеристикаНоменклатуры КАК Характеристика,
	               |	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЕСТЬNULL(Имеющиеся.Количество, 0) КАК ОстатокНаСкладе,
	               |	ЕСТЬNULL(ИмеющиесяВсего.Количество, 0) КАК ОстатокВсего,
	               |	ЕСТЬNULL(Имеющиеся.Количество, 0) - ЕСТЬNULL(ТребуемыеСумма.ТребуетсяПоВыполняемым, 0) КАК ТекущийИзлишекДефицит,
	               |	ЕСТЬNULL(Имеющиеся.Количество, 0) + ЕСТЬNULL(Заказанные.ЗаказанноеКоличество, 0) - ЕСТЬNULL(ТребуемыеСумма.ТребуемоеКоличество, 0) КАК ПланируемыйИзлишекДефицит
	               |ИЗ
	               |	ТребуемыеСуммаГр КАК ТребуемыеСумма
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	               |		ПО ТребуемыеСумма.Номенклатура = СпрНоменклатура.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЗаказанныеГр КАК Заказанные
	               |		ПО ТребуемыеСумма.Номенклатура = Заказанные.Номенклатура
	               |			И ТребуемыеСумма.ХарактеристикаНоменклатуры = Заказанные.ХарактеристикаНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Имеющиеся КАК Имеющиеся
	               |		ПО ТребуемыеСумма.Номенклатура = Имеющиеся.Номенклатура
	               |			И ТребуемыеСумма.ХарактеристикаНоменклатуры = Имеющиеся.ХарактеристикаНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ИмеющиесяВсего КАК ИмеющиесяВсего
	               |		ПО ТребуемыеСумма.Номенклатура = ИмеющиесяВсего.Номенклатура
	               |			И ТребуемыеСумма.ХарактеристикаНоменклатуры = ИмеющиесяВсего.ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НаличиеЗаказовПоРемонтам.Документ КАК ДокументОснование1,
	               |	НаличиеЗаказовПоРемонтам.ОбъектРемонта КАК ОбъектРемонта,
	               |	НаличиеЗаказовПоРемонтам.ВидРемонта КАК ВидРемонта,
	               |	НаличиеЗаказовПоРемонтам.ДатаНачала КАК ДатаНачала,
	               |	НаличиеЗаказовПоРемонтам.РемонтВыполняется КАК РемонтВыполняется,
	               |	НаличиеЗаказовПоРемонтам.ЕстьДокументыЗаказа КАК ЕстьДокументыЗаказа,
	               |	ТребуемыеСумма.Номенклатура КАК Номенклатура,
	               |	ТребуемыеСумма.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТребуемыеСумма.ID_Ремонта КАК ID_Ремонта,
	               |	ТребуемыеСумма.ТребуемоеКоличество КАК ТребуемоеКоличество,
	               |	ЕСТЬNULL(ВТ_НаличиеЗаказовПоНоменклатуре.ВЗаказеЕстьВсеПозиции, 0) КАК ВЗаказеЕстьВсеПозиции,
	               |	НаличиеЗаказовПоРемонтам.ПоследнийДокумент КАК ДокументПлановыхДат,
	               |	НаличиеЗаказовПоРемонтам.ДокументНачалаЦепочки КАК ДокументОснование
	               |ИЗ
	               |	НаличиеЗаказовПоРемонтам КАК НаличиеЗаказовПоРемонтам
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТребуемыеСумма КАК ТребуемыеСумма
	               |		ПО НаличиеЗаказовПоРемонтам.ID_Ремонта = ТребуемыеСумма.ID_Ремонта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НаличиеЗаказовПоНоменклатуре КАК ВТ_НаличиеЗаказовПоНоменклатуре
	               |		ПО (ВТ_НаличиеЗаказовПоНоменклатуре.ID_Ремонта = ТребуемыеСумма.ID_Ремонта)
	               |			И (ВТ_НаличиеЗаказовПоНоменклатуре.Номенклатура = ТребуемыеСумма.Номенклатура)
	               |			И (ВТ_НаличиеЗаказовПоНоменклатуре.ХарактеристикаНоменклатуры = ТребуемыеСумма.ХарактеристикаНоменклатуры)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НаличиеЗаказовПоРемонтам.Документ,
	               |	НаличиеЗаказовПоРемонтам.ОбъектРемонта,
	               |	НаличиеЗаказовПоРемонтам.ВидРемонта,
	               |	НаличиеЗаказовПоРемонтам.ДатаНачала,
	               |	НаличиеЗаказовПоРемонтам.РемонтВыполняется,
	               |	НаличиеЗаказовПоРемонтам.ЕстьДокументыЗаказа,
	               |	ТребуемыеСумма.Номенклатура,
	               |	ТребуемыеСумма.ХарактеристикаНоменклатуры,
	               |	ТребуемыеСумма.ID_Ремонта,
	               |	ТребуемыеСумма.ТребуемоеКоличество,
	               |	ВТ_НаличиеЗаказовПоНоменклатуре.ВЗаказеЕстьВсеПозиции,
	               |	НаличиеЗаказовПоРемонтам.ПоследнийДокумент,
	               |	НаличиеЗаказовПоРемонтам.ДокументНачалаЦепочки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ЗаказанныеГруппировка.ДокументЗаказа КАК ДокументЗаказа,
	               |	СУММА(ВТ_ЗаказанныеГруппировка.ЗаказанноеКоличество) КАК ЗаказанноеКоличество,
	               |	ВТ_ЗаказанныеГруппировка.Номенклатура КАК Номенклатура,
	               |	ВТ_ЗаказанныеГруппировка.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ИЗ
	               |	ВТ_ЗаказанныеГруппировка КАК ВТ_ЗаказанныеГруппировка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ЗаказанныеГруппировка.Номенклатура,
	               |	ВТ_ЗаказанныеГруппировка.ХарактеристикаНоменклатуры,
	               |	ВТ_ЗаказанныеГруппировка.ДокументЗаказа";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПериодЗаполнен", ЗначениеЗаполнено(Период));
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("НеОтбиратьПоСкладу", Не ЗначениеЗаполнено(Склад));
	Запрос.УстановитьПараметр("Склад", Склад);
	Услуга = Новый Массив(2);
	Услуга[0] = Перечисления.ТипыНоменклатуры.Услуга;
	Услуга[1] = Перечисления.ТипыНоменклатуры.Работа;
	Запрос.УстановитьПараметр("Услуга", Услуга);
	Запрос.УстановитьПараметр("НеОтбиратьПоПодразделению", Не ЗначениеЗаполнено(Подразделение));
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ФОИспользованиеХарактеристик", Константы.торо_ИспользоватьХарактеристикиНоменклатуры.Получить());

	Результат = Запрос.ВыполнитьПакет();
	Объект.Материалы.Загрузить(Результат[17].Выгрузить());
	Объект.Ремонты.Загрузить(Результат[18].Выгрузить());
	Объект.Заказы.Загрузить(Результат[19].Выгрузить());
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.Материалы,
		Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются")));
			
	Если СтруктураПоиска <> Неопределено Тогда
		НайденныеСтроки = Объект.Материалы.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденнаяСтрока = НайденныеСтроки[0];
			Элементы.Материалы.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	Если МассивВыделенных.Количество() > 0 Тогда
		Для Каждого СтруктураПоиска Из МассивВыделенных Цикл
			НайденныеСтроки = Объект.Материалы.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				НайденнаяСтрока = НайденныеСтроки[0];
				Элементы.Материалы.ВыделенныеСтроки.Добавить(НайденнаяСтрока.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СортироватьМатериалы();
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыХарактеристика.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Материалы.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<характеристики не используются>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура СортироватьМатериалы()
	
	Объект.Материалы.Сортировать(СтрЗаменить(Сортировка, "Материалы", "") + Направление);
	Элементы.Материалы.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ОбновитьДанные();
КонецПроцедуры

#КонецОбласти