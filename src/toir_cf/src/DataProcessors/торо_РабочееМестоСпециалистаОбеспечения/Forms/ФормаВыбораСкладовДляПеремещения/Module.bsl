#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Параметры.Склады) Тогда 
		Возврат;
	Иначе
		ОстатокСклада = Параметры.ОстатокСклада;
		СкладОтправитель = Параметры.СкладОтправитель;
		МинОстаток = Параметры.МинОстаток;
		Для Каждого Склад Из Параметры.Склады Цикл 
			СтрокаТЧ = Склады.Добавить();
			СтрокаТЧ.Склад = Склад.Ключ;
			СтрокаТЧ.Количество = Мин(Склад.Значение, ОстатокСклада);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьДанные();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСклады

&НаКлиенте
Процедура СкладыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле = Элементы.СкладыСклад Тогда 
		ВыбратьДанные();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкладыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НЕ ОтменаРедактирования Тогда
		ПроверитьДанные(Элемент.ТекущиеДанные, Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьДанные()
	ТекСтрока = Элементы.Склады.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Отказ = Ложь;
		ПроверитьДанные(ТекСтрока, Отказ);
		Если НЕ Отказ Тогда
			Если МинОстаток <> 0 И ОстатокСклада - ТекСтрока.Количество < МинОстаток Тогда
				ОповещениеОбОтвете = Новый ОписаниеОповещения("ЗавершитьВыбор", ЭтаФорма);
				ПоказатьВопрос(ОповещениеОбОтвете, 
					НСтр("ru = 'При перемещении выбранного количества свободный остаток станет меньше минимально установленного. Продолжить?'"),
					РежимДиалогаВопрос.ДаНет);
			Иначе
				СтруктураВозврата = Новый Структура("СкладПолучатель, Количество, СкладОтправитель", 
					ТекСтрока.Склад, ТекСтрока.Количество, СкладОтправитель);
				Закрыть(СтруктураВозврата);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыбор(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекСтрока = Элементы.Склады.ТекущиеДанные;
		СтруктураВозврата = Новый Структура("СкладПолучатель, Количество, СкладОтправитель", 
			ТекСтрока.Склад, ТекСтрока.Количество, СкладОтправитель);
		Закрыть(СтруктураВозврата);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДанные(ТекущиеДанные, Отказ)
	Если ТекущиеДанные.Количество > ОстатокСклада Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(
			НСтр("ru = 'Количество не может превышать <%1> - свободный остаток на складе-отправителе'"), ОстатокСклада));
	ИначеЕсли ТекущиеДанные.Количество = 0 Тогда 
		Отказ = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Количество должно быть > 0'"), , , Отказ);		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти