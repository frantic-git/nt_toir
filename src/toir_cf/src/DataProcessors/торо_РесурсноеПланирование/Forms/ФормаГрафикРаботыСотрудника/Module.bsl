
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сотрудник   = Параметры.Сотрудник;
	ДатаГрафика = Параметры.ДатаГрафика;
	
	ЗаполнитьГрафикРаботысотрудника(Сотрудник, ДатаГрафика, ГрафикРаботы);
	
	ЗаполнитьИнтервалыРаботы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьГрафикРаботысотрудника(Знач лСотрудник, Знач ДатаСреза, лГрафикРаботы)

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ГрафикРаботыСотрудниковСрезПоследних.ГрафикРаботы КАК ГрафикРаботы
	                      |ИЗ
	                      |	РегистрСведений.ГрафикРаботыСотрудников.СрезПоследних(&ДатаСреза, ) КАК ГрафикРаботыСотрудниковСрезПоследних
	                      |ГДЕ
	                      |	ГрафикРаботыСотрудниковСрезПоследних.Сотрудник = &Сотрудник");
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Сотрудник", лСотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		лГрафикРаботы = Выборка.ГрафикРаботы;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнтервалыРаботы()

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	торо_СостояниеРаботыСотрудниковСрезПоследних.СостояниеСотрудника КАК СостояниеСотрудника,
	                      |	торо_СостояниеРаботыСотрудниковСрезПоследних.ВремяНачала КАК ВремяНачала,
	                      |	торо_СостояниеРаботыСотрудниковСрезПоследних.ВремяОкончания КАК ВремяОкончания,
	                      |	торо_СостояниеРаботыСотрудниковСрезПоследних.ВремяРаботы / 3600 КАК ВремяРаботы
	                      |ИЗ
	                      |	РегистрСведений.торо_СостояниеРаботыСотрудников.СрезПоследних(
	                      |			,
	                      |			Сотрудник = &Сотрудник
	                      |				И ДатаГрафика = &ДатаГрафика) КАК торо_СостояниеРаботыСотрудниковСрезПоследних
	                      |ГДЕ
	                      |	НЕ торо_СостояниеРаботыСотрудниковСрезПоследних.Отменен
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	торо_СостояниеРаботыСотрудниковСрезПоследних.НомерПериода");
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ДатаГрафика", ДатаГрафика);
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	ПлановаяДоступность = тзРезультат.Итог("ВремяРаботы");
	
	ИнтервалыРабот.Загрузить(тзРезультат);
	

КонецПроцедуры 

#КонецОбласти


