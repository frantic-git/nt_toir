
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции  

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОтчетОНекорректныхЗначениях") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
			"ОтчетОНекорректныхЗначениях", 
			НСтр("ru='Отчет о некорректных целевых значениях показателей KPI.'"), 
			СформироватьПечатнуюФормуОтчетОНекорректныхЗначениях(ПараметрыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуОтчетОНекорректныхЗначениях(ПараметрыКорректировки)
	
	ТабличныйДокумент = Новый ТабличныйДокумент; 

	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ОтчетОНекорректныхЗначениях");

	ПараметрыКорректировки.Вставить("ПолучатьПредставленияПолейЗапроса");
	
	Запрос = Новый Запрос;
		
	Запрос.Текст = ТекстЗапросаНезаполненыеЦелевыеТренды(ПараметрыКорректировки) +
		ОбщегоНазначения.РазделительПакетаЗапросов() +
		ТекстЗапросаНекорректныеЦелевыеЗначения(ПараметрыКорректировки) +
		ОбщегоНазначения.РазделительПакетаЗапросов() +
		ТекстЗапросаОтсутствующиеЦелевыеЗначения(ПараметрыКорректировки);
	
	Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
		Запрос.УстановитьПараметр("Период", ПараметрыКорректировки.Период);		
		Запрос.УстановитьПараметр("ВариантыАнализа", ПараметрыКорректировки.ВариантыАнализа);
		Запрос.УстановитьПараметр("Организации", ПараметрыКорректировки.Организации);
		Запрос.УстановитьПараметр("Подразделения", ПараметрыКорректировки.Подразделения);
		Запрос.УстановитьПараметр("ОбъектыРемонта", ПараметрыКорректировки.ОбъектыРемонта);
	Иначе
		Запрос.УстановитьПараметр("Период", ТекущаяДата());	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ЕстьНезаполненныеТренды = Не РезультатЗапроса[0].Пустой();
	ЕстьНекорректныеЗначения = Не РезультатЗапроса[1].Пустой();
	ЕстьОтсутствующиеЗначения = Не РезультатЗапроса[4].Пустой(); 

	Если Не ЕстьНезаполненныеТренды И Не ЕстьНекорректныеЗначения И Не ЕстьОтсутствующиеЗначения Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("'Не обнаружено некорретных целевых значений показателей KPI.'"));
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	НомерШага = 0;
	
	Если ЕстьНезаполненныеТренды Тогда
		
		НомерШага = НомерШага + 1;
		
		ОбластьШага = Макет.ПолучитьОбласть("НезаполненныеТренды");
		ОбластьШага.Параметры.НомерШага = СтрШаблон("%1.", НомерШага);
		ТабличныйДокумент.Вывести(ОбластьШага);
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаНезаполненныйТренд");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаНезаполненныйТренд");
		
		Выборка = РезультатЗапроса[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбластьСтрокаТаблицы.Параметры.Показатель = Выборка.ПоказательПредставление;
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЕстьНекорректныеЗначения Тогда
		
		НомерШага = НомерШага + 1;
		
		ОбластьШага = Макет.ПолучитьОбласть("НекорректныеЗначения");
		ОбластьШага.Параметры.НомерШага = СтрШаблон("%1.", НомерШага);
		ТабличныйДокумент.Вывести(ОбластьШага);

		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаНекорректныеЗначения");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		НомерСтроки = 1;
		ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаНекорректныеЗначения");
		
		Выборка = РезультатЗапроса[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбластьСтрокаТаблицы.Параметры.НомерСтроки = НомерСтроки;
			ОбластьСтрокаТаблицы.Параметры.Документ = Выборка.ДокументПредставление;
			ОбластьСтрокаТаблицы.Параметры.Показатель = Выборка.ПоказательПредставление;
			ОбластьСтрокаТаблицы.Параметры.ЦелевойТренд = Выборка.ЦелевойТрендПредставление;
			ОбластьСтрокаТаблицы.Параметры.Организация = Выборка.ОрганизацияПредставление;
			ОбластьСтрокаТаблицы.Параметры.Подразделение = Выборка.ПодразделениеПредставление;
			ОбластьСтрокаТаблицы.Параметры.ОбъектРемонта = торо_ЗаполнениеДокументов.ПолучитьПредоставленияОРДляПечати(Выборка.ОбъектРемонта);
			
			Если Выборка.ЦелевойТренд = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон Тогда
				ОбластьСтрокаТаблицы.Параметры.ОбязательноКЗаполеннию = "Минимум, Максимум";
			Иначе 
				ОбластьСтрокаТаблицы.Параметры.ОбязательноКЗаполеннию = "Значение";
			КонецЕсли;
		
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьОтсутствующиеЗначения Тогда
		
		НомерШага = НомерШага + 1;
		
		ОбластьШага = Макет.ПолучитьОбласть("ОтсутствующиеЗначения");
		ОбластьШага.Параметры.НомерШага = СтрШаблон("%1.", НомерШага);
		ТабличныйДокумент.Вывести(ОбластьШага);

		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаОтсутствующиеЗначения");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаОтсутствующиеЗначения");
		
		Выборка = РезультатЗапроса[4].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбластьСтрокаТаблицы.Параметры.Показатель = Выборка.ПоказательПредставление;
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции	

Функция ТекстЗапросаНезаполненыеЦелевыеТренды(ПараметрыКорректировки) Экспорт
		
	Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&ПредставленияПолей КАК ПредставленияПолей,
		|	торо_ВариантыАнализаПоказателейKPI.Владелец КАК Показатель
		|ИЗ
		|	Справочник.торо_ВариантыАнализаПоказателейKPI КАК торо_ВариантыАнализаПоказателейKPI
		|ГДЕ
		|	торо_ВариантыАнализаПоказателейKPI.Ссылка В(&ВариантыАнализа)
		|	И торо_ВариантыАнализаПоказателейKPI.Владелец.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка)";
		
		Если ПараметрыКорректировки.Свойство("ПолучатьПредставленияПолейЗапроса") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "ПРЕДСТАВЛЕНИЕ(торо_ВариантыАнализаПоказателейKPI.Владелец) КАК ПоказательПредставление,");	
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "");
		КонецЕсли;
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	&ПредставленияПолей КАК ПредставленияПолей,
		|	торо_ПоказателиKPI.Ссылка КАК Показатель
		|ИЗ
		|	Справочник.торо_ПоказателиKPI КАК торо_ПоказателиKPI
		|ГДЕ
		|	торо_ПоказателиKPI.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка)";
		
		Если ПараметрыКорректировки.Свойство("ПолучатьПредставленияПолейЗапроса") Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "ПРЕДСТАВЛЕНИЕССЫЛКИ(торо_ПоказателиKPI.Ссылка) КАК ПоказательПредставление,");	
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "");
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаНекорректныеЦелевыеЗначения(ПараметрыКорректировки, ОтборПоПоказателю = Ложь) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	&ПредставленияПолей КАК ПредставленияПолей,
	|	торо_ЦелевыеЗначенияKPIСрезПоследних.Регистратор КАК Документ,
	|	торо_ЦелевыеЗначенияKPIСрезПоследних.Показатель КАК Показатель,
	|	торо_ЦелевыеЗначенияKPIСрезПоследних.Организация КАК Организация,
	|	торо_ЦелевыеЗначенияKPIСрезПоследних.Подразделение КАК Подразделение,
	|	торо_ЦелевыеЗначенияKPIСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	|	ВЫБОР
	|		КОГДА &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|			ТОГДА 0
	|		ИНАЧЕ торо_ЦелевыеЗначенияKPIСрезПоследних.Значение
	|	КОНЕЦ КАК Значение,
	|	ВЫБОР
	|		КОГДА &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|			ТОГДА 0
	|		ИНАЧЕ торо_ЦелевыеЗначенияKPIСрезПоследних.КритическоеЗначение
	|	КОНЕЦ КАК КритическоеЗначение,
	|	ВЫБОР
	|		КОГДА НЕ &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|			ТОГДА 0
	|		ИНАЧЕ торо_ЦелевыеЗначенияKPIСрезПоследних.МинимальноеЗначение
	|	КОНЕЦ КАК МинимальноеЗначение,
	|	ВЫБОР
	|		КОГДА НЕ &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|			ТОГДА 0
	|		ИНАЧЕ торо_ЦелевыеЗначенияKPIСрезПоследних.КритическийМинимум
	|	КОНЕЦ КАК КритическийМинимум,
	|	ВЫБОР
	|		КОГДА НЕ &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|			ТОГДА 0
	|		ИНАЧЕ торо_ЦелевыеЗначенияKPIСрезПоследних.МаксимальноеЗначение
	|	КОНЕЦ КАК МаксимальноеЗначение,
	|	ВЫБОР
	|		КОГДА НЕ &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|			ТОГДА 0
	|		ИНАЧЕ торо_ЦелевыеЗначенияKPIСрезПоследних.КритическийМаксимум
	|	КОНЕЦ КАК КритическийМаксимум,
	|	&ПолеЦелевойТренд КАК ЦелевойТренд
	|ИЗ
	|	РегистрСведений.торо_ЦелевыеЗначенияKPI.СрезПоследних(&Период, &УсловныеОтборы) КАК торо_ЦелевыеЗначенияKPIСрезПоследних
	|ГДЕ
	|	торо_ЦелевыеЗначенияKPIСрезПоследних.ДатаПО > &Период
	|	И ВЫБОР
	|			КОГДА &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|				ТОГДА торо_ЦелевыеЗначенияKPIСрезПоследних.МинимальноеЗначение = 0
	|						ИЛИ торо_ЦелевыеЗначенияKPIСрезПоследних.МаксимальноеЗначение = 0
	|			КОГДА &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Минимизация)
	|					ИЛИ &ПолеЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Максимизация)
	|				ТОГДА торо_ЦелевыеЗначенияKPIСрезПоследних.Значение = 0
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ";

	
	Если ПараметрыКорректировки.Свойство("ПолучатьПредставленияПолейЗапроса") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", 
			"ПРЕДСТАВЛЕНИЕССЫЛКИ(торо_ЦелевыеЗначенияKPIСрезПоследних.Регистратор) КАК ДокументПредставление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(торо_ЦелевыеЗначенияKPIСрезПоследних.Показатель) КАК ПоказательПредставление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(торо_ЦелевыеЗначенияKPIСрезПоследних.Организация) КАК ОрганизацияПредставление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(торо_ЦелевыеЗначенияKPIСрезПоследних.Подразделение) КАК ПодразделениеПредставление,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(&ПолеЦелевойТренд) КАК ЦелевойТрендПредставление,");	
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "");
	КонецЕсли;
	
	Если Не ПараметрыКорректировки.КорректировкаПриЗаписиПоказателя Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеЦелевойТренд", "торо_ЦелевыеЗначенияKPIСрезПоследних.Показатель.ЦелевойТренд");		
	КонецЕсли;
	
	Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
		
		УсловныйОтбор = "Показатель В
		|	(ВЫБРАТЬ
		|		торо_ВариантыАнализаПоказателейKPI.Владелец
		|	ИЗ
		|		Справочник.торо_ВариантыАнализаПоказателейKPI КАК торо_ВариантыАнализаПоказателейKPI 
		|	ГДЕ торо_ВариантыАнализаПоказателейKPI.Ссылка В(&ВариантыАнализа))";
		
		Если ПараметрыКорректировки.ОтборПоОрганизации Тогда
			УсловныйОтбор = УсловныйОтбор + "И Организация В (&Организации, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";		
		КонецЕсли;
		
		Если ПараметрыКорректировки.ОтборПоПодразделению Тогда
			УсловныйОтбор = УсловныйОтбор + "И Подразделение В (&Подразделения, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяССылка))";
		КонецЕсли;
		
		Если ПараметрыКорректировки.ОтборПоОР Тогда
			УсловныйОтбор = УсловныйОтбор + "И ОбъектРемонта В (&ОбъектыРемонта, ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка))";
		КонецЕсли;				
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловныеОтборы", УсловныйОтбор);
		
	ИначеЕсли ОтборПоПоказателю Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловныеОтборы", "Показатель = &Показатель");
		
	Иначе
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловныеОтборы", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстЗапросаОтсутствующиеЦелевыеЗначения(ПараметрыКорректировки) Экспорт
	
	Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	торо_ВариантыАнализаПоказателейKPI.Владелец КАК Показатель,
		|	торо_ВариантыАнализаПоказателейKPI.Владелец.ЦелевойТренд КАК ЦелевойТренд
		|ПОМЕСТИТЬ ВТ_ПоказателиТрендДиапазон
		|ИЗ
		|	Справочник.торо_ВариантыАнализаПоказателейKPI КАК торо_ВариантыАнализаПоказателейKPI
		|ГДЕ
		|	торо_ВариантыАнализаПоказателейKPI.Ссылка В(&ВариантыАнализа)
		|	И торо_ВариантыАнализаПоказателейKPI.Владелец.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Показатель";
				
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	торо_ПоказателиKPI.Ссылка КАК Показатель,
		|	торо_ПоказателиKPI.ЦелевойТренд КАК ЦелевойТренд
		|ПОМЕСТИТЬ ВТ_ПоказателиТрендДиапазон
		|ИЗ
		|	Справочник.торо_ПоказателиKPI КАК торо_ПоказателиKPI
		|ГДЕ
		|	торо_ПоказателиKPI.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Показатель";
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	торо_ЦелевыеЗначенияKPIСрезПоследних.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТ_ЦелевыеЗначенияПоказателейТрендаДиапазон
		|ИЗ
		|	РегистрСведений.торо_ЦелевыеЗначенияKPI.СрезПоследних(&Период, &УсловныйОтбор) КАК торо_ЦелевыеЗначенияKPIСрезПоследних
		|ГДЕ
		|	торо_ЦелевыеЗначенияKPIСрезПоследних.ДатаПО > &Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Показатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&ПредставленияПолей КАК ПредставленияПолей,
		|	ВТ_ПоказателиТрендДиапазон.Показатель КАК Показатель,
		|	ВТ_ПоказателиТрендДиапазон.ЦелевойТренд КАК ЦелевойТренд
		|ИЗ
		|	ВТ_ПоказателиТрендДиапазон КАК ВТ_ПоказателиТрендДиапазон
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦелевыеЗначенияПоказателейТрендаДиапазон КАК ВТ_ЦелевыеЗначенияПоказателейТрендаДиапазон
		|		ПО ВТ_ПоказателиТрендДиапазон.Показатель = ВТ_ЦелевыеЗначенияПоказателейТрендаДиапазон.Показатель
		|ГДЕ
		|	ВТ_ЦелевыеЗначенияПоказателейТрендаДиапазон.Показатель ЕСТЬ NULL";
	
	Если ПараметрыКорректировки.Свойство("ПолучатьПредставленияПолейЗапроса") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТ_ПоказателиТрендДиапазон.Показатель) КАК ПоказательПредставление,");	
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредставленияПолей КАК ПредставленияПолей,", "");
	КонецЕсли;
	
	Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
		
		УсловныйОтбор = "Показатель В
		|	(ВЫБРАТЬ
		|			ВТ_ПоказателиТрендДиапазон.Показатель КАК Показатель
		|	ИЗ
		|			ВТ_ПоказателиТрендДиапазон КАК ВТ_ПоказателиТрендДиапазон)";
		
		Если ПараметрыКорректировки.ОтборПоОрганизации Тогда
			УсловныйОтбор = УсловныйОтбор + "И Организация В (&Организации, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";		
		КонецЕсли;
		
		Если ПараметрыКорректировки.ОтборПоПодразделению Тогда
			УсловныйОтбор = УсловныйОтбор + "И Подразделение В (&Подразделения, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))";
		КонецЕсли;
		
		Если ПараметрыКорректировки.ОтборПоОР Тогда
			УсловныйОтбор = УсловныйОтбор + "И ОбъектРемонта В (&ОбъектыРемонта, ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка))";
		КонецЕсли;	 
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловныйОтбор", УсловныйОтбор);
		
	Иначе
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловныйОтбор", "Показатель В
		|	(ВЫБРАТЬ
		|			ВТ_ПоказателиТрендДиапазон.Показатель КАК Показатель
		|	ИЗ
		|			ВТ_ПоказателиТрендДиапазон КАК ВТ_ПоказателиТрендДиапазон)");		
	КонецЕсли; 
	
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли



