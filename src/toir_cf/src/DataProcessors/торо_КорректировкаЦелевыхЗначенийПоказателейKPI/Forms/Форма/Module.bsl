
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Не Параметры.ПараметрыКорректировки = Неопределено 
		И Параметры.ПараметрыКорректировки.Свойство("КорректировкаПриЗаписиПоказателя") Тогда
		// Открыта при записи показателя KPI.
		
	    ПараметрыКорректировки = Параметры.ПараметрыКорректировки;
		ПараметрыКорректировки.Вставить("КорректировкаПриФормированииМонитораKPI", Ложь);
				
	ИначеЕсли Не Параметры.ПараметрыКорректировки = Неопределено 
		И Параметры.ПараметрыКорректировки.Свойство("КорректировкаПриФормированииМонитораKPI") Тогда
		// Открыта при формировании монитора KPI.

		ПараметрыКорректировки = Параметры.ПараметрыКорректировки;
		ПараметрыКорректировки.Вставить("КорректировкаПриЗаписиПоказателя", Ложь); 
		
	Иначе
		// Независимое открытие.
		
		ПараметрыКорректировки = Новый Структура();
		ПараметрыКорректировки.Вставить("КорректировкаПриЗаписиПоказателя", Ложь);
		ПараметрыКорректировки.Вставить("КорректировкаПриФормированииМонитораKPI", Ложь);
	КонецЕсли;
	
	ПравоРедактирования = ПравоДоступа("Редактирование", Метаданные.Справочники.торо_ПоказателиKPI)
		И ПравоДоступа("Редактирование", Метаданные.Документы.торо_УстановкаЦелевыхЗначенийПоказателейKPI);
	
	ОпределитьНовыйШагИЗаполнитьДанные();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеСтраницамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПроведенаУстановкаЦелевыхЗначений" Тогда
		
		ОбработатьПроведениеУстановкиЦелевыхЗначений(Параметр);
		
	ИначеЕсли ИмяСобытия = "ИзмененПоказательKPI" Тогда
		
		ОбработатьИзменениеПоказателя(Параметр);
		
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНезаполненныйТренд 

&НаКлиенте
Процедура НезаполненныйТрендВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандарнтнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если Поле = Элементы.НезаполненныйТрендПоказатель Тогда
		ПоказатьЗначение(, ТекущиеДанные.Показатель);
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНекорректныеЗначения 

&НаКлиенте
Процедура НекорректныеЗначенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандарнтнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если Поле = Элементы.НекорректныеЗначенияПоказатель Тогда
		ПоказатьЗначение(, ТекущиеДанные.Показатель); 
	ИначеЕсли Поле = Элементы.НекорректныеЗначенияОрганизация Тогда
		ПоказатьЗначение(, ТекущиеДанные.Организация);
	ИначеЕсли Поле = Элементы.НекорректныеЗначенияПодразделение Тогда
		ПоказатьЗначение(, ТекущиеДанные.Подразделение); 
	ИначеЕсли Поле = Элементы.НекорректныеЗначенияОбъектРемонта Тогда
		ПоказатьЗначение(, ТекущиеДанные.ОбъектРемонта); 
	ИначеЕсли Поле = Элементы.НекорректныеЗначенияДокумент Тогда
		ПоказатьЗначение(, ТекущиеДанные.Документ); 
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура НекорректныеЗначенияКритическоеЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НекорректныеЗначения.ТекущиеДанные;
		
	Если УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) Тогда
		ТекущиеДанные.КритическоеЗначение = 0;
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура НекорректныеЗначенияКритическийМинимумПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НекорректныеЗначения.ТекущиеДанные;
	
	Если УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) Тогда
		ТекущиеДанные.КритическийМинимум = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НекорректныеЗначенияКритическийМаксимумПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НекорректныеЗначения.ТекущиеДанные;
		
	Если УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) Тогда
		ТекущиеДанные.КритическийМаксимум = 0;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НекорректныеЗначенияЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НекорректныеЗначения.ТекущиеДанные;
		
	Если УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) Тогда
		ТекущиеДанные.КритическоеЗначение = 0;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НекорректныеЗначенияМинимальноеЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НекорректныеЗначения.ТекущиеДанные;
	
	Если УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) Тогда
		ТекущиеДанные.КритическийМинимум = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НекорректныеЗначенияМаксимальноеЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НекорректныеЗначения.ТекущиеДанные;
		
	Если УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) Тогда
		ТекущиеДанные.КритическийМаксимум = 0;
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтсутствующиеЗначения 

&НаКлиенте
Процедура ОтсутствующиеЗначенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	СтандарнтнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если Поле = Элементы.ОтсутствующиеЗначенияПоказатель Тогда
		ПоказатьЗначение(, ТекущиеДанные.Показатель);
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ОтсутствующиеЗначенияЦелевойТрендПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОтсутствующиеЗначения.ТекущиеДанные;
	
	ТекущиеДанные.Исправлено = Не ТекущиеДанные.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон")
		И Не ТекущиеДанные.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	торо_Печать.НапечататьДокумент(
		"Обработка.торо_КорректировкаЦелевыхЗначенийПоказателейKPI",
		"ОтчетОНекорректныхЗначениях", 
		ПредопределенноеЗначение("Документ.торо_УстановкаЦелевыхЗначенийПоказателейKPI.ПустаяСсылка"), 
		ПараметрыКорректировки);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийШаг(Команда)
		
	Если ТекущийШаг = 0 Тогда 
		Закрыть(); 
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "";
	
	Если Не ПравоРедактирования Тогда
		// Проверки не нужны.	
	ИначеЕсли ТекущийШаг = 1 Тогда
		
		Для Каждого Строка Из НезаполненныйТренд Цикл
			
			Если Не ЗначениеЗаполнено(Строка.ЦелевойТренд) Тогда
				ТекстВопроса = НСтр("ru = 'В таблице остались показатели с незаполенным целевым трендом. Продолжить?'");	
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
	ИначеЕсли ТекущийШаг = 2 Тогда
		
		Для Каждого Строка Из НекорректныеЗначения Цикл
			
			Если Строка.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон")
				И (Строка.МинимальноеЗначение = 0 ИЛИ Строка.МаксимальноеЗначение = 0)
				ИЛИ Не Строка.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон")
				И Строка.Значение = 0 Тогда  
				
				Если ПараметрыКорректировки.КорректировкаПриЗаписиПоказателя Тогда
					ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'В таблице остались строки с некорректно заполненными целевыми значениями!'"));	
					Возврат;
				Иначе
					ТекстВопроса = НСтр("ru = 'В таблице остались показатели с некорректно заполненными целевыми значениями. Продолжить?'");
				КонецЕсли;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
	ИначеЕсли ТекущийШаг = 3 Тогда
		
		Для Каждого Строка Из ОтсутствующиеЗначения Цикл
			
			Если Не ЗначениеЗаполнено(Строка.ЦелевойТренд) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'В таблице есть строки с незаполненным целевым трендом!'"));	
				Возврат;
			КонецЕсли;	
			
			Если Не Строка.Исправлено Тогда
				ТекстВопроса = НСтр("ru = 'В таблице остались показатели с отсутствующими целевыми значениями. Продолжить?'");	
				Прервать;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("СледующийШагОбработкаОтвета", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		СледующийШагПродолжение();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьЦелевыеЗначения(Команда)
	
	ПоказателиКУстановкеЗначений = Новый Массив;
	
	Для Каждого Строка Из ОтсутствующиеЗначения Цикл
		
		Если Не Строка.Исправлено И Строка.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон") Тогда
			ПоказателиКУстановкеЗначений.Добавить(Строка.Показатель);
		КонецЕсли;
	КонецЦикла;
	
	Если ПоказателиКУстановкеЗначений.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'В таблице нет показателей, требующих обязательной установки целевых значений.'"));
		Возврат;
	КонецЕсли;	
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("Показатели", ПоказателиКУстановкеЗначений);
	Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
		// В таком случае необходимо, чтобы присутствовало целевое значение на конец периода формирования отчета.
		ЗначенияЗаполнения.Вставить("Дата", ПараметрыКорректировки.Период);
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);

	ОткрытьФорму("Документ.торо_УстановкаЦелевыхЗначенийПоказателейKPI.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция УстановленыНекорректноеКритическоеЗначениеВСтроке(ТекущиеДанные) 
	
	Если ТекущиеДанные.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Максимизация")
		ИЛИ ТекущиеДанные.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.ПустаяСсылка") Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.КритическоеЗначение) И ТекущиеДанные.КритическоеЗначение >= ТекущиеДанные.Значение Тогда
			ТекстСообщения = НСтр("ru = 'При установленном целевом тренде ""максимизация"" критическое значение должно быть меньше целевого значения.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Минимизация") Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.КритическоеЗначение) И ТекущиеДанные.КритическоеЗначение >= ТекущиеДанные.Значение Тогда
			ТекстСообщения = НСтр("ru = 'При установленном целевом тренде ""минимизация"" критическое значение должно быть больше целевого значения.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.ЦелевойТренд = ПредопределенноеЗначение("Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон") Тогда	
		
		Если ЗначениеЗаполнено(ТекущиеДанные.КритическийМинимум) И ТекущиеДанные.КритическийМинимум >= ТекущиеДанные.МинимальноеЗначение Тогда
			ТекстСообщения = НСтр("ru = 'При установленном целевом тренде ""диапазон"" критический минимум должен быть меньше минимального целевого значения.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.КритическийМаксимум) И ТекущиеДанные.КритическийМаксимум <= ТекущиеДанные.МаксимальноеЗначение Тогда
			ТекстСообщения = НСтр("ru = 'При установленном целевом тренде ""диапазон"" критический максимум должен быть больше минимального целевого значения.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат Истина;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ОпределитьНовыйШагИЗаполнитьДанные()
	
	Если ТекущийШаг = 3 Тогда
		ТекущийШаг = 0;
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	
	Если ПараметрыКорректировки.КорректировкаПриЗаписиПоказателя И ТекущийШаг = 0 Тогда 
		// Форма открыта перед записью справочника "торо_ПоказателиKPI". 
		
		Запрос.Текст = Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаНекорректныеЦелевыеЗначения(ПараметрыКорректировки, Истина);
		Запрос.УстановитьПараметр("Показатель", ПараметрыКорректировки.Показатель);
		Запрос.УстановитьПараметр("ПолеЦелевойТренд", ПараметрыКорректировки.ЦелевойТренд);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ЕстьНекорректныеЗначения = Не РезультатЗапроса.Пустой();
		
		ПорядковыйНомерШага = ЕстьНекорректныеЗначения;
		КоличествоШагов = ЕстьНекорректныеЗначения;

		Если ЕстьНекорректныеЗначения Тогда
			
			ВыборкаИсточник = РезультатЗапроса.Выбрать();
			ТаблицаПриемник = НекорректныеЗначения;
			
			ТекущийШаг = 2;
						
		Иначе
			ТекущийШаг = 0;
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ПараметрыКорректировки.КорректировкаПриЗаписиПоказателя И ТекущийШаг = 2 Тогда
		// Дальнейшие шаги не требуются.
		
		ТекущийШаг = 0;
		Возврат;
	
	ИначеЕсли ТекущийШаг = 0 Тогда 
		// Первое открытие формы.
		
		Запрос.Текст = Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаНезаполненыеЦелевыеТренды(ПараметрыКорректировки) +
			ОбщегоНазначения.РазделительПакетаЗапросов() +
			Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаНекорректныеЦелевыеЗначения(ПараметрыКорректировки) +
			ОбщегоНазначения.РазделительПакетаЗапросов() +
			Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаОтсутствующиеЦелевыеЗначения(ПараметрыКорректировки);
			
		Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
			Запрос.УстановитьПараметр("Период", ПараметрыКорректировки.Период);		
			Запрос.УстановитьПараметр("ВариантыАнализа", ПараметрыКорректировки.ВариантыАнализа);
            Запрос.УстановитьПараметр("Организации", ПараметрыКорректировки.Организации);
			Запрос.УстановитьПараметр("Подразделения", ПараметрыКорректировки.Подразделения);
			Запрос.УстановитьПараметр("ОбъектыРемонта", ПараметрыКорректировки.ОбъектыРемонта);
		КонецЕсли;
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ЕстьНезаполненныеТренды = Не РезультатЗапроса[0].Пустой();
		ЕстьНекорректныеЗначения = Не РезультатЗапроса[1].Пустой();
		ЕстьОтсутствующиеЗначения = Не РезультатЗапроса[4].Пустой(); 
		
		ПорядковыйНомерШага = 1;
		КоличествоШагов = ЕстьНезаполненныеТренды + ЕстьНекорректныеЗначения + ЕстьОтсутствующиеЗначения;
		
		Если ЕстьНезаполненныеТренды Тогда   
			
			ВыборкаИсточник = РезультатЗапроса[0].Выбрать();
			ТаблицаПриемник = НезаполненныйТренд; 
			ТекущийШаг = 1;
			
		ИначеЕсли ЕстьНекорректныеЗначения Тогда
			
			ВыборкаИсточник = РезультатЗапроса[1].Выбрать();
			ТаблицаПриемник = НекорректныеЗначения;
			ТекущийШаг = 2;
			
		ИначеЕсли ЕстьОтсутствующиеЗначения Тогда 
			
			ВыборкаИсточник = РезультатЗапроса[4].Выбрать();
			ТаблицаПриемник = ОтсутствующиеЗначения;
			ТекущийШаг = 3;
			
		Иначе
			
			ТекущийШаг = 0;
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ТекущийШаг = 1 Тогда
		
		Запрос.Текст = Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаНекорректныеЦелевыеЗначения(ПараметрыКорректировки) +
			ОбщегоНазначения.РазделительПакетаЗапросов() +
            Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаОтсутствующиеЦелевыеЗначения(ПараметрыКорректировки);
			
		Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
			Запрос.УстановитьПараметр("Период", ПараметрыКорректировки.Период);		
			Запрос.УстановитьПараметр("ВариантыАнализа", ПараметрыКорректировки.ВариантыАнализа);
            Запрос.УстановитьПараметр("Организации", ПараметрыКорректировки.Организации);
			Запрос.УстановитьПараметр("Подразделения", ПараметрыКорректировки.Подразделения);
			Запрос.УстановитьПараметр("ОбъектыРемонта", ПараметрыКорректировки.ОбъектыРемонта);
		КонецЕсли;	
			
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ЕстьНекорректныеЗначения = Не РезультатЗапроса[0].Пустой();
		ЕстьОтсутствующиеЗначения = Не РезультатЗапроса[3].Пустой(); 
		
		КоличествоШагов = 1 + ЕстьНекорректныеЗначения + ЕстьОтсутствующиеЗначения;	
		ПорядковыйНомерШага = ПорядковыйНомерШага + 1;
		
		Если ЕстьНекорректныеЗначения Тогда
			
			ВыборкаИсточник = РезультатЗапроса[0].Выбрать();
			ТаблицаПриемник = НекорректныеЗначения;
			ТекущийШаг = 2;
			
		ИначеЕсли ЕстьОтсутствующиеЗначения Тогда 
			
			ВыборкаИсточник = РезультатЗапроса[3].Выбрать();
			ТаблицаПриемник = ОтсутствующиеЗначения;
			ТекущийШаг = 3;
			
		Иначе
			
			ТекущийШаг = 0;
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ТекущийШаг = 2 Тогда
		
		Запрос.Текст = Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаОтсутствующиеЦелевыеЗначения(ПараметрыКорректировки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если ПараметрыКорректировки.КорректировкаПриФормированииМонитораKPI Тогда
			Запрос.УстановитьПараметр("Период", ПараметрыКорректировки.Период);		
			Запрос.УстановитьПараметр("ВариантыАнализа", ПараметрыКорректировки.ВариантыАнализа);
            Запрос.УстановитьПараметр("Организации", ПараметрыКорректировки.Организации);
			Запрос.УстановитьПараметр("Подразделения", ПараметрыКорректировки.Подразделения);
			Запрос.УстановитьПараметр("ОбъектыРемонта", ПараметрыКорректировки.ОбъектыРемонта);
		КонецЕсли;
		
		ЕстьОтсутствующиеЗначения = Не РезультатЗапроса.Пустой(); 
		
		Если ПорядковыйНомерШага = КоличествоШагов Тогда // Предварительно последний шаг.
			КоличествоШагов = КоличествоШагов + ЕстьОтсутствующиеЗначения;
		КонецЕсли;	
	    ПорядковыйНомерШага = ПорядковыйНомерШага + 1;

		Если ЕстьОтсутствующиеЗначения Тогда 
			
			ВыборкаИсточник = РезультатЗапроса.Выбрать();
			ТаблицаПриемник = ОтсутствующиеЗначения;
			ТекущийШаг = 3;
			
		Иначе
			
			ТекущийШаг = 0;
			Возврат;
		КонецЕсли;	
	КонецЕсли;	

	Пока ВыборкаИсточник.Следующий() Цикл
		НоваяСтрока = ТаблицаПриемник.Добавить();	
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаИсточник);
	КонецЦикла;	
		
КонецПроцедуры	

&НаКлиенте
Процедура УправлениеСтраницамиФормы()
	
	Элементы.СтраницыШаги.ТолькоПросмотр = Не ПравоРедактирования;
	Элементы.НадписьНетПравРедактирования.Видимость = Не ПравоРедактирования;	
	
	Если ТекущийШаг = 0 Тогда // Ошибок не найдено.
		
		Элементы.СтраницыШаги.Видимость = Ложь;
		Элементы.СтраницыИнформация.ТекущаяСтраница = Элементы.СтраницаОшибокНеОбнаружено;
		Элементы.СледующийШаг.Заголовок = НСтр("ru = 'Закрыть'");
		
		Возврат;
	КонецЕсли;
	
	ОбозначениеШага = СтрШаблон(НСтр("ru = 'Шаг %1/%2. '"), ПорядковыйНомерШага, КоличествоШагов);
	
	Если ПараметрыКорректировки.КорректировкаПриЗаписиПоказателя Тогда 
		// Форма открыта перед записью справочника "торо_ПоказателиKPI".
		
		Элементы.СтраницыИнформация.ТекущаяСтраница = Элементы.СтраницаОбнаруженыОшибки;
		Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаНекорректныеЗначения;
				
		НаименованиеШага = НСтр("ru = 'Некорректные целевые значения.'");
		Элементы.СледующийШаг.Заголовок = НСтр("ru = 'Завершить'");
		Элементы.Печать.Видимость = Ложь;
		
	ИначеЕсли ТекущийШаг = 1 Тогда
		
		Элементы.СтраницыИнформация.ТекущаяСтраница = Элементы.СтраницаОбнаруженыОшибки;
		Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаОтсутствующийТренд;
		
		НаименованиеШага = НСтр("ru = 'Незаполненный целевой тренд.'");
		
	ИначеЕсли ТекущийШаг = 2 Тогда
		
		Элементы.СтраницыИнформация.ТекущаяСтраница = Элементы.СтраницаОбнаруженыОшибки;
		Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаНекорректныеЗначения;
		
		НаименованиеШага = НСтр("ru = 'Некорректные целевые значения.'");
	
	ИначеЕсли ТекущийШаг = 3 Тогда	
		
		Элементы.СтраницыИнформация.ТекущаяСтраница = Элементы.СтраницаОбнаруженыОшибки;
		Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаОтсутствующиеЗначения;
		
		НаименованиеШага = НСтр("ru = 'Отсутствующие целевые значения.'");
	КонецЕсли;
	
	Если ПорядковыйНомерШага = КоличествоШагов Тогда
		Элементы.СледующийШаг.Заголовок = НСтр("ru = 'Завершить'");
	Иначе
		Элементы.СледующийШаг.Заголовок = НСтр("ru = 'Следующий шаг >'");	
	КонецЕсли;	
	
	ПолужирныйШрифт = Новый Шрифт(,, Истина);
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(ОбозначениеШага, ПолужирныйШрифт));
	ЧастиСтроки.Добавить(НаименованиеШага);
	
	Элементы.НадписьИнформацияОШаге.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтроки);
		
КонецПроцедуры	

&НаКлиенте
Процедура СледующийШагОбработкаОтвета(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СледующийШагПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийШагПродолжение()
	
	ПредварительноПоследнийШаг = ПорядковыйНомерШага = КоличествоШагов;
	
	ОбработатьПереходНаСледующийШаг();
	
	Если ТекущийШаг = 0 Тогда // Выполнен последний шаг.
		
		Если ПараметрыКорректировки.КорректировкаПриЗаписиПоказателя Тогда
			ПараметрыЗакрытия = Новый Структура("КорректировкаЗначенийВыполнена", Истина);
			Закрыть(ПараметрыЗакрытия);
		Иначе	
			Закрыть();
		КонецЕсли;
	Иначе  
		
		Если ПредварительноПоследнийШаг Тогда // В процессе перехода обнаружены еще ошибки. 
			ПоказатьПредупреждение(, НСтр("ru = 'Обнаружены новые ошибки. Количество шагов увеличено.'"));
		КонецЕсли;
		
		УправлениеСтраницамиФормы();
	КонецЕсли;	
		
КонецПроцедуры	

&НаСервере 
Процедура ОбработатьПереходНаСледующийШаг()
	
	Если ТекущийШаг = 1 Тогда
		
    	ЗаполнитьЦелевойТрендВПоказателях();	 	
		
	ИначеЕсли ТекущийШаг = 2 Тогда
		
    	ИсправитьНекорректныеЦелевыеЗначения();
		
	ИначеЕсли ТекущийШаг = 3 Тогда
		
		ИсправитьОтсутствующиеЦелевыеЗначения();
		
	КонецЕсли;	
	
	ОпределитьНовыйШагИЗаполнитьДанные();
	
КонецПроцедуры	

&НаСервере 
Процедура ЗаполнитьЦелевойТрендВПоказателях()
	
	Для Каждого Строка Из НезаполненныйТренд Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ЦелевойТренд) Тогда
			Продолжить;
		КонецЕсли;
		
		ПоказательОбъект = Строка.Показатель.ПолучитьОбъект();
		ПоказательОбъект.ЦелевойТренд = Строка.ЦелевойТренд;
		
		Попытка
			ПоказательОбъект.Записать();
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при записи показателя KPI.'"),
				УровеньЖурналаРегистрации.Ошибка,,
				ПоказательОбъект.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ШаблонСообщения = "ru = 'Не удалось заполнить целевой тренд в показателе ""%1"". Подробнее в журнале регистрации.'";	
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПоказательОбъект.Ссылка);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;	
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Процедура ИсправитьНекорректныеЦелевыеЗначения() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НекорректныеЦелевыеЗначения.Показатель КАК Показатель,
	|	НекорректныеЦелевыеЗначения.ЦелевойТренд КАК ЦелевойТренд,
	|	НекорректныеЦелевыеЗначения.Организация КАК Организация,
	|	НекорректныеЦелевыеЗначения.Подразделение КАК Подразделение,
	|	НекорректныеЦелевыеЗначения.ОбъектРемонта КАК ОбъектРемонта,
	|	НекорректныеЦелевыеЗначения.Значение КАК Значение,
	|	НекорректныеЦелевыеЗначения.КритическоеЗначение КАК КритическоеЗначение,
	|	НекорректныеЦелевыеЗначения.МаксимальноеЗначение КАК МаксимальноеЗначение,
	|	НекорректныеЦелевыеЗначения.КритическийМинимум КАК КритическийМинимум,
	|	НекорректныеЦелевыеЗначения.МинимальноеЗначение КАК МинимальноеЗначение,
	|	НекорректныеЦелевыеЗначения.КритическийМаксимум КАК КритическийМаксимум,
	|	НекорректныеЦелевыеЗначения.Документ КАК Документ
	|ПОМЕСТИТЬ НекорректныеЗначения
	|ИЗ
	|	&НекорректныеЦелевыеЗначения КАК НекорректныеЦелевыеЗначения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НекорректныеЗначения.Показатель КАК Показатель,
	|	НекорректныеЗначения.ЦелевойТренд КАК ЦелевойТренд,
	|	НекорректныеЗначения.Организация КАК Организация,
	|	НекорректныеЗначения.Подразделение КАК Подразделение,
	|	НекорректныеЗначения.ОбъектРемонта КАК ОбъектРемонта,
	|	НекорректныеЗначения.Значение КАК Значение,
	|	НекорректныеЗначения.КритическоеЗначение КАК КритическоеЗначение,
	|	НекорректныеЗначения.МаксимальноеЗначение КАК МаксимальноеЗначение,
	|	НекорректныеЗначения.КритическийМинимум КАК КритическийМинимум,
	|	НекорректныеЗначения.МинимальноеЗначение КАК МинимальноеЗначение,
	|	НекорректныеЗначения.КритическийМаксимум КАК КритическийМаксимум,
	|	НекорректныеЗначения.Документ КАК Документ
	|ИЗ
	|	НекорректныеЗначения КАК НекорректныеЗначения
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НекорректныеЗначения.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон)
	|				ТОГДА НекорректныеЗначения.МинимальноеЗначение <> 0
	|						И НекорректныеЗначения.МаксимальноеЗначение <> 0
	|			КОГДА НекорректныеЗначения.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Минимизация)
	|					ИЛИ НекорректныеЗначения.ЦелевойТренд = ЗНАЧЕНИЕ(Перечисление.торо_ВидыЦелевыхТрендовПоказателейKPI.Максимизация)
	|				ТОГДА НекорректныеЗначения.Значение <> 0
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|ИТОГИ ПО
	|	Документ";

	Запрос.УстановитьПараметр("НекорректныеЦелевыеЗначения", НекорректныеЗначения.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДокумент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СтруктураОтбора = Новый Структура("Показатель, Организация, Подразделение, ОбъектРемонта");

	Пока ВыборкаДокумент.Следующий() Цикл
		
		ДокументОбъект = ВыборкаДокумент.Документ.ПолучитьОбъект();
		
		Выборка = ВыборкаДокумент.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, Выборка);
			КлючевыеПоказателиОтбор = ДокументОбъект.КлючевыеПоказатели.НайтиСтроки(СтруктураОтбора);
			
			Если КлючевыеПоказателиОтбор.Количество() > 0 Тогда
				ИсправляемаяСтрока = КлючевыеПоказателиОтбор[0];
				ЗаполнитьЗначенияСвойств(ИсправляемаяСтрока, Выборка);
			КонецЕсли;	
		КонецЦикла;	
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при проведении документа установки целевых значений показателей KPI.'"),
				УровеньЖурналаРегистрации.Ошибка,,
				ДокументОбъект.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ШаблонСообщения = "ru = 'Не удалось провести документ ""%1"". Подробнее в журнале регистрации.'";	
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ДокументОбъект.Ссылка);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры	

&НаСервере
Процедура ИсправитьОтсутствующиеЦелевыеЗначения()
	
	Для Каждого Строка Из ОтсутствующиеЗначения Цикл
		
		Если Строка.ЦелевойТренд = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон Тогда
			Продолжить;
		КонецЕсли;
		
		ПоказательОбъект = Строка.Показатель.ПолучитьОбъект();
		ПоказательОбъект.ЦелевойТренд = Строка.ЦелевойТренд;
		
		Попытка
			ПоказательОбъект.Записать();
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при записи показателя KPI.'"),
				УровеньЖурналаРегистрации.Ошибка,,
				ПоказательОбъект.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ШаблонСообщения = "ru = 'Не удалось изменить целевой тренд в показателе ""%1"". Подробнее в журнале регистрации.'";	
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПоказательОбъект.Ссылка);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПроведениеУстановкиЦелевыхЗначений(ДокументУстановки)
	
	
	Если ТекущийШаг = 2 Тогда
		// Если удалось провести документ, присутствующий в таблице, то некорретные
		// значения исправлены. Удалим такие строки.
		
		СтруктураОтбора = Новый Структура("Документ", ДокументУстановки);
		НекорректныеЗначенияОтбор = НекорректныеЗначения.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого Строка Из НекорректныеЗначенияОтбор Цикл
			
			НекорректныеЗначения.Удалить(Строка);
		КонецЦикла;		
		
	ИначеЕсли ТекущийШаг = 3 Тогда
		// Установим пометку об исправлении и запретим к изменению целевой тренд.
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	торо_УстановкаЦелевыхЗначенийПоказателейKPIКлючевыеПоказатели.Показатель КАК Показатель
		|ИЗ
		|	Документ.торо_УстановкаЦелевыхЗначенийПоказателейKPI.КлючевыеПоказатели КАК торо_УстановкаЦелевыхЗначенийПоказателейKPIКлючевыеПоказатели
		|ГДЕ
		|	торо_УстановкаЦелевыхЗначенийПоказателейKPIКлючевыеПоказатели.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументУстановки);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		СтруктураОтбора = Новый Структура("Показатель");
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, Выборка);
			
			ОтсутствующиеЗначенияОтбор = ОтсутствующиеЗначения.НайтиСтроки(СтруктураОтбора);
			Если ОтсутствующиеЗначенияОтбор.Количество() > 0 Тогда
				ОтсутствующиеЗначенияОтбор[0].Исправлено = Истина;
				ОтсутствующиеЗначенияОтбор[0].ЦелевойТрендТолькоПросмотр = Истина;
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ОбработатьИзменениеПоказателя(Показатель)
	
	ЦелевойТрендПоказателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Показатель, "ЦелевойТренд");
	
	Если ТекущийШаг = 1 И ЗначениеЗаполнено(ЦелевойТрендПоказателя) Тогда
		// Целевой тренд в показателе стал заполненным. Удалим строку.
		
		НезаполненныйТрендОтбор = НезаполненныйТренд.НайтиСтроки(Новый Структура("Показатель", Показатель));
		Если НезаполненныйТрендОтбор.Количество() > 0 Тогда
			
			УдаляемаяСтрока = НезаполненныйТрендОтбор[0];
			
			НезаполненныйТренд.Удалить(УдаляемаяСтрока);
		КонецЕсли;
		
	ИначеЕсли ТекущийШаг = 2 Тогда
		// Если в строках изменился целевой тренд, то обновим их.	
		
		ИзменилсяЦелевойТренд = Ложь;
		
		НекорректныеЗначенияОтбор = НекорректныеЗначения.НайтиСтроки(Новый Структура("Показатель", Показатель));
		Если НекорректныеЗначенияОтбор.Количество() > 0 Тогда		
			ИзменилсяЦелевойТренд = Не НекорректныеЗначенияОтбор[0].ЦелевойТренд = ЦелевойТрендПоказателя;			
		КонецЕсли;
		
		Если ИзменилсяЦелевойТренд Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = Обработки.торо_КорректировкаЦелевыхЗначенийПоказателейKPI.ТекстЗапросаНекорректныеЦелевыеЗначения(ПараметрыКорректировки, Истина);
			Запрос.УстановитьПараметр("Показатель", Показатель);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать();
			
			СтруктураПоиска = Новый Структура("Показатель, Организация, Подразделение, ОбъектРемонта");
			
			СтрокиКУдалению = Новый Массив;
			
			Для Каждого Строка Из НекорректныеЗначенияОтбор Цикл
				// Пока открыта обработка другие пользователи могли отменить проведение каких-то установок целевых значений.
				// Такие строки по измененному показателю удалим из таблицы. Остальные по этому показателю обновим.
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
				
				Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
					ЗаполнитьЗначенияСвойств(Строка, Выборка);
				Иначе	
					СтрокиКУдалению.Добавить(Строка);		
				КонецЕсли;
				
				Выборка.Сбросить();
			КонецЦикла;
			
			Для Каждого Строка Из СтрокиКУдалению Цикл
				НекорректныеЗначения.Удалить(Строка);	
			КонецЦикла;	
		КонецЕсли;	
		
	ИначеЕсли ТекущийШаг = 3 И Не ЦелевойТрендПоказателя = Перечисления.торо_ВидыЦелевыхТрендовПоказателейKPI.Диапазон Тогда
		// Целевой тренд стал отличным от "диапазон". Удалим строку.
		
		ОтсутствующиеЗначенияОтбор = ОтсутствующиеЗначения.НайтиСтроки(Новый Структура("Показатель", Показатель));
		Если ОтсутствующиеЗначенияОтбор.Количество() > 0 Тогда
			
			УдаляемаяСтрока = ОтсутствующиеЗначенияОтбор[0];
			
			ОтсутствующиеЗначения.Удалить(УдаляемаяСтрока);
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры	

#КонецОбласти
