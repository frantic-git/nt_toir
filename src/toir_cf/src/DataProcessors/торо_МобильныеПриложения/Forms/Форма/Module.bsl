
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Обработка.торо_МобильныеПриложения", Пользователи.ТекущийПользователь().УникальныйИдентификатор());
	Если ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда
		СкрыватьИстекшиеКлючи = ЗначениеНастроек.СкрыватьИстекшиеКлючи;
	КонецЕсли;
	
	ЗаполнитьТаблицыУзловБригады();
	ЗаполнитьТаблицыУзловМонитор(); 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ВведенКлюч" Тогда
		ЗаполнитьТаблицыУзловБригады();
		ЗаполнитьТаблицыУзловМонитор();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУзлыБригады
&НаКлиенте
Процедура УзлыБригадыНаименованиеПриИзменении(Элемент)
	ТекДанные = Элементы.УзлыБригады.ТекущиеДанные;
	Если Не ТекДанные = Неопределено Тогда
		ИзменитьНаименованиеУзла(ТекДанные.ID, ТекДанные.Наименование);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУзлыМонитор
&НаКлиенте
Процедура УзлыМониторНаименованиеПриИзменении(Элемент)
	ТекДанные = Элементы.УзлыМонитор.ТекущиеДанные;
	Если Не ТекДанные = Неопределено Тогда
		ИзменитьНаименованиеУзла(ТекДанные.ID, ТекДанные.Наименование);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВвестиКлюч(Команда)
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаВводаКлюча",, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУзлов(Команда)
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаПодразделений",, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура МестоположенияСотрудников(Команда)
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаТрекинга",, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТаблицуСоответсвияНажатие(Элемент)
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ТаблицаСоответствияВерсийПриложенияВерсиямРелизов",,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

// БРИГАДЫ
&НаКлиенте
Процедура ОбновитьБригады(Команда)
	ЗаполнитьТаблицыУзловБригады();
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьБригады(Команда)
	Таблица = Новый Массив();
	
	Для Каждого Строка Из Элементы.УзлыБригады.ВыделенныеСтроки Цикл
		Стр = УзлыБригады.НайтиПоИдентификатору(Строка);
	    Таблица.Добавить(Стр.ID);
	КонецЦикла;
	
	Если Таблица.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите строки с ключами, которые нужно продлить.'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаЗапроса",
		Новый Структура("Запрос, Приложение, Таблица", "Продлить", ПолучитьСсылкуНаПеречисление("МобильныеБригады"), Таблица), ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьБригады(Команда)
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаЗапроса",
		Новый Структура("Запрос, Приложение", "Увеличить", ПолучитьСсылкуНаПеречисление("МобильныеБригады")), ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьБригады(Команда)
	ТекДанные = Элементы.УзлыБригады.ТекущиеДанные;
	Если Не ТекДанные = Неопределено Тогда
		УдалитьУзелНаСервере(ТекДанные.ID);
		ЗаполнитьТаблицыУзловБригады();
	КонецЕсли;
КонецПроцедуры

// МОНИТОР
&НаКлиенте
Процедура ОбновитьМонитор(Команда)
	ЗаполнитьТаблицыУзловМонитор();
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьМонитор(Команда)
	Таблица = Новый Массив();
	
	Для Каждого Строка Из Элементы.УзлыМонитор.ВыделенныеСтроки Цикл
		Стр = УзлыМонитор.НайтиПоИдентификатору(Строка);
	    Таблица.Добавить(Стр.ID);
	КонецЦикла;
	
	Если Таблица.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите строки с ключами, которые нужно продлить.'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаЗапроса",
		Новый Структура("Запрос, Приложение, Таблица", "Продлить", ПолучитьСсылкуНаПеречисление("МониторСостояния"), Таблица), ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьМонитор(Команда)
	ОткрытьФорму("Обработка.торо_МобильныеПриложения.Форма.ФормаЗапроса",
		Новый Структура("Запрос, Приложение", "Увеличить", ПолучитьСсылкуНаПеречисление("МониторСостояния")), ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьМонитор(Команда)
	ТекДанные = Элементы.УзлыМонитор.ТекущиеДанные;
	Если Не ТекДанные = Неопределено Тогда
		УдалитьУзелНаСервере(ТекДанные.ID);
		ЗаполнитьТаблицыУзловМонитор();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// ОБЩЕЕ 
&НаСервереБезКонтекста
Процедура ИзменитьНаименованиеУзла(ID, Наименование)
	НЗ = РегистрыСведений.торо_МобильныеПриложения.СоздатьНаборЗаписей();
	НЗ.Отбор.ID.Установить(ID);
	НЗ.Прочитать();
	
	Для Каждого НС Из НЗ Цикл
		
		НС.Наименование = Наименование;
		
	КонецЦикла;
	
	Попытка
		НЗ.Записать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Ошибка при переименовании узла'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьУзелНаСервере(ID)
	НЗ = РегистрыСведений.торо_МобильныеПриложения.СоздатьНаборЗаписей();
	НЗ.Отбор.ID.Установить(ID);
	НЗ.Прочитать();
	
	Для Каждого НС Из НЗ Цикл
		НС.Наименование = "";
		НС.Узел = Неопределено;
	КонецЦикла;
	
	Попытка
		НЗ.Записать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Ошибка при отвязки узла'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
КонецПроцедуры

&НаСервере
Функция ПолучитьСсылкуНаПеречисление(Приложение)
	Возврат Перечисления.торо_МобильноеПриложение[Приложение];
КонецФункции

// БРИГАДЫ
&НаСервере
Процедура ЗаполнитьТаблицыУзловБригады()
	УзлыБригады.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_МобильныеПриложения.Узел КАК Узел,
	               |	торо_МобильныеПриложения.ID КАК ID,
	               |	торо_МобильныеПриложения.Наименование КАК Наименование,
	               |	торо_МобильныеПриложения.ДатаОкончания КАК ДатаОкончания
	               |ИЗ
	               |	РегистрСведений.торо_МобильныеПриложения КАК торо_МобильныеПриложения
	               |ГДЕ
	               |	торо_МобильныеПриложения.МобильноеПриложение = ЗНАЧЕНИЕ(Перечисление.торо_МобильноеПриложение.МобильныеБригады)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СЧ = 0;
	
	Пока Выборка.Следующий() Цикл
		
		КлючИстек = Ложь;
		
		Если ЗначениеЗаполнено(Выборка.Узел) Тогда
			СЧ = СЧ + 1;
		КонецЕсли;
		
		ДатаОкончания = Обработки.торо_МобильныеПриложения.ПолучитьДатуОкончания_Session(Выборка.ДатаОкончания);
		
		Если ЗначениеЗаполнено(ДатаОкончания) Тогда
			Стр = Формат(ДатаОкончания, "ДФ=dd.MM.yy");
			Если ДатаОкончания <= ТекущаяДата() Тогда
				Стр = "(не действует) " + Стр;
				КлючИстек = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если КлючИстек И СкрыватьИстекшиеКлючи Тогда
			Продолжить;
		КонецЕсли;   
		
				
		НС = УзлыБригады.Добавить();
		НС.ID						= Выборка.ID;
		НС.Узел						= Выборка.Узел;
		НС.Наименование				= Выборка.Наименование;
		НС.ДатаОкончания			= Стр;
		
		Если ЗначениеЗаполнено(Выборка.Узел) Тогда
			
			НС.IDУзла					= Выборка.Узел.ИДМобильногоУстройства;
			НС.ИмяУстройства			= Выборка.Узел.ИмяУстройства;
			НС.ПоследнийПользователь	= Выборка.Узел.ПоследнийПользователь;
			НС.ДатаПоследнегоСоединения	= Выборка.Узел.ДатаПоследнегоСоединения;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.КоличествоКлючейБригады.Заголовок = СтрШаблон(НСтр("ru = 'Количество %1/%2'"), СЧ, ПолучитьМаксимальноеКоличествоУзловБригады());
КонецПроцедуры

&НаСервере
Функция ПолучитьМаксимальноеКоличествоУзловБригады()
	Возврат Обработки.торо_МобильныеПриложения.ПолучитьЧислоМаксимальногоКоличестваУзловБригады_Session();
КонецФункции

// МОНИТОР
&НаСервере
Процедура ЗаполнитьТаблицыУзловМонитор()
	УзлыМонитор.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_МобильныеПриложения.Узел КАК Узел,
	               |	торо_МобильныеПриложения.ID КАК ID,
	               |	торо_МобильныеПриложения.Наименование КАК Наименование,
	               |	торо_МобильныеПриложения.ДатаОкончания КАК ДатаОкончания
	               |ИЗ
	               |	РегистрСведений.торо_МобильныеПриложения КАК торо_МобильныеПриложения
	               |ГДЕ
	               |	торо_МобильныеПриложения.МобильноеПриложение = ЗНАЧЕНИЕ(Перечисление.торо_МобильноеПриложение.МониторСостояния)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СЧ = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Узел) Тогда
			СЧ = СЧ + 1;
		КонецЕсли;
		
		НС = УзлыМонитор.Добавить();
		НС.ID						= Выборка.ID;
		НС.Узел						= Выборка.Узел;
		НС.Наименование				= Выборка.Наименование;
		
		ДатаОкончания = Обработки.торо_МобильныеПриложения.ПолучитьДатуОкончания_Session(Выборка.ДатаОкончания);
		
		Если ЗначениеЗаполнено(ДатаОкончания) Тогда
			Стр = Формат(ДатаОкончания, "ДФ=dd.MM.yy");
			Если ДатаОкончания <= ТекущаяДата() Тогда
				Стр = "(не действует) " + Стр;
			КонецЕсли;
			
			НС.ДатаОкончания = Стр;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Узел) Тогда
			
			НС.IDУзла					= Выборка.Узел.ИДМобильногоУстройства;
			НС.ИмяУстройства			= Выборка.Узел.ИмяУстройства;
			НС.ПоследнийПользователь	= Выборка.Узел.ПоследнийПользователь;
			НС.ДатаПоследнегоСоединения	= Выборка.Узел.ДатаПоследнегоСоединения;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.КоличествоКлючейМонитор.Заголовок = СтрШаблон(НСтр("ru = 'Количество %1/%2'"), СЧ, ПолучитьМаксимальноеКоличествоУзловМонитор());
КонецПроцедуры

&НаСервере
Функция ПолучитьМаксимальноеКоличествоУзловМонитор()
	Возврат Обработки.торо_МобильныеПриложения.ПолучитьЧислоМаксимальногоКоличестваУзловМонитор_Session();
КонецФункции

&НаКлиенте
Процедура СкрыватьИстекшиеКлючиПриИзменении(Элемент)
	ЗаполнитьТаблицыУзловБригады();
КонецПроцедуры

#КонецОбласти










