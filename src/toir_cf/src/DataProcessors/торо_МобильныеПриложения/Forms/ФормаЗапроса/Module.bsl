
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Запрос") И Параметры.Свойство("Приложение") Тогда
		Если Параметры.Запрос = "Увеличить" Тогда
			Элементы.КолвоПродлеваемыхКлючей.Видимость = Ложь;
			Запрос = Обработки.торо_МобильныеПриложения.ПолучитьКлючДобавления_Session(Параметры.Приложение);
		ИначеЕсли Параметры.Запрос = "Продлить" Тогда
			Элементы.КолвоКлючей.Видимость = Ложь;
			Элементы.Подсказка.Видимость = Ложь;
			Если Параметры.Свойство("Таблица") Тогда
				Элементы.КолвоПродлеваемыхКлючей.Заголовок = "Кол-во продлеваемых ключей: " + Параметры.Таблица.Количество();
				Запрос = Обработки.торо_МобильныеПриложения.ПолучитьКлючПродления_Session(Параметры.Приложение, Параметры.Таблица);
			Иначе
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
		ТипЗапроса = Параметры.Запрос;
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отправить(Команда)
	Если Не ЗначениеЗаполнено(Имя) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения(), НСтр("ru = 'Представьтесь, пожалуйста.'"), РежимДиалогаВопрос.ОК);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Компания) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения(), НСтр("ru = 'Введите наименование компании.'"), РежимДиалогаВопрос.ОК);
		Возврат;
	КонецЕсли;
	
	Если ТипЗапроса = "Увеличить" И КолвоКлючей = 0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения(), НСтр("ru = 'Введите количество ключей.'"), РежимДиалогаВопрос.ОК);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОкончания) Или ДатаОкончания <= ТекущаяДата() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения(), НСтр("ru = 'Дата окончания введена не корректно.'"), РежимДиалогаВопрос.ОК);
		Возврат;
	КонецЕсли;
	
	Текст = 
	"Имя: %Имя%
	|Компания: %Компания%
	|Телефон: %Телефон%
	|
	|%ПараметрыЗапроса%
	|%Комментарий%
	|Запрос находится во вложении.";
	
	Текст = СтрЗаменить(Текст, "%Имя%", Имя);
	Текст = СтрЗаменить(Текст, "%Компания%", Компания);
	Текст = СтрЗаменить(Текст, "%Телефон%", НомерТелефона);
	
	ПараметрыЗапроса = "";
	
	Если ТипЗапроса = "Увеличить" Тогда
		ПараметрыЗапроса = ПараметрыЗапроса + "Желаемое количество ключей: " + Формат(КолвоКлючей, "ЧН=") + Символы.ПС;
	КонецЕсли;
	
	ПараметрыЗапроса = ПараметрыЗапроса + "Желаемая дата окончания: " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	
	Текст = СтрЗаменить(Текст, "%ПараметрыЗапроса%", ПараметрыЗапроса);
	
	Получатели = Новый Массив;
	Получатели.Добавить(Новый Структура("Адрес, Представление, ИсточникКонтактнойИнформации", "toir@desnol.ru", "Деснол Софт", Неопределено));
	Получатель = Новый Структура("Получатель", Получатель);
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		Текст = СтрЗаменить(Текст, "%Комментарий%", Символы.ПС + "Комментарий:" + Символы.ПС + Комментарий + Символы.ПС);
	Иначе
		Текст = СтрЗаменить(Текст, "%Комментарий%", "");
	КонецЕсли;
	
	ДопПараметры = Новый Структура("Текст, Получатели", Текст, Получатели);
	#Если ВебКлиент Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПослеУстановкиРасширения", ЭтотОбъект, ДопПараметры);
		ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(ОписаниеОповещения, ,Ложь);
	#Иначе
		ОтправитьПослеУстановкиРасширения(Истина, ДопПараметры);
	#КонецЕсли

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ОтправитьПослеУстановкиРасширения(РасширениеПодключено, ДополнительныеПараметры) Экспорт
	
	Если НЕ РасширениеПодключено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Расширение работы с файлами не установлено. Работа с файлами с неустановленным расширением в веб-клиенте невозможна.'"));
		Возврат;
	КонецЕсли;
	
	// Прикрепление запроса
	Вложения = Новый Массив;
	
	ФайлРегистрации = Новый ТекстовыйДокумент;
	ФайлРегистрации.УстановитьТекст(Запрос);
	
	АдресВоВременномХранилище = ПоместитьФайлЗапросаВоВременноеХранилищеНаСервере(ФайлРегистрации);
	
	Файл = Новый Структура;
	Файл.Вставить("Представление", "Запрос.txt");
	Файл.Вставить("АдресВоВременномХранилище", АдресВоВременномХранилище);
	Файл.Вставить("Кодировка", "UTF-8");
	Файл.Вставить("Идентификатор", "");
	
	Вложения.Добавить(Файл);
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Получатель", ДополнительныеПараметры.Получатели);
	ПараметрыПисьма.Вставить("Тема", "Лицензирование мобильного приложения ТОИР");
	ПараметрыПисьма.Вставить("Текст", ДополнительныеПараметры.Текст);
	ПараметрыПисьма.Вставить("Вложения", Вложения);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьФайлЗапросаВоВременноеХранилищеНаСервере(ТекстовыйДокумент)
	
	АдресХранилища	= Неопределено;
	ИмяФайла = КаталогВременныхФайлов() + "Запрос.txt"; 
	
	Попытка
		ТекстовыйДокумент.Записать(ИмяФайла);
		АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла), Новый УникальныйИдентификатор);
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат АдресХранилища;
	
КонецФункции
#КонецОбласти
