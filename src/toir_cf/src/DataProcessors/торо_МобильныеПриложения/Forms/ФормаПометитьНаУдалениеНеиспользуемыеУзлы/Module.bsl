
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.МобильноеПриложение.Заголовок = Строка(Параметры.МобильноеПриложение);
	
	ЗаполнитьТаблицу();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УзлыПометкаПриИзменении(Элемент)
	ТекДанные = Элементы.Узлы.ТекущиеДанные;
	Если Не ТекДанные = Неопределено Тогда
		ТекДанные.Картинка = Не ТекДанные.Пометка;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	СохранитьНаСервере();
	Оповестить("ПометитьНаУдалениеНеиспользуемыеУзлы");
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкиСИспользуемыхУзлов(Команда)
	Для Каждого Узел Из Узлы Цикл
		Если Узел.Используется И Узел.Пометка Тогда
			Узел.Пометка = Ложь;
			Узел.Картинка = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеНеиспользуемыеУзлы(Команда)
	Для Каждого Узел Из Узлы Цикл
		Если Не Узел.Используется И Не Узел.Пометка Тогда
			Узел.Пометка = Истина;
			Узел.Картинка = Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНеиспользуемыеУзлы(Команда)
	Для Каждого Узел Из Узлы Цикл
		Если Не Узел.Используется И Узел.Пометка Тогда
			Узел.Пометка = Ложь;
			Узел.Картинка = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицу()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	mobileBrigades.Ссылка КАК Узел,
	               |	mobileBrigades.ИмяУстройства КАК ИмяУстройства,
	               |	mobileBrigades.ПоследнийПользователь КАК ПоследнийПользователь,
	               |	mobileBrigades.ПометкаУдаления КАК Пометка,
	               |	НЕ mobileBrigades.ПометкаУдаления КАК Картинка,
	               |	mobileBrigades.ПометкаУдаления КАК ПрежняяПометка,
	               |	НЕ торо_МобильныеПриложения.Узел ЕСТЬ NULL КАК Используется
	               |ИЗ
	               |	ПланОбмена.mobileBrigades КАК mobileBrigades
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_МобильныеПриложения КАК торо_МобильныеПриложения
	               |		ПО (торо_МобильныеПриложения.Узел = mobileBrigades.Ссылка)
	               |ГДЕ
	               |	mobileBrigades.ЭтотУзел = ЛОЖЬ";
	
	Узлы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	НачатьТранзакцию();
	
	Попытка
		Для Каждого Узел Из Узлы Цикл
			Если Не Узел.Пометка = Узел.ПрежняяПометка Тогда
				УзелОбъект = Узел.Узел.ПолучитьОбъект();
				УзелОбъект.ПометкаУдаления = Узел.Пометка;
				УзелОбъект.Записать();
			КонецЕсли;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
КонецПроцедуры

#КонецОбласти
