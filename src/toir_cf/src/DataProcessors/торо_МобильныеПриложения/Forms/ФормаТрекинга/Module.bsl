
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаНачала = НачалоДня(ТекущаяДата());
	ДатаОкончания = КонецДня(ТекущаяДата());
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокПользователей = ПолучитьСписокПользователейДляОтбора();
	ПараметрыФормы = Новый Структура("Отбор, РежимВыбора, СкрытьПользователейБезПользователяИБ", Новый Структура("Ссылка", СписокПользователей), Истина, Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, Элемент, ЭтаФорма.УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СписокПользователей = ПолучитьСписокПользователейДляОтбора();
	ПараметрыПолученияДанных.Отбор.Вставить("Ссылка", СписокПользователей);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Сотрудник"" не заполнено.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Сотрудник");
		Возврат;
	КонецЕсли;
	
	Если ДатаОкончания - ДатаНачала > 60 * 60 * 24 * 3 Тогда
		ТекстСообщения = НСтр("ru = 'Заданный период превышает рекомендуемый (3 дня).'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	HTML = СформироватьСсылкуНаСервере(Сотрудник, ДатаНачала, ДатаОкончания, ТолькоДействия);
	
	Если HTML = "" Тогда
		ТекстСообщения = НСтр("ru = 'Координат в заданном интервале не существует.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СформироватьСсылкуНаСервере(Пользователь, ДатНач, ДатКон, ТолькоДействия)
	Ссылка = "https://yandex.ru/maps/?ll=FIRST_COORDS_W,FIRST_COORDS_H&z=13&rl=";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	торо_ПередвиженияМобильныхБригад.Широта КАК Широта,
	               |	торо_ПередвиженияМобильныхБригад.Долгота КАК Долгота
	               |ИЗ
	               |	РегистрСведений.торо_ПередвиженияМобильныхБригад КАК торо_ПередвиженияМобильныхБригад
	               |ГДЕ
	               |	торо_ПередвиженияМобильныхБригад.Период > &ДатНач
	               |	И торо_ПередвиженияМобильныхБригад.Период < &ДатКон
	               |	И торо_ПередвиженияМобильныхБригад.Пользователь = &Пользователь
	               |	И торо_ПередвиженияМобильныхБригад.ВидУстановкиМестоположения <> ЗНАЧЕНИЕ(Перечисление.торо_ВидУстановкиМестоположения.Обновление)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	торо_ПередвиженияМобильныхБригад.Период";
	
	Запрос.УстановитьПараметр("ДатНач", ДатНач);
	Запрос.УстановитьПараметр("ДатКон", ДатКон);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Если Не ТолькоДействия Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "торо_ПередвиженияМобильныхБригад.ВидУстановкиМестоположения <> ЗНАЧЕНИЕ(Перечисление.торо_ВидУстановкиМестоположения.Обновление)", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПКШ = Выборка.Широта;
		ПКД = Выборка.Долгота;
	Иначе
		Возврат "";
	КонецЕсли;
	
	Ссылка = Ссылка + Формат(ПКД, "ЧРД=.") + "," + Формат(ПКШ, "ЧРД=.");
	
	ПредШ = ПКШ;
	ПредД = ПКД;
	
	Пока Выборка.Следующий() Цикл
		ВШ = Выборка.Широта;
		ВД = Выборка.Долгота;
		Ссылка = Ссылка + "~" + Формат(ВД - ПредД, "ЧРД=.") + "," + Формат(ВШ - ПредШ, "ЧРД=.");
		ПредШ = ВШ;
		ПредД = ВД;
	КонецЦикла;
	
	Ссылка = СтрЗаменить(Ссылка, "FIRST_COORDS_W", ПредД);
	Ссылка = СтрЗаменить(Ссылка, "FIRST_COORDS_H", ПредШ);
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	Ссылка = Ссылка + "&_rnd=" + ГСЧ.СлучайноеЧисло(0, 1000);
	
	Возврат Ссылка;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокПользователейДляОтбора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	торо_ПередвиженияМобильныхБригад.Пользователь КАК Пользователь
	|ИЗ
	|	РегистрСведений.торо_ПередвиженияМобильныхБригад КАК торо_ПередвиженияМобильныхБригад";
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат.ВыгрузитьКолонку("Пользователь");
	
КонецФункции

&НаКлиенте
Процедура СотрудникОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	СписокПользователей = ПолучитьСписокПользователейДляОтбора();
	ПараметрыПолученияДанных.Отбор.Вставить("Ссылка", СписокПользователей);
КонецПроцедуры

#КонецОбласти
