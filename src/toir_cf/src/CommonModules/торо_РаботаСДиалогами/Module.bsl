#Область ПрограммныйИнтерфейс

// Формирует и устанавливает текст заголовка формы документа.
//
// Параметры:
//  СтрокаВидаОперации - Строка - строка вида операции документа, 
//  ДокументОбъект     - ДокументОбъект - объект документа, 
//  ФормаДокумента     - ФормаКлиентскогоПриложения - форма документа.
//
Процедура УстановитьЗаголовокФормыДокумента(ДокументОбъект, ФормаДокумента, СтрокаВидаОперации = "") Экспорт

	Если ПустаяСтрока(СтрокаВидаОперации) Тогда
		Заголовок = ДокументОбъект.Ссылка.Метаданные().ПредставлениеОбъекта + "";
	Иначе
		Заголовок = ДокументОбъект.Ссылка.Метаданные().ПредставлениеОбъекта + " [" + СтрокаВидаОперации + "]";
	КонецЕсли;
		
	Попытка
		Если Не ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда  
			Заголовок = Заголовок + НСтр("ru = '. Новый'");
		Иначе
			Заголовок = Заголовок + СтрШаблон(НСтр("ru = ' №%1 от %2. '"),ДокументОбъект.Номер,ДокументОбъект.Дата);
			Если ДокументОбъект.Проведен Тогда
				Заголовок = Заголовок + НСтр("ru = 'Проведен'");
			ИначеЕсли ДокументОбъект.Ссылка.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить Тогда
				Заголовок = Заголовок + НСтр("ru = 'Не проведен'");
			Иначе
				Заголовок = Заголовок + НСтр("ru = 'Записан'");
			КонецЕсли;
		КонецЕсли;
		
		// Добавления в заголовок статуса
		ФОИспользоватьСтатусыДокументов = ПолучитьФункциональнуюОпцию("торо_ИспользоватьСтатусыДокументовТОиР");
		Если ФОИспользоватьСтатусыДокументов Тогда
			Заголовок = торо_РаботаСоСтатусамиДокументовСервер.ДобавитьСтатусВЗаголовокФормы(Заголовок, ФормаДокумента);
		КонецЕсли;
		
		ФормаДокумента.Заголовок = Заголовок;
	Исключение
		//	Ошибка может возникнуть, если конфигурация запущена на языке, отличном от ru
		// при подстановке в шаблоне "Номер от Дата". В таком случае оставим стандартный заголовок формы.
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;

КонецПроцедуры 

#КонецОбласти
