#Область СлужебныйПрограммныйИнтерфейс

#Область СписочныеДокументы

Функция МаксимальныйИдентификаторСтроки(КоллекцияСтрок, ИмяРеквизитаИдентификаторСтроки) Экспорт
	
	МаксимальныйИдентификатор = 0;
	Для каждого СтрокаДокумента Из КоллекцияСтрок Цикл
		Если МаксимальныйИдентификатор < СтрокаДокумента[ИмяРеквизитаИдентификаторСтроки] Тогда
			МаксимальныйИдентификатор = СтрокаДокумента[ИмяРеквизитаИдентификаторСтроки];
		КонецЕсли; 
	КонецЦикла;
	
	Возврат МаксимальныйИдентификатор;
	
КонецФункции

Функция ПараметрыОткрытияФормыРедактированияСтрокиДокумента(Объект, ТекущиеДанные, ПолноеИмяОбъектаМетаданных, ПараметрыОткрытия = Неопределено) Экспорт
	
	ОписаниеСтроки = ЗарплатаКадрыРасширенныйВызовСервера.СтруктураПоМетаданным(ПолноеИмяОбъектаМетаданных);
	ЗаполнитьОбъектПоОбразцу(ОписаниеСтроки, Объект, ТекущиеДанные, "ИдентификаторСтрокиСотрудника");
	ОписаниеСтроки.Вставить("СсылкаНаОбъект", Объект.Ссылка);
	
	Если ПараметрыОткрытия = Неопределено Тогда
		ПараметрыОткрытия = Новый Структура;
	КонецЕсли;
	
	ПараметрыОткрытия.Вставить("СтрокаСписочногоДокумента", ОписаниеСтроки);
	ПараметрыОткрытия.Вставить("КлючНазначенияИспользования", "РедактированиеСтрокиСписочногоДокумента");
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

Процедура ЗаполнитьОбъектПоОбразцу(ОписаниеОбъекта, Объект, Строка = Неопределено, ИмяКлючевогоРеквизита = Неопределено) Экспорт
	
	ЗаполнитьЗначенияСвойств(ОписаниеОбъекта, Объект);
	
	Если Строка <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ОписаниеОбъекта, Строка);
	КонецЕсли; 
	
	Если ИмяКлючевогоРеквизита <> Неопределено Тогда
		СтруктураОтбора = Новый Структура(ИмяКлючевогоРеквизита, Строка[ИмяКлючевогоРеквизита]);
	Иначе
		СтруктураОтбора = Неопределено;
	КонецЕсли;
	
	ТабличныеЧастиОбъекта = ОписаниеОбъекта.ТабличныеЧасти;
	
	Для каждого ОписаниеТабличнойЧасти Из ТабличныеЧастиОбъекта Цикл
		
		Если ОписаниеТабличнойЧасти.Ключ = "ОписаниеТабличныхЧастей" Тогда
			Продолжить;
		КонецЕсли; 
		
		ИменаРеквизитов = ТабличныеЧастиОбъекта.ОписаниеТабличныхЧастей[ОписаниеТабличнойЧасти.Ключ];
		
		Если СтруктураОтбора = Неопределено Тогда
			СтрокиТабличнойЧасти = Объект[ОписаниеТабличнойЧасти.Ключ];
		Иначе
			СтрокиТабличнойЧасти = Объект[ОписаниеТабличнойЧасти.Ключ].НайтиСтроки(СтруктураОтбора);
		КонецЕсли;
		
		Если СтрокиТабличнойЧасти.Количество() > 0 Тогда
			
			Для каждого СтрокаТабличнойЧасти Из СтрокиТабличнойЧасти Цикл
				
				СтруктураСтроки = Новый Структура(ИменаРеквизитов);
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрокаТабличнойЧасти);
				
				ТабличныеЧастиОбъекта[ОписаниеТабличнойЧасти.Ключ].Добавить(СтруктураСтроки);
				
			КонецЦикла;
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область МногофункциональныеДокументы

Процедура УстановитьВидимостьКомандПечатиМногофункциональногоДокумента(Форма, МестоРазмещенияКомандПоУмолчанию = Неопределено) Экспорт 
	
	КомандыПечати = УправлениеПечатью.КомандыПечатиФормы(Форма);
	
	ТаблицаКоманд = КомандыПечати.Скопировать(,"МестоРазмещения");
	ТаблицаКоманд.Свернуть("МестоРазмещения");
	МестаРазмещения = ТаблицаКоманд.ВыгрузитьКолонку("МестоРазмещения");
	
	Для Каждого МестоРазмещения Из МестаРазмещения Цикл
		
		НайденныеКоманды = КомандыПечати.НайтиСтроки(Новый Структура("МестоРазмещения,СкрытаФункциональнымиОпциями", МестоРазмещения, Ложь));
		
		МестоРазмещенияКоманд = Форма.Элементы.Найти(МестоРазмещения);
		Если МестоРазмещенияКоманд = Неопределено Тогда
			МестоРазмещенияКоманд = МестоРазмещенияКомандПоУмолчанию;
		КонецЕсли;
		
		Если НайденныеКоманды.Количество() > 0 Тогда
			
			Если МестоРазмещенияКоманд = Неопределено Тогда
				МестоРазмещенияКоманд = Форма.КоманднаяПанель;
			КонецЕсли;
			
			КоличествоВидимыхКоманд = 0;
			МестоРазмещенияКомандИмя = МестоРазмещенияКоманд.Имя;
			
			ОднаКомандаПечати = КомандыПечати.Количество() = 1;
			Если Не ОднаКомандаПечати Тогда
				МестоРазмещенияКомандИмя = МестоРазмещенияКомандИмя + "ПодменюПечать";
			КонецЕсли;
			
			Для Каждого ОписаниеКомандыПечати Из НайденныеКоманды Цикл
				
				Видимость = Истина;
				
				Если Видимость Тогда 
					КоличествоВидимыхКоманд = КоличествоВидимыхКоманд + 1;
				КонецЕсли;
				
			КонецЦикла;

			Если Не ОднаКомандаПечати Тогда 
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, МестоРазмещенияКомандИмя, "Видимость", КоличествоВидимыхКоманд <> 0);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОформлениеНесколькихДокументовНаОднуДату

Функция ЗначениеСдвигаПериодаЗаписиРегистра(Документ) Экспорт 
	
	ТипДокумента = ТипЗнч(Документ);
	
	Сдвиг = Неопределено;
	
	Если ТипДокумента = Тип("ДокументСсылка.ПриемНаРаботу") ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриемНаРаботуСписком") Тогда
		Сдвиг = 20;
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.Увольнение") ИЛИ ТипДокумента = Тип("ДокументСсылка.УвольнениеСписком") Тогда
		Сдвиг = 86360;
	КонецЕсли;
	
	Возврат Сдвиг;
	
КонецФункции

Функция КонкурирующиеПоПериодуРегистраторыНачислений() Экспорт 
	
	КонкурирующиеРегистраторы = Новый Массив;
	
	КонкурирующиеРегистраторы.Добавить(Тип("ДокументСсылка.КадровыйПеревод"));
	КонкурирующиеРегистраторы.Добавить(Тип("ДокументСсылка.КадровыйПереводСписком"));

	Возврат КонкурирующиеРегистраторы;
	
КонецФункции

Процедура УстановитьВремяРегистрацииДокумента(Движения, СотрудникиДаты, Регистратор, ИмяКолонкиПериод = "ДатаСобытия") Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СдвигПериода = ЗначениеСдвигаПериодаЗаписиРегистра(Регистратор);
	
	Если СдвигПериода <> Неопределено Тогда 
		
		// Документ с фиксированным временем
		СотрудникиДаты.Свернуть(ИмяКолонкиПериод + ", Сотрудник");
		
		НаборЗаписей = РегистрыСведений.ВремяРегистрацииДокументовПлановыхНачислений.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументНачисления.Установить(Регистратор);
		
		Для Каждого ДанныеСотрудника Из СотрудникиДаты Цикл 
			
			ДатаСобытия = НачалоДня(ДанныеСотрудника[ИмяКолонкиПериод]);
			ВремяРегистрации = ДатаСобытия + СдвигПериода;
			
			ЗаписьРегистра = НаборЗаписей.Добавить();
			ЗаписьРегистра.Дата = ДатаСобытия;
			ЗаписьРегистра.Сотрудник = ДанныеСотрудника.Сотрудник;
			ЗаписьРегистра.ДокументНачисления = Регистратор;
			ЗаписьРегистра.ВремяРегистрации = ВремяРегистрации;
			
		КонецЦикла;
	
		НаборЗаписей.Записать();
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("СотрудникиДаты", СотрудникиДаты);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СотрудникиДаты." + ИмяКолонкиПериод + " КАК Дата,
	               |	СотрудникиДаты.Сотрудник КАК Сотрудник
	               |ПОМЕСТИТЬ ВТСотрудникиДаты
	               |ИЗ
	               |	&СотрудникиДаты КАК СотрудникиДаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	НАЧАЛОПЕРИОДА(СотрудникиДаты.Дата, ДЕНЬ) КАК Дата,
	               |	СотрудникиДаты.Сотрудник
	               |ПОМЕСТИТЬ ВТИзмеренияДаты
	               |ИЗ
	               |	ВТСотрудникиДаты КАК СотрудникиДаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВремяРегистрацииДокументов.Дата КАК Дата,
	               |	ВремяРегистрацииДокументов.Сотрудник КАК Сотрудник,
	               |	ВремяРегистрацииДокументов.ДокументНачисления КАК ДокументНачисления,
	               |	ВремяРегистрацииДокументов.ВремяРегистрации КАК ВремяРегистрации
	               |ПОМЕСТИТЬ ВТЗаписиРегистра
	               |ИЗ
	               |	ВТИзмеренияДаты КАК ИзмеренияДаты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументов
	               |		ПО ИзмеренияДаты.Дата = ВремяРегистрацииДокументов.Дата
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ДокументНачисления,
	               |	Дата,
	               |	Сотрудник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИзмеренияДаты.Дата,
	               |	ЗаписиРегистра.ВремяРегистрации КАК ВремяРегистрации
	               |ПОМЕСТИТЬ ВТВремяРегистрацииДокументов
	               |ИЗ
	               |	ВТИзмеренияДаты КАК ИзмеренияДаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаписиРегистра КАК ЗаписиРегистра
	               |		ПО ИзмеренияДаты.Дата = ЗаписиРегистра.Дата
	               |			И (ЗаписиРегистра.ДокументНачисления = &Регистратор)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВремяРегистрацииДокументов.Дата,
	               |	ВремяРегистрацииДокументов.ВремяРегистрации
	               |ИЗ
	               |	ВТВремяРегистрацииДокументов КАК ВремяРегистрацииДокументов
	               |ГДЕ
	               |	ВремяРегистрацииДокументов.ВремяРегистрации ЕСТЬ NULL ";
				   
	РезультатЗапроса = Запрос.Выполнить();
	
	ВремяРегистрацииДокумента = Новый Соответствие;
	
	ТребуетсяНовоеВремяРегистрации = Не РезультатЗапроса.Пустой();
	
	Если Не ТребуетсяНовоеВремяРегистрации Тогда
		
		// Проверим, что по набору сотрудников нет конфликтов с другими регистраторами.
		Запрос.Текст = "ВЫБРАТЬ
		               |	ИзмеренияДаты.Сотрудник
		               |ИЗ
		               |	ВТИзмеренияДаты КАК ИзмеренияДаты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяРегистрацииДокументов КАК ВремяРегистрацииДокументов
		               |		ПО ИзмеренияДаты.Дата = ВремяРегистрацииДокументов.Дата
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаписиРегистра КАК ЗаписиРегистра
		               |		ПО ИзмеренияДаты.Дата = ЗаписиРегистра.Дата
		               |			И ИзмеренияДаты.Сотрудник = ЗаписиРегистра.Сотрудник
		               |			И (ВремяРегистрацииДокументов.ВремяРегистрации = ЗаписиРегистра.ВремяРегистрации)
		               |			И (ЗаписиРегистра.ДокументНачисления <> &Регистратор)";
					   
		РезультатЗапроса = Запрос.Выполнить();
					   
		ТребуетсяНовоеВремяРегистрации = Не РезультатЗапроса.Пустой();
		
	КонецЕсли;
	
	Если Не ТребуетсяНовоеВремяРегистрации Тогда
	
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВремяРегистрацииДокументов.Дата,
		               |	ВремяРегистрацииДокументов.ВремяРегистрации
		               |ИЗ
		               |	ВТВремяРегистрацииДокументов КАК ВремяРегистрацииДокументов";
					   
		Выборка = Запрос.Выполнить().Выбрать();			   
		
		Пока Выборка.Следующий() Цикл 
			ВремяРегистрацииДокумента.Вставить(Выборка.Дата, Выборка.ВремяРегистрации);
		КонецЦикла;
		
		// Если список сотрудников и дат в переданной таблице и в регистре не совпадает - нужно перезаписать набор.
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВремяРегистрацииДокументов.Дата КАК Дата,
		               |	ВремяРегистрацииДокументов.Сотрудник КАК Сотрудник
		               |ПОМЕСТИТЬ ВТЗаписиРегистратора
		               |ИЗ
		               |	РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументов
		               |ГДЕ
		               |	ВремяРегистрацииДокументов.ДокументНачисления = &Регистратор
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ИзмеренияДаты.Сотрудник
		               |ИЗ
		               |	ВТИзмеренияДаты КАК ИзмеренияДаты
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаписиРегистратора КАК ЗаписиРегистратора
		               |		ПО ИзмеренияДаты.Дата = ЗаписиРегистратора.Дата
		               |			И ИзмеренияДаты.Сотрудник = ЗаписиРегистратора.Сотрудник
		               |ГДЕ
		               |	ЗаписиРегистратора.Сотрудник ЕСТЬ NULL 
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ЗаписиРегистратора.Сотрудник
		               |ИЗ
		               |	ВТЗаписиРегистратора КАК ЗаписиРегистратора
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТИзмеренияДаты КАК ИзмеренияДаты
		               |		ПО ЗаписиРегистратора.Дата = ИзмеренияДаты.Дата
		               |			И ЗаписиРегистратора.Сотрудник = ИзмеренияДаты.Сотрудник
		               |ГДЕ
		               |	ИзмеренияДаты.Сотрудник ЕСТЬ NULL ";
					   
		РезультатЗапроса = Запрос.Выполнить();
					   
		ЗаписатьНабор = Не РезультатЗапроса.Пустой();
		
	Иначе 
		
		// Определим свободное время для регистрации движений документа.
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗаписиРегистра.Дата КАК Дата,
		               |	МАКСИМУМ(ДОБАВИТЬКДАТЕ(ЗаписиРегистра.ВремяРегистрации, СЕКУНДА, 1)) КАК ВремяРегистрации
		               |ПОМЕСТИТЬ ВТСвободноеВремяРегистрации
		               |ИЗ
		               |	ВТИзмеренияДаты КАК ИзмеренияДаты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаписиРегистра КАК ЗаписиРегистра
		               |		ПО ИзмеренияДаты.Дата = ЗаписиРегистра.Дата
		               |			И ИзмеренияДаты.Сотрудник = ЗаписиРегистра.Сотрудник
		               |			И (ЗаписиРегистра.ДокументНачисления <> &Регистратор)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ЗаписиРегистра.Дата
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВремяРегистрацииДокументов.Дата КАК Дата,
		               |	ЕСТЬNULL(СвободноеВремяРегистрации.ВремяРегистрации, ДАТАВРЕМЯ(1, 1, 1)) КАК ВремяРегистрации
		               |ИЗ
		               |	ВТВремяРегистрацииДокументов КАК ВремяРегистрацииДокументов
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТСвободноеВремяРегистрации КАК СвободноеВремяРегистрации
		               |		ПО ВремяРегистрацииДокументов.Дата = СвободноеВремяРегистрации.Дата";
					   
		Выборка = Запрос.Выполнить().Выбрать();			   
		
		СдвигПериода = 60;
		
		Пока Выборка.Следующий() Цикл
			ВремяРегистрации = ?(ЗначениеЗаполнено(Выборка.ВремяРегистрации), Выборка.ВремяРегистрации, Выборка.Дата + СдвигПериода);
			ВремяРегистрацииДокумента.Вставить(Выборка.Дата, ВремяРегистрации);
		КонецЦикла;
		
		ЗаписатьНабор = Истина;
		
	КонецЕсли;
	
	Если ЗаписатьНабор Тогда 
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ИзмеренияДаты.Дата КАК Дата,
		               |	ИзмеренияДаты.Сотрудник КАК Сотрудник
		               |ИЗ
		               |	ВТИзмеренияДаты КАК ИзмеренияДаты";
					  
		Выборка = Запрос.Выполнить().Выбрать();
		
		НаборЗаписей = РегистрыСведений.ВремяРегистрацииДокументовПлановыхНачислений.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументНачисления.Установить(Регистратор);
			
		Пока Выборка.Следующий() Цикл 
			
			ДатаСобытия = Выборка.Дата;
			ВремяРегистрации = ВремяРегистрацииДокумента.Получить(ДатаСобытия);
			
			ЗаписьРегистра = НаборЗаписей.Добавить();
			ЗаписьРегистра.Дата = ДатаСобытия;
			ЗаписьРегистра.Сотрудник = Выборка.Сотрудник;
			ЗаписьРегистра.ДокументНачисления = Регистратор;
			ЗаписьРегистра.ВремяРегистрации = ВремяРегистрации;
			
		КонецЦикла;
	
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	Для Каждого НаборЗаписейРегистра Из Движения Цикл 
		НаборЗаписейРегистра.ДополнительныеСвойства.Вставить("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти