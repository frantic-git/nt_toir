#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьЗначенияЗаполненныхСвойств(Приемник, Источник) Экспорт
	
	Для каждого СвойствоИсточника Из Источник Цикл
		
		Если Не ЗначениеЗаполнено(СвойствоИсточника.Значение) Тогда
			Продолжить;
		КонецЕсли; 
		
		Если Не Приемник.Свойство(СвойствоИсточника.Ключ) Тогда
			Продолжить;
		КонецЕсли; 
		
		Приемник[СвойствоИсточника.Ключ] = СвойствоИсточника.Значение;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьДанныеСотрудниковСписочногоДокумента(Объект, СтруктураОписания, ЗначенияПоиска, КлючПоиска = "ИдентификаторСтрокиСотрудника") Экспорт
	
	Если ТипЗнч(ЗначенияПоиска) = Тип("Массив")
		Или ТипЗнч(ЗначенияПоиска) = Тип("ФиксированныйМассив") Тогда
		МассивЗначений = ЗначенияПоиска;
	Иначе
		МассивЗначений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначенияПоиска);
	КонецЕсли;
	
	Для Каждого ЗначениеПоиска Из МассивЗначений Цикл
		
		СтруктураПоиска = Новый Структура(КлючПоиска, ЗначениеПоиска);
		Для каждого ОписаниеТабличнойЧасти Из СтруктураОписания.ТабличныеЧасти Цикл
			
			Если ОписаниеТабличнойЧасти.Ключ = "ОписаниеТабличныхЧастей"
				ИЛИ ОписаниеТабличнойЧасти.Ключ = "Сотрудники" Тогда
				Продолжить;
			КонецЕсли;
			
			УдалитьСтрокиТабличнойЧасти(Объект[ОписаниеТабличнойЧасти.Ключ], СтруктураПоиска);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьСтрокиТабличнойЧасти(ТабличнаяЧасть, СтруктураПоиска)
	
	УдаляемыеСтроки = ТабличнаяЧасть.НайтиСтроки(СтруктураПоиска);
	Для каждого УдаляемаяСтроки Из УдаляемыеСтроки Цикл
		ТабличнаяЧасть.Удалить(УдаляемаяСтроки);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
