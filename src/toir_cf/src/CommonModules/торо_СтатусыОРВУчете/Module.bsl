
#Область ПрограммныйИнтерфейс

// Возвращает список статусов объектов ремонта в учете, для которых
// в настройках  системы установлен флаг "Доступность при подборе".
//
// Параметры:
//		ИсключитьНепринятые - Булево - если истина, то из результирующего списка
//			будет исключено значение "Не принят к учету".
//
// Возвращаемое значение:
//		Массив - массив стусов ОР в учете.
//
Функция СписокСтатусовДляПодбора(ИсключитьНепринятые = Ложь) Экспорт
	
	СписокСтатусов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_НастройкиДоступностиОбъектовРемонта.СтатусОРВУчете
	|ИЗ
	|	РегистрСведений.торо_НастройкиДоступностиОбъектовРемонта КАК торо_НастройкиДоступностиОбъектовРемонта
	|ГДЕ
	|	торо_НастройкиДоступностиОбъектовРемонта.ДоступностьПриПодборе";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ИсключитьНепринятые И Выборка.СтатусОРВУчете = Перечисления.торо_СтатусыОРВУчете.НеПринятоКУчету Тогда
			Продолжить;
		КонецЕсли;
		СписокСтатусов.Добавить(Выборка.СтатусОРВУчете);
	КонецЦикла;
	
	Возврат СписокСтатусов;
	
КонецФункции

// Возвращает структуру настроек для статуса ОР в учете.
// Параметры:
//		СтатусОРВУчете - ПеречислениеСсылка.торо_СтатусыОРВУчете - статус ОР в учете.
// Возвращаемое значение:
//		Структура, Неопределено - структура значений настроек.
//
Функция НастройкиСтатуса(СтатусОРВУчете) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_НастройкиДоступностиОбъектовРемонта.СтатусОРВУчете КАК СтатусОРВУчете,
	|	торо_НастройкиДоступностиОбъектовРемонта.ДоступностьПриПодборе КАК ДоступностьПриПодборе,
	|	торо_НастройкиДоступностиОбъектовРемонта.ДоступностьФлага КАК ДоступностьФлага,
	|	торо_НастройкиДоступностиОбъектовРемонта.ЗначениеПоУмолчанию КАК ЗначениеПоУмолчанию
	|ИЗ
	|	РегистрСведений.торо_НастройкиДоступностиОбъектовРемонта КАК торо_НастройкиДоступностиОбъектовРемонта
	|ГДЕ
	|	торо_НастройкиДоступностиОбъектовРемонта.СтатусОРВУчете = &СтатусОРВУчете";
	
	Запрос.УстановитьПараметр("СтатусОРВУчете", СтатусОРВУчете);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтруктураВозврата = Новый Структура("СтатусОРВУчете, ДоступностьПриПодборе, ДоступностьФлага, ЗначениеПоУмолчанию");
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Фильтрует переданный массив объектов ремонта по статусам в учете.
//
// Параметры:
//		МассивОР - Массив - Массив объектов ремонта, который нужно отфильтровать.
//		ОтборСтатусыВУчете - СписокЗначений, Массив - список статусов в учете для отбора.
//		ДатаСреза - Дата - дата, на которую надо отобрать по статусам объекты ремонта.
//		НеУдалятьГруппы - Булево - ложь, то из результирующего массива будут исключены группы.
// Возвращаемое значение:
//		Массив - отфильтрованный массив объектов ремонта.
//
Функция ОтборатьОбъектыПоСтатусамВУчете(МассивОР, ОтборСтатусыВУчете, ДатаСреза = Неопределено, НеУдалятьГруппы = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних.СтатусОР КАК СтатусОР
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	РегистрСведений.торо_СтатусыОбъектовРемонтаВУчете.СрезПоследних(&ДатаСреза, ОбъектРемонта В (&МассивОР)) КАК торо_СтатусыОбъектовРемонтаВУчетеСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта
	|ИЗ
	|	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Статусы КАК Статусы
	|		ПО (Статусы.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка)
	|ГДЕ
	|	торо_ОбъектыРемонта.Ссылка В(&МассивОР)
	|	И ЕСТЬNULL(Статусы.СтатусОР, ЗНАЧЕНИЕ(Перечисление.торо_СтатусыОРВУчете.НеПринятоКУчету)) В (&СписокСтатусов)";
	
	Если НеУдалятьГруппы Тогда 
		Запрос.Текст = Запрос.Текст +"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|" 
		+ "ВЫБРАТЬ
		  |	торо_ОбъектыРемонта.Ссылка КАК Ссылка
		  |ИЗ
		  |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		  |ГДЕ
		  |	торо_ОбъектыРемонта.Ссылка В(&МассивОР)
		  |	И торо_ОбъектыРемонта.ЭтоГруппа = ИСТИНА";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОР", МассивОР);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("СписокСтатусов", ОтборСтатусыВУчете);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат.ВыгрузитьКолонку("ОбъектРемонта");
	
КонецФункции

#КонецОбласти
