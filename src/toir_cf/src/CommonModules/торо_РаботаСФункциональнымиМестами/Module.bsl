
#Область ПрограммныйИнтерфейс

// Вовзаращает табицу объектов ремонта, не соответсвующих требованиям функционального места.
//
// Параметры:
//		МВТДляПроверки - МенеджерВременныхТаблиц - МВТ, в котором есть ВТ ОР для проверки ограничения, содержащая поля:
//			*ФункциональноеМесто - СправочникСсылка.торо_ОбъектыРемонта - функциональное место.
//			*ОбъектРемонта - СправочникСсылка.торо_ОбъектыРемонта - объект ремонта.
//			*СтруктураИерархии - СправочникСсылка.торо_СтруктурыОР - структура иерархии.
//		ПроверятьПоВсемИерархиям - Булево - если Истина, то проверка выполняется без учета структуры иерархии.
//
// Возвращаемое значение:
//		ТаблицаЗначений - таблица ОР, не соответствующих требованиям. Содержит колонки:
//			*ФункциональноеМесто - СправочникСсылка.торо_ОбъектыРемонта - функциональное место.
//			*ОбъектРемонта - СправочникСсылка.торо_ОбъектыРемонта - объект ремонта.
//			*СтруктураИерархии - СправочникСсылка.торо_СтруктурыОР - структура иерархии.
//
Функция ПолучитьНесоответствующиеОР(МВТДляПроверки, ПроверятьПоВсемИерархиям = Ложь) Экспорт
	
	ТабНесоответствующихОР = Новый ТаблицаЗначений;
	ТабНесоответствующихОР.Колонки.Добавить("ОбъектРемонта");
	ТабНесоответствующихОР.Колонки.Добавить("ФункциональноеМесто");
	ТабНесоответствующихОР.Колонки.Добавить("СтруктураИерархии");
	
	СформироватьВТХарактеристикОР(МВТДляПроверки);
	СформироватьВТОграниченийФМ(МВТДляПроверки);
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МВТДляПроверки;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТ_Ограничения.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	ВТ_ХарактеристикиОР.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_Ограничения.СтруктураИерархии КАК СтруктураИерархии,
	               |	ВТ_Ограничения.Характеристика КАК Характеристика,
	               |	ВТ_Ограничения.ВидСравнения КАК ВидСравнения,
	               |	ВТ_Ограничения.Значение КАК ЗначениеОграничения,
	               |	ВТ_ХарактеристикиОР.Значение КАК ЗначениеОР
	               |ИЗ
	               |	ВТ_Ограничения КАК ВТ_Ограничения
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ХарактеристикиОР КАК ВТ_ХарактеристикиОР
	               |		ПО ВТ_Ограничения.ФункциональноеМесто = ВТ_ХарактеристикиОР.ФункциональноеМесто
	               |			И ВТ_Ограничения.Характеристика = ВТ_ХарактеристикиОР.Характеристика
	               |ИТОГИ ПО
	               |	ФункциональноеМесто,
	               |	ОбъектРемонта" + ?(ПроверятьПоВсемИерархиям, ", СтруктураИерархии", "");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	    Возврат ТабНесоответствующихОР;
	КонецЕсли;
	
	ВыборкаФМ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФМ.Следующий() Цикл
		ВыборкаОР = ВыборкаФМ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОР.Следующий() Цикл
			ВыборкаТретийУровень = ВыборкаОР.Выбрать(?(ПроверятьПоВсемИерархиям, ОбходРезультатаЗапроса.ПоГруппировкам, ОбходРезультатаЗапроса.Прямой));
			Пока ВыборкаТретийУровень.Следующий() Цикл
				Если ПроверятьПоВсемИерархиям Тогда
					ВыборкаДетальныеЗаписи = ВыборкаТретийУровень.Выбрать();
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						Если Не ОРСоответствуетОграничениям(ВыборкаДетальныеЗаписи) Тогда
							ЗаполнитьЗначенияСвойств(ТабНесоответствующихОР.Добавить(), ВыборкаДетальныеЗаписи);
							Прервать;
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли Не ОРСоответствуетОграничениям(ВыборкаТретийУровень) Тогда
					ЗаполнитьЗначенияСвойств(ТабНесоответствующихОР.Добавить(), ВыборкаТретийУровень);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТабНесоответствующихОР; 

КонецФункции

// Выполняет необходимые действия при добавлении новых ОР в иерархию, если родитель является ФМ.
//
// Параметры:
//		МассивОР - Массив из СправочникСсылка.торо_ОбъектыРемонта - массив добавляемых объектов ремонта.
//		РодительИерархии - СправочникСсылка.торо_ОбъектыРемонта - родитель, в которого выполняется добавление.
//		СтруктураИерархии - СправочникСсылка.торо_СтруктурыОР - структура иерархии.
//
Процедура ДобавлениеВИерархию(МассивОР, РодительИерархии, СтруктураИерархии) Экспорт
	
	Если ТипЗнч(РодительИерархии) = Тип("СправочникСсылка.торо_ОбъектыРемонта") И НЕ РодительИерархии.ЭтоГруппа 
		И РодительИерархии.ТипОбъекта = Перечисления.торо_ТипыОбъектовRCM.ФункциональноеМесто Тогда
		
		Для каждого ОбъектРемонта Из МассивОР Цикл
			Если ТипЗнч(ОбъектРемонта) = Тип("СправочникСсылка.торо_ОбъектыРемонта") И НЕ ОбъектРемонта.ЭтоГруппа И ОбъектРемонта.ТипОбъекта = Перечисления.торо_ТипыОбъектовRCM.Объект Тогда
				УстановкаОР(РодительИерархии, ОбъектРемонта, СтруктураИерархии);
				ДобавитьОбъектДляПроверки(ОбъектРемонта);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет необходимые действия при перемещении или удалении ОР из иерархии.
//
// Параметры:
//		МассивВыделенныхОбъектов - Массив из СправочникСсылка.торо_ОбъектыРемонта - массив перетаскиваемых объектов ремонта.
//		НовыйРодитель - СправочникСсылка.торо_ОбъектыРемонта - родитель, в которого выполняется добавление.
//		СтруктураИерархии - СправочникСсылка.торо_СтруктурыОР - структура иерархии.
//
Процедура Перетаскивание(МассивВыделенныхОбъектов, НовыйРодитель, СтруктураИерархии) Экспорт
	
	МассивПеремещаемыхОР = Новый Массив;
	Для каждого ПереносимыйОбъект Из МассивВыделенныхОбъектов Цикл
		Если ТипЗнч(ПереносимыйОбъект) = Тип("СправочникСсылка.торо_ОбъектыРемонта") И НЕ ПереносимыйОбъект.ЭтоГруппа И ПереносимыйОбъект.ТипОбъекта = Перечисления.торо_ТипыОбъектовRCM.Объект Тогда
			МассивПеремещаемыхОР.Добавить(ПереносимыйОбъект);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивПеремещаемыхОР.Количество() > 0 Тогда
		ПеремещениеОборудования(МассивПеремещаемыхОР, НовыйРодитель, СтруктураИерархии);
	КонецЕсли;
	
КонецПроцедуры 

// Выполняет необходимые действия при пометке на удаление ОР с переносом подчиненных элементов в корень иерархии.
//
// Параметры:
//		МассивОР - Массив из СправочникСсылка.торо_ОбъектыРемонта - массив подчиненных объектов ремонта.
//		СПодчиненными - Булево - нужно обработать и подчиненные объекты.
//
Процедура ДемонтажПриПометкеНаУдаление(МассивОР, СПодчиненными) Экспорт
	
	Если НЕ СПодчиненными Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_ИсторияПеремещенияОборудованияМеждуФМСрезПоследних.ФункциональноеМесто КАК ФункциональноеМесто,
		|	торо_ИсторияПеремещенияОборудованияМеждуФМСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
		|   торо_ИсторияПеремещенияОборудованияМеждуФМСрезПоследних.СтруктураИерархии КАК СтруктураИерархии,
		|	торо_ИсторияПеремещенияОборудованияМеждуФМСрезПоследних.Событие КАК Событие
		|ИЗ
		|	РегистрСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ.СрезПоследних(, ФункциональноеМесто В (&МассивРодителей)) КАК торо_ИсторияПеремещенияОборудованияМеждуФМСрезПоследних
		|ГДЕ
		|	торо_ИсторияПеремещенияОборудованияМеждуФМСрезПоследних.Событие = &Событие";
		
		Запрос.УстановитьПараметр("МассивРодителей", МассивОР);
		Запрос.УстановитьПараметр("Событие", Перечисления.торо_СобытияОборудованияНаФМ.Установка);
		
		Результат = Запрос.Выполнить();
		Выборка  = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ДемонтажОР(Выборка.ФункциональноеМесто, Выборка.ОбъектРемонта, Выборка.СтруктураИерархии);
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик регламентного задания.
Процедура торо_ПроверкаОборудованияНаФункциональныхМестах() Экспорт
	ПроверитьОборудованиеНаФМ();
	ПроверитьДокументы();
КонецПроцедуры

Процедура ДобавитьОбъектДляПроверки(ОбъектДляПроверки) Экспорт
	
	НаборЗаписей = РегистрыСведений.торо_ОбъектыДляПроверкиТребованийФМ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ОбъектДляПроверки);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
		Запись.Объект = ОбъектДляПроверки;
		НаборЗаписей.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьДокументДляПроверки(ТекущийДокумент) Экспорт
	
	НаборЗаписей = РегистрыСведений.торо_ДокументыДляПроверкиТребованийФМ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПроверяемыйДокумент.Установить(ТекущийДокумент);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
		Запись.ПроверяемыйДокумент = ТекущийДокумент;
		Запись.Период = ТекущийДокумент.Дата;
	Иначе 
		НаборЗаписей[0].Период = ТекущийДокумент.Дата;
	КонецЕсли;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура УдалитьДокументДляПроверки(ТекущийДокумент) Экспорт
	
	НаборЗаписей = РегистрыСведений.торо_ДокументыДляПроверкиТребованийФМ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПроверяемыйДокумент.Установить(ТекущийДокумент);
	НаборЗаписей.Прочитать();

	Если НаборЗаписей.Количество() > 0  Тогда
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановкаОР(ФункциональноеМесто, ОбъектРемонта, СтруктураИерархии) Экспорт
	
	НоваяЗапись = РегистрыСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ.СоздатьМенеджерЗаписи();
	НоваяЗапись.ФункциональноеМесто = ФункциональноеМесто;
	НоваяЗапись.ОбъектРемонта = ОбъектРемонта;
	НоваяЗапись.СтруктураИерархии = СтруктураИерархии;
	НоваяЗапись.Событие = Перечисления.торо_СобытияОборудованияНаФМ.Установка;
	НоваяЗапись.Период = ТекущаяДата() + 1;
	НоваяЗапись.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьОборудованиеНаФМ()

	МассивФМ        = Новый Массив;
	МассивОР        = Новый Массив;
	МассивТиповыхФМ = Новый Массив;
	МассивТиповыхОР = Новый Массив;
	
	РегистрОбъектовДляПроверки = РегистрыСведений.торо_ОбъектыДляПроверкиТребованийФМ.СоздатьНаборЗаписей();
	РегистрОбъектовДляПроверки.Прочитать();
	
	Для каждого СтрокаРегистра Из РегистрОбъектовДляПроверки Цикл
		
		ТекущийОбъект = СтрокаРегистра.Объект;
		Если ТипЗнч(ТекущийОбъект) = Тип("СправочникСсылка.торо_ТиповыеОР") И ТекущийОбъект.ТипОбъекта = перечисления.торо_ТипыОбъектовRCM.Объект Тогда
			МассивТиповыхОР.Добавить(ТекущийОбъект);
		ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("СправочникСсылка.торо_ТиповыеОР") И ТекущийОбъект.ТипОбъекта = перечисления.торо_ТипыОбъектовRCM.ФункциональноеМесто Тогда 
			МассивТиповыхФМ.Добавить(ТекущийОбъект);
		ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("СправочникСсылка.торо_ОбъектыРемонта") И ТекущийОбъект.ТипОбъекта = перечисления.торо_ТипыОбъектовRCM.Объект Тогда
			МассивОР.Добавить(ТекущийОбъект);
		ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("СправочникСсылка.торо_ОбъектыРемонта") И ТекущийОбъект.ТипОбъекта = перечисления.торо_ТипыОбъектовRCM.ФункциональноеМесто Тогда
			МассивФМ.Добавить(ТекущийОбъект);
		КонецЕсли;
				
	КонецЦикла;
	
	ДозаполнитьМассивИзТиповых(МассивТиповыхОР, МассивОР);
	ДозаполнитьМассивИзТиповых(МассивТиповыхФМ, МассивФМ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ИсторияПеремещенияСрезПоследних.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	торо_ИсторияПеремещенияСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_ИсторияПеремещенияСрезПоследних.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ВсеРодители
	               |ИЗ
	               |	РегистрСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ.СрезПоследних(, ОбъектРемонта В (&МассивОР)) КАК торо_ИсторияПеремещенияСрезПоследних
	               |ГДЕ
	               |	торо_ИсторияПеремещенияСрезПоследних.Событие = ЗНАЧЕНИЕ(Перечисление.торо_СобытияОборудованияНаФМ.Установка)
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	торо_ИсторияПеремещенияСрезПоследних.ФункциональноеМесто,
	               |	торо_ИсторияПеремещенияСрезПоследних.ОбъектРемонта,
	               |	торо_ИсторияПеремещенияСрезПоследних.СтруктураИерархии
	               |ИЗ
	               |	РегистрСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ.СрезПоследних(, ФункциональноеМесто В (&МассивФМ)) КАК торо_ИсторияПеремещенияСрезПоследних
	               |ГДЕ
	               |	торо_ИсторияПеремещенияСрезПоследних.Событие = ЗНАЧЕНИЕ(Перечисление.торо_СобытияОборудованияНаФМ.Установка)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ВсеРодители.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	ВТ_ВсеРодители.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_ВсеРодители.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ОРДляПроверки
	               |ИЗ
	               |	ВТ_ВсеРодители КАК ВТ_ВсеРодители
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ВсеРодители.ФункциональноеМесто,
	               |	ВТ_ВсеРодители.ОбъектРемонта,
	               |	ЗНАЧЕНИЕ(Справочник.торо_СтруктурыОР.ПустаяСсылка)
	               |ИЗ
	               |	ВТ_ВсеРодители КАК ВТ_ВсеРодители
	               |
	               |ИНДЕКСИРОВАТЬ ПО
				   |	ОбъектРемонта,
	               |	ФункциональноеМесто,
				   |	СтруктураИерархии
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_ВсеРодители";
	
	Запрос.УстановитьПараметр("МассивОР", МассивОР);
	Запрос.УстановитьПараметр("МассивФМ", МассивФМ);
	
	Результат = Запрос.Выполнить();
	
	ТабНесоответствующихОР = торо_РаботаСФункциональнымиМестами.ПолучитьНесоответствующиеОР(Запрос.МенеджерВременныхТаблиц);

	РегистрОбъектовДляПроверки.Очистить();
	РегистрОбъектовДляПроверки.Записать();
	
	ПараметрыОповещения = Константы.торо_ПараметрыОповещенияОПроверкеОборудованияНаФМ.Получить();
	РазделятьПолучателей = ПараметрыОповещения.РазделятьПолучателейПоПодразделениям;
	
	Для каждого Строка Из ТабНесоответствующихОР Цикл
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Ссылка", Строка.ОбъектРемонта);
		СтруктураДанных.Вставить("ОбъектРемонта", Строка.ОбъектРемонта);
		СтруктураДанных.Вставить("ФункциональноеМесто", Строка.ФункциональноеМесто);
		
		Если РазделятьПолучателей Тогда
			СтруктураДанных.Вставить("Подразделение", Строка.ФункциональноеМесто.Подразделение);
		КонецЕсли;
		
		торо_РаботаСУведомлениями.ДобавитьЗаписиВОчередьУведомленийПоПараметрам(ПараметрыОповещения, СтруктураДанных);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДозаполнитьМассивИзТиповых(МассивТиповых, МассивОбъектов)
	
	Если МассивТиповых.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта
		               |ИЗ
		               |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		               |ГДЕ
		               |	торо_ОбъектыРемонта.ТиповойОР В(&МассивТиповых)
		               |
		               |ОБЪЕДИНИТЬ
		               |
		               |ВЫБРАТЬ
		               |	торо_ОбъектыРемонта.Ссылка
		               |ИЗ
		               |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		               |ГДЕ
		               |	торо_ОбъектыРемонта.Ссылка В(&МассивОбъектов)";
		
		Запрос.УстановитьПараметр("МассивТиповых", МассивТиповых);
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		МассивОбъектов.Очистить();
		Пока Выборка.Следующий() Цикл
			МассивОбъектов.Добавить(Выборка.ОбъектРемонта);
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДокументы()
	
	РегистрДокументовДляПроверки = РегистрыСведений.торо_ДокументыДляПроверкиТребованийФМ.СоздатьНаборЗаписей();
	РегистрДокументовДляПроверки.Прочитать();
	
	МассивДокументов = Новый Массив;
	
	Для каждого СтрокаРегистра Из РегистрДокументовДляПроверки Цикл
		МассивДокументов.Добавить(СтрокаРегистра.ПроверяемыйДокумент);
	КонецЦикла;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Ссылка КАК Документ,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Показатель КАК Показатель,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_ОРДокумента
	               |ИЗ
	               |	Документ.торо_УчетКонтролируемыхПоказателей.Показатели КАК торо_УчетКонтролируемыхПоказателейПоказатели
	               |ГДЕ
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Ссылка В(&МассивДокументов)
				   |
				   |ИНДЕКСИРОВАТЬ ПО
				   |	ОбъектРемонта,
				   |	Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_РасположениеОРВСтруктуреИерархииСрезПоследних.РодительИерархии КАК РодительИерархии,
	               |	торо_РасположениеОРВСтруктуреИерархииСрезПоследних.ОбъектИерархии КАК ОбъектИерархии,
	               |	торо_РасположениеОРВСтруктуреИерархииСрезПоследних.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ВсеРодители
	               |ИЗ
	               |	РегистрСведений.торо_РасположениеОРВСтруктуреИерархии.СрезПоследних(
	               |			,
	               |			ОбъектИерархии В
	               |				(ВЫБРАТЬ
	               |					ВТ_ОРДокумента.ОбъектРемонта
	               |				ИЗ
	               |					ВТ_ОРДокумента КАК ВТ_ОРДокумента)) КАК торо_РасположениеОРВСтруктуреИерархииСрезПоследних
	               |ГДЕ
	               |	торо_РасположениеОРВСтруктуреИерархииСрезПоследних.Удален = ЛОЖЬ
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	торо_ИерархическиеСтруктурыОР.РодительИерархии,
	               |	торо_ИерархическиеСтруктурыОР.ОбъектИерархии,
	               |	торо_ИерархическиеСтруктурыОР.СтруктураИерархии
	               |ИЗ
	               |	РегистрСведений.торо_ИерархическиеСтруктурыОР КАК торо_ИерархическиеСтруктурыОР
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДокумента КАК ВТ_ОРДокумента
	               |		ПО торо_ИерархическиеСтруктурыОР.ОбъектИерархии = ВТ_ОРДокумента.ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ВсеРодители.РодительИерархии КАК ФункциональноеМесто,
	               |	ВТ_ВсеРодители.ОбъектИерархии КАК ОбъектРемонта,
	               |	ВТ_ВсеРодители.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ОРДляПроверки
	               |ИЗ
	               |	ВТ_ВсеРодители КАК ВТ_ВсеРодители
	               |ГДЕ
	               |	НЕ ВТ_ВсеРодители.ОбъектИерархии.ЭтоГруппа
	               |	И НЕ ВТ_ВсеРодители.ОбъектИерархии.ПометкаУдаления
	               |	И ВТ_ВсеРодители.ОбъектИерархии.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.торо_ТипыОбъектовRCM.Объект)
	               |	И ВТ_ВсеРодители.РодительИерархии.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.торо_ТипыОбъектовRCM.ФункциональноеМесто)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ_ВсеРодители.РодительИерархии,
	               |	ВТ_ВсеРодители.ОбъектИерархии,
	               |	ЗНАЧЕНИЕ(Справочник.торо_СтруктурыОР.ПустаяСсылка)
	               |ИЗ
	               |	ВТ_ВсеРодители КАК ВТ_ВсеРодители
	               |ГДЕ
	               |	НЕ ВТ_ВсеРодители.ОбъектИерархии.ЭтоГруппа
	               |	И НЕ ВТ_ВсеРодители.ОбъектИерархии.ПометкаУдаления
	               |	И ВТ_ВсеРодители.ОбъектИерархии.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.торо_ТипыОбъектовRCM.Объект)
	               |	И ВТ_ВсеРодители.РодительИерархии.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.торо_ТипыОбъектовRCM.ФункциональноеМесто)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ФункциональноеМесто,
				   |	СтруктураИерархии
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_ВсеРодители";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Результат = Запрос.Выполнить();
	
	СформироватьВТОграниченийФМ(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТ_ОРДокумента.Документ КАК Ссылка,
	               |	ВТ_ОРДокумента.Документ.Дата КАК Дата,
	               |	ВТ_ОРДокумента.Документ.Номер КАК Номер,
	               |	ВТ_ОРДокумента.Документ.Ответственный КАК Ответственный,
	               |	ВТ_ОРДокумента.Документ.Комментарий КАК Комментарий,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_ОРДокумента.Показатель) КАК Показатель,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_ОРДокумента.Показатель.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПоказателя,
	               |	ВТ_ОРДокумента.Значение КАК ЗначениеОР,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_ОРДокумента.Значение) КАК ЗначениеПоказателя,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_Ограничения.ФункциональноеМесто) КАК ФункциональноеМесто,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_Ограничения.ФункциональноеМесто.Подразделение) КАК Подразделение,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_ОРДокумента.ОбъектРемонта) КАК ОбъектРемонта,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_Ограничения.СтруктураИерархии) КАК СтруктураИерархии,
	               |	ВТ_Ограничения.ВидСравнения КАК ВидСравнения,
	               |	ВТ_Ограничения.Значение КАК ЗначениеОграничения,
	               |	ПРЕДСТАВЛЕНИЕ(ВТ_Ограничения.Ограничение) КАК Ограничение
	               |ИЗ
	               |	ВТ_ОРДокумента КАК ВТ_ОРДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДляПроверки КАК ВТ_ОРДляПроверки
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ограничения КАК ВТ_Ограничения
	               |			ПО ВТ_ОРДляПроверки.ФункциональноеМесто = ВТ_Ограничения.ФункциональноеМесто
	               |				И ВТ_ОРДляПроверки.СтруктураИерархии = ВТ_Ограничения.СтруктураИерархии
	               |		ПО ВТ_ОРДокумента.ОбъектРемонта = ВТ_ОРДляПроверки.ОбъектРемонта
	               |			И (ВТ_ОРДокумента.Показатель = ВТ_Ограничения.Характеристика)";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Результат = Запрос.Выполнить();
	
	РегистрДокументовДляПроверки.Очистить();
	РегистрДокументовДляПроверки.Записать();
	
	ПараметрыОповещения = Константы.торо_ПараметрыОповещенияОПроверкеТребованийФМПоДокументам.Получить();
	РазделятьПолучателей = ПараметрыОповещения.РазделятьПолучателейПоПодразделениям;
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ОРСоответствуетОграничениям(ВыборкаДетальныеЗаписи) Тогда
		    Продолжить;
		КонецЕсли;
		
		СтруктураДанных = Новый Структура("Ссылка, Дата, Номер, Ответственный, Комментарий, Показатель,
										|ЗначениеПоказателя, ЕдиницаИзмеренияПоказателя, ОбъектРемонта,
										|ФункциональноеМесто,Ограничение, СтруктураИерархии");
		ЗаполнитьЗначенияСвойств(СтруктураДанных, ВыборкаДетальныеЗаписи);
		
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СтруктураИерархии) Тогда
			СтруктураДанных.СтруктураИерархии = "'По всем иерархиям'";
		КонецЕсли;
		
		Если РазделятьПолучателей Тогда
			СтруктураДанных.Вставить("Подразделение", ВыборкаДетальныеЗаписи.Подразделение);
		КонецЕсли;
		
		торо_РаботаСУведомлениями.ДобавитьЗаписиВОчередьУведомленийПоПараметрам(ПараметрыОповещения, СтруктураДанных);
	КонецЦикла;
КонецПроцедуры

Процедура ПеремещениеОборудования(МассивПеремещаемыхОР, НовыйРодитель, СтруктураИерархии) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ИсторияПеремещенияСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
				   |    торо_ИсторияПеремещенияСрезПоследних.СтруктураИерархии КАК СтруктураИерархии,
	               |	МАКСИМУМ(торо_ИсторияПеремещенияСрезПоследних.Период) КАК Период
	               |ПОМЕСТИТЬ ВТ_МаксПериод
	               |ИЗ
	               |	РегистрСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ.СрезПоследних(
	               |			,
	               |			ОбъектРемонта В (&МассивУстанавливаемыхОР)
	               |				И СтруктураИерархии = &СтруктураИерархии) КАК торо_ИсторияПеремещенияСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
				   |    торо_ИсторияПеремещенияСрезПоследних.СтруктураИерархии,
	               |	торо_ИсторияПеремещенияСрезПоследних.ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_МаксПериод.Период КАК ДатаСобытия,
	               |	торо_ИсторияПеремещения.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	торо_ИсторияПеремещения.ОбъектРемонта КАК ОбъектРемонта,
				   |    торо_ИсторияПеремещения.СтруктураИерархии КАК СтруктураИерархии, 
	               |	торо_ИсторияПеремещения.Событие КАК Событие
	               |ПОМЕСТИТЬ ВТ_ИзРегистра
	               |ИЗ
	               |	ВТ_МаксПериод КАК ВТ_МаксПериод
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ КАК торо_ИсторияПеремещения
	               |		ПО ВТ_МаксПериод.ОбъектРемонта = торо_ИсторияПеремещения.ОбъектРемонта
	               |			И ВТ_МаксПериод.Период = торо_ИсторияПеремещения.Период
				   |            И ВТ_МаксПериод.СтруктураИерархии = торо_ИсторияПеремещения.СтруктураИерархии
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
	               |	ВТ_ИзРегистра.ФункциональноеМесто КАК ФункциональноеМесто,
				   |    ВТ_ИзРегистра.СтруктураИерархии КАК СтруктураИерархии,
	               |	ВТ_ИзРегистра.Событие КАК Событие
	               |ИЗ
	               |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзРегистра КАК ВТ_ИзРегистра
	               |		ПО торо_ОбъектыРемонта.Ссылка = ВТ_ИзРегистра.ОбъектРемонта
	               |ГДЕ
	               |	торо_ОбъектыРемонта.Ссылка В(&МассивУстанавливаемыхОР)";
	
	Запрос.УстановитьПараметр("МассивУстанавливаемыхОР", МассивПеремещаемыхОР);
	Запрос.УстановитьПараметр("СтруктураИерархии", СтруктураИерархии);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Событие = Перечисления.торо_СобытияОборудованияНаФМ.Установка Тогда
			ДемонтажОР(Выборка.ФункциональноеМесто, Выборка.ОбъектРемонта, Выборка.СтруктураИерархии);
			ОбновитьТиповойСоставФМ(Выборка.ФункциональноеМесто, Выборка.ОбъектРемонта);
		КонецЕсли;
		
		Если ТипЗнч(НовыйРодитель) = Тип("СправочникСсылка.торо_ОбъектыРемонта") И НЕ НовыйРодитель.ЭтоГруппа 
		  И НовыйРодитель.ТипОбъекта = Перечисления.торо_ТипыОбъектовRCM.ФункциональноеМесто Тогда
		   УстановкаОР(НовыйРодитель, Выборка.ОбъектРемонта, СтруктураИерархии);
		   ДобавитьОбъектДляПроверки(Выборка.ОбъектРемонта);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДемонтажОР(ФункциональноеМесто, ОбъектРемонта, СтруктураИерархии)
	
	НоваяЗапись = РегистрыСведений.торо_ИсторияПеремещенияОборудованияМеждуФМ.СоздатьМенеджерЗаписи();
	НоваяЗапись.ФункциональноеМесто = ФункциональноеМесто;
	НоваяЗапись.ОбъектРемонта = ОбъектРемонта;
	НоваяЗапись.СтруктураИерархии = СтруктураИерархии;
	НоваяЗапись.Событие = Перечисления.торо_СобытияОборудованияНаФМ.Демонтаж;
	НоваяЗапись.Период = ТекущаяДата();
	НоваяЗапись.Записать(Ложь);

КонецПроцедуры

Процедура ОбновитьТиповойСоставФМ(ФункциональноеМесто, ОбъектРемонта)
	
	Попытка
		НаборЗаписей = РегистрыСведений.торо_СоставФункциональныхМест.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ФункциональноеМесто.Установить(ФункциональноеМесто);
		НаборЗаписей.Отбор.ОбъектРемонта.Установить(ОбъектРемонта);
		НаборЗаписей.Записать(Истина);
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось обновить типовой состав функционального места: '") + ОписаниеОшибки();
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
			
КонецПроцедуры

Процедура СформироватьВТХарактеристикОР(МВТДляПроверки)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТДляПроверки;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ОРДляПроверки.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
	               |	торо_ТиповыеОРДополнительныеРеквизиты.Ссылка КАК ТиповойОР,
	               |	торо_ТиповыеОРДополнительныеРеквизиты.Свойство КАК Свойство,
	               |	торо_ТиповыеОРДополнительныеРеквизиты.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_ИзТиповогоОР
	               |ИЗ
	               |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДляПроверки КАК ВТ_ОРДляПроверки
	               |		ПО торо_ОбъектыРемонта.Ссылка = ВТ_ОРДляПроверки.ОбъектРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТиповыеОР.ДополнительныеРеквизиты КАК торо_ТиповыеОРДополнительныеРеквизиты
	               |		ПО торо_ОбъектыРемонта.ТиповойОР = торо_ТиповыеОРДополнительныеРеквизиты.Ссылка
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ОбъектРемонта,
	               |	Свойство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ОРДляПроверки.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	торо_ОбъектыРемонтаДополнительныеРеквизиты.Ссылка КАК ОбъектРемонта,
	               |	торо_ОбъектыРемонтаДополнительныеРеквизиты.Свойство КАК Свойство,
	               |	торо_ОбъектыРемонтаДополнительныеРеквизиты.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_ИзОР
	               |ИЗ
	               |	Справочник.торо_ОбъектыРемонта.ДополнительныеРеквизиты КАК торо_ОбъектыРемонтаДополнительныеРеквизиты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДляПроверки КАК ВТ_ОРДляПроверки
	               |		ПО торо_ОбъектыРемонтаДополнительныеРеквизиты.Ссылка = ВТ_ОРДляПроверки.ОбъектРемонта
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ОбъектРемонта,
	               |	Свойство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЕСТЬNULL(ВТ_ИзОР.ФункциональноеМесто, ВТ_ИзТиповогоОР.ФункциональноеМесто) КАК ФункциональноеМесто,
	               |	ЕСТЬNULL(ВТ_ИзОР.ОбъектРемонта, ВТ_ИзТиповогоОР.ОбъектРемонта) КАК ОбъектРемонта,
	               |	ЕСТЬNULL(ВТ_ИзОР.Свойство, ВТ_ИзТиповогоОР.Свойство) КАК Характеристика,
	               |	ЕСТЬNULL(ВТ_ИзОР.Значение, ВТ_ИзТиповогоОР.Значение) КАК Значение
	               |ПОМЕСТИТЬ ВТ_ДопРеквизиты
	               |ИЗ
	               |	ВТ_ИзТиповогоОР КАК ВТ_ИзТиповогоОР
	               |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ИзОР КАК ВТ_ИзОР
	               |		ПО ВТ_ИзТиповогоОР.ОбъектРемонта = ВТ_ИзОР.ОбъектРемонта
	               |			И ВТ_ИзТиповогоОР.Свойство = ВТ_ИзОР.Свойство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	МАКСИМУМ(торо_ЗначенияКонтролируемыхПоказателейСрезПоследних.Период) КАК Период,
	               |	торо_ЗначенияКонтролируемыхПоказателейСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_ЗначенияКонтролируемыхПоказателейСрезПоследних.Показатель КАК Показатель
	               |ПОМЕСТИТЬ ВТ_МаксПериод
	               |ИЗ
	               |	РегистрСведений.торо_ЗначенияКонтролируемыхПоказателей.СрезПоследних(
	               |			,
	               |			ОбъектРемонта В
	               |				(ВЫБРАТЬ
	               |					ВТ_ОРДляПроверки.ОбъектРемонта
	               |				ИЗ
	               |					ВТ_ОРДляПроверки)) КАК торо_ЗначенияКонтролируемыхПоказателейСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	торо_ЗначенияКонтролируемыхПоказателейСрезПоследних.ОбъектРемонта,
	               |	торо_ЗначенияКонтролируемыхПоказателейСрезПоследних.Показатель
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Период,
	               |	ОбъектРемонта,
	               |	Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ВТ_ОРДляПроверки.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	ВТ_МаксПериод.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_МаксПериод.Показатель КАК Характеристика,
	               |	торо_ЗначенияКонтролируемыхПоказателей.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_Показатели
	               |ИЗ
	               |	ВТ_МаксПериод КАК ВТ_МаксПериод
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ЗначенияКонтролируемыхПоказателей КАК торо_ЗначенияКонтролируемыхПоказателей
	               |		ПО ВТ_МаксПериод.Период = торо_ЗначенияКонтролируемыхПоказателей.Период
	               |			И ВТ_МаксПериод.ОбъектРемонта = торо_ЗначенияКонтролируемыхПоказателей.ОбъектРемонта
	               |			И ВТ_МаксПериод.Показатель = торо_ЗначенияКонтролируемыхПоказателей.Показатель
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДляПроверки КАК ВТ_ОРДляПроверки
	               |		ПО ВТ_МаксПериод.ОбъектРемонта = ВТ_ОРДляПроверки.ОбъектРемонта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДопРеквизиты.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	ВТ_ДопРеквизиты.ОбъектРемонта КАК ОбъектРемонта,
	               |	ВТ_ДопРеквизиты.Характеристика КАК Характеристика,
	               |	ВТ_ДопРеквизиты.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_ХарактеристикиОР
	               |ИЗ
	               |	ВТ_ДопРеквизиты КАК ВТ_ДопРеквизиты
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_Показатели.ФункциональноеМесто,
	               |	ВТ_Показатели.ОбъектРемонта,
	               |	ВТ_Показатели.Характеристика,
	               |	ВТ_Показатели.Значение
	               |ИЗ
	               |	ВТ_Показатели КАК ВТ_Показатели
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ФункциональноеМесто,
	               |	Характеристика";
	
	Запрос.Выполнить();
КонецПроцедуры

Процедура СформироватьВТОграниченийФМ(МВТДляПроверки)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТДляПроверки;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	торо_ОбъектыРемонта.Ссылка КАК ФункциональноеМесто,
	               |	ОграниченияИзТипового.Ограничение КАК Ограничение,
	               |	ОграниченияИзТипового.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ИзТиповогоФМ
	               |ИЗ
	               |	Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДляПроверки КАК ВТ_ОРДляПроверки
	               |		ПО торо_ОбъектыРемонта.Ссылка = ВТ_ОРДляПроверки.ФункциональноеМесто
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТиповыеОР.ОграниченияНаХарактеристикиОборудования КАК ОграниченияИзТипового
	               |		ПО торо_ОбъектыРемонта.ТиповойОР = ОграниченияИзТипового.Ссылка
	               |			И (ВТ_ОРДляПроверки.СтруктураИерархии = ОграниченияИзТипового.СтруктураИерархии)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	торо_ОграниченияНаХарактеристикиОборудования.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	торо_ОграниченияНаХарактеристикиОборудования.Ограничение КАК Ограничение,
	               |	торо_ОграниченияНаХарактеристикиОборудования.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ИзРСОграничений
	               |ИЗ
	               |	РегистрСведений.торо_ОграниченияНаХарактеристикиОборудования КАК торо_ОграниченияНаХарактеристикиОборудования
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОРДляПроверки КАК ВТ_ОРДляПроверки
	               |		ПО торо_ОграниченияНаХарактеристикиОборудования.ФункциональноеМесто = ВТ_ОРДляПроверки.ФункциональноеМесто
	               |			И торо_ОграниченияНаХарактеристикиОборудования.СтруктураИерархии = ВТ_ОРДляПроверки.СтруктураИерархии
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ИзТиповогоФМ.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	ВТ_ИзТиповогоФМ.Ограничение КАК Ограничение,
	               |	ВТ_ИзТиповогоФМ.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_ОграниченияНачальная
	               |ИЗ
	               |	ВТ_ИзТиповогоФМ КАК ВТ_ИзТиповогоФМ
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ИзРСОграничений.ФункциональноеМесто,
	               |	ВТ_ИзРСОграничений.Ограничение,
	               |	ВТ_ИзРСОграничений.СтруктураИерархии
	               |ИЗ
	               |	ВТ_ИзРСОграничений КАК ВТ_ИзРСОграничений
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Ограничение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_ОграниченияНачальная.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	торо_Ограничения.Характеристика КАК Характеристика,
	               |	ВТ_ОграниченияНачальная.Ограничение КАК Ограничение,
	               |	торо_Ограничения.Значение КАК Значение,
	               |	торо_Ограничения.ВидСравнения КАК ВидСравнения,
	               |	ВТ_ОграниченияНачальная.СтруктураИерархии КАК СтруктураИерархии
	               |ПОМЕСТИТЬ ВТ_СписокОграниченийСГруппами
	               |ИЗ
	               |	Справочник.торо_ОграниченияНаХарактеристикиОборудования.СписокОграничений КАК торо_Ограничения
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОграниченияНачальная КАК ВТ_ОграниченияНачальная
	               |		ПО (ВТ_ОграниченияНачальная.Ограничение = торо_Ограничения.Ссылка)
				   |
				   |ИНДЕКСИРОВАТЬ ПО
				   |	Характеристика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_СписокОграниченийСГруппами.ФункциональноеМесто КАК ФункциональноеМесто,
	               |	ВТ_СписокОграниченийСГруппами.СтруктураИерархии КАК СтруктураИерархии,
	               |	ВТ_СписокОграниченийСГруппами.Ограничение КАК Ограничение,
	               |	СписокХарактеристикГруппы.Характеристика КАК Характеристика,
	               |	ВТ_СписокОграниченийСГруппами.ВидСравнения КАК ВидСравнения,
	               |	ВТ_СписокОграниченийСГруппами.Значение КАК Значение
	               |ПОМЕСТИТЬ ВТ_Ограничения
	               |ИЗ
	               |	ВТ_СписокОграниченийСГруппами КАК ВТ_СписокОграниченийСГруппами
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.торо_ГруппыХарактеристикОборудования.СписокХарактеристик КАК СписокХарактеристикГруппы
	               |		ПО ВТ_СписокОграниченийСГруппами.Характеристика = СписокХарактеристикГруппы.Ссылка
	               |ГДЕ
	               |	ТИПЗНАЧЕНИЯ(ВТ_СписокОграниченийСГруппами.Характеристика) = ТИП(ПланВидовХарактеристик.торо_ГруппыХарактеристикОборудования)
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_СписокОграниченийСГруппами.ФункциональноеМесто,
	               |	ВТ_СписокОграниченийСГруппами.СтруктураИерархии,
	               |	ВТ_СписокОграниченийСГруппами.Ограничение,
	               |	ВТ_СписокОграниченийСГруппами.Характеристика,
	               |	ВТ_СписокОграниченийСГруппами.ВидСравнения,
	               |	ВТ_СписокОграниченийСГруппами.Значение
	               |ИЗ
	               |	ВТ_СписокОграниченийСГруппами КАК ВТ_СписокОграниченийСГруппами
	               |ГДЕ
	               |	НЕ ТИПЗНАЧЕНИЯ(ВТ_СписокОграниченийСГруппами.Характеристика) = ТИП(ПланВидовХарактеристик.торо_ГруппыХарактеристикОборудования)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ФункциональноеМесто,
				   |	СтруктураИерархии";
	
	Запрос.Выполнить();
КонецПроцедуры

Функция ОРСоответствуетОграничениям(ВыборкаДетальныеЗаписи)
	Если ВыборкаДетальныеЗаписи.ВидСравнения = Перечисления.торо_ВидыСравнения.Равно 
			И ВыборкаДетальныеЗаписи.ЗначениеОР <> ВыборкаДетальныеЗаписи.ЗначениеОграничения
		ИЛИ ВыборкаДетальныеЗаписи.ВидСравнения = Перечисления.торо_ВидыСравнения.НеРавно 
			И ВыборкаДетальныеЗаписи.ЗначениеОР = ВыборкаДетальныеЗаписи.ЗначениеОграничения
		ИЛИ ВыборкаДетальныеЗаписи.ВидСравнения = Перечисления.торо_ВидыСравнения.Больше 
	  		И НЕ ВыборкаДетальныеЗаписи.ЗначениеОР > ВыборкаДетальныеЗаписи.ЗначениеОграничения
		ИЛИ ВыборкаДетальныеЗаписи.ВидСравнения = Перечисления.торо_ВидыСравнения.БольшеИлиРавно 
	  		И НЕ ВыборкаДетальныеЗаписи.ЗначениеОР >= ВыборкаДетальныеЗаписи.ЗначениеОграничения
		ИЛИ ВыборкаДетальныеЗаписи.ВидСравнения = Перечисления.торо_ВидыСравнения.Меньше
	  		И НЕ ВыборкаДетальныеЗаписи.ЗначениеОР < ВыборкаДетальныеЗаписи.ЗначениеОграничения
		ИЛИ ВыборкаДетальныеЗаписи.ВидСравнения = Перечисления.торо_ВидыСравнения.МеньшеИлиРавно 
	  		И НЕ ВыборкаДетальныеЗаписи.ЗначениеОР <= ВыборкаДетальныеЗаписи.ЗначениеОграничения Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

#КонецОбласти