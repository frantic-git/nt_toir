
#Область ПрограммныйИнтерфейс

// Открывает форму подбора значений из классификатора
//
// Параметры:
//			ИмяСправочника - Строка - имя справочника как оно задано в конфигураторе
//  		ПолныйПутьКМакету - Строка - формат полного пути:
//								"Документ.<ИмяДокумента>.<ИмяМакета>"
//								"Обработка.<ИмяОбработки>.<ИмяМакета>"
//								"ОбщийМакет.<ИмяМакета>"
//			Заголовок - Строка - заголовок формы.
//			Форма - ФормаКлиентскогоПриложения - форма.
//			СоответствиеПолей - Соответствие - задает соответствие имен реквизитов справочника\
//							именам полей классификатора.
//
Процедура ОткрытьФормуПодбораИзКлассификатора(ИмяСправочника, ПолныйПутьКМакету, Заголовок, Форма, СоответствиеПолей = Неопределено) Экспорт
	
	СтруктураПараметров = Новый Структура("ИмяСправочника,ПолныйПутьКМакету,Заголовок",
		ИмяСправочника,
		ПолныйПутьКМакету,
		Заголовок);
		
	Если СоответствиеПолей <> Неопределено Тогда
		СтруктураПараметров.Вставить("СоответствиеПолей", СоответствиеПолей);
	КонецЕсли; 
	
	ДопПараметры = Новый Структура("Форма", Форма);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуПодбораИзКлассификатораЗавершение", КадровыйУчетКлиент, ДопПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ЗаполнениеСправочниковИзКлассификаторов", СтруктураПараметров, Форма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

// Открывает форму выбора справочника Сотрудники с отбором только работающих
// сотрудников на указанную дату и в указанных, организации и подразделении.
//
// Параметры:
//		ВладелецФормыВыбора					- Форма или элемент формы, для которого открывается форма выбора.
//		Организация							- СправочникСсылка.Организации, если не указана, в отбор попадают все сотрудники компании.
//		Подразделение						- СправочникСсылка.ПодразделенияОрганизации, если не указано - в отбор попадают все сотрудники.
//		ДатаПримененияОтбора				- Дата, на которую необходимо выбрать работающих сотрудников.
//		МножественныйВыбор					- Булево, если ложь - форма откроется для выбора одного сотрудника.
//		АдресСпискаПодобранныхСотрудников	- Строка, адрес массива уже подобранных сотрудников во временном хранилище.
//		ПараметрыОткрытия					- Структура, аналогичная структуре параметров формы списка.
//
Процедура ВыбратьСотрудниковРаботающихНаДатуПоПараметрамОткрытияФормыСписка(ВладелецФормыВыбора, Организация = Неопределено, Подразделение = Неопределено, ДатаПримененияОтбора = '00010101', МножественныйВыбор = Истина, АдресСпискаПодобранныхСотрудников = "", ПараметрыОткрытия = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ДатаПримененияОтбора) Тогда
		
		Если ПараметрыОткрытия = Неопределено Тогда
			ПараметрыОткрытия = Новый Структура;
		КонецЕсли; 
		
		Если Не ПараметрыОткрытия.Свойство("Отбор") Тогда
			ПараметрыОткрытия.Вставить("Отбор", Новый Структура);
		КонецЕсли; 
	
		ПараметрыОткрытия.Отбор.Вставить("ДатаПримененияОтбора", ДатаПримененияОтбора);
		
	КонецЕсли; 
	
	ОткрытьФормуВыбораСотрудников(ВладелецФормыВыбора, Организация, Подразделение, МножественныйВыбор, ПараметрыОткрытия, АдресСпискаПодобранныхСотрудников);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  

Процедура ОткрытьФормуВыбораСотрудников(ВладелецФормыВыбора, Организация, Подразделение, МножественныйВыбор, ПараметрыФормы, АдресСпискаПодобранныхСотрудников)
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли; 
	
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", МножественныйВыбор);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", НЕ МножественныйВыбор);
		
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Если Не ПараметрыФормы.Свойство("Отбор") Тогда
			ПараметрыФормы.Вставить("Отбор", Новый Структура);
		КонецЕсли; 
		
		ПараметрыФормы.Отбор.Вставить("ТекущаяОрганизация", Организация);
		
	КонецЕсли; 
		
	Если ЗначениеЗаполнено(Подразделение) Тогда
		
		Если Не ПараметрыФормы.Свойство("Отбор") Тогда
			ПараметрыФормы.Вставить("Отбор", Новый Структура);
		КонецЕсли; 
		
		ПараметрыФормы.Отбор.Вставить("ТекущееПодразделение", Подразделение);
		
	КонецЕсли; 
	
	Если МножественныйВыбор И НЕ ПустаяСтрока(АдресСпискаПодобранныхСотрудников) Тогда
		ПараметрыФормы.Вставить("АдресСпискаПодобранныхСотрудников", АдресСпискаПодобранныхСотрудников);
	КонецЕсли; 
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаВыбора", ПараметрыФормы, ВладелецФормыВыбора);	
	
КонецПроцедуры

Процедура ОповеститьОбИзмененииРабочегоМеста(Форма) Экспорт
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ФизическоеЛицо", Форма.Объект.ФизическоеЛицо);
	ПараметрыОповещения.Вставить("Сотрудник", Форма.Объект.Сотрудник);
	
	Оповестить("ИзменениеДанныхМестаРаботы", ПараметрыОповещения, Форма);
	
КонецПроцедуры

Процедура ОткрытьФормуПодбораИзКлассификатораЗавершение(Результат, ДопПараметры) Экспорт
	ДопПараметры.Форма.Элементы.Список.Обновить();
КонецПроцедуры
 
#КонецОбласти
