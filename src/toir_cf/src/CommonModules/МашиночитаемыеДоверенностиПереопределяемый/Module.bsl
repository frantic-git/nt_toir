

#Область ПрограммныйИнтерфейс

// Заполняет при изменении данные физического лица по ссылке.
//
// Параметры:
//  ФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. МашиночитаемыеДоверенности.НовыеДанныеФизЛица
//
Процедура ПриИзмененииДанныеФизЛица(ФизЛицо, Сведения) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ФизЛицо) Тогда
		Возврат
	КонецЕсли;
	
	Сведения.Ссылка = ФизЛицо;
	
	Реквизиты = "ДатаРождения, ИНН, СтраховойНомерПФР, Пол, МестоРождения";
	
	СтруктураДанных = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФизЛицо, Реквизиты);
	
	СтруктураДанных.Свойство("ДатаРождения", Сведения.ДатаРождения);
	СтруктураДанных.Свойство("ИНН", Сведения.ИНН);
	СтруктураДанных.Свойство("СтраховойНомерПФР", Сведения.СтраховойНомерПФР);
	СтруктураДанных.Свойство("Пол", Сведения.Пол);
	СтруктураДанных.Свойство("МестоРождения", Сведения.МестоРождения);
	
	ЗапросФИО = Новый Запрос;
	ЗапросФИО.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ФИОФизическихЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизическихЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизическихЛицСрезПоследних.Отчество КАК Отчество
	|ИЗ
	|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(, ФизическоеЛицо = &ФизЛицо) КАК ФИОФизическихЛицСрезПоследних";
	
	ЗапросФИО.УстановитьПараметр("ФизЛицо", ФизЛицо);
	
	Выборка = ЗапросФИО.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Сведения, Выборка);
	КонецЕсли;

	МенеджерЗаписи = РегистрыСведений.ДокументыФизическихЛиц.СоздатьМенеджерЗаписи();
	
	// Ищем последнюю запись
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	РегистрСведений.Период КАК Период,
	|	РегистрСведений.Физлицо КАК Физлицо,
	|	РегистрСведений.ВидДокумента КАК ВидДокумента
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК РегистрСведений
	|ГДЕ
	|	РегистрСведений.Физлицо = &ВедущийОбъект
	|	И РегистрСведений.ЯвляетсяДокументомУдостоверяющимЛичность
	|
	|УПОРЯДОЧИТЬ ПО
	|	РегистрСведений.Период УБЫВ";
	Запрос.УстановитьПараметр("ВедущийОбъект", ФизЛицо);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Прочитать();
		ЗаполнитьЗначенияСвойств(Сведения, МенеджерЗаписи);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
