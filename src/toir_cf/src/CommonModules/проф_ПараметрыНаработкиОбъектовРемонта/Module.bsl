#Область ПрограммныйИнтерфейс

//++ Проф-ИТ, #150, Соловьёв.А.А., 18.08.2023

Процедура проф_РасчетПараметровНаработкиОР() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних.Показатель КАК Показатель
	|ПОМЕСТИТЬ втОсновной
	|ИЗ
	|	РегистрСведений.торо_ПараметрыНаработкиОбъектовРемонта.СрезПоследних(&ТекущаяДата, ) КАК торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель КАК Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач КАК ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон КАК ДатаКон,
	|	ВЫБОР
	|		КОГДА &МесяцПрошлогоГодаНачало > торо_НаработкаОбъектовРемонта.ДатаНач
	|			ТОГДА &МесяцПрошлогоГодаНачало
	|		ИНАЧЕ торо_НаработкаОбъектовРемонта.ДатаНач
	|	КОНЕЦ КАК ДатаНачРасчет,
	|	ВЫБОР
	|		КОГДА &МесяцПрошлогоГодаКонец > торо_НаработкаОбъектовРемонта.ДатаКон
	|			ТОГДА КОНЕЦПЕРИОДА(торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)
	|		ИНАЧЕ &МесяцПрошлогоГодаКонец
	|	КОНЕЦ КАК ДатаКонРасчет,
	|	СУММА(РАЗНОСТЬДАТ(ВЫБОР
	|				КОГДА &МесяцПрошлогоГодаНачало >= торо_НаработкаОбъектовРемонта.ДатаНач
	|					ТОГДА &МесяцПрошлогоГодаНачалоМинусДень
	|				ИНАЧЕ торо_НаработкаОбъектовРемонта.ДатаНач
	|			КОНЕЦ, ВЫБОР
	|				КОГДА &МесяцПрошлогоГодаКонец > торо_НаработкаОбъектовРемонта.ДатаКон
	|					ТОГДА КОНЕЦПЕРИОДА(торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)
	|				ИНАЧЕ &МесяцПрошлогоГодаКонец
	|			КОНЕЦ, ДЕНЬ)) КАК КоличествоДнейМесяца,
	|	СУММА(торо_НаработкаОбъектовРемонта.Наработка) КАК Наработка,
	|	СУММА(РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)) КАК КоличествоДнейПериодНаработки
	|ПОМЕСТИТЬ втНаработки
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.торо_НаработкаОбъектовРемонта КАК торо_НаработкаОбъектовРемонта
	|		ПО втОсновной.ОбъектРемонта = торо_НаработкаОбъектовРемонта.ОбъектРемонта
	|			И втОсновной.Показатель = торо_НаработкаОбъектовРемонта.Показатель
	|ГДЕ
	|	торо_НаработкаОбъектовРемонта.ДатаНач <> ДАТАВРЕМЯ(1, 1, 1)
	|	И (&МесяцПрошлогоГодаНачало МЕЖДУ торо_НаработкаОбъектовРемонта.ДатаНач И торо_НаработкаОбъектовРемонта.ДатаКон
	|			ИЛИ &МесяцПрошлогоГодаКонец МЕЖДУ торо_НаработкаОбъектовРемонта.ДатаНач И торо_НаработкаОбъектовРемонта.ДатаКон)
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон,
	|	ВЫБОР
	|		КОГДА &МесяцПрошлогоГодаНачало > торо_НаработкаОбъектовРемонта.ДатаНач
	|			ТОГДА &МесяцПрошлогоГодаНачало
	|		ИНАЧЕ торо_НаработкаОбъектовРемонта.ДатаНач
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &МесяцПрошлогоГодаКонец > торо_НаработкаОбъектовРемонта.ДатаКон
	|			ТОГДА КОНЕЦПЕРИОДА(торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)
	|		ИНАЧЕ &МесяцПрошлогоГодаКонец
	|	КОНЕЦ,
	|	СУММА(РАЗНОСТЬДАТ(ВЫБОР
	|				КОГДА &МесяцПрошлогоГодаНачало > торо_НаработкаОбъектовРемонта.ДатаНач
	|					ТОГДА &МесяцПрошлогоГодаНачалоМинусДень
	|				ИНАЧЕ торо_НаработкаОбъектовРемонта.ДатаНач
	|			КОНЕЦ, ВЫБОР
	|				КОГДА &МесяцПрошлогоГодаКонец > торо_НаработкаОбъектовРемонта.ДатаКон
	|					ТОГДА КОНЕЦПЕРИОДА(торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)
	|				ИНАЧЕ &МесяцПрошлогоГодаКонец
	|			КОНЕЦ, ДЕНЬ)),
	|	СУММА(торо_НаработкаОбъектовРемонта.Наработка),
	|	СУММА(РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ))
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.торо_НаработкаОбъектовРемонта КАК торо_НаработкаОбъектовРемонта
	|		ПО втОсновной.ОбъектРемонта = торо_НаработкаОбъектовРемонта.ОбъектРемонта
	|			И втОсновной.Показатель = торо_НаработкаОбъектовРемонта.Показатель
	|ГДЕ
	|	торо_НаработкаОбъектовРемонта.ДатаНач <> ДАТАВРЕМЯ(1, 1, 1)
	|	И торо_НаработкаОбъектовРемонта.ДатаНач МЕЖДУ &МесяцПрошлогоГодаНачало И &МесяцПрошлогоГодаКонец
	|	И торо_НаработкаОбъектовРемонта.ДатаКон МЕЖДУ &МесяцПрошлогоГодаНачало И &МесяцПрошлогоГодаКонец
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта КАК ОбъектРемонта,
	|	втОсновной.Показатель КАК Показатель,
	|	СУММА((ВЫРАЗИТЬ(втНаработки.Наработка / втНаработки.КоличествоДнейПериодНаработки КАК ЧИСЛО(15, 2))) * втНаработки.КоличествоДнейМесяца / &КоличествоДнейМесяца) КАК ПлановаяНаработка
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработки КАК втНаработки
	|		ПО втОсновной.ОбъектРемонта = втНаработки.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработки.Показатель
	|ГДЕ
	|	&КоличествоДнейМесяца > 0
	|	И втНаработки.КоличествоДнейПериодНаработки > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втОсновной.ОбъектРемонта,
	|	втОсновной.Показатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта,
	|	втОсновной.Показатель,
	|	0
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработки КАК втНаработки
	|		ПО втОсновной.ОбъектРемонта = втНаработки.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработки.Показатель
	|ГДЕ
	|	&КоличествоДнейМесяца = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта,
	|	втОсновной.Показатель,
	|	0
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработки КАК втНаработки
	|		ПО втОсновной.ОбъектРемонта = втНаработки.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработки.Показатель
	|ГДЕ
	|	втНаработки.КоличествоДнейПериодНаработки = 0";
	
	ТекущаяДата = ТекущаяДатаСеанса();
	КонецПрошлогоМесяца = КонецМесяца(ДобавитьМесяц(ТекущаяДата, -1));
	
	МесяцПрошлогоГодаКонец = КонецМесяца(ДобавитьМесяц(КонецПрошлогоМесяца, -11));
	МесяцПрошлогоГодаНачало = НачалоМесяца(МесяцПрошлогоГодаКонец);
	
	КоличествоДнейМесяца = День(МесяцПрошлогоГодаКонец);
	
	Запрос.УстановитьПараметр("МесяцПрошлогоГодаНачалоМинусДень",	МесяцПрошлогоГодаНачало - 1);
	Запрос.УстановитьПараметр("МесяцПрошлогоГодаНачало",			МесяцПрошлогоГодаНачало);
	Запрос.УстановитьПараметр("МесяцПрошлогоГодаКонец", 			МесяцПрошлогоГодаКонец);
	Запрос.УстановитьПараметр("ТекущаяДата",						ТекущаяДата);
	Запрос.УстановитьПараметр("КоличествоДнейМесяца",				КоличествоДнейМесяца);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаТекущийПериод = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаТекущийПериод.Следующий() Цикл
		СоздатьЗаписьРСПараметрыНаработкиОбъектовРемонта(НачалоМесяца(ТекущаяДата), 
			ВыборкаТекущийПериод, ВыборкаТекущийПериод.ПлановаяНаработка);
	КонецЦикла;
	
КонецПроцедуры

Процедура проф_РасчетПараметровНаработкиОРСтратегический(НачалоПериода = Неопределено, КонецПериода = Неопределено, ПериодЗаписи = Неопределено, ВыводитьСообщения = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних.Показатель КАК Показатель
	|ПОМЕСТИТЬ втОсновной
	|ИЗ
	|	РегистрСведений.торо_ПараметрыНаработкиОбъектовРемонта.СрезПоследних(&ТекущаяДата, ) КАК торо_ПараметрыНаработкиОбъектовРемонтаСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_НаработкаОбъектовРемонтаОбороты.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонтаОбороты.Показатель КАК Показатель,
	|	СУММА(торо_НаработкаОбъектовРемонтаОбороты.НаработкаОборот) КАК Наработка
	|ПОМЕСТИТЬ втНаработка
	|ИЗ
	|	РегистрНакопления.торо_НаработкаОбъектовРемонта.Обороты(
	|			&НачалоПрошлогоГода,
	|			&КонецПрошлогоГода,
	|			Год,
	|			(ОбъектРемонта, Показатель) В
	|				(ВЫБРАТЬ
	|					втОсновной.ОбъектРемонта КАК ОбъектРемонта,
	|					втОсновной.Показатель КАК Показатель
	|				ИЗ
	|					втОсновной КАК втОсновной)) КАК торо_НаработкаОбъектовРемонтаОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_НаработкаОбъектовРемонтаОбороты.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонтаОбороты.Показатель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта КАК ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель КАК Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач КАК ДатаНачала,
	|	торо_НаработкаОбъектовРемонта.ДатаКон КАК ДатаОкончания,
	|	СУММА(торо_НаработкаОбъектовРемонта.Наработка) КАК ОбщаяНаработка,
	|	СУММА(РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)) КАК КоличествоДнейВсего,
	|	СУММА(РАЗНОСТЬДАТ(&КонецПрошлогоГода, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)) КАК КоличествоДней,
	|	СУММА(торо_НаработкаОбъектовРемонта.Наработка - ВЫБОР
	|			КОГДА РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ) = 0
	|				ТОГДА 0
	|			ИНАЧЕ торо_НаработкаОбъектовРемонта.Наработка / РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ) * РАЗНОСТЬДАТ(&КонецПрошлогоГода, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)
	|		КОНЕЦ) КАК Наработка,
	|	ИСТИНА КАК НаработкаПослеПериода
	|ПОМЕСТИТЬ втПереходящаяНаработкаПредв
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_НаработкаОбъектовРемонта КАК торо_НаработкаОбъектовРемонта
	|		ПО втОсновной.ОбъектРемонта = торо_НаработкаОбъектовРемонта.ОбъектРемонта
	|			И втОсновной.Показатель = торо_НаработкаОбъектовРемонта.Показатель
	|ГДЕ
	|	торо_НаработкаОбъектовРемонта.ДатаНач МЕЖДУ &НачалоПрошлогоГода И &КонецПрошлогоГода
	|	И торо_НаработкаОбъектовРемонта.ДатаКон > &КонецПрошлогоГода
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон,
	|	СУММА(торо_НаработкаОбъектовРемонта.Наработка),
	|	СУММА(РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ)),
	|	СУММА(РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, &НачалоПрошлогоГода, ДЕНЬ)),
	|	СУММА((торо_НаработкаОбъектовРемонта.Наработка - ВЫБОР
	|			КОГДА РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ) = 0
	|				ТОГДА 0
	|			ИНАЧЕ торо_НаработкаОбъектовРемонта.Наработка / РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ) * (РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, торо_НаработкаОбъектовРемонта.ДатаКон, ДЕНЬ) - РАЗНОСТЬДАТ(торо_НаработкаОбъектовРемонта.ДатаНач, &НачалоПрошлогоГода, ДЕНЬ))
	|		КОНЕЦ) * -1),
	|	ЛОЖЬ
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_НаработкаОбъектовРемонта КАК торо_НаработкаОбъектовРемонта
	|		ПО втОсновной.ОбъектРемонта = торо_НаработкаОбъектовРемонта.ОбъектРемонта
	|			И втОсновной.Показатель = торо_НаработкаОбъектовРемонта.Показатель
	|ГДЕ
	|	торо_НаработкаОбъектовРемонта.ДатаКон МЕЖДУ &НачалоПрошлогоГода И &КонецПрошлогоГода
	|	И торо_НаработкаОбъектовРемонта.ДатаНач < &НачалоПрошлогоГода
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
	|	торо_НаработкаОбъектовРемонта.Показатель,
	|	торо_НаработкаОбъектовРемонта.ДатаНач,
	|	торо_НаработкаОбъектовРемонта.ДатаКон
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПереходящаяНаработкаПредв.ОбъектРемонта КАК ОбъектРемонта,
	|	втПереходящаяНаработкаПредв.Показатель КАК Показатель,
	|	СУММА(втПереходящаяНаработкаПредв.ОбщаяНаработка) КАК ОбщаяНаработка,
	|	СУММА(втПереходящаяНаработкаПредв.КоличествоДнейВсего) КАК КоличествоДнейВсего,
	|	СУММА(втПереходящаяНаработкаПредв.КоличествоДней) КАК КоличествоДней,
	|	СУММА(втПереходящаяНаработкаПредв.Наработка) КАК Наработка
	|ПОМЕСТИТЬ втПереходящаяНаработка
	|ИЗ
	|	втПереходящаяНаработкаПредв КАК втПереходящаяНаработкаПредв
	|
	|СГРУППИРОВАТЬ ПО
	|	втПереходящаяНаработкаПредв.ОбъектРемонта,
	|	втПереходящаяНаработкаПредв.Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНаработка.ОбъектРемонта КАК ОбъектРемонта,
	|	втНаработка.Показатель КАК Показатель,
	|	СУММА(втНаработка.Наработка + ЕСТЬNULL(втПереходящаяНаработка.Наработка, 0)) КАК Наработка
	|ПОМЕСТИТЬ втНаработкаИтог
	|ИЗ
	|	втНаработка КАК втНаработка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПереходящаяНаработка КАК втПереходящаяНаработка
	|		ПО втНаработка.ОбъектРемонта = втПереходящаяНаработка.ОбъектРемонта
	|			И втНаработка.Показатель = втПереходящаяНаработка.Показатель
	|
	|СГРУППИРОВАТЬ ПО
	|	втНаработка.ОбъектРемонта,
	|	втНаработка.Показатель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРемонта,
	|	Показатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта КАК ОбъектРемонта,
	|	втОсновной.Показатель КАК Показатель,
	|	втНаработкаИтог.Наработка / &КоличествоДнейГода КАК ПлановаяНаработка,
	|	втНаработкаИтог.Наработка КАК ЗначениеНаработки,
	|	торо_ОбъектыРемонта.Код КАК КодОР,
	|	торо_ОбъектыРемонта.Наименование КАК НаименованиеОР,
	|	ПараметрыВыработкиОС.Представление КАК Параметр,
	|	ПараметрыВыработкиОС.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработкаИтог КАК втНаработкаИтог
	|		ПО втОсновной.ОбъектРемонта = втНаработкаИтог.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработкаИтог.Показатель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|		ПО втОсновной.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|		ПО втОсновной.Показатель = ПараметрыВыработкиОС.Ссылка
	|ГДЕ
	|	&КоличествоДнейГода > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта,
	|	втОсновной.Показатель,
	|	втНаработкаИтог.Наработка / &КоличествоДнейГода,
	|	втНаработкаИтог.Наработка,
	|	торо_ТиповыеОР.Код,
	|	торо_ТиповыеОР.Наименование,
	|	ПараметрыВыработкиОС.Представление,
	|	ПараметрыВыработкиОС.ЕдиницаИзмерения.Представление
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработкаИтог КАК втНаработкаИтог
	|		ПО втОсновной.ОбъектРемонта = втНаработкаИтог.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработкаИтог.Показатель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТиповыеОР КАК торо_ТиповыеОР
	|		ПО втОсновной.ОбъектРемонта = торо_ТиповыеОР.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|		ПО втОсновной.Показатель = ПараметрыВыработкиОС.Ссылка
	|ГДЕ
	|	&КоличествоДнейГода > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта,
	|	втОсновной.Показатель,
	|	0,
	|	0,
	|	торо_ОбъектыРемонта.Код,
	|	торо_ОбъектыРемонта.Наименование,
	|	ПараметрыВыработкиОС.Представление,
	|	ПараметрыВыработкиОС.ЕдиницаИзмерения.Представление
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработкаИтог КАК втНаработкаИтог
	|		ПО втОсновной.ОбъектРемонта = втНаработкаИтог.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработкаИтог.Показатель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|		ПО втОсновной.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|		ПО втОсновной.Показатель = ПараметрыВыработкиОС.Ссылка
	|ГДЕ
	|	&КоличествоДнейГода = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втОсновной.ОбъектРемонта,
	|	втОсновной.Показатель,
	|	0,
	|	0,
	|	торо_ТиповыеОР.Код,
	|	торо_ТиповыеОР.Наименование,
	|	ПараметрыВыработкиОС.Представление,
	|	ПараметрыВыработкиОС.ЕдиницаИзмерения.Представление
	|ИЗ
	|	втОсновной КАК втОсновной
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНаработкаИтог КАК втНаработкаИтог
	|		ПО втОсновной.ОбъектРемонта = втНаработкаИтог.ОбъектРемонта
	|			И втОсновной.Показатель = втНаработкаИтог.Показатель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ТиповыеОР КАК торо_ТиповыеОР
	|		ПО втОсновной.ОбъектРемонта = торо_ТиповыеОР.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|		ПО втОсновной.Показатель = ПараметрыВыработкиОС.Ссылка
	|ГДЕ
	|	&КоличествоДнейГода = 0";
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Если НачалоПериода = Неопределено Тогда
		ПрошлыйГод = ДобавитьМесяц(ТекущаяДата, -12);
		НачалоПрошлогоГода = НачалоГода(ПрошлыйГод);
		КонецПрошлогоГода = КонецГода(ПрошлыйГод);
		КоличествоДнейГода = ДеньГода(КонецГода(ПрошлыйГод));
		ПериодЗаписи = НачалоМесяца(ТекущаяДата);
	Иначе
		НачалоПрошлогоГода = НачалоПериода;
		КонецПрошлогоГода = КонецПериода;
		КоличествоДнейГода = (НачалоДня(КонецПериода) - НачалоДня(НачалоПериода)) / (60 * 60 * 24) + 1;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПрошлогоГода",			НачалоПрошлогоГода);
	Запрос.УстановитьПараметр("КонецПрошлогоГода",			КонецПрошлогоГода);
	Запрос.УстановитьПараметр("ТекущаяДата",				ТекущаяДата);
	Запрос.УстановитьПараметр("КоличествоДнейГода",			КоличествоДнейГода);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СоздатьЗаписьРСПараметрыНаработкиОбъектовРемонта(ПериодЗаписи, 
			Выборка, Выборка.ПлановаяНаработка, Истина);
			
		Если ВыводитьСообщения Тогда 
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Запись в регистр Параметры наработки объектов ремонта (стратегический): 
				|Период ""%1"", Код ОР: ""%2"", Наименование ОР: ""%3"", Параметр: ""%4"", 
				|Значение параметра: ""%5"", Ед. изм: ""%6"", Значение наработки: ""%7"", Количество рабочих дней: ""%8""'"), 
				ПериодЗаписи,
				Выборка.КодОР,
				Выборка.НаименованиеОР,
				Выборка.Параметр,
				Выборка.ПлановаяНаработка,
				Выборка.ЕдиницаИзмерения,
				Выборка.ЗначениеНаработки,
				КоличествоДнейГода);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//-- Проф-ИТ, #150, Соловьёв.А.А., 18.08.2023

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Проф-ИТ, #150, Соловьёв.А.А., 18.08.2023

Процедура СоздатьЗаписьРСПараметрыНаработкиОбъектовРемонта(Период, Выборка, ПлановаяНаработка, Стратегический = Ложь)
	
	Если Стратегический Тогда 
		Менеджер = РегистрыСведений.проф_ПараметрыНаработкиОбъектовРемонта.СоздатьМенеджерЗаписи();
	Иначе
		Менеджер = РегистрыСведений.торо_ПараметрыНаработкиОбъектовРемонта.СоздатьМенеджерЗаписи();
	КонецЕсли;
	Менеджер.Период = Период;
	Менеджер.ОбъектРемонта = Выборка.ОбъектРемонта;
	Менеджер.Показатель = Выборка.Показатель;
	Менеджер.Прочитать();
	
	Менеджер.Период = Период;
	Менеджер.ОбъектРемонта = Выборка.ОбъектРемонта;
	Менеджер.Показатель = Выборка.Показатель;
	Менеджер.ПлановаяНаработка = ПлановаяНаработка;
	Менеджер.ПериодПлановойНаработки = Перечисления.Периодичность.День;
	Менеджер.ПродолжительностьПериодаПлановойНаработки = 1;
	Менеджер.КоэффициентИспользованияОборудования = 1;
	Менеджер.Записать();
	
КонецПроцедуры

//-- Проф-ИТ, #150, Соловьёв.А.А., 18.08.2023

#КонецОбласти