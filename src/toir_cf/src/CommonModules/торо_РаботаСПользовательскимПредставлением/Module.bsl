#Область ПрограммныйИнтерфейс

// Получает список полей, необходимых для построения представления.
//
// Параметры:
//		ФорматнаяСтрока - Строка - Строка, по которой формируется представление.
//
// Возвращаемое значение:
//		Массив - массив полей для получения представления.
//
Функция ПолучитьСписокПолей(ФорматнаяСтрока) Экспорт
	
	МассивПолей = Новый Массив;
	
	ПодстрокаПоиска = ФорматнаяСтрока;
	
	Пока Истина Цикл
		
		НомерСимвола1 = Найти(ПодстрокаПоиска,"%");
		Если НомерСимвола1 = 0 Тогда
			Прервать;
		КонецЕсли;
		ПодстрокаПоиска = Сред(ПодстрокаПоиска,НомерСимвола1+1);
		НомерСимвола2 = Найти(ПодстрокаПоиска,"%");
		Если НомерСимвола2 = 0 Тогда
			Прервать;
		КонецЕсли;
		МассивПолей.Добавить(Лев(ПодстрокаПоиска,НомерСимвола2-1));
		ПодстрокаПоиска = Сред(ПодстрокаПоиска,НомерСимвола2+1);
		
	КонецЦикла;
	
	Возврат МассивПолей;
	
КонецФункции

// Получает строковое представление для ОР по форматной строке и данным.
//
// Параметры:
//		ФорматнаяСтрока - Строка - Строка, по которой формируется представление.
//		Данные - Структура - данные для получения представления.
//		УчитыватьМультиязычность - Булево - Если истина, то справочник подключен к 
//			мультиязычному хранению данных БСП и необходимо это учесть.
//
// Возвращаемое значение:
//		Строка - представление.
Функция ПолучитьПредставлениеПоФорматнойСтроке(ФорматнаяСтрока, Данные, УчитыватьМультиязычность = Ложь) Экспорт
	
	СуффиксЯзыка = "";
	
	Если УчитыватьМультиязычность И НЕ МультиязычностьСервер.ЭтоОсновнойЯзык() Тогда
		СуффиксЯзыка = МультиязычностьСервер.СуффиксТекущегоЯзыка();
		Если НЕ ЗначениеЗаполнено(СуффиксЯзыка) Тогда
			// На этом языке данные не хранятся, значит ничего особенного предпринимать не нужно.
			УчитыватьМультиязычность = Ложь;
		КонецЕсли;	
	КонецЕсли;
	
	Представление = ФорматнаяСтрока;
	
	Для Каждого КлючИЗначение Из Данные Цикл
		ЗначениеДляПодстановки = КлючИЗначение.Значение;
		
		Если УчитыватьМультиязычность Тогда
			ЗначениеНаДопЯзыке = Неопределено;
			Данные.Свойство(КлючИЗначение.Ключ + СуффиксЯзыка, ЗначениеНаДопЯзыке);
			Если ЗначениеЗаполнено(ЗначениеНаДопЯзыке) Тогда
				ЗначениеДляПодстановки = ЗначениеНаДопЯзыке;
			КонецЕсли;
		КонецЕсли;
		
		Представление = СтрЗаменить(Представление,"%"+КлючИЗначение.Ключ+"%",Строка(ЗначениеДляПодстановки));
	КонецЦикла;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти