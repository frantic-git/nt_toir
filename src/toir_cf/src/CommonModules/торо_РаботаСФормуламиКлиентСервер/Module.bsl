
#Область ПрограммныйИнтерфейс

// Получает текст операнда для вставки в формулу.
//
// Параметры:
//  Операнд - Строка - имя операнда.
//
// Возвращаемое значение:
//  Строка - текст операнда.
//
Функция ПолучитьТекстОперандаДляВставки(Операнд) Экспорт
	
	Возврат "[" + Операнд + "]";
	
КонецФункции // ПолучитьТекстОперандаДляВставки()

// Извлекает операнды из текстовой формулы.
//
// Параметры:
//  Формула - Строка - текст формулы.
//
// Возвращаемое значение:
//  Массив - Операнды из текстовой формулы.
//
Функция ПолучитьМассивОперандовТекстовойФормулы(Формула) Экспорт
	
	МассивОперандов = Новый Массив();
	
	ТекстФормулы = СокрЛП(Формула);
	Если СтрЧислоВхождений(ТекстФормулы, "[") <> СтрЧислоВхождений(ТекстФормулы, "]") Тогда
		ЕстьОперанды = Ложь;
	Иначе
		ЕстьОперанды = Истина;
	КонецЕсли;
	
	Пока ЕстьОперанды = Истина Цикл
		НачалоОперанда = СтрНайти(ТекстФормулы, "[");
		КонецОперанда = СтрНайти(ТекстФормулы, "]");
		
		Если НачалоОперанда = 0
			Или КонецОперанда = 0
			Или НачалоОперанда > КонецОперанда Тогда
			ЕстьОперанды = Ложь;
			Прервать;
			
		КонецЕсли;
		
		ИмяОперанда = Сред(ТекстФормулы, НачалоОперанда + 1, КонецОперанда - НачалоОперанда - 1);
		МассивОперандов.Добавить(ИмяОперанда);
		ТекстФормулы = СтрЗаменить(ТекстФормулы, "[" + ИмяОперанда + "]", "");
		
	КонецЦикла;
	
	Возврат МассивОперандов;
	
КонецФункции // ПолучитьМассивОперандовТекстовойФормулы()

// Получает массив доступных операндов из дерева
//
// Параметры:
//  ДеревоОперандов  - ДанныеФормыКоллекция - Дерево, в котором два уровня, операнд получается посредством склеивания
//                 идентификаторов строк верхнего и нижнего уровня.
//
// Возвращаемое значение:
//   Массив - Массив достуных операндов.
//
Функция МассивОперандовДляДерева(ДеревоОперандов) Экспорт

	МассивОперандов = Новый Массив();
	
	Для Каждого СтрокаВерхнегоУровня Из ДеревоОперандов.ПолучитьЭлементы() Цикл
		Для Каждого СтрокаВторогоУровня Из СтрокаВерхнегоУровня.ПолучитьЭлементы() Цикл
			МассивОперандов.Добавить(СтрокаВерхнегоУровня.Показатель +"." + СтрокаВторогоУровня.Ресурс);
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивОперандов;

КонецФункции

#КонецОбласти
