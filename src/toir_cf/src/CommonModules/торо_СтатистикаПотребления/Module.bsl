#Область СлужебныйПрограммныйИнтерфейс

Функция РасчитатьСтатистикуПоАгрегатнойФункции(АгрегатнаяФункция, ДатаУстановки, СписокНоменклатуры, Склад = Неопределено, УчитыватьСклад = Истина) Экспорт
	
	ЗначениеКонстанты = Константы.торо_ПериодРасчетаСтатистикиПотребления.Получить();
	ГодНачалаОтсчета = Формат(Год(ДатаУстановки)-(ЗначениеКонстанты-1),"ЧГ=");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТоварыНаСкладахОбороты.Период, МЕСЯЦ) КАК Период,
	|	ТоварыНаСкладахОбороты.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОбороты.Характеристика КАК Характеристика,
	|	ТоварыНаСкладахОбороты.Склад КАК Склад,
	|	СУММА(ТоварыНаСкладахОбороты.ВНаличииРасход) КАК СуммаРасход
	|ПОМЕСТИТЬ ВТ_ДанныеПоМесяцам
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Обороты(ДАТАВРЕМЯ("+ГодНачалаОтсчета+",1, 1),
	|				КОНЕЦПЕРИОДА(&ДатаУстановки,МЕСЯЦ),
	|				Регистратор,
	|				Номенклатура В (&СписокНоменклатуры)
	|				И (&НеИспользоватьОтборПоСкладу
	|					ИЛИ Склад = &Склад)) КАК ТоварыНаСкладахОбороты
	|ГДЕ
	|	МЕСЯЦ(ТоварыНаСкладахОбороты.Период) = &Период
	|	И ТИПЗНАЧЕНИЯ(ТоварыНаСкладахОбороты.Регистратор) = ТИП(Документ.ВнутреннееПотреблениеТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ТоварыНаСкладахОбороты.Период, МЕСЯЦ),
	|	ТоварыНаСкладахОбороты.Номенклатура,
	|	ТоварыНаСкладахОбороты.Характеристика,
	|	ТоварыНаСкладахОбороты.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТоварыНаРукахОбороты.Период, МЕСЯЦ) КАК Период,
	|	ТоварыНаРукахОбороты.Номенклатура КАК Номенклатура,
	|	ТоварыНаРукахОбороты.Характеристика КАК Характеристика,
	|	ТоварыНаРукахОбороты.Склад,
	|	СУММА(ТоварыНаРукахОбороты.КоличествоРасход) КАК СуммаРасход
	|ИЗ
	|	РегистрНакопления.торо_ТоварыНаРуках.Обороты(ДАТАВРЕМЯ("+ГодНачалаОтсчета+",1, 1),
	|				КОНЕЦПЕРИОДА(&ДатаУстановки,МЕСЯЦ),
	|				Регистратор,
	|				Номенклатура В (&СписокНоменклатуры)
	|				И (&НеИспользоватьОтборПоСкладу
	|					ИЛИ Склад = &Склад)) КАК ТоварыНаРукахОбороты
	|ГДЕ
	|	МЕСЯЦ(ТоварыНаРукахОбороты.Период) = &Период
	|	И ТИПЗНАЧЕНИЯ(ТоварыНаРукахОбороты.Регистратор) = ТИП(Документ.ВнутреннееПотреблениеТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ТоварыНаРукахОбороты.Период, МЕСЯЦ),
	|	ТоварыНаРукахОбороты.Номенклатура,
	|	ТоварыНаРукахОбороты.Характеристика,
	|	ТоварыНаРукахОбороты.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеПоМесяцам.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеПоМесяцам.Номенклатура.ЕдиницаИзмерения.торо_ОкруглятьПриРасчетеСтатистики КАК ОкруглятьКоличество,
	|	ВТ_ДанныеПоМесяцам.Характеристика КАК Характеристика,
	|	ВТ_ДанныеПоМесяцам.Склад,
	|	"+АгрегатнаяФункция+"(ВТ_ДанныеПоМесяцам.СуммаРасход) КАК СуммаРасход
	|ИЗ
	|	ВТ_ДанныеПоМесяцам КАК ВТ_ДанныеПоМесяцам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеПоМесяцам.Номенклатура,
	|	ВТ_ДанныеПоМесяцам.Склад,
	|	ВТ_ДанныеПоМесяцам.Характеристика";
	
	Если Склад = Неопределено Тогда
		Запрос.УстановитьПараметр("НеИспользоватьОтборПоСкладу", Истина);
	Иначе
		Запрос.УстановитьПараметр("НеИспользоватьОтборПоСкладу", Ложь);
	КонецЕсли;
	
	Если УчитыватьСклад = Ложь Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТ_ДанныеПоМесяцам.Склад,","");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Склад",Склад);
	Запрос.УстановитьПараметр("ДатаУстановки",ДатаУстановки);
	Запрос.УстановитьПараметр("Период",Месяц(ДатаУстановки));
	Запрос.УстановитьПараметр("СписокНоменклатуры",СписокНоменклатуры);
	
	Если ЗначениеЗаполнено(ЗначениеКонстанты) Тогда	
		Возврат Запрос.Выполнить().Выгрузить();	
	КонецЕсли;	
	
КонецФункции	

Функция РасчитатьСтатистикуПоПрошломуГоду(ДатаУстановки,СписокНоменклатуры,Склад = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТоварыНаСкладахОбороты.Период, МЕСЯЦ) КАК Период,
	|	ТоварыНаСкладахОбороты.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОбороты.Характеристика КАК Характеристика,
	|	СУММА(ТоварыНаСкладахОбороты.ВНаличииРасход) КАК СуммаРасход,
	|	ТоварыНаСкладахОбороты.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ДанныеПоМесяцам
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Обороты(
	|			НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаУстановки, ГОД, -1), ГОД),
	|			КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаУстановки, ГОД, -1), ГОД),
	|			Регистратор,
	|			Номенклатура В (&СписокНоменклатуры)
	|				И (&НеИспользоватьОтборПоСкладу
	|					ИЛИ Склад = &Склад)) КАК ТоварыНаСкладахОбороты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТоварыНаСкладахОбороты.Регистратор) = ТИП(Документ.ВнутреннееПотреблениеТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ТоварыНаСкладахОбороты.Период, МЕСЯЦ),
	|	ТоварыНаСкладахОбороты.Номенклатура,
	|	ТоварыНаСкладахОбороты.Характеристика,
	|	ТоварыНаСкладахОбороты.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТоварыНаРукахОбороты.Период, МЕСЯЦ),
	|	ТоварыНаРукахОбороты.Номенклатура,
	|	ТоварыНаРукахОбороты.Характеристика,
	|	СУММА(ТоварыНаРукахОбороты.КоличествоРасход),
	|	ТоварыНаРукахОбороты.Склад
	|ИЗ
	|	РегистрНакопления.торо_ТоварыНаРуках.Обороты(
	|			НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаУстановки, ГОД, -1), ГОД),
	|			КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаУстановки, ГОД, -1), ГОД),
	|			Регистратор,
	|			Номенклатура В (&СписокНоменклатуры)
	|				И (&НеИспользоватьОтборПоСкладу
	|					ИЛИ Склад = &Склад)) КАК ТоварыНаРукахОбороты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТоварыНаРукахОбороты.Регистратор) = ТИП(Документ.ВнутреннееПотреблениеТоваров)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ТоварыНаРукахОбороты.Период, МЕСЯЦ),
	|	ТоварыНаРукахОбороты.Номенклатура,
	|	ТоварыНаРукахОбороты.Характеристика,
	|	ТоварыНаРукахОбороты.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеПоМесяцам.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеПоМесяцам.Характеристика КАК Характеристика,
	|	СУММА(ВТ_ДанныеПоМесяцам.СуммаРасход) КАК СуммаРасход
	|ПОМЕСТИТЬ ВТ_СОбщейСуммой
	|ИЗ
	|	ВТ_ДанныеПоМесяцам КАК ВТ_ДанныеПоМесяцам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеПоМесяцам.Номенклатура,
	|	ВТ_ДанныеПоМесяцам.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеПоМесяцам.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеПоМесяцам.Номенклатура.ЕдиницаИзмерения.торо_ОкруглятьПриРасчетеСтатистики КАК ОкруглятьКоличество,
	|	ВТ_ДанныеПоМесяцам.Характеристика КАК Характеристика,
	|	ВТ_ДанныеПоМесяцам.СуммаРасход КАК КоличествоРасход,
	|	ВТ_ДанныеПоМесяцам.Склад КАК Склад,
	|	ВТ_СОбщейСуммой.СуммаРасход / 12 КАК СреднееЗаМесяц,
	|	ВТ_СОбщейСуммой.СуммаРасход КАК ОбщийРасход
	|ИЗ
	|	ВТ_ДанныеПоМесяцам КАК ВТ_ДанныеПоМесяцам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СОбщейСуммой КАК ВТ_СОбщейСуммой
	|		ПО ВТ_ДанныеПоМесяцам.Номенклатура = ВТ_СОбщейСуммой.Номенклатура
	|			И ВТ_ДанныеПоМесяцам.Характеристика = ВТ_СОбщейСуммой.Характеристика";
		
	Если Склад = Неопределено Тогда
		Запрос.УстановитьПараметр("НеИспользоватьОтборПоСкладу", Истина);
	Иначе
		Запрос.УстановитьПараметр("НеИспользоватьОтборПоСкладу", Ложь);
	КонецЕсли;
		
	Запрос.УстановитьПараметр("Склад",Склад);		
	Запрос.УстановитьПараметр("Период",Месяц(ДатаУстановки));
	Запрос.УстановитьПараметр("ДатаУстановки",ДатаУстановки);
	Запрос.УстановитьПараметр("СписокНоменклатуры",СписокНоменклатуры);
	
	Возврат Запрос.Выполнить().Выгрузить();
		
КонецФункции

#КонецОбласти