
#Область ПрограммныйИнтерфейс

// Процедура находит в настройках схемы компоновки данных все группировки с указанным полем
// и устанавливает для них заданный тип иерархической группировки (иерархия, только иерархия).
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик настроек отчета.
//		ИмяПоля - Строка - имя поля группировки.
//		ТипГруппировки - ТипГруппировкиКомпоновкиДанных - нужный тип группировки.
//
Процедура УстановитьТипИерархическойГруппировкиВНастройках(КомпоновщикНастроек, ИмяПоля, ТипГруппировки) Экспорт
	
	Структура = КомпоновщикНастроек.Настройки.Структура;
	МассивЭлементовГруппировки = Новый Массив;	
	НайтиРекурсивноЭлементыГруппировки(Структура, Новый ПолеКомпоновкиДанных(ИмяПоля), МассивЭлементовГруппировки);
	
	Для каждого Элемент из МассивЭлементовГруппировки Цикл
		Элемент.ТипГруппировки = ТипГруппировки;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НайтиРекурсивноЭлементыГруппировки(ЭлементыСтруктурыНастроек, ПолеКомпоновкиДанных, МассивРезультатов)
	
	Для каждого ЭлемнтСтруктуры из ЭлементыСтруктурыНастроек Цикл
		Если ТипЗнч(ЭлемнтСтруктуры) = Тип("ГруппировкаКомпоновкиДанных") 
			ИЛИ ТипЗнч(ЭлемнтСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")	Тогда
			
			ЭлементыГруппировки = ЭлемнтСтруктуры.ПоляГруппировки.Элементы;
			Для каждого Элемент из ЭлементыГруппировки Цикл
				Если ТипЗнч(Элемент) = Тип("ПолеГруппировкиКомпоновкиДанных") И  Элемент.Поле = ПолеКомпоновкиДанных Тогда
					МассивРезультатов.Добавить(Элемент);
				КонецЕсли;
			КонецЦикла;
			
			НайтиРекурсивноЭлементыГруппировки(ЭлемнтСтруктуры.Структура, ПолеКомпоновкиДанных, МассивРезультатов);
			
		ИначеЕсли ТипЗнч(ЭлемнтСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			
			НайтиРекурсивноЭлементыГруппировки(ЭлемнтСтруктуры.Строки, ПолеКомпоновкиДанных, МассивРезультатов);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти