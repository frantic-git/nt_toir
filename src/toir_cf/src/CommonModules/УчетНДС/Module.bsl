#Область ПрограммныйИнтерфейс

// Заполняет данные выбора ставки НДС
//
// Параметры:
// 	 ДанныеВыбора - СписокЗначений - Данные выбора
// 	 Параметры - Структура - Параметры выбора:
// 	 	* Отбор - Структура - содержит:
// 	 		** Ссылка - ПеречислениеСсылка.ТипыНалогообложенияНДС - 
// 	 СтандартнаяОбработка - Булево - флаг стандартной обработки события
//
Процедура ПолучитьДанныеВыбораСтавкиНДС(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт 
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос();
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтавкиНДСТипыНалогообложенияНДС.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(СтавкиНДСТипыНалогообложенияНДС.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.СтавкиНДС.ТипыНалогообложенияНДС КАК СтавкиНДСТипыНалогообложенияНДС
	| ГДЕ
	|	&ОтборПоТипамНалогообложения
	|	И &ОтборПоПараметрам
	|   И &ФильтрПоРассчетнойСтавке
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтавкиНДСТипыНалогообложенияНДС.Ссылка.Представление";
	
	НалогообложениеНДС = Неопределено;
	Если Параметры.Свойство("НалогообложениеНДС", НалогообложениеНДС) Тогда
		Если Не ЗначениеЗаполнено(НалогообложениеНДС) Тогда
			НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоТипамНалогообложения", "СтавкиНДСТипыНалогообложенияНДС.ТипНалогообложенияНДС В(&ТипыНалогообложения)");
		Запрос.УстановитьПараметр("ТипыНалогообложения", НалогообложениеНДС);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоТипамНалогообложения", "ИСТИНА");
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Ссылка") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПараметрам", "СтавкиНДСТипыНалогообложенияНДС.Ссылка В (&ОтборПоСсылке)");
		Запрос.УстановитьПараметр("ОтборПоСсылке", Параметры.Отбор.Ссылка);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПараметрам", "ИСТИНА");
	КонецЕсли;
	
	Если Параметры.Свойство("ИсключатьДробныеСтавки") И Параметры.ИсключатьДробныеСтавки Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ФильтрПоРассчетнойСтавке", "СтавкиНДСТипыНалогообложенияНДС.Ссылка.РасчетнаяСтавка = ЛОЖЬ"); 
	ИначеЕсли Параметры.Свойство("ТолькоДробныеСтавки") И Параметры.ТолькоДробныеСтавки Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ФильтрПоРассчетнойСтавке", "СтавкиНДСТипыНалогообложенияНДС.Ссылка.РасчетнаяСтавка = ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ФильтрПоРассчетнойСтавке", "ИСТИНА");
	КонецЕсли; 
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Параметры.СтрокаПоиска)
			ИЛИ СтрНайти(НРег(Выборка.Представление), НРег(Параметры.СтрокаПоиска)) = 1 Тогда
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти