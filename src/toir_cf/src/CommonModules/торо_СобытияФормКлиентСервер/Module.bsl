
#Область СлужебныеПроцедурыИФункции

// Сохраняет данные строки до редактирования.
//
// Параметры:
//		СтруктураДанных - Структура - реквизит формы, где хранятся данные строк до изменения.
//		ИмяТаблицыФормы - Строка - имя таблицы формы для которой сохраняются данные.
//		ТекущиеДанные - ДанныеФормыЭлементКоллекции - данные которые необходимо сохранить.
//						ДанныеФормыЭлементДерева
//
Процедура СохранитьДанныеСтрокиДоРедактирования(СтруктураДанных, ИмяТаблицыФормы, ТекущиеДанные) Экспорт
	
	ДанныеСтроки = Новый Структура;	
	
	Для Каждого ИмяРеквизита Из СтруктураДанных[ИмяТаблицыФормы + "Реквизиты"] Цикл
		ДанныеСтроки.Вставить(ИмяРеквизита);	
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущиеДанные);
	
	СтруктураДанных.Вставить(ИмяТаблицыФормы, ДанныеСтроки);
	
КонецПроцедуры	

// Обновляет данные строки после изменения одного из реквизитов. Используется, если необходимо получать 
// данные строки до редактирования в обработчике событий элементов таблиц формы "При изменении".
//
// Параметры:
//		СтруктураДанных - Структура - реквизит формы, где хранятся данные строк до изменения.
//		ИмяТаблицыФормы - Строка - имя таблицы формы для которой сохраняются данные.
//		ИмяРеквизита - Строка - имя обновляемого реквизита.
//		Значение - Произвольный - значение реквизита для обновления.
//
Процедура ОбновитьДанныеСтрокиДоРедактирования(СтруктураДанных, ИмяТаблицыФормы, ИмяРеквизита, Значение) Экспорт
	
	Если СтруктураДанных[ИмяТаблицыФормы] = Неопределено Тогда // Для случаев, когда данные строки еще не сохранялись.
		
		ДанныеСтроки = Новый Структура;
		ДанныеСтроки.Вставить(ИмяРеквизита, Значение);
		
		СтруктураДанных.Вставить(ИмяТаблицыФормы, ДанныеСтроки);
	Иначе  
		СтруктураДанных[ИмяТаблицыФормы].Вставить(ИмяРеквизита, Значение);
	КонецЕсли;
	
КонецПроцедуры	

// Процедура, зависимости от заполнения табличной части и доступности формы, устанавливает доступность кнопок 
//
// Параметры:
//		ТЧ - ЭлементФормы - Элемент формы, который проверяется на заполнение.
//		СписокЭлементов - Список значений - Список значений, содержащий названия кнопок.
//		Форма - ФормаКлиентскогоПриложения - форма документа.
//		ПравоРедактирование - Булево - право на редактирование объектов формы.
//		СогласованиеТолькоПросмотр - Булево - Доступность формы.
Процедура УстановитьДоступностьКнопок(ТЧ, СписокЭлементов, Форма, ПравоРедактирование = Истина, СогласованиеТолькоПросмотр = Ложь) Экспорт
	
	ЗаполненоЗначение = ЗначениеЗаполнено(ТЧ);
		
	Для Каждого Строка из СписокЭлементов Цикл	
		Форма.Элементы[Строка.Значение].Доступность = ЗаполненоЗначение И ПравоРедактирование И Не СогласованиеТолькоПросмотр;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
