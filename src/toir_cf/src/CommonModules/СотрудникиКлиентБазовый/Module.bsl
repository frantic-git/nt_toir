
#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы Сотрудника

// Обработчик события "Перед записью".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента.
//  Отказ - Булево - отказ.
//  ПараметрыЗаписи - Структура - параметры записи.
//  
Процедура СотрудникиПередЗаписью(Форма, Отказ, ПараметрыЗаписи) Экспорт
	
	// Запрос про полное имя
	ЗапроситьРежимИзмененияФИО(Форма, Форма.ФИОФизическихЛиц, Форма.ФИОФизическихЛицНоваяЗапись, Отказ, НСтр("ru = 'сотрудника'"));
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
			
	Если Форма.ФИОФизическихЛицНоваяЗапись И Форма.ВыполненаКомандаСменыФИО = Ложь Тогда
		СотрудникиКлиент.ИзменитьФИОСотрудника(Форма, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события "Перед записью".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента.
//  Отказ - Булево - отказ.
//  ПараметрыЗаписи - Структура - параметры записи.
//  ОповещениеЗавершения - ОписаниеОповещения - описание оповещения о закрытии.
//  ЗакрытьПослеЗаписи - Булево - закрыть форму после записи.
// 
Процедура ФизическиеЛицаПередЗаписью(Форма, Отказ, ПараметрыЗаписи, ОповещениеЗавершения = Неопределено, ЗакрытьПослеЗаписи = Истина) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Отказ", Отказ);
	ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи", ЗакрытьПослеЗаписи);
	
	Если Форма.ПроверяютсяОднофамильцы Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Форма.СозданиеНового И Не Отказ Тогда
		// запрос про гражданство
		Оповещение = Новый ОписаниеОповещения("ФизическиеЛицаПередЗаписьюФИО", ЭтотОбъект, ДополнительныеПараметры);
		Форма.ЗапроситьРежимИзмененияГражданства(Форма, Форма.ГражданствоФизическихЛиц.Период, Отказ, Оповещение);
	Иначе 
		ФизическиеЛицаПередЗаписьюФИО(Отказ, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения для формы справочника ФизЛица.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента.
//  ИмяСобытия - Строка - ИмяСобытия.
//  Параметр - Произвольный - параметр события.
//  Источник - Произвольный - источник события.
//
Процедура ФизическиеЛицаОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(Форма, ИмяСобытия, Параметр) Тогда
		Форма.ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	
	СотрудникиКлиент.ЛичныеДанныеФизическогоЛицаОбработкаОповещения(ИмяСобытия, Параметр, Источник, Форма);
	
	Если ИмяСобытия = "ОтредактированаИстория" И Форма.ФизическоеЛицоСсылка = Источник Тогда
		Если (Параметр.ИмяРегистра = "ГражданствоФизическихЛиц"
			ИЛИ Параметр.ИмяРегистра = "ДокументыФизическихЛиц"
			ИЛИ Параметр.ИмяРегистра = "ФИОФизическихЛиц")
			И Форма[Параметр.ИмяРегистра + "НаборЗаписейПрочитан"] Тогда
			Если Параметр.ИмяРегистра = "ДокументыФизическихЛиц" Тогда
				СотрудникиКлиентСервер.ОбработатьОтображениеСерияДокументаФизическогоЛица(Форма.ДокументыФизическихЛиц.ВидДокумента, Форма.ДокументыФизическихЛиц.Серия ,Форма.Элементы.ДокументыФизическихЛицСерия, Форма);
				СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(Форма.ДокументыФизическихЛиц.ВидДокумента, Форма.ДокументыФизическихЛиц.Номер ,Форма.Элементы.ДокументыФизическихЛицНомер, Форма);
				СотрудникиКлиентСервер.ОбновитьПолеУдостоверениеЛичностиПериод(Форма);
			Иначе
				РедактированиеПериодическихСведенийКлиентСервер.ОбработкаОповещения(Форма, Форма.ФизическоеЛицоСсылка, ИмяСобытия, Параметр, Источник);
				Если Параметр.ИмяРегистра = "ГражданствоФизическихЛиц" Тогда
					Если ЗначениеЗаполнено(Форма.ГражданствоФизическихЛиц.Страна) Тогда
						Форма.ГражданствоФизическихЛицЛицоБезГражданства = 0;
					Иначе
						Форма.ГражданствоФизическихЛицЛицоБезГражданства = 1;
					КонецЕсли;
					СотрудникиКлиентСервер.ОбновитьДоступностьПолейВводаГражданства(Форма, ОбщегоНазначенияКлиент.ДатаСеанса());
				ИначеЕсли Параметр.ИмяРегистра = "ФИОФизическихЛиц" Тогда
					НаименованиеПоМенеджеруЗаписи = Форма.ФИОФизическихЛиц.Фамилия + " " + Форма.ФИОФизическихЛиц.Имя + " " + Форма.ФИОФизическихЛиц.Отчество;
					Если Не ПустаяСтрока(НаименованиеПоМенеджеруЗаписи) И Форма.ФизическоеЛицо.ФИО <> НаименованиеПоМенеджеруЗаписи Тогда
						Форма.ФизическоеЛицо.ФИО = НаименованиеПоМенеджеруЗаписи;
					КонецЕсли; 
					СотрудникиКлиентСервер.УстановитьВидимостьПолейФИО(Форма);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции

// Обработчик события "При изменении".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента.
//  Элемент - ПолеВвода - элемент формы.
// 
Процедура ФизическиеЛицаИННПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентСервер.ОбработатьОтображениеПоляИНН(Форма.ФизическоеЛицо.ИНН, Элемент,  Форма);
КонецПроцедуры

// Обработчик события "При изменении".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента.
//  Элемент - ПолеВвода - элемент формы.
// 
Процедура ФизическиеЛицаСтраховойНомерПФРПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентСервер.ОбработатьОтображениеПоляСтраховойНомерПФР(Форма.ФизическоеЛицо.СтраховойНомерПФР, Элемент, Форма);
КонецПроцедуры

// Обработчик события "ПриИзменении".
// Параметры:
//		Форма - ФормаКлиентскогоПриложения - форма документа.
//
Процедура ДокументыФизическихЛицВидДокументаПриИзменении(Форма) Экспорт 
	СотрудникиКлиентСервер.ОбновитьПолеУдостоверениеЛичностиПериод(Форма);
	СотрудникиКлиентСервер.ОбработатьОтображениеСерияДокументаФизическогоЛица(Форма.ДокументыФизическихЛиц.ВидДокумента, Форма.ДокументыФизическихЛиц.Серия ,Форма.Элементы.ДокументыФизическихЛицСерия, Форма);
	СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(Форма.ДокументыФизическихЛиц.ВидДокумента, Форма.ДокументыФизическихЛиц.Номер ,Форма.Элементы.ДокументыФизическихЛицНомер, Форма);
КонецПроцедуры

// Обработчик события "ПриИзменении".
// Параметры:
//		Форма - ФормаКлиентскогоПриложения - форма документа.
//		Элемент - ПолеВвода - элемент формы.
Процедура ДокументыФизическихЛицНомерПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентСервер.ОбработатьОтображениеНомерДокументаФизическогоЛица(Форма.ДокументыФизическихЛиц.ВидДокумента, Форма.ДокументыФизическихЛиц.Номер, Элемент,  Форма);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ФизическиеЛицаПередЗаписьюФИО(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
		
	// Запрос про полное имя
	Оповещение = Новый ОписаниеОповещения("ФизическиеЛицаПередЗаписьюИзменитьФИО", ЭтотОбъект, ДополнительныеПараметры);
	ЗапроситьРежимИзмененияФИО(Форма, Форма.ФИОФизическихЛиц, Форма.ФИОФизическихЛицНоваяЗапись, Отказ, НСтр("ru = 'сотрудника'"), Оповещение);
	
КонецПроцедуры

Процедура ФизическиеЛицаПередЗаписьюИзменитьФИО(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	// Если внесли изменение в ФИО.
	Если Форма.ФИОФизическихЛицНоваяЗапись И Форма.ВыполненаКомандаСменыФИО = Ложь Тогда
		Оповещение = Новый ОписаниеОповещения("ФизическиеЛицаПередЗаписьюУдостоверениеЛичности", ЭтотОбъект, ДополнительныеПараметры);
		СотрудникиКлиент.ИзменитьФИОФизическогоЛица(Форма, Оповещение);
	Иначе 
		ФизическиеЛицаПередЗаписьюУдостоверениеЛичности(Ложь, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ФизическиеЛицаПередЗаписьюУдостоверениеЛичности(Результат, ДополнительныеПараметры) Экспорт

	Отказ = (Результат = Неопределено);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	// Запрос про документ удостоверяющий личность.
	Оповещение = Новый ОписаниеОповещения("ФизическиеЛицаПередЗаписьюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
    СотрудникиКлиент.ЗапроситьРежимИзмененияУдостоверенияЛичности(Форма, Форма.ДокументыФизическихЛиц.Период, Отказ, Оповещение);	
	
КонецПроцедуры

Процедура ФизическиеЛицаПередЗаписьюЗавершение(Отказ, ДополнительныеПараметры) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	ПараметрыЗаписи = ДополнительныеПараметры.ПараметрыЗаписи;
	ПараметрыЗаписи.Вставить("ПроверкаПередЗаписьюВыполнена", Истина);
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ПараметрыЗаписи);
	ИначеЕсли Форма.Записать(ПараметрыЗаписи) Тогда
		
		Форма.Модифицированность = Ложь;
		Если ДополнительныеПараметры.ЗакрытьПослеЗаписи Тогда 
			Форма.Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапроситьРежимИзмененияФИО(Форма, МенеджерЗаписиФИО, НоваяЗапись, Отказ, ПредставлениеСущности, ОповещениеЗавершения = Неопределено)
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти