#Область ПрограммныйИнтерфейс

// Формирует уведомления и дефекты.
//	Параметры:
//		Источник - ДокументОбъект.торо_УчетКонтролируемыхПоказателей - документ-источник.
//		ДокументыВДКОткрытию - СписокЗначений - выходной параметр, список свормированных выявленных дефектов.
Процедура СформироватьУведомленияИДефекты(Источник,ДокументыВДКОткрытию) Экспорт
	
	СтруктураДанных = Источник.СтруктураДанных;
	СтруктураДанных.Вставить("ПеречислениеПроизвольноеЗначение",Перечисления.торо_ВариантыЗначенийКонтролируемыхПоказателей.ПроизвольноеЗначение);
	торо_РаботаСУведомлениями.ЗаполнитьСтруктуруДанныхДокумента(СтруктураДанных, Источник);
	СтруктураДанных.Вставить("ВидДокумента",СокрЛП(Источник.Метаданные().Имя));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Показатель КАК Показатель,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Значение КАК Значение,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Ссылка КАК ДокументИсточник,
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Ссылка.Дата КАК ДатаДокументаИсточник,
	               |	торо_ОбъектыРемонта.ТиповойОР КАК ТиповойОР
	               |ПОМЕСТИТЬ ТаблицаПоказателей
	               |ИЗ
	               |	Документ.торо_УчетКонтролируемыхПоказателей.Показатели КАК торо_УчетКонтролируемыхПоказателейПоказатели
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	               |		ПО торо_УчетКонтролируемыхПоказателейПоказатели.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	               |ГДЕ
	               |	торо_УчетКонтролируемыхПоказателейПоказатели.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК ОбъектРемонта,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель КАК Показатель,
	               |	ТаблицаПоказателей.ТиповойОР КАК ТиповойОР,
	               |	2 КАК Приоритет,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМинимум КАК ДопустимыйМинимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМаксимум КАК ДопустимыйМаксимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМинимум КАК КритическийМинимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМаксимум КАК КритическийМаксимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ЗначенияКонтролируемыхПоказателейИзОР КАК ЗначенияКонтролируемыхПоказателейИзОР,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ТочкаЗамера КАК ТочкаЗамера,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален КАК Удален
	               |ПОМЕСТИТЬ ПоказателиОРИТиповогоОР
	               |ИЗ
	               |	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателей
	               |		ПО ((ВЫРАЗИТЬ(торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК Справочник.торо_ОбъектыРемонта)) = ТаблицаПоказателей.ОбъектРемонта)
	               |			И торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель = ТаблицаПоказателей.Показатель
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТаблицаПоказателей.ОбъектРемонта,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта,
	               |	1,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМинимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМаксимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМинимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМаксимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ЗначенияКонтролируемыхПоказателейИзОР,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ТочкаЗамера,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален
	               |ИЗ
	               |	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателей
	               |		ПО ((ВЫРАЗИТЬ(торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК Справочник.торо_ТиповыеОР)) = ТаблицаПоказателей.ТиповойОР)
	               |			И торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель = ТаблицаПоказателей.Показатель
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТаблицаПоказателей.ОбъектРемонта,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта,
	               |	0,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМинимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ДопустимыйМаксимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМинимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.КритическийМаксимум,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ЗначенияКонтролируемыхПоказателейИзОР,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.ТочкаЗамера,
	               |	торо_ИзмеряемыеПоказателиОбъектовРемонта.Удален
	               |ИЗ
	               |	РегистрСведений.торо_ИзмеряемыеПоказателиОбъектовРемонта КАК торо_ИзмеряемыеПоказателиОбъектовРемонта
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателей
	               |		ПО ((ВЫРАЗИТЬ(торо_ИзмеряемыеПоказателиОбъектовРемонта.ОбъектРемонта КАК Справочник.торо_ТиповыеОР)) = ТаблицаПоказателей.ТиповойОР.Родитель)
	               |			И торо_ИзмеряемыеПоказателиОбъектовРемонта.Показатель = ТаблицаПоказателей.Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПриоритетовПолная.Показатель КАК Показатель,
	               |	МАКСИМУМ(ТаблицаПриоритетовПолная.Приоритет) КАК Приоритет,
	               |	МАКСИМУМ(ТаблицаПриоритетовПолная.Удален) КАК Удален
	               |ПОМЕСТИТЬ ТаблицаПриоритетов
	               |ИЗ
	               |	ПоказателиОРИТиповогоОР КАК ТаблицаПриоритетовПолная
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаПриоритетовПолная.Показатель
	               |
	               |ИМЕЮЩИЕ
	               |	МАКСИМУМ(ТаблицаПриоритетовПолная.Удален) = ЛОЖЬ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПоказателиОРИТиповогоОР.ОбъектРемонта КАК ОбъектРемонта,
	               |	ПоказателиОРИТиповогоОР.Показатель КАК Показатель,
	               |	ПоказателиОРИТиповогоОР.ТиповойОР КАК ТиповойОР,
	               |	торо_ПараметрыОповещенияОСостоянииОборудования.СобытиеВозникновенияОповещения КАК СобытиеВозникновенияОповещения,
	               |	торо_ПараметрыОповещенияОСостоянииОборудования.ВидСравнения КАК ВидСравнения,
	               |	торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения КАК ВариантЗначения,
	               |	торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения1 КАК ВариантЗначения1,
	               |	ВЫБОР
	               |		КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ПроизвольноеЗначение)
	               |				ИЛИ торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ЗначениеИзмеряемогоПоказателяОР)
	               |			ТОГДА торо_ПараметрыОповещенияОСостоянииОборудования.Значение
	               |		КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.КритическийМаксимум)
	               |			ТОГДА ПоказателиОРИТиповогоОР.КритическийМаксимум
	               |		КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.КритическийМинимум)
	               |			ТОГДА ПоказателиОРИТиповогоОР.КритическийМинимум
	               |		КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ДопустимыйМаксимум)
	               |			ТОГДА ПоказателиОРИТиповогоОР.ДопустимыйМаксимум
	               |		КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ДопустимыйМинимум)
	               |			ТОГДА ПоказателиОРИТиповогоОР.ДопустимыйМинимум
	               |	КОНЕЦ КАК Значение,
	               |	ВЫБОР
	               |		КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.ВИнтервале)
	               |				ИЛИ торо_ПараметрыОповещенияОСостоянииОборудования.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.НЕВИнтервале)
	               |			ТОГДА ВЫБОР
	               |					КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения1 = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ПроизвольноеЗначение)
	               |						ТОГДА торо_ПараметрыОповещенияОСостоянииОборудования.Значение1
	               |					КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения1 = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.КритическийМаксимум)
	               |						ТОГДА ПоказателиОРИТиповогоОР.КритическийМаксимум
	               |					КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения1 = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.КритическийМинимум)
	               |						ТОГДА ПоказателиОРИТиповогоОР.КритическийМинимум
	               |					КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения1 = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ДопустимыйМаксимум)
	               |						ТОГДА ПоказателиОРИТиповогоОР.ДопустимыйМаксимум
	               |					КОГДА торо_ПараметрыОповещенияОСостоянииОборудования.ВариантЗначения1 = ЗНАЧЕНИЕ(Перечисление.торо_ВариантыЗначенийКонтролируемыхПоказателей.ДопустимыйМинимум)
	               |						ТОГДА ПоказателиОРИТиповогоОР.ДопустимыйМинимум
	               |				КОНЕЦ
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Значение1,
	               |	торо_ПараметрыОповещенияОСостоянииОборудования.ПараметрыВводаДефекта КАК ПараметрыВводаДефекта,
	               |	ПоказателиОРИТиповогоОР.КритическийМаксимум КАК КритическийМаксимум,
	               |	ПоказателиОРИТиповогоОР.ДопустимыйМаксимум КАК ДопустимыйМаксимум,
	               |	ПоказателиОРИТиповогоОР.ДопустимыйМинимум КАК ДопустимыйМинимум,
	               |	ПоказателиОРИТиповогоОР.КритическийМинимум КАК КритическийМинимум
	               |ПОМЕСТИТЬ ТаблицаЗначенийПоказателей
	               |ИЗ
	               |	ПоказателиОРИТиповогоОР КАК ПоказателиОРИТиповогоОР
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПриоритетов КАК ТаблицаПриоритетов
	               |		ПО ПоказателиОРИТиповогоОР.Показатель = ТаблицаПриоритетов.Показатель
	               |			И ПоказателиОРИТиповогоОР.Приоритет = ТаблицаПриоритетов.Приоритет
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.торо_ПараметрыОповещенияОСостоянииОборудования КАК торо_ПараметрыОповещенияОСостоянииОборудования
	               |		ПО (ВЫБОР
	               |				КОГДА ТаблицаПриоритетов.Приоритет = 2
	               |					ТОГДА ПоказателиОРИТиповогоОР.ОбъектРемонта = торо_ПараметрыОповещенияОСостоянииОборудования.ОбъектРемонта
	               |				ИНАЧЕ ПоказателиОРИТиповогоОР.ТиповойОР = торо_ПараметрыОповещенияОСостоянииОборудования.ТиповойОР
	               |			КОНЕЦ)
	               |			И ПоказателиОРИТиповогоОР.Показатель = торо_ПараметрыОповещенияОСостоянииОборудования.Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТаблицаПоказателей.ДокументИсточник КАК ДокументИсточник,
	               |	ТаблицаПоказателей.ДатаДокументаИсточник КАК ДатаДокументаИсточник,
	               |	ЕСТЬNULL(торо_СобытияУведомленийВДокументах.Ссылка, ЗНАЧЕНИЕ(Справочник.торо_СобытияУведомленийВДокументах.ПустаяСсылка)) КАК СобытиеУведомления,
	               |	торо_ШаблоныСообщенийИУведомлений.ТемаПисьма КАК ТемаПисьма,
	               |	торо_ШаблоныСообщенийИУведомлений.ТелоПисьма КАК ТелоПисьма,
	               |	торо_ШаблоныСообщенийИУведомлений.Ссылка КАК ШаблонСообщения,
	               |	ТаблицаЗначенийПоказателей.ОбъектРемонта КАК ОбъектРемонта,
	               |	ТаблицаЗначенийПоказателей.Показатель КАК Показатель,
	               |	ТаблицаЗначенийПоказателей.ТиповойОР КАК ТиповойОР,
	               |	ТаблицаЗначенийПоказателей.СобытиеВозникновенияОповещения КАК СобытиеВозникновенияОповещения,
	               |	ТаблицаЗначенийПоказателей.ВидСравнения КАК ВидСравнения,
	               |	ТаблицаЗначенийПоказателей.Значение КАК Значение,
	               |	ТаблицаЗначенийПоказателей.Значение1 КАК Значение1,
	               |	ТаблицаЗначенийПоказателей.ВариантЗначения КАК ВариантЗначения,
	               |	ТаблицаЗначенийПоказателей.ВариантЗначения1 КАК ВариантЗначения1,
	               |	ТаблицаЗначенийПоказателей.КритическийМаксимум КАК КритическийМаксимум,
	               |	ТаблицаЗначенийПоказателей.ДопустимыйМаксимум КАК ДопустимыйМаксимум,
	               |	ТаблицаЗначенийПоказателей.ДопустимыйМинимум КАК ДопустимыйМинимум,
	               |	ТаблицаЗначенийПоказателей.КритическийМинимум КАК КритическийМинимум,
	               |	ТаблицаЗначенийПоказателей.ПараметрыВводаДефекта КАК ПараметрыВводаДефекта,
	               |	ТаблицаПоказателей.Значение КАК ЗначениеПоказателя,
	               |	ВЫБОР
	               |		КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(перечисление.торо_видысравнения.Винтервале)
	               |			ТОГДА ""в интервале""
	               |		КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(перечисление.торо_видысравнения.НЕВинтервале)
	               |			ТОГДА ""не в интервале""
	               |		ИНАЧЕ ВЫБОР
	               |				КОГДА ТаблицаПоказателей.Значение > ТаблицаЗначенийПоказателей.Значение
	               |					ТОГДА ""больше""
	               |				КОГДА ТаблицаПоказателей.Значение < ТаблицаЗначенийПоказателей.Значение
	               |					ТОГДА ""меньше""
	               |				ИНАЧЕ ""равно""
	               |			КОНЕЦ
	               |	КОНЕЦ КАК ВидСравненияСтрока
	               |ПОМЕСТИТЬ ИтоговаяТаблица
	               |ИЗ
	               |	ТаблицаПоказателей КАК ТаблицаПоказателей
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаЗначенийПоказателей КАК ТаблицаЗначенийПоказателей
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_СобытияУведомленийВДокументах КАК торо_СобытияУведомленийВДокументах
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ШаблоныСообщенийИУведомлений КАК торо_ШаблоныСообщенийИУведомлений
	               |				ПО торо_СобытияУведомленийВДокументах.ШаблонСообщения = торо_ШаблоныСообщенийИУведомлений.Ссылка
	               |			ПО ТаблицаЗначенийПоказателей.СобытиеВозникновенияОповещения = торо_СобытияУведомленийВДокументах.Ссылка
	               |		ПО ТаблицаПоказателей.ОбъектРемонта = ТаблицаЗначенийПоказателей.ОбъектРемонта
	               |			И ТаблицаПоказателей.Показатель = ТаблицаЗначенийПоказателей.Показатель
	               |			И (ВЫБОР
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.Больше)
	               |					И НЕ ТаблицаПоказателей.Значение ССЫЛКА Справочник.торо_ЗначенияИзмеряемыхПоказателейОбъектовРемонта
	               |					ТОГДА ТаблицаПоказателей.Значение > ТаблицаЗначенийПоказателей.Значение
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.Меньше)
	               |					И НЕ ТаблицаПоказателей.Значение ССЫЛКА Справочник.торо_ЗначенияИзмеряемыхПоказателейОбъектовРемонта
	               |					ТОГДА ТаблицаПоказателей.Значение < ТаблицаЗначенийПоказателей.Значение
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.БольшеИлиРавно)
	               |					И НЕ ТаблицаПоказателей.Значение ССЫЛКА Справочник.торо_ЗначенияИзмеряемыхПоказателейОбъектовРемонта
	               |					ТОГДА ТаблицаПоказателей.Значение >= ТаблицаЗначенийПоказателей.Значение
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.МеньшеИлиРавно)
	               |					И НЕ ТаблицаПоказателей.Значение ССЫЛКА Справочник.торо_ЗначенияИзмеряемыхПоказателейОбъектовРемонта
	               |					ТОГДА ТаблицаПоказателей.Значение <= ТаблицаЗначенийПоказателей.Значение
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.ВИнтервале)
	               |					И НЕ ТаблицаПоказателей.Значение ССЫЛКА Справочник.торо_ЗначенияИзмеряемыхПоказателейОбъектовРемонта
	               |					ТОГДА ((ТаблицаПоказателей.Значение >= ТаблицаЗначенийПоказателей.Значение
	               |							И ТаблицаПоказателей.Значение <= ТаблицаЗначенийПоказателей.Значение1) 
				   |						ИЛИ (ТаблицаПоказателей.Значение <= ТаблицаЗначенийПоказателей.Значение
	               |							И ТаблицаПоказателей.Значение >= ТаблицаЗначенийПоказателей.Значение1))
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.НеВИнтервале)
	               |					И НЕ ТаблицаПоказателей.Значение ССЫЛКА Справочник.торо_ЗначенияИзмеряемыхПоказателейОбъектовРемонта
	               |					ТОГДА ТаблицаПоказателей.Значение < ТаблицаЗначенийПоказателей.Значение
	               |							И ТаблицаПоказателей.Значение > ТаблицаЗначенийПоказателей.Значение1
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.Равно)
	               |					ТОГДА ТаблицаПоказателей.Значение = ТаблицаЗначенийПоказателей.Значение
	               |				КОГДА ТаблицаЗначенийПоказателей.ВидСравнения = ЗНАЧЕНИЕ(Перечисление.торо_ВидыСравнения.НеРавно)
	               |					ТОГДА ТаблицаПоказателей.Значение <> ТаблицаЗначенийПоказателей.Значение
	               |				ИНАЧЕ ЛОЖЬ
	               |			КОНЕЦ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка КАК ПараметрыВводаДефекта,
	               |	ЕСТЬNULL(ЗаполнениеШапкиОрганизация.ШапкаЗначениеПоля, НЕОПРЕДЕЛЕНО) КАК ЗначениеОрганизации,
	               |	ЕСТЬNULL(ЗаполнениеШапкиПодразделение.ШапкаЗначениеПоля, НЕОПРЕДЕЛЕНО) КАК ЗначениеПодразделения
	               |ПОМЕСТИТЬ ДляПроверкиРЛСПоШапке
	               |ИЗ
	               |	Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов КАК торо_ПараметрыАвтоматическойФиксацииДефектов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов.ЗаполнениеШапки КАК ЗаполнениеШапкиОрганизация
	               |		ПО торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка = ЗаполнениеШапкиОрганизация.Ссылка
	               |			И (ЗаполнениеШапкиОрганизация.ШапкаИмяПоля = ""Организация"")
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов.ЗаполнениеШапки КАК ЗаполнениеШапкиПодразделение
	               |		ПО торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка = ЗаполнениеШапкиПодразделение.Ссылка
	               |			И (ЗаполнениеШапкиОрганизация.ШапкаИмяПоля = ""Подразделение"")
	               |ГДЕ
	               |	торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ИтоговаяТаблица.ПараметрыВводаДефекта КАК ПараметрыВводаДефекта
	               |			ИЗ
	               |				ИтоговаяТаблица КАК ИтоговаяТаблица)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	ДляПроверкиРЛСПоШапке.ПараметрыВводаДефекта КАК ПараметрыВводаДефекта
	               |ПОМЕСТИТЬ ПараметрыСРазрешеннойШапкой
	               |ИЗ
	               |	ДляПроверкиРЛСПоШапке КАК ДляПроверкиРЛСПоШапке
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	               |		ПО (ДляПроверкиРЛСПоШапке.ЗначениеОрганизации = Организации.Ссылка
	               |				ИЛИ ДляПроверкиРЛСПоШапке.ЗначениеОрганизации = НЕОПРЕДЕЛЕНО
	               |				ИЛИ ДляПроверкиРЛСПоШапке.ЗначениеОрганизации = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	               |		ПО (ДляПроверкиРЛСПоШапке.ЗначениеПодразделения = СтруктураПредприятия.Ссылка
	               |				ИЛИ ДляПроверкиРЛСПоШапке.ЗначениеПодразделения = НЕОПРЕДЕЛЕНО
	               |				ИЛИ ДляПроверкиРЛСПоШапке.ЗначениеПодразделения = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ИтоговаяТаблица.ДокументИсточник КАК ДокументИсточник,
	               |	ИтоговаяТаблица.ДатаДокументаИсточник КАК ДатаДокументаИсточник,
	               |	ИтоговаяТаблица.СобытиеУведомления КАК СобытиеУведомления,
	               |	ИтоговаяТаблица.ТемаПисьма КАК ТемаПисьма,
	               |	ИтоговаяТаблица.ТелоПисьма КАК ТелоПисьма,
	               |	ИтоговаяТаблица.ШаблонСообщения КАК ШаблонСообщения,
	               |	ИтоговаяТаблица.ОбъектРемонта КАК ОбъектРемонта,
	               |	ИтоговаяТаблица.Показатель КАК Показатель,
	               |	ИтоговаяТаблица.ТиповойОР КАК ТиповойОР,
	               |	ИтоговаяТаблица.СобытиеВозникновенияОповещения КАК СобытиеВозникновенияОповещения,
	               |	ИтоговаяТаблица.ВидСравнения КАК ВидСравнения,
	               |	ИтоговаяТаблица.Значение КАК Значение,
	               |	ИтоговаяТаблица.Значение1 КАК Значение1,
	               |	ИтоговаяТаблица.ВариантЗначения КАК ВариантЗначения,
	               |	ИтоговаяТаблица.ВариантЗначения1 КАК ВариантЗначения1,
	               |	ИтоговаяТаблица.КритическийМаксимум КАК КритическийМаксимум,
	               |	ИтоговаяТаблица.ДопустимыйМаксимум КАК ДопустимыйМаксимум,
	               |	ИтоговаяТаблица.ДопустимыйМинимум КАК ДопустимыйМинимум,
	               |	ИтоговаяТаблица.КритическийМинимум КАК КритическийМинимум,
	               |	ИтоговаяТаблица.ПараметрыВводаДефекта КАК ПараметрыВводаДефекта,
	               |	ИтоговаяТаблица.ЗначениеПоказателя КАК ЗначениеПоказателя,
	               |	ИтоговаяТаблица.ВидСравненияСтрока КАК ВидСравненияСтрока
	               |ИЗ
	               |	ИтоговаяТаблица КАК ИтоговаяТаблица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.торо_ИсторияУведомленийПользователей КАК торо_ИсторияУведомленийПользователей
	               |		ПО ИтоговаяТаблица.ДокументИсточник = торо_ИсторияУведомленийПользователей.Источник
	               |			И ((ВЫРАЗИТЬ(ИтоговаяТаблица.ТемаПисьма КАК СТРОКА(100))) = (ВЫРАЗИТЬ(торо_ИсторияУведомленийПользователей.ТемаПисьма КАК СТРОКА(100))))
	               |ГДЕ
	               |	торо_ИсторияУведомленийПользователей.Пользователь ЕСТЬ NULL
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ИтоговаяТаблица.ДокументИсточник КАК ДокументИсточник,
	               |	ИтоговаяТаблица.ДатаДокументаИсточник КАК ДатаДокументаИсточник,
	               |	ИтоговаяТаблица.СобытиеУведомления КАК СобытиеУведомления,
	               |	ИтоговаяТаблица.ТемаПисьма КАК ТемаПисьма,
	               |	ИтоговаяТаблица.ТелоПисьма КАК ТелоПисьма,
	               |	ИтоговаяТаблица.ШаблонСообщения КАК ШаблонСообщения,
	               |	ИтоговаяТаблица.ОбъектРемонта КАК ОбъектРемонта,
	               |	ИтоговаяТаблица.Показатель КАК Показатель,
	               |	ИтоговаяТаблица.ТиповойОР КАК ТиповойОР,
	               |	ИтоговаяТаблица.СобытиеВозникновенияОповещения КАК СобытиеВозникновенияОповещения,
	               |	ИтоговаяТаблица.ВидСравнения КАК ВидСравнения,
	               |	ИтоговаяТаблица.Значение КАК Значение,
	               |	ИтоговаяТаблица.Значение1 КАК Значение1,
	               |	ИтоговаяТаблица.ВариантЗначения КАК ВариантЗначения,
	               |	ИтоговаяТаблица.ВариантЗначения1 КАК ВариантЗначения1,
	               |	ИтоговаяТаблица.КритическийМаксимум КАК КритическийМаксимум,
	               |	ИтоговаяТаблица.ДопустимыйМаксимум КАК ДопустимыйМаксимум,
	               |	ИтоговаяТаблица.ДопустимыйМинимум КАК ДопустимыйМинимум,
	               |	ИтоговаяТаблица.КритическийМинимум КАК КритическийМинимум,
	               |	ИтоговаяТаблица.ПараметрыВводаДефекта КАК ПараметрыВводаДефекта,
	               |	ИтоговаяТаблица.ЗначениеПоказателя КАК ЗначениеПоказателя,
	               |	ИтоговаяТаблица.ВидСравненияСтрока КАК ВидСравненияСтрока
	               |ИЗ
	               |	ИтоговаяТаблица КАК ИтоговаяТаблица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ВыявленныеДефекты.СписокДефектов КАК торо_ВыявленныеДефектыСписокДефектов
	               |		ПО ИтоговаяТаблица.ДокументИсточник = торо_ВыявленныеДефектыСписокДефектов.ДокументИсточник
	               |			И ИтоговаяТаблица.ОбъектРемонта = торо_ВыявленныеДефектыСписокДефектов.ОбъектРемонта
	               |			И ИтоговаяТаблица.ОбъектРемонта = торо_ВыявленныеДефектыСписокДефектов.ОтказавшийЭлемент
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыСРазрешеннойШапкой КАК ПараметрыСРазрешеннойШапкой
	               |		ПО ИтоговаяТаблица.ПараметрыВводаДефекта = ПараметрыСРазрешеннойШапкой.ПараметрыВводаДефекта
	               |ГДЕ
	               |	торо_ВыявленныеДефектыСписокДефектов.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Ссылка",Источник.Ссылка);
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	// Обработка сообщений
	Если НЕ РезультатПакета[7].Пустой() Тогда
		
		Выборка = РезультатПакета[7].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.СобытиеУведомления) Тогда
				ДополнитьСтруктуруПараметровПодстановкиКонтролируемыхПоказателей(СтруктураДанных,Выборка);
				торо_РаботаСУведомлениями.ДобавитьЗаписиВОчередьУведомленийПоПараметрам(Выборка.СобытиеУведомления, СтруктураДанных);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Обработка создания дефектов
	Если НЕ РезультатПакета[8].Пустой() Тогда
		
		МассивКолонок = Новый Массив;
		Для Каждого Колонка Из РезультатПакета[8].Колонки Цикл
			МассивКолонок.Добавить(Колонка.Имя);
		КонецЦикла;
		
		Выборка = РезультатПакета[8].Выбрать();
		Пока Выборка.Следующий() Цикл
			ВвестиДокументДефектНаОсновании(Выборка,СтруктураДанных,ДокументыВДКОткрытию,МассивКолонок);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьСтруктуруПараметровПодстановкиКонтролируемыхПоказателей(Структура,Выборка)
	
	ПеречислениеПроизвольноеЗначение = Структура.ПеречислениеПроизвольноеЗначение;
	ЕдиницаИзмеренияПоказателя = Строка(Выборка.Показатель.ЕдиницаИзмерения);
	Структура.Вставить("ОбъектРемонта",Строка(Выборка.ОбъектРемонта));
	Структура.Вставить("Показатель",Строка(Выборка.Показатель));
	Структура.Вставить("ЗначениеПоказателя",Строка(Выборка.ЗначениеПоказателя));
	Структура.Вставить("УсловиеУведомления",Выборка.ВидСравненияСтрока);
	Структура.Вставить("Значение",?(Выборка.ВариантЗначения = ПеречислениеПроизвольноеЗначение,Выборка.Значение, Строка(Выборка.ВариантЗначения) +"("+Выборка.Значение+" "+ЕдиницаИзмеренияПоказателя+")"));
	Структура.Вставить("ГраницаИнтервала",?(Выборка.ВариантЗначения1 = ПеречислениеПроизвольноеЗначение,Выборка.Значение, Строка(Выборка.ВариантЗначения1) +"("+Выборка.Значение1+")"));
	Структура.Вставить("ЕдиницаИзмеренияПоказателя",ЕдиницаИзмеренияПоказателя);
	Структура.Вставить("КритическиеЗначенияПоказателей", "Критический максимум: "+Выборка.КритическийМаксимум+" "+ЕдиницаИзмеренияПоказателя+"
														 |Допустимый максимум: "+Выборка.ДопустимыйМаксимум+" "+ЕдиницаИзмеренияПоказателя+"
														 |Допустимый минимум:    "+Выборка.ДопустимыйМинимум+" "+ЕдиницаИзмеренияПоказателя+"
														 |Допустимый максимум:  "+Выборка.КритическийМинимум+" "+ЕдиницаИзмеренияПоказателя);
КонецПроцедуры

Процедура ВвестиДокументДефектНаОсновании(Выборка,СтруктураДанных,ДокументыВДКОткрытию,МассивКолонок)
	
	Если ЗначениеЗаполнено(Выборка.ПараметрыВводаДефекта) Тогда
		
		СтруктураПараметров = ПолучитьСтруктуруНовогоДокумента(Выборка.ПараметрыВводаДефекта);
		Если СтруктураПараметров = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураДокументаОснования = Новый Структура;
		Для Каждого ИмяКолонки Из МассивКолонок Цикл
			СтруктураДокументаОснования.Вставить(ИмяКолонки,Выборка[ИмяКолонки]);
		КонецЦикла;
		
		СтруктураПараметров.Вставить("СтруктураДокументаОснования", СтруктураДокументаОснования);
		
		СсылкаНаДокумент = Неопределено;
		
		Если СтруктураПараметров.Общее.ЗаписыватьДокумент Тогда
			НовДокумент = Документы.торо_ВыявленныеДефекты.СоздатьДокумент();
			НовДокумент.Заполнить(Новый Структура("ДанныеДокументаПриСозданииПоСостоянию", СтруктураПараметров));
			
			Попытка
				НовДокумент.Записать(?(СтруктураПараметров.Общее.ПроводитьДокумент, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
				СсылкаНаДокумент = НовДокумент.Ссылка;
			Исключение
				ОписаниеОшибки = ОписаниеОшибки();
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Исключение'"), УровеньЖурналаРегистрации.Предупреждение,, ОписаниеОшибки);
				ТекстСообщения = НСтр("ru = 'Не удалось создать документ ""Выявленный дефект"" автоматически: '") + ОписаниеОшибки;
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецПопытки;
		КонецЕсли;
		
		Если СтруктураПараметров.Общее.ОткрыватьФорму Тогда
			Если СсылкаНаДокумент <> Неопределено Тогда
				СтруктураПараметров.Вставить("СсылкаНаДокумент", СсылкаНаДокумент);
			КонецЕсли;
			
			ДокументыВДКОткрытию.Добавить(СтруктураПараметров);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСтруктуруНовогоДокумента(ПараметрыВводаДефекта) 
	
	СтруктураВозврата = Новый Структура("Шапка,Таблица,Общее",Новый Структура,Новый Структура,Новый Структура);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеШапки.ШапкаИмяПоля КАК ИмяПоля,
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеШапки.ШапкаЗначениеПоля КАК ЗначениеПоля,
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеШапки.ШапкаСинонимПоля КАК СинонимПоля,
	|	""Шапка"" КАК Назначение
	|ИЗ
	|	Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов.ЗаполнениеШапки КАК торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеШапки
	|ГДЕ
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеШапки.Ссылка = &ПараметрыВводаДефекта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеТабличнойЧасти.ТаблицаИмяПоля,
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеТабличнойЧасти.ТаблицаЗначениеПоля,
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеТабличнойЧасти.ТаблицаСинонимПоля,
	|	""Таблица""
	|ИЗ
	|	Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов.ЗаполнениеТабличнойЧасти КАК торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеТабличнойЧасти
	|ГДЕ
	|	торо_ПараметрыАвтоматическойФиксацииДефектовЗаполнениеТабличнойЧасти.Ссылка = &ПараметрыВводаДефекта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ОткрыватьФорму"",
	|	торо_ПараметрыАвтоматическойФиксацииДефектов.ОткрыватьФорму,
	|	NULL,
	|	""Общее""
	|ИЗ
	|	Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов КАК торо_ПараметрыАвтоматическойФиксацииДефектов
	|ГДЕ
	|	торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка = &ПараметрыВводаДефекта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ЗаписыватьДокумент"",
	|	торо_ПараметрыАвтоматическойФиксацииДефектов.ЗаписыватьДокумент,
	|	NULL,
	|	""Общее""
	|ИЗ
	|	Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов КАК торо_ПараметрыАвтоматическойФиксацииДефектов
	|ГДЕ
	|	торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка = &ПараметрыВводаДефекта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПроводитьДокумент"",
	|	торо_ПараметрыАвтоматическойФиксацииДефектов.ПроводитьДокумент,
	|	NULL,
	|	""Общее""
	|ИЗ
	|	Справочник.торо_ПараметрыАвтоматическойФиксацииДефектов КАК торо_ПараметрыАвтоматическойФиксацииДефектов
	|ГДЕ
	|	торо_ПараметрыАвтоматическойФиксацииДефектов.Ссылка = &ПараметрыВводаДефекта";
	
	Запрос.УстановитьПараметр("ПараметрыВводаДефекта",ПараметрыВводаДефекта);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтруктураВозврата[Выборка.Назначение].Вставить(Выборка.ИмяПоля,Выборка.ЗначениеПоля);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

