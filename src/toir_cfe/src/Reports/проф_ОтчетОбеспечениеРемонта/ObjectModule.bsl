
//++ Проф-ИТ, #82, Башинская А., 25.08.2023

#Область ОбработчикиСобытий 

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	УстановитьЗначениеПараметров(Настройки);
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
	МассивОбластейДляСнятияЗащиты = Новый Массив;
	
	Область = ДокументРезультат.НайтиТекст("Текущий статус");
	МассивОбластейДляСнятияЗащиты.Добавить(Область);
	Область = ДокументРезультат.НайтиТекст("Текущий статус реализации проблемы");
	МассивОбластейДляСнятияЗащиты.Добавить(Область);
	Область = ДокументРезультат.НайтиТекст("Комментарий");
	МассивОбластейДляСнятияЗащиты.Добавить(Область);
	
	Для Каждого Область Из МассивОбластейДляСнятияЗащиты Цикл
		Если ТипЗнч(Область) <> Тип("Неопределено") Тогда			
			НомерКолонки = Область.Лево;
			Область = ДокументРезультат.Область( , НомерКолонки, , НомерКолонки);
			Область.Защита = Ложь;			
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Процедура УстановитьЗначениеПараметров(Настройки)
	
	Сообщение = Новый СообщениеПользователю;
	
	СкладБГЛЦ = Справочники.проф_НастройкиСистемы.ПолучитьНастройкуСистемы("ОтчетОбеспечениеРемонта", "СкладБГЛЦ");
	Если СкладБГЛЦ = Неопределено Тогда
		СкладБГЛЦ = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
		Сообщение.Текст = "Не заполнена настройка системы СкладБГЛЦ в пространстве имен ОтчетОбеспечениеРемонта";
		Сообщение.Сообщить();
	КонецЕсли;	
		
	СкладБилибино = Справочники.проф_НастройкиСистемы.ПолучитьНастройкуСистемы("ОтчетОбеспечениеРемонта", "СкладБилибино");
	Если СкладБилибино = Неопределено Тогда
		СкладБилибино = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
		Сообщение.Текст = "Не заполнена настройка системы СкладБилибино в пространстве имен ОтчетОбеспечениеРемонта";
		Сообщение.Сообщить();
	КонецЕсли;	
	
	ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("БГЛЦ"));
    Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Значение		= СкладБГЛЦ;
		ЗначениеПараметра.Использование	= Истина;
	КонецЕсли;
	
	ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Билибино"));
    Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Значение		= СкладБилибино;
		ЗначениеПараметра.Использование	= Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

//-- Проф-ИТ, #82, Башинская А., 25.08.2023