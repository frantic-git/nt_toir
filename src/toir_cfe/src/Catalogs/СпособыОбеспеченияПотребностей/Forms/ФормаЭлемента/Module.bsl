//++ Проф-ИТ, #44, Соловьев А.А., 19.12.2023

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура проф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ДобавляемыеРеквизиты = Новый Массив;
	НовыйРеквизит = Новый РеквизитФормы("РежимИспользованияСкладов", Новый ОписаниеТипов("Число",
																	 Новый КвалификаторыЧисла(1, 0)));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ЭтотОбъект["РежимИспользованияСкладов"] = ЗначениеЗаполнено(Объект.ИсточникОбеспеченияПотребностей) 
		И Объект.ТипОбеспечения = Перечисления.ТипыОбеспечения.проф_Перемещение;
	
	НовыйЭлемент				= Элементы.Добавить("ТипОбеспечения", Тип("ПолеФормы"), ЭтотОбъект);
	НовыйЭлемент.Вид			= ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.ПутьКДанным	= "Объект.ТипОбеспечения";
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ТипыОбеспечения.Покупка);
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ТипыОбеспечения.проф_Перемещение);
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ТипОбеспеченияПриИзменении");
	
	Элементы.Переместить(НовыйЭлемент, ЭтотОбъект, Элементы.ГруппаРежимИспользования);
	
	СтраницаРежимИспользованияПеремещение = Элементы.Добавить("СтраницаРежимИспользованияПеремещение", Тип("ГруппаФормы"),
		Элементы.СтраницыРежимИспользования);
	СтраницаРежимИспользованияПеремещение.Вид					= ВидГруппыФормы.Страница;
	СтраницаРежимИспользованияПеремещение.ОтображатьЗаголовок	= Ложь;
	СтраницаРежимИспользованияПеремещение.Группировка			= ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовыйЭлемент				= Элементы.Добавить("РежимИспользованияНесколькоСкладов", Тип("ПолеФормы"), СтраницаРежимИспользованияПеремещение);
	НовыйЭлемент.Вид			= ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.ПутьКДанным	= "РежимИспользованияСкладов";
	НовыйЭлемент.СписокВыбора.Добавить(0, "С несколькими складам");
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
	НоваяГруппа						= Элементы.Добавить("ГруппаИспользоватьОдинСклад", Тип("ГруппаФормы"), СтраницаРежимИспользованияПеремещение);
	НоваяГруппа.Вид					= ВидГруппыФормы.ОбычнаяГруппа;
	НоваяГруппа.Отображение			= ОтображениеОбычнойГруппы.Нет;
	НоваяГруппа.ОтображатьЗаголовок	= Ложь; 
	НоваяГруппа.Группировка			= ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	НовыйЭлемент				= Элементы.Добавить("РежимИспользованияОдинСклад", Тип("ПолеФормы"), НоваяГруппа);
	НовыйЭлемент.Вид			= ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.ПутьКДанным	= "РежимИспользованияСкладов";
	НовыйЭлемент.СписокВыбора.Добавить(1, "С распределительным центром:");
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
	НовыйЭлемент  						= Элементы.Добавить("проф_ИсточникОбеспеченияПотребностей", Тип("ПолеФормы"), НоваяГруппа);
	НовыйЭлемент.Вид 					= ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПоложениеЗаголовка 	= ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.ПутьКДанным 			= "Объект.ИсточникОбеспеченияПотребностей";
	НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
	НовыйЭлемент.МаксимальнаяШирина 	= 22;
	НовыйЭлемент.КнопкаВыпадающегоСписка= Ложь;
	НовыйЭлемент.КнопкаВыбора 			= Истина;
	НовыйЭлемент.КнопкаОткрытия 		= Ложь;
	
	Если Объект.ТипОбеспечения = Перечисления.ТипыОбеспечения.Покупка Тогда 
		Элементы.СтраницыРежимИспользования.ТекущаяСтраница = Элементы.СтраницаРежимИспользованияПокупка;
	Иначе
		Элементы.СтраницыРежимИспользования.ТекущаяСтраница = Элементы.СтраницаРежимИспользованияПеремещение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОбеспеченияПриИзменении(Элемент)
	
	Если Объект.ТипОбеспечения = ПредопределенноеЗначение("Перечисление.ТипыОбеспечения.Покупка") Тогда 
		Элементы.СтраницыРежимИспользования.ТекущаяСтраница = Элементы.СтраницаРежимИспользованияПокупка;
	Иначе
		Элементы.СтраницыРежимИспользования.ТекущаяСтраница = Элементы.СтраницаРежимИспользованияПеремещение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- Проф-ИТ, #44, Соловьев А.А., 19.12.2023