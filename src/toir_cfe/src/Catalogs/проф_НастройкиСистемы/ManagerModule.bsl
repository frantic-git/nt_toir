#Область ПрограммныйИнтерфейс

Функция ПолучитьНастройкуСистемы(НаименованиеГруппы, Наименование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованиеГруппы", НаименованиеГруппы);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.Текст = ТекстЗапросаПолучитьНастройкуСистемы();
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда		
		Если Выборка.СодержитСписок Тогда
			Массив = Новый Массив;
			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл
				Массив.Добавить(Выборка.Значение);
			КонецЦикла;	
			Возврат Массив;
		Иначе	
			Возврат Выборка.Значение;			
		КонецЕсли;			
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьНастройкиСистемы(СтруктураПараметровНастроек) Экспорт

	проф_НастройкиСистемы = Новый Структура;	
	
	КС_150 = Новый КвалификаторыСтроки(150);
	ОТСтрока_150 = Новый ОписаниеТипов("Строка", , , , КС_150);	
	
	ТаблицаПараметровНастроек = Новый ТаблицаЗначений;
	ТаблицаПараметровНастроек.Колонки.Добавить("Ключ", ОТСтрока_150);
	ТаблицаПараметровНастроек.Колонки.Добавить("НаименованиеГруппы", ОТСтрока_150);
	ТаблицаПараметровНастроек.Колонки.Добавить("Наименование", ОТСтрока_150);	
		
	Для Каждого Элемент Из СтруктураПараметровНастроек Цикл
		НСтр = ТаблицаПараметровНастроек.Добавить();						
		НСтр.Ключ = Элемент.Ключ;
		НСтр.НаименованиеГруппы = Элемент.Значение.НаименованиеГруппы;
		НСтр.Наименование = Элемент.Значение.Наименование;
		
		проф_НастройкиСистемы.Вставить(Элемент.Ключ, Неопределено);	
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПараметровНастроек", ТаблицаПараметровНастроек);
	Запрос.Текст = ТекстЗапросаПолучитьНастройкиСистемы();		
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаКлючи = Результат[3].Выбрать();
	Выборка = Результат[4].Выбрать();
	
	// освобождаем память
	ТаблицаПараметровНастроек = Неопределено;
	
	Пока ВыборкаКлючи.Следующий() Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ключ", ВыборкаКлючи.Ключ); 
		Если ВыборкаКлючи.СодержитСписок Тогда
			Массив = Новый Массив;
			Пока Выборка.НайтиСледующий(Отбор) Цикл
				Массив.Добавить(Выборка.Значение);		
			КонецЦикла;	 			
			проф_НастройкиСистемы.Вставить(ВыборкаКлючи.Ключ, Массив);			
		Иначе
			Если Выборка.НайтиСледующий(Отбор) Тогда
				проф_НастройкиСистемы.Вставить(ВыборкаКлючи.Ключ, Выборка.Значение);
			КонецЕсли;	
		КонецЕсли;	
		Выборка.Сбросить();
	КонецЦикла;	
	
    Возврат проф_НастройкиСистемы;
	
КонецФункции	

Функция НовыйПараметрНастроек(НаименованиеГруппы = "", Наименование = "") Экспорт
	
	Возврат Новый Структура("НаименованиеГруппы, Наименование", НаименованиеГруппы, Наименование);
	
КонецФункции
                                                                                                         
Процедура ДобавитьПараметрНастроекВСтруктуру(СтруктураПараметровНастроек, Ключ, НаименованиеГруппы = "", Наименование = "") Экспорт
	
	ПараметрНастроек = НовыйПараметрНастроек(НаименованиеГруппы, Наименование);
	СтруктураПараметровНастроек.Вставить(Ключ, ПараметрНастроек);
	
КонецПроцедуры	

//++ Проф-ИТ, Гончаров А.В., 27.09.2022
Функция ПроверитьЗаполненностьНастроекСистемы(проф_НастройкиСистемы, ВыводитьСообщениеОбОшибке = Ложь) Экспорт

	РезультатПроверки = Истина;
	
	СписокПараметров = "";
	
	Для Каждого ПараметрНастройки Из проф_НастройкиСистемы Цикл
		Если НЕ ЗначениеЗаполнено(ПараметрНастройки.Значение) Тогда
			СписокПараметров = СписокПараметров + ?(ЗначениеЗаполнено(СписокПараметров), ", ", "") + ПараметрНастройки.Ключ;
			РезультатПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ РезультатПроверки И ВыводитьСообщениеОбОшибке Тогда
		ТекстСообщения = СтрШаблон("Не заполнены следующие реквизиты в справочнике Настройки системы: %1", СписокПараметров);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

    Возврат РезультатПроверки;
	
КонецФункции	
//-- Проф-ИТ, Гончаров А.В., 27.09.2022

//++ 23.11.2022, Ванькин А.А.
//Аналог ПолучитьНастройкуСистемы с возвратом Списка значений  
// <Описание функции>
//
// Параметры:
//  НаименованиеГруппы  - Строка - Точное наименование группы настройки системы
//  Наименование  - Строка - Точное наименование настройки системы
//
// Возвращаемое значение:
//   СписокЗначений   - <описание возвращаемого значения>
//
Функция ПолучитьСпЗначНастройкиСистемы(НаименованиеГруппы, Наименование) Экспорт 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованиеГруппы", НаименованиеГруппы);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.Текст = ТекстЗапросаПолучитьНастройкуСистемы();
	Выборка = Запрос.Выполнить().Выбрать();
	
	СпЗначений = Новый СписокЗначений(); 
	Если Выборка.Следующий() Тогда		
		Если Выборка.СодержитСписок Тогда
			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл
				СпЗначений.Добавить(Выборка.Значение);
			КонецЦикла;	
		Иначе	
			СпЗначений.Добавить(Выборка.Значение);			
		КонецЕсли;			
	КонецЕсли;	
	
	Если СпЗначений.Количество() <> 0  Тогда
	    Возврат СпЗначений;
	КонецЕсли;
	
	
	Возврат Неопределено;	

КонецФункции // ПолучитьСпЗначНастройкиСистемы(НаименованиеГруппы, Наименование)()
//-- 23.11.2022, Ванькин А.А.

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПолучитьНастройкуСистемы()
	
	ТекстЗапроса = "ВЫБРАТЬ
       |	проф_НастройкиСистемы.Ссылка КАК Родитель
       |ПОМЕСТИТЬ ВтРодители
       |ИЗ
       |	Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
       |ГДЕ
       |	проф_НастройкиСистемы.Наименование = &НаименованиеГруппы
       |	И проф_НастройкиСистемы.ЭтоГруппа
       |;
       |
       |////////////////////////////////////////////////////////////////////////////////
       |ВЫБРАТЬ
       |	проф_НастройкиСистемы.Ссылка КАК Ссылка,
       |	проф_НастройкиСистемы.СодержитСписок КАК СодержитСписок
       |ПОМЕСТИТЬ ВтНастройки
       |ИЗ
       |	ВтРодители КАК ВтРодители
       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
       |		ПО ВтРодители.Родитель = проф_НастройкиСистемы.Родитель
       |			И (проф_НастройкиСистемы.Наименование = &Наименование)
       |;
       |
       |////////////////////////////////////////////////////////////////////////////////
       |ВЫБРАТЬ
       |	проф_НастройкиСистемы.Значение КАК Значение,
       |	проф_НастройкиСистемы.СодержитСписок КАК СодержитСписок
       |ИЗ
       |	ВтНастройки КАК ВтНастройки
       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
       |		ПО ВтНастройки.Ссылка = проф_НастройкиСистемы.Ссылка
       |			И (ВтНастройки.СодержитСписок = ЛОЖЬ)
       |
       |ОБЪЕДИНИТЬ ВСЕ
       |
       |ВЫБРАТЬ
       |	ЕСТЬNULL(проф_НастройкиСистемыСписок.Значение, НЕОПРЕДЕЛЕНО),
       |	ВтНастройки.СодержитСписок
       |ИЗ
       |	ВтНастройки КАК ВтНастройки
       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
       |		ПО ВтНастройки.Ссылка = проф_НастройкиСистемы.Ссылка
       |			И (ВтНастройки.СодержитСписок)
       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы.Список КАК проф_НастройкиСистемыСписок
       |		ПО ВтНастройки.Ссылка = проф_НастройкиСистемыСписок.Ссылка";
  	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаПолучитьНастройкиСистемы()

	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТаблицаПараметровНастроек.Ключ КАК Ключ,
	               |	ТаблицаПараметровНастроек.НаименованиеГруппы КАК НаименованиеГруппы,
	               |	ТаблицаПараметровНастроек.Наименование КАК Наименование
	               |ПОМЕСТИТЬ ВтПараметрыНастроек
	               |ИЗ
	               |	&ТаблицаПараметровНастроек КАК ТаблицаПараметровНастроек
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтПараметрыНастроек.Ключ КАК Ключ,
	               |	ЕСТЬNULL(проф_НастройкиСистемы.Ссылка, ЗНАЧЕНИЕ(Справочник.проф_НастройкиСистемы.ПустаяСсылка)) КАК Родитель,
	               |	ВтПараметрыНастроек.Наименование КАК Наименование
	               |ПОМЕСТИТЬ ВтРодители
	               |ИЗ
	               |	ВтПараметрыНастроек КАК ВтПараметрыНастроек
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
	               |		ПО ВтПараметрыНастроек.НаименованиеГруппы = проф_НастройкиСистемы.Наименование
	               |			И (проф_НастройкиСистемы.ЭтоГруппа)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтПараметрыНастроек.Ключ,
	               |	ЕСТЬNULL(проф_НастройкиСистемы.Ссылка, ЗНАЧЕНИЕ(Справочник.проф_НастройкиСистемы.ПустаяСсылка)),
	               |	ВтПараметрыНастроек.Наименование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтРодители.Ключ КАК Ключ,
	               |	проф_НастройкиСистемы.Ссылка КАК Ссылка,
	               |	проф_НастройкиСистемы.СодержитСписок КАК СодержитСписок
	               |ПОМЕСТИТЬ ВтНастройки
	               |ИЗ
	               |	ВтРодители КАК ВтРодители
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
	               |		ПО ВтРодители.Родитель = проф_НастройкиСистемы.Родитель
	               |			И ВтРодители.Наименование = проф_НастройкиСистемы.Наименование
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтРодители.Ключ,
	               |	проф_НастройкиСистемы.Ссылка,
	               |	проф_НастройкиСистемы.СодержитСписок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтНастройки.Ключ КАК Ключ,
	               |	ВтНастройки.СодержитСписок КАК СодержитСписок
	               |ИЗ
	               |	ВтНастройки КАК ВтНастройки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтНастройки.Ключ,
	               |	ВтНастройки.СодержитСписок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтНастройки.Ключ КАК Ключ,
	               |	проф_НастройкиСистемы.Значение КАК Значение,
	               |	проф_НастройкиСистемы.СодержитСписок КАК СодержитСписок
	               |ИЗ
	               |	ВтНастройки КАК ВтНастройки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
	               |		ПО ВтНастройки.Ссылка = проф_НастройкиСистемы.Ссылка
	               |			И (ВтНастройки.СодержитСписок = ЛОЖЬ)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВтНастройки.Ключ,
	               |	ЕСТЬNULL(проф_НастройкиСистемыСписок.Значение, НЕОПРЕДЕЛЕНО),
	               |	ВтНастройки.СодержитСписок
	               |ИЗ
	               |	ВтНастройки КАК ВтНастройки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы КАК проф_НастройкиСистемы
	               |		ПО ВтНастройки.Ссылка = проф_НастройкиСистемы.Ссылка
	               |			И (ВтНастройки.СодержитСписок = ИСТИНА)
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_НастройкиСистемы.Список КАК проф_НастройкиСистемыСписок
	               |		ПО (проф_НастройкиСистемыСписок.Ссылка = проф_НастройкиСистемы.Ссылка)";

	Возврат ТекстЗапроса;
	
КонецФункции	

#КонецОбласти