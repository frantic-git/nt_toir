//++ Проф-ИТ, #180, Соловьев А.А., 04.10.2023
		   
// Записать объект и замерить длительность его записи.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма в которой выполняется вызов этой функции.
//  ЕстьВопросыПередЗаписью - Булево - Истина, если при записи могут отображаться вопросы пользователю.
//  ДействиеПослеЗаписи - ОписаниеОповещения - обработчик, выполняемый после записи объекта.
//
Процедура Записать(Форма, ЕстьВопросыПередЗаписью = Ложь, ДействиеПослеЗаписи = Неопределено) Экспорт
	
	ПараметрыЗаписи = СтруктураПараметровЗаписиОбъекта();
	ПараметрыЗаписи.ЕстьВопросыПередЗаписью = ЕстьВопросыПередЗаписью;
	
	Если ДействиеПослеЗаписи <> Неопределено Тогда
		ПараметрыЗаписи.Вставить("ДействиеПослеЗаписи", ДействиеПослеЗаписи);
	КонецЕсли;
	
	ЗаписатьОбъект(Форма, ПараметрыЗаписи);
	
КонецПроцедуры		   
		   
// Выполняет запись документа в форме
//
// Параметры:
//  Форма - РасширениеУправляемойФормыДляОбъектов - Форма документа в которой выполняется вызов этой функции:
//  	* Объект - ДокументОбъект -
//	ПараметрыЗаписи - Структура - 
//
Функция ЗаписатьОбъект(Форма, ПараметрыЗаписи)
	
	Перем Проведен, ДействиеПослеЗаписи;
	
	СтрДействиеПослеЗаписи = "ДействиеПослеЗаписи";
	
	ОчиститьСообщения();
	
	Если Не ПараметрыЗаписи.ЕстьВопросыПередЗаписью Тогда
		Если Не Форма.Объект.Свойство("Проведен", Проведен) Тогда
			Проведен =  Ложь;
		КонецЕсли;
		
		Если Проведен И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
			ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;		
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство(СтрДействиеПослеЗаписи, ДействиеПослеЗаписи) Тогда
		ПараметрыЗаписи.Удалить(СтрДействиеПослеЗаписи);
	КонецЕсли;
	
	Результат = Форма.Записать(ПараметрыЗаписи);
	
	Если ДействиеПослеЗаписи <> Неопределено Тогда
		Если Результат Тогда
			ВыполнитьОбработкуОповещения(ДействиеПослеЗаписи, Результат);
		КонецЕсли;
		
		ПараметрыЗаписи.Вставить(СтрДействиеПослеЗаписи, ДействиеПослеЗаписи);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Записать объект, замерить длительность его записи и закрыть форму объекта.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма в которой выполняется вызов этой функции.
//  ЕстьВопросыПередЗаписью - Булево - Истина, если при записи могут отображаться вопросы пользователю.
//  ДействиеПослеЗаписи - ОписаниеОповещения - обработчик, выполняемый после записи объекта.
//
Процедура ЗаписатьИЗакрыть(Форма, ЕстьВопросыПередЗаписью = Ложь, ДействиеПослеЗаписи = Неопределено) Экспорт
	
	ПараметрыЗаписи = СтруктураПараметровЗаписиОбъекта();
	ПараметрыЗаписи.ЕстьВопросыПередЗаписью = ЕстьВопросыПередЗаписью;
	
	Если ДействиеПослеЗаписи <> Неопределено Тогда
		ПараметрыЗаписи.Вставить("ДействиеПослеЗаписи", ДействиеПослеЗаписи);
	КонецЕсли;
	
	ЗаписатьОбъектИЗакрыть(Форма, ПараметрыЗаписи);
	
КонецПроцедуры 

// Выполняет запись документа в форме
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа в которой выполняется вызов этой функции:
//  	* Объект - ДокументОбъект -
//	ПараметрыЗаписи - Структура - 
//
Процедура ЗаписатьОбъектИЗакрыть(Форма, ПараметрыЗаписи)
	
	ПараметрыЗаписи.ПринудительноЗакрытьФорму = Истина;
	ОчиститьСообщения();
	
	Если ПараметрыЗаписи.ЕстьВопросыПередЗаписью Тогда
		ПараметрыЗаписи.НовыйОбъект = Не ЗначениеЗаполнено(Форма.Объект.Ссылка);
		Форма.ПринудительноЗакрытьФорму = ПараметрыЗаписи.ПринудительноЗакрытьФорму;
		Форма.Записать(ПараметрыЗаписи);
	Иначе
		Если ЗаписатьОбъект(Форма, ПараметрыЗаписи) Тогда
			Форма.Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураПараметровЗаписиОбъекта()
	
	ПараметрыЗаписи = Новый Структура;
	
	ПараметрыЗаписи.Вставить("ЕстьВопросыПередЗаписью", Ложь);
	ПараметрыЗаписи.Вставить("НовыйОбъект", Ложь);
	ПараметрыЗаписи.Вставить("ПринудительноЗакрытьФорму", Ложь);
	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
	ПараметрыЗаписи.Вставить("РежимПроведения", РежимПроведенияДокумента.Неоперативный);
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

//-- Проф-ИТ, #180, Соловьев А.А., 04.10.2023