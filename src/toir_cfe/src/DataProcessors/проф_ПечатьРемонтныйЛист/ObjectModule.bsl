
//++ Проф-ИТ, #131, Башинская А., 13.10.2023

#Область ПрограммныйИнтерфейс

Функция ПечатьРемонтныйЛист(МассивОбъектов, ПараметрыПечати) Экспорт 
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("проф_РемонтныйЛист");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов); 
	Запрос.Текст = ТекстЗапросаПечатьРемонтныйЛист(); 
	
	ОбщаяВыборка = Запрос.ВыполнитьПакет(); 
	
	ВыборкаВсеID			  = ОбщаяВыборка[0].Выбрать();
	ВыборкаИнформация		  = ОбщаяВыборка[2].Выбрать();
	ВыборкаЗаявкаНаРаботы	  = ОбщаяВыборка[3].Выбрать();
	ВыборкаЗапасныеЧасти   	  = ОбщаяВыборка[4].Выбрать();
	ВыборкаРасходныеМатериалы = ОбщаяВыборка[5].Выбрать();
	
	Если ЗначениеЗаполнено(ПараметрыПечати) Тогда
		МассивID = ПараметрыПечати.МассивID;
	Иначе		
		МассивID = ПолучитьВсеID(ВыборкаВсеID);
	КонецЕсли;
	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		Для Каждого ОбъектРемонтаID Из МассивID Цикл	
			
			Отбор = Новый Структура;
			Отбор.Вставить("Ссылка", СсылкаНаОбъект);
			Отбор.Вставить("ID", ОбъектРемонтаID);
			
			Печатать = Ложь;
			Если ВыборкаВсеID.НайтиСледующий(Отбор) Тогда
				Печатать = Истина;
			КонецЕсли;
			
			Если Печатать Тогда	
				
				// ШАПКА
				ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
				
				ОбластьШапка.Параметры.НомерДокумента = СсылкаНаОбъект.Номер;
				ОбластьШапка.Параметры.ДатаДокумента  = СсылкаНаОбъект.Дата;
				
				ТабДок.Вывести(ОбластьШапка);
				
				// ИНФОРМАЦИЯ				
				Информация(ВыборкаИнформация, Отбор, ТабДок, Макет);
				
				// ЗАЯВКИ НА РАБОТЫ
				ЗаявкиНаРаботы(ВыборкаЗаявкаНаРаботы, Отбор, ТабДок, Макет);
				
				// ЗАПАСНЫЕ ЧАСТИ
				ЗапасныеЧасти(ВыборкаЗапасныеЧасти, Отбор, ТабДок, Макет);
				
				// РАСХОДНЫЕ МАТЕРИАЛЫ
				РасходныеМатериалы(ВыборкаРасходныеМатериалы, Отбор, ТабДок, Макет);
				
				// КОММЕНТАРИЙ  
				ОбластьКомментарий = Макет.ПолучитьОбласть("Комментарий");
				
				ОбластьКомментарий.Параметры.Комментарий = СсылкаНаОбъект.Комментарий;
				
				ТабДок.Вывести(ОбластьКомментарий);
				
				// ПОДВАЛ
				ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
				
				ТабДок.Вывести(ОбластьПодвал);
				
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц(); 
				
			КонецЕсли;
			
			ВыборкаВсеID.Сбросить();
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
Функция ПечатьРемонтныйЛистСводно(МассивОбъектов, ПараметрыПечати) Экспорт 
		
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("проф_РемонтныйЛист");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаПечатьРемонтныйЛист(Истина); 
	УбратьУсловиеНаМассивID(Запрос.Текст);
	
	ОбщаяВыборка = Запрос.ВыполнитьПакет(); 
	
	ВыборкаВсеID			  = ОбщаяВыборка[0].Выбрать();
	ВыборкаИнформация		  = ОбщаяВыборка[ОбщаяВыборка.ВГраница()].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаЗаявкаНаРаботы	  = ОбщаяВыборка[3].Выбрать();
	ВыборкаЗапасныеЧасти      = ОбщаяВыборка[4].Выбрать();
	ВыборкаРасходныеМатериалы = ОбщаяВыборка[5].Выбрать();
	
	МассивID = ПолучитьВсеID(ВыборкаВсеID);
	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл
		
		МассивОбъектов = Новый Массив;
		
		Для Каждого ОбъектРемонтаID Из МассивID Цикл
			
			Отбор = Новый Структура;
			Отбор.Вставить("Ссылка", СсылкаНаОбъект);
			Отбор.Вставить("ID", ОбъектРемонтаID);
			
			Если ВыборкаВсеID.НайтиСледующий(Отбор) 
				И МассивОбъектов.Найти(ВыборкаВсеID.ОбъектРемонта) = Неопределено Тогда
				МассивОбъектов.Добавить(ВыборкаВсеID.ОбъектРемонта);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ОбъектРемонта Из МассивОбъектов Цикл
			
			ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
			
			ОбластьШапка.Параметры.НомерДокумента = СсылкаНаОбъект.Номер;
			ОбластьШапка.Параметры.ДатаДокумента  = Формат(СсылкаНаОбъект.Дата, "ДЛФ=Д");
			
			ТабДок.Вывести(ОбластьШапка);
			
			Отбор = Новый Структура;
			Отбор.Вставить("Ссылка", СсылкаНаОбъект);
			Отбор.Вставить("ОбъектРемонта", ОбъектРемонта);
			
			// ИНФОРМАЦИЯ
			ИнформацияСводно(ВыборкаИнформация, Отбор, ТабДок, Макет);
			
			// ЗАЯВКИ НА РАБОТЫ
			ЗаявкиНаРаботы(ВыборкаЗаявкаНаРаботы, Отбор, ТабДок, Макет);
			
			// ЗАПАСНЫЕ ЧАСТИ
			ЗапасныеЧасти(ВыборкаЗапасныеЧасти, Отбор, ТабДок, Макет);
			
			// РАСХОДНЫЕ МАТЕРИАЛЫ
			РасходныеМатериалы(ВыборкаРасходныеМатериалы, Отбор, ТабДок, Макет);
			
			// КОММЕНТАРИЙ
			ОбластьКомментарий = Макет.ПолучитьОбласть("Комментарий");
			
			ОбластьКомментарий.Параметры.Комментарий = СсылкаНаОбъект.Комментарий;
			
			ТабДок.Вывести(ОбластьКомментарий);
			
			// ПОДВАЛ
			ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
			
			ТабДок.Вывести(ОбластьПодвал);
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц(); 
			
			ВыборкаВсеID.Сбросить();
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПечатьРемонтныйЛист(РемонтныйЛистСводно = Ложь)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ID КАК ID,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка КАК Ссылка,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта
	|ИЗ
	|	Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|ГДЕ
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка В(&МассивОбъектов)
	|	И торо_ЗаявкаНаРемонтРемонтыОборудования.ID В(&МассивID)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ОбъектыРемонта.Ссылка КАК ОбъектРемонта,
	|	торо_ОбъектыРемонта.проф_Модель КАК Модель,
	|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_VIN, """") КАК VIN,
	|	ЕСТЬNULL(ГОД(торо_ОбъектыРемонта.ДатаВыпуска), ДАТАВРЕМЯ(1, 1, 1)) КАК ГодВыпуска,
	|	торо_ОбъектыРемонта.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ID КАК ID,
	|	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачала,
	|	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ДатаОкончания, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОкончания,
	|	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ВидРемонтныхРабот, ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка)) КАК ВидРемонтныхРабот,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ДокументИсточник КАК ДокументИсточник,
	|	ЕСТЬNULL(торо_ВыявленныеДефекты.Инициатор, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Инициатор,
	|	ЕСТЬNULL(торо_ВыявленныеДефекты.КонтактноеЛицоИнициатора, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК КонтактноеЛицоИнициатора,
	|	ЕСТЬNULL(торо_ВыявленныеДефектыСписокДефектов.ДефектОписание, """") КАК Описание,
	|	торо_ЗаявкаНаРемонт.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Гараж, """") КАК Гараж,
	|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Госномер, """") КАК Госномер
	|ПОМЕСТИТЬ ВТ_Информация
	|ИЗ
	|	Документ.торо_ЗаявкаНаРемонт КАК торо_ЗаявкаНаРемонт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|			ПО торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ВыявленныеДефекты КАК торо_ВыявленныеДефекты
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ВыявленныеДефекты.СписокДефектов КАК торо_ВыявленныеДефектыСписокДефектов
	|				ПО торо_ВыявленныеДефекты.Ссылка = торо_ВыявленныеДефектыСписокДефектов.Ссылка
	|			ПО торо_ЗаявкаНаРемонтРемонтыОборудования.ДокументИсточник = торо_ВыявленныеДефекты.Ссылка
	|		ПО (торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка = торо_ЗаявкаНаРемонт.Ссылка)
	|ГДЕ
	|	торо_ЗаявкаНаРемонт.Ссылка В(&МассивОбъектов)
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	И торо_ЗаявкаНаРемонтРемонтыОборудования.ID В(&МассивID)
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_ОбъектыРемонта.Ссылка,
	|	торо_ОбъектыРемонта.проф_Модель,
	|	торо_ОбъектыРемонта.ИнвентарныйНомер,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ID,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ДокументИсточник,
	|	торо_ЗаявкаНаРемонт.Ссылка,
	|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Гараж, """"),
	|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Госномер, """"),
	|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_VIN, """"),
	|	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ДатаНачала, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ДатаОкончания, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(торо_ВыявленныеДефекты.Инициатор, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ЕСТЬNULL(торо_ВыявленныеДефекты.КонтактноеЛицоИнициатора, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)),
	|	ЕСТЬNULL(торо_ВыявленныеДефектыСписокДефектов.ДефектОписание, """"),
	|	ЕСТЬNULL(ГОД(торо_ОбъектыРемонта.ДатаВыпуска), ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ВидРемонтныхРабот, ЗНАЧЕНИЕ(Справочник.торо_ВидыРемонтов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Информация.ОбъектРемонта КАК ОбъектРемонта,
	|	ВТ_Информация.Модель КАК Модель,
	|	ВТ_Информация.VIN КАК VIN,
	|	ВТ_Информация.ГодВыпуска КАК ГодВыпуска,
	|	ВТ_Информация.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВТ_Информация.ID КАК ID,
	|	ВТ_Информация.ДатаНачала КАК ДатаНачала,
	|	ВТ_Информация.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_Информация.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
	|	ВТ_Информация.ДокументИсточник КАК ДокументИсточник,
	|	ВТ_Информация.Инициатор КАК Инициатор,
	|	ВТ_Информация.КонтактноеЛицоИнициатора КАК КонтактноеЛицоИнициатора,
	|	ВТ_Информация.Описание КАК Описание,
	|	ВТ_Информация.Ссылка КАК Ссылка,
	|	ВТ_Информация.Гараж КАК Гараж,
	|	ВТ_Информация.Госномер КАК Госномер,
	|	МАКСИМУМ(торо_УчетНаработкиОборудованияНаработкаОбъектов.НовоеЗначение) КАК Пробег
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|ПОМЕСТИТЬ втИнформацияОбщая
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|ИЗ
	|	ВТ_Информация КАК ВТ_Информация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_УчетНаработкиОборудования.НаработкаОбъектов КАК торо_УчетНаработкиОборудованияНаработкаОбъектов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|			ПО торо_УчетНаработкиОборудованияНаработкаОбъектов.Показатель = ПараметрыВыработкиОС.Ссылка
	|				И (ПараметрыВыработкиОС.Наименование = ""Пробег"")
	|		ПО ВТ_Информация.ОбъектРемонта = торо_УчетНаработкиОборудованияНаработкаОбъектов.Объект
	|			И (торо_УчетНаработкиОборудованияНаработкаОбъектов.ДатаРаботыПо < ВТ_Информация.ДатаНачала)
	|ГДЕ
	|	(торо_УчетНаработкиОборудованияНаработкаОбъектов.НовоеЗначение ЕСТЬ NULL) = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Информация.ОбъектРемонта,
	|	ВТ_Информация.Модель,
	|	ВТ_Информация.VIN,
	|	ВТ_Информация.ГодВыпуска,
	|	ВТ_Информация.ИнвентарныйНомер,
	|	ВТ_Информация.ID,
	|	ВТ_Информация.ДатаНачала,
	|	ВТ_Информация.ДатаОкончания,
	|	ВТ_Информация.ВидРемонтныхРабот,
	|	ВТ_Информация.ДокументИсточник,
	|	ВТ_Информация.Инициатор,
	|	ВТ_Информация.КонтактноеЛицоИнициатора,
	|	ВТ_Информация.Описание,
	|	ВТ_Информация.Ссылка,
	|	ВТ_Информация.Гараж,
	|	ВТ_Информация.Госномер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Информация.ОбъектРемонта,
	|	ВТ_Информация.Модель,
	|	ВТ_Информация.VIN,
	|	ВТ_Информация.ГодВыпуска,
	|	ВТ_Информация.ИнвентарныйНомер,
	|	ВТ_Информация.ID,
	|	ВТ_Информация.ДатаНачала,
	|	ВТ_Информация.ДатаОкончания,
	|	ВТ_Информация.ВидРемонтныхРабот,
	|	ВТ_Информация.ДокументИсточник,
	|	ВТ_Информация.Инициатор,
	|	ВТ_Информация.КонтактноеЛицоИнициатора,
	|	ВТ_Информация.Описание,
	|	ВТ_Информация.Ссылка,
	|	ВТ_Информация.Гараж,
	|	ВТ_Информация.Госномер,
	|	МАКСИМУМ(торо_ВводНачальныхДанныхНаработкаОбъектыРемонта.Значение)
	|ИЗ
	|	ВТ_Информация КАК ВТ_Информация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ВводНачальныхДанных.НаработкаОбъектыРемонта КАК торо_ВводНачальныхДанныхНаработкаОбъектыРемонта
	|		ПО ВТ_Информация.ОбъектРемонта = торо_ВводНачальныхДанныхНаработкаОбъектыРемонта.ОбъектРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_УчетНаработкиОборудования.НаработкаОбъектов КАК торо_УчетНаработкиОборудованияНаработкаОбъектов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|			ПО торо_УчетНаработкиОборудованияНаработкаОбъектов.Показатель = ПараметрыВыработкиОС.Ссылка
	|		ПО ВТ_Информация.ОбъектРемонта = торо_УчетНаработкиОборудованияНаработкаОбъектов.Объект
	|			И (торо_УчетНаработкиОборудованияНаработкаОбъектов.ДатаРаботыПо < ВТ_Информация.ДатаНачала)
	|ГДЕ
	|	торо_УчетНаработкиОборудованияНаработкаОбъектов.НовоеЗначение ЕСТЬ NULL
	|	И (торо_ВводНачальныхДанныхНаработкаОбъектыРемонта.Значение ЕСТЬ NULL) = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Информация.ОбъектРемонта,
	|	ВТ_Информация.Модель,
	|	ВТ_Информация.VIN,
	|	ВТ_Информация.ГодВыпуска,
	|	ВТ_Информация.ИнвентарныйНомер,
	|	ВТ_Информация.ID,
	|	ВТ_Информация.ДатаНачала,
	|	ВТ_Информация.ДатаОкончания,
	|	ВТ_Информация.ВидРемонтныхРабот,
	|	ВТ_Информация.ДокументИсточник,
	|	ВТ_Информация.Инициатор,
	|	ВТ_Информация.КонтактноеЛицоИнициатора,
	|	ВТ_Информация.Описание,
	|	ВТ_Информация.Ссылка,
	|	ВТ_Информация.Гараж,
	|	ВТ_Информация.Госномер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Информация.ОбъектРемонта,
	|	ВТ_Информация.Модель,
	|	ВТ_Информация.VIN,
	|	ВТ_Информация.ГодВыпуска,
	|	ВТ_Информация.ИнвентарныйНомер,
	|	ВТ_Информация.ID,
	|	ВТ_Информация.ДатаНачала,
	|	ВТ_Информация.ДатаОкончания,
	|	ВТ_Информация.ВидРемонтныхРабот,
	|	ВТ_Информация.ДокументИсточник,
	|	ВТ_Информация.Инициатор,
	|	ВТ_Информация.КонтактноеЛицоИнициатора,
	|	ВТ_Информация.Описание,
	|	ВТ_Информация.Ссылка,
	|	ВТ_Информация.Гараж,
	|	ВТ_Информация.Госномер,
	|	0
	|ИЗ
	|	ВТ_Информация КАК ВТ_Информация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ВводНачальныхДанных.НаработкаОбъектыРемонта КАК торо_ВводНачальныхДанныхНаработкаОбъектыРемонта
	|		ПО ВТ_Информация.ОбъектРемонта = торо_ВводНачальныхДанныхНаработкаОбъектыРемонта.ОбъектРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_УчетНаработкиОборудования.НаработкаОбъектов КАК торо_УчетНаработкиОборудованияНаработкаОбъектов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
	|			ПО торо_УчетНаработкиОборудованияНаработкаОбъектов.Показатель = ПараметрыВыработкиОС.Ссылка
	|		ПО ВТ_Информация.ОбъектРемонта = торо_УчетНаработкиОборудованияНаработкаОбъектов.Объект
	|			И (торо_УчетНаработкиОборудованияНаработкаОбъектов.ДатаРаботыПо < ВТ_Информация.ДатаНачала)
	|ГДЕ
	|	торо_УчетНаработкиОборудованияНаработкаОбъектов.НовоеЗначение ЕСТЬ NULL
	|	И торо_ВводНачальныхДанныхНаработкаОбъектыРемонта.Значение ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Информация.ОбъектРемонта,
	|	ВТ_Информация.Модель,
	|	ВТ_Информация.VIN,
	|	ВТ_Информация.ГодВыпуска,
	|	ВТ_Информация.ИнвентарныйНомер,
	|	ВТ_Информация.ID,
	|	ВТ_Информация.ДатаНачала,
	|	ВТ_Информация.ДатаОкончания,
	|	ВТ_Информация.ВидРемонтныхРабот,
	|	ВТ_Информация.ДокументИсточник,
	|	ВТ_Информация.Инициатор,
	|	ВТ_Информация.КонтактноеЛицоИнициатора,
	|	ВТ_Информация.Описание,
	|	ВТ_Информация.Ссылка,
	|	ВТ_Информация.Гараж,
	|	ВТ_Информация.Госномер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(торо_ЗаявкаНаРемонтРемонтныеРаботы.Количество) КАК Количество,
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтнаяРабота КАК РемонтнаяРабота,
	|	торо_ИдентификаторыТехКарт.Наименование КАК Наименование,
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID КАК ID,
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.Ссылка КАК Ссылка,
	|	торо_ЗаявкаНаРемонтТрудовыеЗатраты.Квалификация КАК Исполнитель,
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	торо_ИдентификаторыТехКарт.Код КАК Код
	|ИЗ
	|	Документ.торо_ЗаявкаНаРемонт.РемонтныеРаботы КАК торо_ЗаявкаНаРемонтРемонтныеРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ИдентификаторыТехКарт КАК торо_ИдентификаторыТехКарт
	|		ПО торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтнаяРабота = торо_ИдентификаторыТехКарт.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.ТрудовыеЗатраты КАК торо_ЗаявкаНаРемонтТрудовыеЗатраты
	|		ПО торо_ЗаявкаНаРемонтРемонтныеРаботы.Ссылка = торо_ЗаявкаНаРемонтТрудовыеЗатраты.Ссылка
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|		ПО торо_ЗаявкаНаРемонтРемонтныеРаботы.Ссылка = торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка
	|			И торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID = торо_ЗаявкаНаРемонтРемонтыОборудования.ID
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|ГДЕ
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.Ссылка В(&МассивОбъектов)
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	И торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID В(&МассивID)
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_ИдентификаторыТехКарт.Наименование,
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтнаяРабота,
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID,
	|	торо_ЗаявкаНаРемонтРемонтныеРаботы.Ссылка,
	|	торо_ЗаявкаНаРемонтТрудовыеЗатраты.Квалификация,
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта,
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	торо_ИдентификаторыТехКарт.Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.Ссылка КАК Ссылка,
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID КАК ID,
	|	ПРЕДСТАВЛЕНИЕ(торо_ЗаявкаНаРемонтЗапчастиРемонта.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(торо_ЗаявкаНаРемонтЗапчастиРемонта.ХарактеристикаНоменклатуры) КАК Характеристика,
	|	СУММА(ЕСТЬNULL(торо_ЗаявкаНаРемонтЗапчастиРемонта.Количество, 0)) КАК Количество,
	|	ЕСТЬNULL(ТНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ТНоменклатура.Код КАК Код,
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	ЕСТЬNULL(СтруктураПредприятия.Склад, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Склад
	|ИЗ
	|	Документ.торо_ЗаявкаНаРемонт.ЗапчастиРемонта КАК торо_ЗаявкаНаРемонтЗапчастиРемонта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТНоменклатура
	|		ПО торо_ЗаявкаНаРемонтЗапчастиРемонта.Номенклатура = ТНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт КАК торо_ЗаявкаНаРемонт
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|			ПО торо_ЗаявкаНаРемонт.Подразделение = СтруктураПредприятия.Ссылка
	|		ПО торо_ЗаявкаНаРемонтЗапчастиРемонта.Ссылка = торо_ЗаявкаНаРемонт.Ссылка
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|		ПО торо_ЗаявкаНаРемонтЗапчастиРемонта.Ссылка = торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка
	|			И торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID = торо_ЗаявкаНаРемонтРемонтыОборудования.ID
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|ГДЕ
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.Ссылка В(&МассивОбъектов)
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	И торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID В(&МассивID)
	|
	|СГРУППИРОВАТЬ ПО 
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.Ссылка,
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID,
	|	ПРЕДСТАВЛЕНИЕ(торо_ЗаявкаНаРемонтЗапчастиРемонта.Номенклатура),
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(торо_ЗаявкаНаРемонтЗапчастиРемонта.ХарактеристикаНоменклатуры),
	|	ЕСТЬNULL(ТНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)),
	|	ТНоменклатура.Код,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта,
	|	ЕСТЬNULL(СтруктураПредприятия.Склад, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	торо_ЗаявкаНаРемонтЗапчастиРемонта.Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.Номенклатура КАК Номенклатура,
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.ХарактеристикаНоменклатуры КАК Характеристика,
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID КАК ID,
	|	СУММА(ЕСТЬNULL(торо_ЗаявкаНаРемонтМатериальныеЗатраты.Количество, 0)) КАК Количество,
	|	ЕСТЬNULL(ТНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.Ссылка КАК Ссылка,
	|	ТНоменклатура.Код КАК Код,
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023	
	|	ЕСТЬNULL(СтруктураПредприятия.Склад, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Склад
	|ИЗ
	|	Документ.торо_ЗаявкаНаРемонт.МатериальныеЗатраты КАК торо_ЗаявкаНаРемонтМатериальныеЗатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТНоменклатура
	|		ПО торо_ЗаявкаНаРемонтМатериальныеЗатраты.Номенклатура = ТНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт КАК торо_ЗаявкаНаРемонт
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|			ПО торо_ЗаявкаНаРемонт.Подразделение = СтруктураПредприятия.Ссылка
	|		ПО торо_ЗаявкаНаРемонтМатериальныеЗатраты.Ссылка = торо_ЗаявкаНаРемонт.Ссылка
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
	|		ПО торо_ЗаявкаНаРемонтМатериальныеЗатраты.Ссылка = торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка
	|			И торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID = торо_ЗаявкаНаРемонтРемонтыОборудования.ID
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|ГДЕ
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.Ссылка В(&МассивОбъектов)
	//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	|	И торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID В(&МассивID)
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.Номенклатура,
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.ХарактеристикаНоменклатуры,
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID,
	|	ЕСТЬNULL(ТНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)),
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.Ссылка,
	|	ТНоменклатура.Код,
	|	торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта,
	|	ЕСТЬNULL(СтруктураПредприятия.Склад, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	торо_ЗаявкаНаРемонтМатериальныеЗатраты.Номенклатура
	|АВТОУПОРЯДОЧИВАНИЕ";

	Если РемонтныйЛистСводно Тогда 
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID КАК ID,", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID,", "");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID КАК ID,", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID,", "");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID КАК ID,", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID,", "");
		
		ТекстЗапроса = ТекстЗапроса + ";" + Символы.ПС;
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	втИнформацияОбщая.ОбъектРемонта КАК ОбъектРемонта,
		|	втИнформацияОбщая.Модель КАК Модель,
		|	втИнформацияОбщая.VIN КАК VIN,
		|	втИнформацияОбщая.ГодВыпуска КАК ГодВыпуска,
		|	втИнформацияОбщая.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	МИНИМУМ(втИнформацияОбщая.ДатаНачала) КАК ДатаНачала,
		|	МАКСИМУМ(втИнформацияОбщая.ДатаОкончания) КАК ДатаОкончания,
		|	втИнформацияОбщая.ВидРемонтныхРабот КАК ВидРемонтныхРабот,
		|	втИнформацияОбщая.ДокументИсточник КАК ДокументИсточник,
		|	втИнформацияОбщая.Инициатор КАК Инициатор,
		|	втИнформацияОбщая.КонтактноеЛицоИнициатора КАК КонтактноеЛицоИнициатора,
		|	втИнформацияОбщая.Описание КАК Описание,
		|	втИнформацияОбщая.Ссылка КАК Ссылка,
		|	втИнформацияОбщая.Гараж КАК Гараж,
		|	втИнформацияОбщая.Госномер КАК Госномер,
		|	втИнформацияОбщая.Пробег КАК Пробег
		|ИЗ
		|	втИнформацияОбщая КАК втИнформацияОбщая
		|
		|СГРУППИРОВАТЬ ПО
		|	втИнформацияОбщая.ОбъектРемонта,
		|	втИнформацияОбщая.Модель,
		|	втИнформацияОбщая.VIN,
		|	втИнформацияОбщая.ГодВыпуска,
		|	втИнформацияОбщая.ИнвентарныйНомер,
		|	втИнформацияОбщая.ВидРемонтныхРабот,
		|	втИнформацияОбщая.ДокументИсточник,
		|	втИнформацияОбщая.Инициатор,
		|	втИнформацияОбщая.КонтактноеЛицоИнициатора,
		|	втИнформацияОбщая.Описание,
		|	втИнформацияОбщая.Ссылка,
		|	втИнформацияОбщая.Гараж,
		|	втИнформацияОбщая.Госномер,
		|	втИнформацияОбщая.Пробег
		|ИТОГИ
		|	МАКСИМУМ(Модель),
		|	МАКСИМУМ(VIN),
		|	МАКСИМУМ(ГодВыпуска),
		|	МАКСИМУМ(ИнвентарныйНомер),
		|	МИНИМУМ(ДатаНачала),
		|	МАКСИМУМ(ДатаОкончания),
		|	МАКСИМУМ(Госномер),
		|	МАКСИМУМ(Пробег)
		|ПО
		|	Ссылка,
		|	ОбъектРемонта";
		
	Иначе
		
		УбратьУсловиеНаМассивID(ТекстЗапроса);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ втИнформацияОбщая", "");
		
	КонецЕсли;
	//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьВсеID(ВыборкаВсеID)
	
	МассивID = Новый Массив;
	
	Пока ВыборкаВсеID.Следующий() Цикл
		МассивID.Добавить(ВыборкаВсеID.ID);		
	КонецЦикла; 
	
	ВыборкаВсеID.Сбросить();
	
	Возврат МассивID;
	
КонецФункции

Процедура Информация(ВыборкаИнформация, Отбор, ТабДок, Макет)
	
	ОбластьИнформация = Макет.ПолучитьОбласть("Информация"); 
	
	Если ВыборкаИнформация.НайтиСледующий(Отбор) Тогда
		
		Если ЗначениеЗаполнено(ВыборкаИнформация.КонтактноеЛицоИнициатора) Тогда
			Стр = "%1, %2";
		Иначе
			Стр = "%1%2";					
		КонецЕсли;
		
		ОбластьИнформация.Параметры.Заказчик = СтрШаблон(Стр, ВыборкаИнформация.Инициатор,
			ВыборкаИнформация.КонтактноеЛицоИнициатора);
		ЗаполнитьЗначенияСвойств(ОбластьИнформация.Параметры, ВыборкаИнформация);
		
	КонецЕсли;
	ВыборкаИнформация.Сбросить();
	
	ТабДок.Вывести(ОбластьИнформация);
	
КонецПроцедуры 

Процедура ЗаявкиНаРаботы(ВыборкаЗаявкаНаРаботы, Отбор, ТабДок, Макет)
	
	ОбластьШапкаЗаявкиНаРаботы = Макет.ПолучитьОбласть("ШапкаЗаявкиНаРаботы");
	ТабДок.Вывести(ОбластьШапкаЗаявкиНаРаботы);
	
	ОбластьСтрокаЗаявкиНаРаботы = Макет.ПолучитьОбласть("СтрокаЗаявкиНаРаботы");
	
	НомерСтр = 1;
	Пока ВыборкаЗаявкаНаРаботы.НайтиСледующий(Отбор) Цикл
		
		ОбластьСтрокаЗаявкиНаРаботы.Параметры.НомерСтр = НомерСтр;
		
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаЗаявкиНаРаботы.Параметры, ВыборкаЗаявкаНаРаботы);	
		
		ТабДок.Вывести(ОбластьСтрокаЗаявкиНаРаботы);
		
		НомерСтр = НомерСтр + 1;
		
	КонецЦикла;
	ВыборкаЗаявкаНаРаботы.Сбросить();
	
КонецПроцедуры    

Процедура ЗапасныеЧасти(ВыборкаЗапасныеЧасти, Отбор, ТабДок, Макет)
	
	ОбластьШапкаПереченьЗапасныхЧастейПоЗаказу = Макет.ПолучитьОбласть("ШапкаПереченьЗапасныхЧастейПоЗаказу");
	ТабДок.Вывести(ОбластьШапкаПереченьЗапасныхЧастейПоЗаказу);
	
	ОбластьСтрокаПереченьЗапасныхЧастейПоЗаказу = Макет.ПолучитьОбласть("СтрокаПереченьЗапасныхЧастейПоЗаказу");
	
	НомерСтр = 1;
	Пока ВыборкаЗапасныеЧасти.НайтиСледующий(Отбор) Цикл
		
		ОбластьСтрокаПереченьЗапасныхЧастейПоЗаказу.Параметры.НомерСтр = НомерСтр;
		
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаПереченьЗапасныхЧастейПоЗаказу.Параметры, ВыборкаЗапасныеЧасти);
		
		ОбластьСтрокаПереченьЗапасныхЧастейПоЗаказу.Параметры.НоменклатураИХарактеристика = СтрШаблон("%1, %2",
																			 ВыборкаЗапасныеЧасти.Номенклатура,
																			 ВыборкаЗапасныеЧасти.Характеристика);
		
		ТабДок.Вывести(ОбластьСтрокаПереченьЗапасныхЧастейПоЗаказу);
		
		НомерСтр = НомерСтр + 1;
		
	КонецЦикла;
	ВыборкаЗапасныеЧасти.Сбросить();
	
КонецПроцедуры 

Процедура РасходныеМатериалы(ВыборкаРасходныеМатериалы, Отбор, ТабДок, Макет)
	
	ОбластьШапкаПереченьЗапасныхЧастейКЗаказу = Макет.ПолучитьОбласть("ШапкаПереченьЗапасныхЧастейКЗаказу");
	ТабДок.Вывести(ОбластьШапкаПереченьЗапасныхЧастейКЗаказу);
	
	ОбластьСтрокаПереченьЗапасныхЧастейКЗаказу = Макет.ПолучитьОбласть("СтрокаПереченьЗапасныхЧастейКЗаказу");
	
	НомерСтр = 1;
	Пока ВыборкаРасходныеМатериалы.НайтиСледующий(Отбор) Цикл 
		
		ОбластьСтрокаПереченьЗапасныхЧастейКЗаказу.Параметры.НомерСтр = НомерСтр;
		
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаПереченьЗапасныхЧастейКЗаказу.Параметры, ВыборкаРасходныеМатериалы);
		ОбластьСтрокаПереченьЗапасныхЧастейКЗаказу.Параметры.НоменклатураИХарактеристика = СтрШаблон("%1, %2",
																		ВыборкаРасходныеМатериалы.Номенклатура, 
																		ВыборкаРасходныеМатериалы.Характеристика); 
		
		ТабДок.Вывести(ОбластьСтрокаПереченьЗапасныхЧастейКЗаказу);
		
		НомерСтр = НомерСтр + 1;
		
	КонецЦикла;
	ВыборкаРасходныеМатериалы.Сбросить();
	
КонецПроцедуры

//++ Проф-ИТ, #323, Соловьев А.А., 03.11.2023
Процедура ИнформацияСводно(ВыборкаДокумент, Отбор, ТабДок, Макет)
	
	ОбластьИнформация = Макет.ПолучитьОбласть("Информация"); 
	
	Пока ВыборкаДокумент.Следующий() Цикл
		
		Если ВыборкаДокумент.Ссылка <> Отбор.Ссылка Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаОбъектРемонта = ВыборкаДокумент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОбъектРемонта.Следующий() Цикл
			
			Если ВыборкаОбъектРемонта.ОбъектРемонта <> Отбор.ОбъектРемонта Тогда
				Продолжить;
			КонецЕсли;
			
			Выборка = ВыборкаОбъектРемонта.Выбрать();
			
			ВидРемонтныхРабот = "";
			Заказчик = "";
			
			Пока Выборка.Следующий() Цикл 
				
				ВидРемонтныхРабот = ВидРемонтныхРабот + Выборка.ВидРемонтныхРабот + ", ";
				
				Если ЗначениеЗаполнено(Выборка.КонтактноеЛицоИнициатора) Тогда
					Стр = "%1, %2";
				Иначе
					Стр = "%1%2";
				КонецЕсли;
				
				Заказчик = Заказчик + СтрШаблон(Стр, Выборка.Инициатор,
					Выборка.КонтактноеЛицоИнициатора);
				
				Если Не ПустаяСтрока(Заказчик) Тогда 
					Заказчик = Заказчик + ", ";
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ОбластьИнформация.Параметры, Выборка);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ОбластьИнформация.Параметры.ДатаНачала = Формат(ВыборкаДокумент.ДатаНачала, "ДЛФ=Д");
		ОбластьИнформация.Параметры.ДатаОкончания = Формат(ВыборкаДокумент.ДатаОкончания, "ДЛФ=Д");
		
	КонецЦикла;
	
	ВыборкаДокумент.Сбросить();
	
	ОбластьИнформация.Параметры.Заказчик = Лев(Заказчик, СтрДлина(Заказчик) - 2);
	ОбластьИнформация.Параметры.ВидРемонтныхРабот = Лев(ВидРемонтныхРабот, СтрДлина(ВидРемонтныхРабот) - 2);
	
	ТабДок.Вывести(ОбластьИнформация);
	
КонецПроцедуры

Процедура УбратьУсловиеНаМассивID(ТекстЗапроса)
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И торо_ЗаявкаНаРемонтРемонтыОборудования.ID В(&МассивID)", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И торо_ЗаявкаНаРемонтРемонтныеРаботы.РемонтыОборудования_ID В(&МассивID)", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И торо_ЗаявкаНаРемонтМатериальныеЗатраты.РемонтыОборудования_ID В(&МассивID)", "");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И торо_ЗаявкаНаРемонтЗапчастиРемонта.РемонтыОборудования_ID В(&МассивID)", "");
	
КонецПроцедуры
//-- Проф-ИТ, #323, Соловьев А.А., 03.11.2023

#КонецОбласти

//-- Проф-ИТ, #131, Башинская А., 13.10.2023