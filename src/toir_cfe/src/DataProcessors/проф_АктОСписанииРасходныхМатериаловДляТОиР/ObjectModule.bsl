
//++ Проф-ИТ, #132, Башинская А., 13.10.2023

#Область ПрограммныйИнтерфейс

Функция ПечатьАктОСписании(МассивОбъектов, ПараметрыПечати) Экспорт 
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("проф_АктОСписании");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов); 
	Запрос.Текст = ТекстЗапросаПечатьАктОСписании();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры,ПолучитьПарамерыШапки(ВыборкаСсылка));
		ТабДок.Вывести(ОбластьШапка);
		
		нпп = 0;
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗаполнитьДанныеСтроки(ОбластьСтрока, ВыборкаДетальныеЗаписи, нпп);
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(ОбластьИтого.Параметры, ВыборкаСсылка);
		ТабДок.Вывести(ОбластьИтого);
		
		ТабДок.Вывести(ОбластьПодвал);
		
	КонецЦикла;
	
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	Возврат ТабДок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПечатьАктОСписании()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
       |	ВнутреннееПотреблениеТоваров.Ссылка КАК Ссылка,
       |	ВнутреннееПотреблениеТоваров.Номер КАК Номер,
       |	ВнутреннееПотреблениеТоваров.Дата КАК Дата,
       |	ВнутреннееПотреблениеТоваров.проф_ДокументОснование КАК проф_ДокументОснование
       |ПОМЕСТИТЬ ВТ_Документы
       |ИЗ
       |	Документ.ВнутреннееПотреблениеТоваров КАК ВнутреннееПотреблениеТоваров
       |ГДЕ
       |	ВнутреннееПотреблениеТоваров.Ссылка В(&МассивОбъектов)
       |;
       |
       |////////////////////////////////////////////////////////////////////////////////
       |ВЫБРАТЬ ПЕРВЫЕ 1
       |	ВТ_Документы.Ссылка КАК Ссылка,
       |	ЕСТЬNULL(торо_ЗаявкаНаРемонтРемонтыОборудования.ОбъектРемонта, ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)) КАК ОбъектРемонта
       |ПОМЕСТИТЬ ВТ_ОбъектыРемонта
       |ИЗ
       |	ВТ_Документы КАК ВТ_Документы
       |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_ЗаявкаНаРемонт.РемонтыОборудования КАК торо_ЗаявкаНаРемонтРемонтыОборудования
       |		ПО ВТ_Документы.проф_ДокументОснование = торо_ЗаявкаНаРемонтРемонтыОборудования.Ссылка
       |;
       |
       |////////////////////////////////////////////////////////////////////////////////
       |ВЫБРАТЬ РАЗРЕШЕННЫЕ
       |	ВТ_Документы.Ссылка КАК Ссылка,
       |	ВТ_Документы.Номер КАК Номер,
       |	ВТ_Документы.Дата КАК Дата,
       |	ЕСТЬNULL(Товары.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
       |	ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(Товары.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))) КАК НоменклатураПредставление,
       |	ЕСТЬNULL(Товары.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
       |	ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(Товары.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))) КАК ХарактеристикаПредставление,
       |	ВЫБОР
       |		КОГДА ЕСТЬNULL(Товары.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
       |			ТОГДА ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(спрНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)))
       |		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(Товары.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)))
       |	КОНЕЦ КАК ЕдиницаИзмерения,
       |	ЕСТЬNULL(Товары.Количество, 0) КАК Количество,
       |	ЕСТЬNULL(проф_СебестоимостьТоваров.Себестоимость, 0) КАК Сумма,
       |	ВЫБОР
       |		КОГДА ЕСТЬNULL(проф_СебестоимостьТоваров.Количество, 0) = 0
       |			ТОГДА 0
       |		ИНАЧЕ ВЫРАЗИТЬ(проф_СебестоимостьТоваров.Себестоимость / проф_СебестоимостьТоваров.Количество КАК ЧИСЛО(15, 2))
       |	КОНЕЦ КАК Цена,
       |	ЕСТЬNULL(ПРЕДСТАВЛЕНИЕ(ВТ_ОбъектыРемонта.ОбъектРемонта), """") КАК Объект
       |ИЗ
       |	ВТ_Документы КАК ВТ_Документы
       |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Товары КАК Товары
       |		ПО ВТ_Документы.Ссылка = Товары.Ссылка
       |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.проф_СебестоимостьТоваров КАК проф_СебестоимостьТоваров
       |		ПО ВТ_Документы.Ссылка = проф_СебестоимостьТоваров.Документ
       |			И (Товары.Номенклатура = проф_СебестоимостьТоваров.Номенклатура)
       |			И (Товары.Характеристика = проф_СебестоимостьТоваров.Характеристика)
       |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
       |		ПО (Товары.Номенклатура = спрНоменклатура.Ссылка)
       |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбъектыРемонта КАК ВТ_ОбъектыРемонта
       |		ПО ВТ_Документы.Ссылка = ВТ_ОбъектыРемонта.Ссылка
       |ИТОГИ
       |	МАКСИМУМ(Номер),
       |	МАКСИМУМ(Дата),
       |	СУММА(Сумма)
       |ПО
       |	Ссылка";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьПарамерыШапки(ВыборкаСсылка)
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаДок", Формат(ВыборкаСсылка.Дата, "ДЛФ=D"));
	
	МесяцВПП = ПолучитьСклоненияСтроки(Формат(ВыборкаСсылка.Дата, "ДФ=ММММ"), "ПЛ=Мужской", "ПД=Предложный");
	
	СтрВПериоде = СтрШаблон("в %1 %2г.",
								НРег(МесяцВПП[0]),
								Формат(ВыборкаСсылка.Дата, "ДФ=гггг"));
	Результат.Вставить("ВПериоде", СтрВПериоде);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьДанныеСтроки(ОбластьСтрока, ВыборкаДетальныеЗаписи, нпп)
	
	ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, ВыборкаДетальныеЗаписи);
	нпп = нпп + 1;
	ОбластьСтрока.Параметры.нпп = нпп;
	
	Материал = ВыборкаДетальныеЗаписи.НоменклатураПредставление;
	Если ВыборкаДетальныеЗаписи.ХарактеристикаПредставление <> "" Тогда
		Материал = Материал + ", " + ВыборкаДетальныеЗаписи.ХарактеристикаПредставление; 
	КонецЕсли;
	
	ОбластьСтрока.Параметры.Материал = Материал;
	
КонецПроцедуры

#КонецОбласти

//-- Проф-ИТ, #132, Башинская А., 13.10.2023