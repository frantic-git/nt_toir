
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура проф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	//++ Проф-ИТ, #178, Соловьев А.А., 06.09.2023
	ВывестиНовыеРеквизиты();
	ЭтотОбъект["ПодразделенияСлужбыТОИР"] = Справочники.проф_НастройкиСистемы.ПолучитьСпЗначНастройкиСистемы("Подразделения", "ПодразделенияСлужбыТОИР");
	
	ЭтотОбъект["Дата"] = ЭтотОбъект.Параметры.Дата;
	Если Не ЗначениеЗаполнено(ЭтотОбъект["Дата"]) Тогда 
		ЭтотОбъект["Дата"] = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ВывестиСписокНоменклатуры();
	ВывестиСписокАналогов();
	ВывестиОстаткиТоваров();
	ВывестиОстаткиПоНазначению();

	УстановитьУсловноеОформление();	
	//-- Проф-ИТ, #178, Соловьев А.А., 06.09.2023
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатура

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 12.09.2023
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатураАналогХарактеристикаАналога.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатураАналог.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<характеристики не используются>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатураАналогСерия.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатураАналог.СерииИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.НейтральноСерый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<серии не используются>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного",Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Доступность",Ложь);
	//-- Проф-ИТ, #178, Соловьев А.А., 12.09.2023
	
КонецПроцедуры	

&НаКлиенте
Процедура проф_СписокНоменклатураПриАктивизацииЯчейки(Элемент)
	
	//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	текДанные = Элемент.ТекущиеДанные;
	
	Если текДанные <> Неопределено Тогда
		
		Если ТекДанные.Свойство("Серия") Тогда 
			ЗаполнитьОстаткиТоваров(Объект.Склад, текДанные.Номенклатура, текДанные.Характеристика, текДанные.Серия);
		Иначе
			ЗаполнитьОстаткиТоваров(Объект.Склад, текДанные.Номенклатура, текДанные.Характеристика);
		КонецЕсли;
		
		ЭтотОбъект["СписокНоменклатураАналог"].Отбор.Элементы.Очистить();
		
		ЭлементОтбора = ЭтотОбъект["СписокНоменклатураАналог"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НоменклатураОсновная");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = текДанные.Номенклатура;
		
		ЭлементОтбора = ЭтотОбъект["СписокНоменклатураАналог"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ХарактеристикаОсновная");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = текДанные.Характеристика;
		
		ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Очистить();
		
		ЭлементОтбора = ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = текДанные.Номенклатура;
		
		ЭлементОтбора = ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Характеристика");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = текДанные.Характеристика;
		
		ЭлементОтбора = ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Доступно");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, "СписокНоменклатура", "");
		
	Иначе
		ЗаполнитьОстаткиТоваров(Объект.Склад, Неопределено, Неопределено, Неопределено);
	КонецЕсли;
	//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	
КонецПроцедуры	

&НаКлиенте
Процедура СписокНоменклатураАналогВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	//++ Проф-ИТ, #178, Соловьев А.А., 12.09.2023
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Если Не ЗапрашиватьКоличество Тогда
			СписокНоменклатураАналогВыборЗавершение(1, Новый Структура("ТекДанные", ТекДанные));
		Иначе
			ВыбЗнач = 0;
			ПоказатьВводЗначения(Новый ОписаниеОповещения("СписокНоменклатураАналогВыборЗавершение", 
				ЭтотОбъект, Новый Структура("ТекДанные", ТекДанные)), ВыбЗнач, НСтр("ru = 'Введите количество'"), 
				Новый ОписаниеТипов("Число",,,новый КвалификаторыЧисла(12,3)));
		КонецЕсли;		
	КонецЕсли;
	//-- Проф-ИТ, #178, Соловьев А.А., 12.09.2023
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатураАналогВыборЗавершение(Значение, ДополнительныеПараметры) Экспорт
	//++ Проф-ИТ, #178, Соловьев А.А., 12.09.2023
	ТекДанные = ДополнительныеПараметры.ТекДанные;
	
	Если Значение = Неопределено Или Значение = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Номенклатура, Характеристика", ТекДанные.Аналог, ТекДанные.ХарактеристикаАналога);
	
	НайденныеСтроки = Объект.Корзина.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() = 0 Тогда
		НоваяСтрока = Объект.Корзина.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекДанные);
		НоваяСтрока.КоличествоУпаковок = Значение;
		НоваяСтрока.Склад = Объект.Склад;
		НоваяСтрока.Номенклатура = ТекДанные.Аналог;
		НоваяСтрока.Характеристика = ТекДанные.ХарактеристикаАналога;
	Иначе
		текСтрока = НайденныеСтроки[0];
		текСтрока.КоличествоУпаковок = текСтрока.КоличествоУпаковок + Значение;
	КонецЕсли;
	//-- Проф-ИТ, #178, Соловьев А.А., 12.09.2023
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатураАналогПриАктивизацииЯчейки(Элемент)
	
	//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	Если Элемент.ТекущийЭлемент.Имя <> "СписокНоменклатураАналогРезервНеТОиР" 
		И Элемент.ТекущийЭлемент.Имя <> "СписокНоменклатураАналогДоступно"
		И Элемент.ТекущийЭлемент.Имя <> "СписокНоменклатураАналогЗаказИнициатора"
		И Элемент.ТекущийЭлемент.Имя <> "СписокНоменклатураАналогПоступит" Тогда  
		Возврат;
	КонецЕсли;	
	
	текДанные = Элемент.ТекущиеДанные;
	
	Если текДанные <> Неопределено Тогда
		
		Если ТекДанные.Свойство("Серия") Тогда 
			ЗаполнитьОстаткиТоваров(Объект.Склад, текДанные.Аналог, текДанные.ХарактеристикаАналога, текДанные.Серия);
		Иначе
			ЗаполнитьОстаткиТоваров(Объект.Склад, текДанные.Аналог, текДанные.ХарактеристикаАналога);
		КонецЕсли;
		
		ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Очистить();
		
		ЭлементОтбора = ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = текДанные.Аналог;
		
		ЭлементОтбора = ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Характеристика");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = текДанные.ХарактеристикаАналога;
		
		ЭлементОтбора = ЭтотОбъект["ОстаткиПоНазначению"].Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Доступно");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, "СписокНоменклатураАналог", "");
		
	КонецЕсли;
	//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура проф_СкладПриИзмененииПосле(Элемент)
	//++ Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	ЭтотОбъект["СписокНоменклатура"].Параметры.УстановитьЗначениеПараметра("Склад", Объект.Склад);
	ЭтотОбъект["СписокНоменклатура"].Параметры.УстановитьЗначениеПараметра("ИспользоватьОтборПоСкладу", ЗначениеЗаполнено(Объект.Склад));
	
	ЭтотОбъект["СписокНоменклатураАналог"].Параметры.УстановитьЗначениеПараметра("Склад", Объект.Склад);
	ЭтотОбъект["СписокНоменклатураАналог"].Параметры.УстановитьЗначениеПараметра("ИспользоватьОтборПоСкладу", ЗначениеЗаполнено(Объект.Склад));
	
	ЭтотОбъект["ОстаткиПоНазначению"].Параметры.УстановитьЗначениеПараметра("Склад", Объект.Склад);
	ЭтотОбъект["ОстаткиПоНазначению"].Параметры.УстановитьЗначениеПараметра("ИспользоватьОтборПоСкладу", ЗначениеЗаполнено(Объект.Склад));
	//-- Проф-ИТ, #178, Соловьев А.А., 11.09.2023
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
&ИзменениеИКонтроль("ЗаполнитьОстаткиТоваров")
Процедура проф_ЗаполнитьОстаткиТоваров(Склад, Номенклатура, Характеристика, Серия)

	ОстаткиТоваров.Очистить();
	#Удаление
	МассивОстатков = ПолучитьОстаткиТоваров(Склад, Номенклатура, Характеристика, Серия);
	#КонецУдаления
	#Вставка
	//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	МассивОстатков = проф_ПолучитьОстаткиТоваров(ЭтотОбъект["Дата"], Склад, Номенклатура, Характеристика, Серия, ЭтотОбъект["ПодразделенияСлужбыТОИР"]);
	//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	#КонецВставки

	Если МассивОстатков.Количество() = 0 тогда
		нс = ЭтаФорма.ОстаткиТоваров.Добавить();
		нс.Склад = Объект.Склад;
	Иначе

		Для каждого СтрокаОстатка из МассивОстатков Цикл
			нс = ЭтаФорма.ОстаткиТоваров.Добавить();
			нс.Склад = СтрокаОстатка.Склад;
			#Удаление
			нс.Доступно = СтрокаОстатка.ВНаличииОстаток;
			#КонецУдаления
			#Вставка
			//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
			ЗаполнитьЗначенияСвойств(нс, СтрокаОстатка);
			//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023
			#КонецВставки
		КонецЦикла;

	КонецЕсли;

	ВсегоДоступно = ОстаткиТоваров.Итог("Доступно");

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("УстановитьОтборВСпискеНоменклатуры")
Процедура проф_УстановитьОтборВСпискеНоменклатуры()

	Если Элементы.ИерархияНоменклатуры.ТекущаяСтрока = Неопределено ИЛИ ИспользоватьФильтры = Ложь Тогда
		ОтборПоИерархииНоменклатуры = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	Иначе
		ОтборПоИерархииНоменклатуры = Элементы.ИерархияНоменклатуры.ТекущаяСтрока;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ОтборПоИерархииНоменклатуры) Тогда

		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокНоменклатура, "Родитель", ОтборПоИерархииНоменклатуры, 
		ВидСравненияКомпоновкиДанных.ВИерархии, "Родитель", Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);

	Иначе

		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокНоменклатура, "Родитель", ОтборПоИерархииНоменклатуры, 
		ВидСравненияКомпоновкиДанных.ВИерархии, "Родитель", Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);

	КонецЕсли;

	Попытка
		Элементы.СписокНоменклатура.ТекущаяСтрока = 1;
	Исключение
	КонецПопытки;
	#Удаление
	СписокНоменклатураПриАктивизацииСтроки(Элементы.СписокНоменклатура);
	#КонецУдаления
	
КонецПроцедуры

&НаСервере
Процедура ВывестиНовыеРеквизиты()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	ДобавляемыеРеквизиты = Новый Массив;
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("РезервНеТОиР", ТипЧисло, "ОстаткиТоваров"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЗаказИнициатора", ТипЧисло, "ОстаткиТоваров"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Поступит", ТипЧисло, "ОстаткиТоваров"));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Дата", 
		Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя))));
	
	ТипыРеквизита = Новый Массив;                             
	ТипыРеквизита.Добавить(Тип("ДинамическийСписок"));
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита); 
	ДинамическийСписок = Новый РеквизитФормы("СписокНоменклатураАналог", ОписаниеТиповДляРеквизита);
	ДобавляемыеРеквизиты.Добавить(ДинамическийСписок);
	
	ТипыРеквизита = Новый Массив;                             
	ТипыРеквизита.Добавить(Тип("ДинамическийСписок"));
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита); 
	ДинамическийСписок = Новый РеквизитФормы("ОстаткиПоНазначению", ОписаниеТиповДляРеквизита);
	ДобавляемыеРеквизиты.Добавить(ДинамическийСписок);
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПодразделенияСлужбыТОИР", Новый ОписаниеТипов("СписокЗначений")));
	
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	
КонецПроцедуры	

&НаСервере
Процедура ВывестиСписокНоменклатуры()

	//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	ТекстЗапросаСписокНоменклатура = ВернутьТекстЗапросаСписокНоменклатура();
	СтруктураСвойствСписокНоменклатура = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СтруктураСвойствСписокНоменклатура.ТекстЗапроса = ТекстЗапросаСписокНоменклатура;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокНоменклатура, СтруктураСвойствСписокНоменклатура);
	СписокНоменклатура.Параметры.УстановитьЗначениеПараметра("ПодразделенияСлужбыТОИР", ЭтотОбъект["ПодразделенияСлужбыТОИР"]);
	
	СтруктураКолонок = Новый Структура;
	СтруктураКолонок.Вставить("Доступно", 		Истина);
	СтруктураКолонок.Вставить("РезервНеТОиР", 	Истина);
	СтруктураКолонок.Вставить("ЗаказИнициатора",Истина);
	СтруктураКолонок.Вставить("Поступит", 		Истина);
	
	ВывестиКолонкиДинамическогоСписка("СписокНоменклатура", СтруктураКолонок, Элементы.СписокНоменклатура);
	Элементы.СписокНоменклатураРезервНеТОиР.Заголовок = НСтр("ru = 'Резерв не ТОиР'");
	
	Элементы.СписокНоменклатура.УстановитьДействие("ПриАктивизацииЯчейки", "проф_СписокНоменклатураПриАктивизацииЯчейки");
	//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023

КонецПроцедуры

&НаСервере
Функция ВернутьТекстЗапросаСписокНоменклатура()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 06.09.2023
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникНоменклатура.Артикул КАК Артикул,
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	СправочникНоменклатура.Родитель КАК Родитель,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СправочникНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	СправочникВидыНоменклатуры.ИспользоватьСерии КАК СерииИспользуются,
	|	СправочникНоменклатура.ИспользованиеХарактеристик КАК ИспользованиеХарактеристик,
	|	СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры) КАК ХарактеристикиИспользуются,
	|	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
	|	СправочникВидыНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар) КАК ЭтоТовар,
	|	СправочникВидыНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга) КАК ЭтоУслуга,
	|	СправочникВидыНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ХарактеристикиНоменклатурыИнд.Ссылка, ЕСТЬNULL(ХарактеристикиНоменклатурыОбщ.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))) КАК Характеристика
	|ПОМЕСТИТЬ НоменклатураСХарактеристиками
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
	|		ПО СправочникНоменклатура.ВидНоменклатуры = СправочникВидыНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатурыИнд
	|		ПО (&ФОИспользоватьХарактеристикиНоменклатуры)
	|			И (СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			И СправочникНоменклатура.Ссылка = ХарактеристикиНоменклатурыИнд.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатурыОбщ
	|		ПО (&ФОИспользоватьХарактеристикиНоменклатуры)
	|			И (СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры))
	|			И СправочникНоменклатура.ВидНоменклатуры = ХарактеристикиНоменклатурыОбщ.Владелец
	|ГДЕ
	|	СправочникНоменклатура.ЭтоГруппа = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	ВидНоменклатуры,
	|	СправочникНоменклатура.ИспользованиеХарактеристик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика КАК Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Склад КАК Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение,
	|	проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток КАК ВНаличии
	|ПОМЕСТИТЬ втЗапасыИПотребностиВНаличии
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|			&ИспользоватьОтборПоСкладу
	|				И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура КАК Номенклатура,
	|	втЗапасыИПотребности.Характеристика КАК Характеристика,
	|	втЗапасыИПотребности.Назначение КАК Назначение,
	|	втЗапасыИПотребности.Склад КАК Склад,
	|	втЗапасыИПотребности.ВНаличии КАК Доступно,
	|	0 КАК РезервНеТОиР
	|ПОМЕСТИТЬ втДоступноРезервНеТОиР
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО втЗапасыИПотребности.Назначение = проф_Назначения.Ссылка
	|			И (проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение,
	|	втЗапасыИПотребности.Склад,
	|	втЗапасыИПотребности.ВНаличии,
	|	0
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребности
	|ГДЕ
	|	втЗапасыИПотребности.Назначение = ЗНАЧЕНИЕ(Справочник.проф_Назначения.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение,
	|	втЗапасыИПотребности.Склад,
	|	0,
	|	втЗапасыИПотребности.ВНаличии
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО втЗапасыИПотребности.Назначение = проф_Назначения.Ссылка
	|			И (НЕ проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыИПотребностиПредварительно.Номенклатура КАК Номенклатура,
	|	ЗапасыИПотребностиПредварительно.Характеристика КАК Характеристика,
	|	СУММА(ЗапасыИПотребностиПредварительно.КОбеспечениюОстаток) - СУММА(ЗапасыИПотребностиПредварительно.ВНаличииОстаток) КАК КОбеспечениюОстаток,
	|	СУММА(ЗапасыИПотребностиПредварительно.ПоступитОстаток) КАК ПоступитОстаток,
	|	СУММА(ЗапасыИПотребностиПредварительно.РезервНеТОиР) КАК РезервНеТОиР,
	|	СУММА(ЗапасыИПотребностиПредварительно.Доступно) КАК Доступно
	|ПОМЕСТИТЬ ЗапасыИПотребности
	|ИЗ
	|	(ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Характеристика,
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток КАК КОбеспечениюОстаток,
	|		проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток КАК ВНаличииОстаток,
	|		0 КАК ПоступитОстаток,
	|		0 КАК Доступно,
	|		0 КАК РезервНеТОиР,
	|		проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|	ГДЕ
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.ПоступитОстаток,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток,
	|		проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток,
	|		0,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|				&Дата,
	|				&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|	ГДЕ
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.ПоступитОстаток,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|				&Дата,
	|				&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втДоступноРезервНеТОиР.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА втДоступноРезервНеТОиР.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		0,
	|		0,
	|		0,
	|		втДоступноРезервНеТОиР.Доступно,
	|		втДоступноРезервНеТОиР.РезервНеТОиР,
	|		втДоступноРезервНеТОиР.Назначение
	|	ИЗ
	|		втДоступноРезервНеТОиР КАК втДоступноРезервНеТОиР) КАК ЗапасыИПотребностиПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыИПотребностиПредварительно.Характеристика,
	|	ЗапасыИПотребностиПредварительно.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСХарактеристиками.Артикул КАК Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура КАК Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель КАК Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика КАК Характеристика,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры КАК ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются КАК СерииИспользуются,
	|	НоменклатураСХарактеристиками.ПометкаУдаления КАК ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ЭтоТовар КАК ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга КАК ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.РезервНеТОиР, 0)) КАК РезервНеТОиР,
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.Доступно, 0)) КАК Доступно,
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.КОбеспечениюОстаток, 0)) КАК ЗаказИнициатора,
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.ПоступитОстаток, 0)) КАК Поступит
	|ИЗ
	|	НоменклатураСХарактеристиками КАК НоменклатураСХарактеристиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИПотребности КАК ЗапасыИПотребности
	|		ПО НоменклатураСХарактеристиками.Номенклатура = ЗапасыИПотребности.Номенклатура
	|			И НоменклатураСХарактеристиками.Характеристика = ЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ИСТИНА
	|	И &ИспользоватьОтборПоСкладу = ИСТИНА
	|	И ЕСТЬNULL(ЗапасыИПотребности.РезервНеТОиР, 0) + ЕСТЬNULL(ЗапасыИПотребности.Доступно, 0) + ЕСТЬNULL(ЗапасыИПотребности.КОбеспечениюОстаток, 0) + ЕСТЬNULL(ЗапасыИПотребности.ПоступитОстаток, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.РезервНеТОиР, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.Доступно, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.КОбеспечениюОстаток, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.ПоступитОстаток, 0))
	|ИЗ
	|	НоменклатураСХарактеристиками КАК НоменклатураСХарактеристиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИПотребности КАК ЗапасыИПотребности
	|		ПО НоменклатураСХарактеристиками.Номенклатура = ЗапасыИПотребности.Номенклатура
	|			И НоменклатураСХарактеристиками.Характеристика = ЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ИСТИНА
	|	И &ИспользоватьОтборПоСкладу = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.РезервНеТОиР, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.Доступно, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.КОбеспечениюОстаток, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.ПоступитОстаток, 0))
	|ИЗ
	|	НоменклатураСХарактеристиками КАК НоменклатураСХарактеристиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИПотребности КАК ЗапасыИПотребности
	|		ПО НоменклатураСХарактеристиками.Номенклатура = ЗапасыИПотребности.Номенклатура
	|			И НоменклатураСХарактеристиками.Характеристика = ЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ЛОЖЬ
	|	И &ИспользоватьОтборПоСкладу = ИСТИНА
	|	И ЕСТЬNULL(ЗапасыИПотребности.РезервНеТОиР, 0) + ЕСТЬNULL(ЗапасыИПотребности.Доступно, 0) + ЕСТЬNULL(ЗапасыИПотребности.КОбеспечениюОстаток, 0) + ЕСТЬNULL(ЗапасыИПотребности.ПоступитОстаток, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.РезервНеТОиР, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.Доступно, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.КОбеспечениюОстаток, 0)),
	|	СУММА(ЕСТЬNULL(ЗапасыИПотребности.ПоступитОстаток, 0))
	|ИЗ
	|	НоменклатураСХарактеристиками КАК НоменклатураСХарактеристиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИПотребности КАК ЗапасыИПотребности
	|		ПО НоменклатураСХарактеристиками.Номенклатура = ЗапасыИПотребности.Номенклатура
	|			И НоменклатураСХарактеристиками.Характеристика = ЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ЛОЖЬ
	|	И &ИспользоватьОтборПоСкладу = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСХарактеристиками.Артикул,
	|	НоменклатураСХарактеристиками.Номенклатура,
	|	НоменклатураСХарактеристиками.Родитель,
	|	НоменклатураСХарактеристиками.ЕдиницаИзмерения,
	|	НоменклатураСХарактеристиками.ХарактеристикиИспользуются,
	|	НоменклатураСХарактеристиками.Характеристика,
	|	НоменклатураСХарактеристиками.ПометкаУдаления,
	|	НоменклатураСХарактеристиками.ВидНоменклатуры,
	|	НоменклатураСХарактеристиками.СерииИспользуются,
	|	НоменклатураСХарактеристиками.ЭтоТовар,
	|	НоменклатураСХарактеристиками.ЭтоУслуга,
	|	НоменклатураСХарактеристиками.ТипНоменклатуры";
	//-- Проф-ИТ, #178, Соловьев А.А., 06.09.2023
	
КонецФункции

&НаСервереБезКонтекста
Функция проф_ПолучитьОстаткиТоваров(Дата, Склад, Номенклатура, Характеристика, Серия, ПодразделенияСлужбыТОИР)

	//++ Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика КАК Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Склад КАК Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение,
	|	проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток КАК ВНаличии
	|ПОМЕСТИТЬ втЗапасыИПотребностиВНаличии
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|				Номенклатура = &Номенклатура
	|				И Характеристика = &Характеристика
	|				И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доступно.Номенклатура КАК Номенклатура,
	|	Доступно.Склад КАК Склад,
	|	Доступно.Назначение КАК Назначение,
	|	Доступно.ВНаличии КАК Доступно,
	|	0 КАК РезервНеТоИР,
	|	0 КАК КОбеспечению,
	|	0 КАК Поступит
	|ПОМЕСТИТЬ втОстаткиДоступно
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК Доступно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО Доступно.Назначение = проф_Назначения.Ссылка
	|			И (проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Доступно.Номенклатура,
	|	Доступно.Склад,
	|	Доступно.Назначение,
	|	Доступно.ВНаличии,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК Доступно
	|ГДЕ
	|	Доступно.Назначение = ЗНАЧЕНИЕ(Справочник.проф_Назначения.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервНеТоИР.Номенклатура,
	|	РезервНеТоИР.Склад,
	|	РезервНеТоИР.Назначение,
	|	0,
	|	РезервНеТоИР.ВНаличии,
	|	0,
	|	0
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК РезервНеТоИР
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО РезервНеТоИР.Назначение = проф_Назначения.Ссылка
	|			И (НЕ проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	0,
	|	0,
	|	0,
	|	СУММА(проф_ЗапасыИПотребностиОстатки.ПоступитОстаток)
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|			Номенклатура = &Номенклатура
	|				И Характеристика = &Характеристика
	|				И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	0,
	|	0,
	|	СУММА(проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток) - СУММА(проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток),
	|	0
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|			Номенклатура = &Номенклатура
	|				И Характеристика = &Характеристика
	|				И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|ГДЕ
	|	проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстаткиДоступно.Номенклатура КАК Номенклатура,
	|	втОстаткиДоступно.Склад КАК Склад,
	|	СУММА(втОстаткиДоступно.Доступно) КАК Доступно,
	|	СУММА(втОстаткиДоступно.РезервНеТоИР) КАК РезервНеТоИР,
	|	СУММА(втОстаткиДоступно.КОбеспечению) КАК ЗаказИнициатора,
	|	СУММА(втОстаткиДоступно.Поступит) КАК Поступит
	|ИЗ
	|	втОстаткиДоступно КАК втОстаткиДоступно
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстаткиДоступно.Склад,
	|	втОстаткиДоступно.Номенклатура";
		
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ПодразделенияСлужбыТОИР", ПодразделенияСлужбыТОИР);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Запрос.УстановитьПараметр("Склад", Склад);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Склад = &Склад", "ИСТИНА");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Характеристика) Тогда
		Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Характеристика = &Характеристика", "ИСТИНА");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Возврат торо_ОбщегоНазначения.РезультатЗапросаВМассивСтруктур(Результат);
	//-- Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	
КонецФункции

&НаСервере
Процедура ВывестиСписокАналогов()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 11.09.2023																									
	ИмяСписка = "СписокНоменклатураАналог";     
	
	РеквизитДинамическийСписок = ЭтаФорма[ИмяСписка];
	РеквизитДинамическийСписок.ПроизвольныйЗапрос = Истина;
	РеквизитДинамическийСписок.ТекстЗапроса = ВернутьТекстЗапросаТаблицаАналогов();
	
	ФОИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры");
	
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("Склад", Объект.Склад);
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ФОИспользоватьСерииНоменклатуры", ФОИспользоватьСерииНоменклатуры);
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ИспользоватьОтборПоСкладу", ЗначениеЗаполнено(Объект.Склад));
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("Дата", ЭтотОбъект["Дата"]);
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ПодразделенияСлужбыТОИР", ЭтотОбъект["ПодразделенияСлужбыТОИР"]);
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ФОИспользоватьХарактеристикиНоменклатуры", ИспользоватьХарактеристикиНоменклатуры);
	
	ГруппаСписокНоменклатураАналог = ЭтаФорма.Элементы.Добавить("ГруппаСписокНоменклатураАналог", Тип("ГруппаФормы"), Элементы.СтраницаСтандартныйПоискНоменклатура);
	ГруппаСписокНоменклатураАналог.Вид 						= ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСписокНоменклатураАналог.Заголовок 				= НСтр("ru = 'Аналог номенклатуры (скрыть)'");
	ГруппаСписокНоменклатураАналог.ЗаголовокСвернутогоОтображения = НСтр("ru = 'Аналог номенклатуры (показать)'");
	ГруппаСписокНоменклатураАналог.Отображение 				= ОтображениеОбычнойГруппы.Нет;
	ГруппаСписокНоменклатураАналог.ОтображатьЗаголовок 		= Истина;
	ГруппаСписокНоменклатураАналог.Группировка 				= ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаСписокНоменклатураАналог.РастягиватьПоГоризонтали = Истина;
	ГруппаСписокНоменклатураАналог.Поведение 				= ПоведениеОбычнойГруппы.Свертываемая;
	ГруппаСписокНоменклатураАналог.ОтображениеУправления 	= ОтображениеУправленияОбычнойГруппы.ГиперссылкаЗаголовка;
	ГруппаСписокНоменклатураАналог.Скрыть();
	
	ТаблицаФормы = Элементы.Добавить(ИмяСписка, Тип("ТаблицаФормы"), ГруппаСписокНоменклатураАналог);	
	ТаблицаФормы.ПутьКДанным = ИмяСписка;
	ТаблицаФормы.КоманднаяПанель.Видимость = Ложь;
	
	СтруктураКолонок = Новый Структура;
	СтруктураКолонок.Вставить("Регистратор", 							Истина);
	СтруктураКолонок.Вставить("НоменклатураОсновная", 					Ложь);
	СтруктураКолонок.Вставить("ХарактеристикаОсновная", 				Ложь);
	СтруктураКолонок.Вставить("Артикул",			 					Истина);
	СтруктураКолонок.Вставить("Номенклатура_Аналог", 					Истина);
	СтруктураКолонок.Вставить("Серия", 									ФОИспользоватьСерииНоменклатуры);
	СтруктураКолонок.Вставить("СерииИспользуются", 						Ложь);
	СтруктураКолонок.Вставить("Характеристика_ХарактеристикаАналога", 	Истина);
	СтруктураКолонок.Вставить("ЕдиницаХранения", 						Истина);
	СтруктураКолонок.Вставить("ХарактеристикиИспользуются", 			Ложь);
	СтруктураКолонок.Вставить("Доступно", 								Истина);
	СтруктураКолонок.Вставить("РезервНеТОиР", 							Истина);
	СтруктураКолонок.Вставить("ЗаказИнициатора",						Истина);
	СтруктураКолонок.Вставить("Поступит", 								Истина);

	ВывестиКолонкиДинамическогоСписка(ИмяСписка, СтруктураКолонок, ТаблицаФормы);	
	
	Элементы[ИмяСписка + "РезервНеТОиР"].Заголовок = НСтр("ru = 'Резерв не ТОиР'");
	Элементы[ИмяСписка].УстановитьДействие("Выбор", "СписокНоменклатураАналогВыбор");
	Элементы[ИмяСписка].УстановитьДействие("ПриАктивизацииЯчейки", "СписокНоменклатураАналогПриАктивизацииЯчейки");
	//-- Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	
КонецПроцедуры

&НаСервере
Функция ВернутьТекстЗапросаТаблицаАналогов()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	торо_АналогиНоменклатурыДляРемонтов.Материал КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|			ТОГДА торо_АналогиНоменклатурыДляРемонтов.ХарактеристикаМатериала
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	торо_АналогиНоменклатурыДляРемонтов.Аналог КАК Аналог,
	|	торо_АналогиНоменклатурыДляРемонтов.Регистратор КАК Регистратор,
	|	ВЫБОР
	|		КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|			ТОГДА торо_АналогиНоменклатурыДляРемонтов.ХарактеристикаАналога
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ХарактеристикаАналога,
	|	СправочникНоменклатура.ЕдиницаИзмерения КАК ЕдиницаХранения,
	|	СправочникВидыНоменклатуры.ИспользоватьСерии КАК СерииИспользуются,
	|	торо_АналогиНоменклатурыДляРемонтов.Регистратор КАК ДокументУстановкиАналогов,
	|	СправочникНоменклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)) КАК ХарактеристикиИспользуются,
	|	СправочникНоменклатура.Артикул КАК Артикул
	|ПОМЕСТИТЬ втТаблицаАналогов
	|ИЗ
	|	РегистрСведений.торо_АналогиНоменклатурыДляРемонтов КАК торо_АналогиНоменклатурыДляРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
	|			ПО СправочникНоменклатура.ВидНоменклатуры = СправочникВидыНоменклатуры.Ссылка
	|		ПО торо_АналогиНоменклатурыДляРемонтов.Аналог = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	торо_АналогиНоменклатурыДляРемонтов.Период <= &Дата
	|	И торо_АналогиНоменклатурыДляРемонтов.ПериодЗавершения = ДАТАВРЕМЯ(1, 1, 1)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	торо_АналогиНоменклатурыДляРемонтов.Материал,
	|	ВЫБОР
	|		КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|			ТОГДА торо_АналогиНоменклатурыДляРемонтов.ХарактеристикаМатериала
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	торо_АналогиНоменклатурыДляРемонтов.Аналог,
	|	торо_АналогиНоменклатурыДляРемонтов.Регистратор,
	|	ВЫБОР
	|		КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|			ТОГДА торо_АналогиНоменклатурыДляРемонтов.ХарактеристикаАналога
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	СправочникНоменклатура.ЕдиницаИзмерения,
	|	СправочникВидыНоменклатуры.ИспользоватьСерии,
	|	торо_АналогиНоменклатурыДляРемонтов.Регистратор,
	|	СправочникНоменклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)),
	|	СправочникНоменклатура.Артикул
	|ИЗ
	|	РегистрСведений.торо_АналогиНоменклатурыДляРемонтов КАК торо_АналогиНоменклатурыДляРемонтов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
	|			ПО СправочникНоменклатура.ВидНоменклатуры = СправочникВидыНоменклатуры.Ссылка
	|		ПО торо_АналогиНоменклатурыДляРемонтов.Аналог = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	торо_АналогиНоменклатурыДляРемонтов.Период <= &Дата
	|	И торо_АналогиНоменклатурыДляРемонтов.ПериодЗавершения = ДАТАВРЕМЯ(1, 1, 1) = ЛОЖЬ
	|	И торо_АналогиНоменклатурыДляРемонтов.ПериодЗавершения >= &Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Аналог,
	|	ХарактеристикаАналога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика КАК Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Склад КАК Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение,
	|	проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток КАК ВНаличии
	|ПОМЕСТИТЬ втЗапасыИПотребностиВНаличии
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|			(Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						втТаблицаАналогов.Аналог КАК Аналог,
	|						втТаблицаАналогов.ХарактеристикаАналога КАК ХарактеристикаАналога
	|					ИЗ
	|						втТаблицаАналогов КАК втТаблицаАналогов)
	|				И &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Склад,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|			(Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						втТаблицаАналогов.Аналог КАК Аналог,
	|						втТаблицаАналогов.ХарактеристикаАналога КАК ХарактеристикаАналога
	|					ИЗ
	|						втТаблицаАналогов КАК втТаблицаАналогов)
	|				И (&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад)) КАК проф_ЗапасыИПотребностиОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура КАК Номенклатура,
	|	втЗапасыИПотребности.Характеристика КАК Характеристика,
	|	втЗапасыИПотребности.Назначение КАК Назначение,
	|	втЗапасыИПотребности.Склад КАК Склад,
	|	втЗапасыИПотребности.ВНаличии КАК Доступно,
	|	0 КАК РезервНеТОиР
	|ПОМЕСТИТЬ втДоступноРезервНеТОиР
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО втЗапасыИПотребности.Назначение = проф_Назначения.Ссылка
	|			И (проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение,
	|	втЗапасыИПотребности.Склад,
	|	втЗапасыИПотребности.ВНаличии,
	|	0
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребности
	|ГДЕ
	|	втЗапасыИПотребности.Назначение = ЗНАЧЕНИЕ(Справочник.проф_Назначения.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение,
	|	втЗапасыИПотребности.Склад,
	|	0,
	|	втЗапасыИПотребности.ВНаличии
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребности
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО втЗапасыИПотребности.Назначение = проф_Назначения.Ссылка
	|			И (НЕ проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыИПотребностиПредварительно.Номенклатура КАК Номенклатура,
	|	ЗапасыИПотребностиПредварительно.Характеристика КАК Характеристика,
	|	СУММА(ЗапасыИПотребностиПредварительно.КОбеспечениюОстаток) - СУММА(ЗапасыИПотребностиПредварительно.ВНаличииОстаток) КАК КОбеспечению,
	|	СУММА(ЗапасыИПотребностиПредварительно.ПоступитОстаток) КАК Поступит,
	|	СУММА(ЗапасыИПотребностиПредварительно.Доступно) КАК Доступно,
	|	СУММА(ЗапасыИПотребностиПредварительно.РезервНеТОиР) КАК РезервНеТОиР
	|ПОМЕСТИТЬ втЗапасыИПотребности
	|ИЗ
	|	(ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Характеристика,
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток КАК КОбеспечениюОстаток,
	|		проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток КАК ВНаличииОстаток,
	|		0 КАК ПоступитОстаток,
	|		0 КАК Доступно,
	|		0 КАК РезервНеТОиР,
	|		проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|	ГДЕ
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.ПоступитОстаток,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток,
	|		проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток,
	|		0,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|				&Дата,
	|				&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|	ГДЕ
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА проф_ЗапасыИПотребностиОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.ПоступитОстаток,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|				&Дата,
	|				&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втДоступноРезервНеТОиР.Номенклатура,
	|		ВЫБОР
	|			КОГДА &ФОИспользоватьХарактеристикиНоменклатуры
	|				ТОГДА втДоступноРезервНеТОиР.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		0,
	|		0,
	|		0,
	|		втДоступноРезервНеТОиР.Доступно,
	|		втДоступноРезервНеТОиР.РезервНеТОиР,
	|		втДоступноРезервНеТОиР.Назначение
	|	ИЗ
	|		втДоступноРезервНеТОиР КАК втДоступноРезервНеТОиР) КАК ЗапасыИПотребностиПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыИПотребностиПредварительно.Характеристика,
	|	ЗапасыИПотребностиПредварительно.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаАналогов.Регистратор КАК Регистратор,
	|	втТаблицаАналогов.Номенклатура КАК НоменклатураОсновная,
	|	втТаблицаАналогов.Характеристика КАК ХарактеристикаОсновная,
	|	втТаблицаАналогов.Артикул КАК Артикул,
	|	втТаблицаАналогов.Аналог КАК Аналог,
	|	втТаблицаАналогов.ХарактеристикаАналога КАК ХарактеристикаАналога,
	|	втТаблицаАналогов.ЕдиницаХранения КАК ЕдиницаХранения,
	|	втТаблицаАналогов.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	втТаблицаАналогов.СерииИспользуются КАК СерииИспользуются,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Доступно, 0)) КАК Доступно,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.РезервНеТОиР, 0)) КАК РезервНеТОиР,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.КОбеспечению, 0)) КАК ЗаказИнициатора,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Поступит, 0)) КАК Поступит
	|ИЗ
	|	втТаблицаАналогов КАК втТаблицаАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗапасыИПотребности КАК втЗапасыИПотребности
	|		ПО втТаблицаАналогов.Аналог = втЗапасыИПотребности.Номенклатура
	|			И втТаблицаАналогов.ХарактеристикаАналога = втЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ИСТИНА
	|	И &ИспользоватьОтборПоСкладу
	|	И ЕСТЬNULL(втЗапасыИПотребности.РезервНеТОиР, 0) + ЕСТЬNULL(втЗапасыИПотребности.Доступно, 0) + ЕСТЬNULL(втЗапасыИПотребности.КОбеспечению, 0) + ЕСТЬNULL(втЗапасыИПотребности.Поступит, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.СерииИспользуются
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	втТаблицаАналогов.СерииИспользуются,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Доступно, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.РезервНеТОиР, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.КОбеспечению, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Поступит, 0))
	|ИЗ
	|	втТаблицаАналогов КАК втТаблицаАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗапасыИПотребности КАК втЗапасыИПотребности
	|		ПО втТаблицаАналогов.Аналог = втЗапасыИПотребности.Номенклатура
	|			И втТаблицаАналогов.ХарактеристикаАналога = втЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ИСТИНА
	|	И &ИспользоватьОтборПоСкладу = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.СерииИспользуются
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	втТаблицаАналогов.СерииИспользуются,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Доступно, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.РезервНеТОиР, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.КОбеспечению, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Поступит, 0))
	|ИЗ
	|	втТаблицаАналогов КАК втТаблицаАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗапасыИПотребности КАК втЗапасыИПотребности
	|		ПО втТаблицаАналогов.Аналог = втЗапасыИПотребности.Номенклатура
	|			И втТаблицаАналогов.ХарактеристикаАналога = втЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ЛОЖЬ
	|	И &ИспользоватьОтборПоСкладу
	|	И ЕСТЬNULL(втЗапасыИПотребности.РезервНеТОиР, 0) + ЕСТЬNULL(втЗапасыИПотребности.Доступно, 0) + ЕСТЬNULL(втЗапасыИПотребности.КОбеспечению, 0) + ЕСТЬNULL(втЗапасыИПотребности.Поступит, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.СерииИспользуются
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	втТаблицаАналогов.СерииИспользуются,
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Доступно, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.РезервНеТОиР, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.КОбеспечению, 0)),
	|	СУММА(ЕСТЬNULL(втЗапасыИПотребности.Поступит, 0))
	|ИЗ
	|	втТаблицаАналогов КАК втТаблицаАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗапасыИПотребности КАК втЗапасыИПотребности
	|		ПО втТаблицаАналогов.Аналог = втЗапасыИПотребности.Номенклатура
	|			И втТаблицаАналогов.ХарактеристикаАналога = втЗапасыИПотребности.Характеристика
	|ГДЕ
	|	&ФОИспользоватьСерииНоменклатуры = ЛОЖЬ
	|	И &ИспользоватьОтборПоСкладу = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаАналогов.Регистратор,
	|	втТаблицаАналогов.ХарактеристикиИспользуются,
	|	втТаблицаАналогов.ЕдиницаХранения,
	|	втТаблицаАналогов.Артикул,
	|	втТаблицаАналогов.ХарактеристикаАналога,
	|	втТаблицаАналогов.Номенклатура,
	|	втТаблицаАналогов.Характеристика,
	|	втТаблицаАналогов.Аналог,
	|	втТаблицаАналогов.СерииИспользуются";
		
	Возврат ТекстЗапроса;
	//-- Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	
КонецФункции

&НаСервере
Процедура ВывестиОстаткиПоНазначению()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика КАК Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение,
	|	СУММА(проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток) КАК ВНаличии
	|ПОМЕСТИТЬ втЗапасыИПотребностиВНаличии
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|	проф_ЗапасыИПотребностиОстатки.Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	СУММА(проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток - проф_ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток)
	|ИЗ
	|	РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|			&Дата,
	|			&ИспользоватьОтборПоСкладу
	|				И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	проф_ЗапасыИПотребностиОстатки.Характеристика,
	|	проф_ЗапасыИПотребностиОстатки.Назначение,
	|	проф_ЗапасыИПотребностиОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыИПотребностиПредварительно.Номенклатура КАК Номенклатура,
	|	ЗапасыИПотребностиПредварительно.Характеристика КАК Характеристика,
	|	ЗапасыИПотребностиПредварительно.Назначение КАК Назначение,
	|	СУММА(ЗапасыИПотребностиПредварительно.КОбеспечениюОстаток) - СУММА(ЗапасыИПотребностиПредварительно.ВНаличииОстаток) КАК КОбеспечению,
	|	СУММА(ЗапасыИПотребностиПредварительно.ПоступитОстаток) КАК Поступит
	|ПОМЕСТИТЬ втЗапасыИПотребности
	|ИЗ
	|	(ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	|		проф_ЗапасыИПотребностиОстатки.Характеристика КАК Характеристика,
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток КАК КОбеспечениюОстаток,
	|		проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток КАК ВНаличииОстаток,
	|		0 КАК ПоступитОстаток,
	|		проф_ЗапасыИПотребностиОстатки.Назначение КАК Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|	ГДЕ
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		проф_ЗапасыИПотребностиОстатки.Характеристика,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.ПоступитОстаток,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(&Дата, &ИспользоватьОтборПоСкладу = ЛОЖЬ) КАК проф_ЗапасыИПотребностиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		проф_ЗапасыИПотребностиОстатки.Характеристика,
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток,
	|		проф_ЗапасыИПотребностиОстатки.ВНаличииОстаток,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|				&Дата,
	|				&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки
	|	ГДЕ
	|		проф_ЗапасыИПотребностиОстатки.КОбеспечениюОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		проф_ЗапасыИПотребностиОстатки.Номенклатура,
	|		проф_ЗапасыИПотребностиОстатки.Характеристика,
	|		0,
	|		0,
	|		проф_ЗапасыИПотребностиОстатки.ПоступитОстаток,
	|		проф_ЗапасыИПотребностиОстатки.Назначение
	|	ИЗ
	|		РегистрНакопления.проф_ЗапасыИПотребности.Остатки(
	|				&Дата,
	|				&ИспользоватьОтборПоСкладу
	|					И Склад = &Склад) КАК проф_ЗапасыИПотребностиОстатки) КАК ЗапасыИПотребностиПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыИПотребностиПредварительно.Характеристика,
	|	ЗапасыИПотребностиПредварительно.Номенклатура,
	|	ЗапасыИПотребностиПредварительно.Назначение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗапасыИПотребностиВНаличии.Номенклатура КАК Номенклатура,
	|	втЗапасыИПотребностиВНаличии.Характеристика КАК Характеристика,
	|	втЗапасыИПотребностиВНаличии.Назначение КАК Назначение,
	|	втЗапасыИПотребностиВНаличии.ВНаличии КАК ВНаличии,
	|	""Доступно"" КАК Доступно
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребностиВНаличии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО втЗапасыИПотребностиВНаличии.Назначение = проф_Назначения.Ссылка
	|			И (проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|ГДЕ
	|	втЗапасыИПотребностиВНаличии.ВНаличии > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗапасыИПотребностиВНаличии.Номенклатура,
	|	втЗапасыИПотребностиВНаличии.Характеристика,
	|	втЗапасыИПотребностиВНаличии.Назначение,
	|	втЗапасыИПотребностиВНаличии.ВНаличии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребностиВНаличии.Номенклатура,
	|	втЗапасыИПотребностиВНаличии.Характеристика,
	|	втЗапасыИПотребностиВНаличии.Назначение,
	|	втЗапасыИПотребностиВНаличии.ВНаличии,
	|	""Доступно""
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребностиВНаличии
	|ГДЕ
	|	втЗапасыИПотребностиВНаличии.Назначение = ЗНАЧЕНИЕ(Справочник.проф_Назначения.ПустаяСсылка)
	|	И втЗапасыИПотребностиВНаличии.ВНаличии > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗапасыИПотребностиВНаличии.Номенклатура,
	|	втЗапасыИПотребностиВНаличии.Характеристика,
	|	втЗапасыИПотребностиВНаличии.Назначение,
	|	втЗапасыИПотребностиВНаличии.ВНаличии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребностиВНаличии.Номенклатура,
	|	втЗапасыИПотребностиВНаличии.Характеристика,
	|	втЗапасыИПотребностиВНаличии.Назначение,
	|	втЗапасыИПотребностиВНаличии.ВНаличии,
	|	""РезервНеТОиР""
	|ИЗ
	|	втЗапасыИПотребностиВНаличии КАК втЗапасыИПотребностиВНаличии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.проф_Назначения КАК проф_Назначения
	|		ПО втЗапасыИПотребностиВНаличии.Назначение = проф_Назначения.Ссылка
	|			И (НЕ проф_Назначения.Подразделение В (&ПодразделенияСлужбыТОИР))
	|ГДЕ
	|	втЗапасыИПотребностиВНаличии.ВНаличии > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗапасыИПотребностиВНаличии.Назначение,
	|	втЗапасыИПотребностиВНаличии.Номенклатура,
	|	втЗапасыИПотребностиВНаличии.Характеристика,
	|	втЗапасыИПотребностиВНаличии.ВНаличии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение,
	|	СУММА(втЗапасыИПотребности.КОбеспечению),
	|	""ЗаказИнициатора""
	|ИЗ
	|	втЗапасыИПотребности КАК втЗапасыИПотребности
	|ГДЕ
	|	втЗапасыИПотребности.КОбеспечению > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение,
	|	СУММА(втЗапасыИПотребности.Поступит),
	|	""Поступит""
	|ИЗ
	|	втЗапасыИПотребности КАК втЗапасыИПотребности
	|ГДЕ
	|	втЗапасыИПотребности.Поступит > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗапасыИПотребности.Номенклатура,
	|	втЗапасыИПотребности.Характеристика,
	|	втЗапасыИПотребности.Назначение";
	
	ИмяСписка = "ОстаткиПоНазначению";
	      
	РеквизитДинамическийСписок = ЭтаФорма[ИмяСписка];
	РеквизитДинамическийСписок.ПроизвольныйЗапрос = Истина;
	РеквизитДинамическийСписок.ТекстЗапроса = ТекстЗапроса;
	
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("Склад", Объект.Склад);
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ИспользоватьОтборПоСкладу", ЗначениеЗаполнено(Объект.Склад));
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("Дата", ЭтотОбъект["Дата"]);
	РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра("ПодразделенияСлужбыТОИР", ЭтотОбъект["ПодразделенияСлужбыТОИР"]);
	
	ТаблицаФормы = Элементы.Добавить(ИмяСписка, Тип("ТаблицаФормы"), Элементы.Навигация);
	ТаблицаФормы.ПутьКДанным = ИмяСписка;
	ТаблицаФормы.КоманднаяПанель.Видимость = Ложь;
	ТаблицаФормы.Высота = 5;
	ТаблицаФормы.ГоризонтальнаяПолосаПрокрутки = ИспользованиеПолосыПрокрутки.НеИспользовать;

	СтруктураКолонок = Новый Структура;
	СтруктураКолонок.Вставить("Номенклатура", Ложь);
	СтруктураКолонок.Вставить("Характеристика", Ложь);
	СтруктураКолонок.Вставить("Назначение", Истина);
	СтруктураКолонок.Вставить("Количество_ВНаличии", Истина);

	ВывестиКолонкиДинамическогоСписка(ИмяСписка, СтруктураКолонок, ТаблицаФормы);
	//-- Проф-ИТ, #178, Соловьев А.А., 11.09.2023
	
КонецПроцедуры	

&НаСервере
Процедура ВывестиОстаткиТоваров()
	
	//++ Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	НовыйЭлемент  = Элементы.Добавить("ОстаткиТоваровРезервНеТОиР", Тип("ПолеФормы"), Элементы.ОстаткиТоваров);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "ОстаткиТоваров.РезервНеТОиР";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Резерв не ТОиР'");
	
	НовыйЭлемент  = Элементы.Добавить("ОстаткиТоваровЗаказИнициатора", Тип("ПолеФормы"), Элементы.ОстаткиТоваров);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "ОстаткиТоваров.ЗаказИнициатора";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Заказ инициатора'");
	
	НовыйЭлемент  = Элементы.Добавить("ОстаткиТоваровПоступит", Тип("ПолеФормы"), Элементы.ОстаткиТоваров);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "ОстаткиТоваров.Поступит";
	НовыйЭлемент.Заголовок = НСтр("ru = 'Поступит'");
	//-- Проф-ИТ, #178, Соловьев А.А., 15.09.2023
	
КонецПроцедуры	

&НаСервере
Процедура ВывестиКолонкиДинамическогоСписка(ИмяСписка, СтруктураКолонок, ТаблицаФормы)
	
	//++ Проф-ИТ, #178, Соловьев А.А., 12.09.2023
	Для Каждого Элемент Из СтруктураКолонок Цикл
		
		ПараметрыКолонки = СтрЗаменить(Элемент.Ключ, "_", Символы.ПС);
		Если СтрЧислоСтрок(ПараметрыКолонки) > 1 Тогда
			Заголовок = СтрПолучитьСтроку(ПараметрыКолонки, 1);
			ИмяКолонки = СтрПолучитьСтроку(ПараметрыКолонки, 2);
		Иначе
			Заголовок = "";
			ИмяКолонки = ПараметрыКолонки; 
		КонецЕсли;
		
		НоваяКолонкаТаблицы = Элементы.Добавить(ИмяСписка + ИмяКолонки, Тип("ПолеФормы"), ТаблицаФормы);
		Если Заголовок <> "" Тогда 
			НоваяКолонкаТаблицы.Заголовок = Заголовок; 
		КонецЕсли;
		НоваяКолонкаТаблицы.ПутьКДанным = ИмяСписка + "." + ИмяКолонки; 
		НоваяКолонкаТаблицы.Видимость = Элемент.Значение;
		НоваяКолонкаТаблицы.Ширина = 20;
		
	КонецЦикла;
	//-- Проф-ИТ, #178, Соловьев А.А., 12.09.2023
	
КонецПроцедуры	

#КонецОбласти
