
//++ Проф-ИТ, #129, Башинская А., 13.10.2023

#Область ПрограммныйИнтерфейс

Функция ПечатьАктТехническогоСостоянияТС(МассивОбъектов, ПараметрыПечати, ВидПечатнойФормы) Экспорт
	
	МассивРасширенийФалов = Новый Массив;
	МассивРасширенийФалов.Добавить("jpg");
	МассивРасширенийФалов.Добавить("jpeg");
	МассивРасширенийФалов.Добавить("png");
	МассивРасширенийФалов.Добавить("bmp");
	МассивРасширенийФалов.Добавить("gif");  
	МассивРасширенийФалов.Добавить("tiff");
	
	ТабДок = Новый ТабличныйДокумент;
	
	//++ Проф-ИТ, #129, Антонов Б.Л.,14.09.2023
	Если ВидПечатнойФормы = "проф_АктТехническогоСостоянияТС" Тогда
		Макет = ПолучитьМакет("проф_ПФ_MXL_АктТехническогоСостоянияТС");		
	ИначеЕсли ВидПечатнойФормы = "проф_АктТехническогоСостоянияДСТ" Тогда
		Макет = ПолучитьМакет("проф_ПФ_MXL_АктТехническогоСостоянияДСТ");
	Иначе
		Возврат ТабДок;
	КонецЕсли;
	//-- Проф-ИТ, #129, Антонов Б.Л.,14.09.2023

	ЗаголовокАкта   = Макет.ПолучитьОбласть("ШапкаДокумента");
	ДатаАкта        = Макет.ПолучитьОбласть("ДатаДокумента");
	//++ Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
	ДанныеТС        = Макет.ПолучитьОбласть("ДанныеТС"); 
	Наработка       = Макет.ПолучитьОбласть("ДанныеТСНаработка");
	ДанныеТС2       = Макет.ПолучитьОбласть("ДанныеТС2");
	//-- Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
	ОсновнаяТаблица = Макет.ПолучитьОбласть("ТабличнаяЧасть");
	ШапкаВИН        = Макет.ПолучитьОбласть("ШапкаФотоВИН");
	СтрокаВИН       = Макет.ПолучитьОбласть("СтрокаФотоВИН");
	ШапкаФото1      = Макет.ПолучитьОбласть("ШапкаФото1");
	СтрокаФото1     = Макет.ПолучитьОбласть("СтрокаФото1");
	ШапкаФото2      = Макет.ПолучитьОбласть("ШапкаФото2");
	СтрокаФото2     = Макет.ПолучитьОбласть("СтрокаФото2");
	ШапкаФото3      = Макет.ПолучитьОбласть("ШапкаФото3");
	СтрокаФото3     = Макет.ПолучитьОбласть("СтрокаФото3");
	ШапкаФото4      = Макет.ПолучитьОбласть("ШапкаФото4");
	СтрокаФото4     = Макет.ПолучитьОбласть("СтрокаФото4");
	ВтораяТаблица   = Макет.ПолучитьОбласть("ТабличнаяЧасть2");
	ШапкаКомиссия   = Макет.ПолучитьОбласть("ШапкаКомиссия");
	СтрокаКомиссия  = Макет.ПолучитьОбласть("СтрокаКомиссия");
	ПустаяСтрока    = Макет.ПолучитьОбласть("ПустаяСтрокаКомиссия");
	
	ПерваяСтраница = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаАктТехническогоСостоянияТС();		
	Запрос.УстановитьПараметр("МассивСсылка", МассивОбъектов);
	Запрос.УстановитьПараметр("МассивРасширений", МассивРасширенийФалов);

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаАкт = РезультатЗапроса[5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "АктСсылка"); 
	
	Пока ВыборкаАкт.Следующий() Цикл 
		
		Если НЕ ПерваяСтраница Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПерваяСтраница = Ложь;
		КонецЕсли;
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
		ВыборкаОбъектРемонта = ВыборкаАкт.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОбъектВРемонте");	
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
		Пока ВыборкаОбъектРемонта.Следующий() Цикл
			
			Если НЕ ПерваяСтраница Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ЗаголовокАкта.Параметры.ИнвНомер = ВыборкаОбъектРемонта.ИнвентарныйНомер;
			ТабДок.Вывести(ЗаголовокАкта);
			
			ДатаАкта.Параметры.ДатаДокумента = Формат(ВыборкаАкт.Дата,"ДФ=dd.MM.yyyy");
			ТабДок.Вывести(ДатаАкта);
			
			ЗаполнитьЗначенияСвойств(ДанныеТС.Параметры, ВыборкаОбъектРемонта);
			
			ТабДок.Вывести(ДанныеТС);
			
			//++ Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
			ВывестиСтрокуНаработка(ВыборкаОбъектРемонта, Наработка, ТабДок);
			
			ЗаполнитьЗначенияСвойств(ДанныеТС2.Параметры, ВыборкаОбъектРемонта);
			
			ТабДок.Вывести(ДанныеТС2);
			//-- Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления 
			
			ТабДок.Вывести(ОсновнаяТаблица);
			
			ПустаяКартинка = Новый Картинка();
			СтрокаВИН.Рисунки.ФотоВИН.Картинка = ПустаяКартинка; 
			
			СтрокаФото1.Рисунки.ФотоОбщийВид1.Картинка = ПустаяКартинка; 
			СтрокаФото2.Рисунки.ФотоОбщийВид2.Картинка = ПустаяКартинка; 
			СтрокаФото3.Рисунки.ФотоОбщийВид3.Картинка = ПустаяКартинка; 
			СтрокаФото4.Рисунки.ФотоОбщийВид4.Картинка = ПустаяКартинка; 
			
			ВыборкаВсеФайлыПоОбъекту = РезультатЗапроса[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОбъектВРемонте");
			
			ВывестиВыборкуВсеФайлыПоОбъекту(ВыборкаВсеФайлыПоОбъекту, ВыборкаОбъектРемонта, СтрокаВИН,
												СтрокаФото1, СтрокаФото2, СтрокаФото3, СтрокаФото4);			 
			
			ТабДок.Вывести(ШапкаВИН);
			ТабДок.Вывести(СтрокаВИН);
			ТабДок.Вывести(ШапкаФото1);
			ТабДок.Вывести(СтрокаФото1);  
			ТабДок.Вывести(ШапкаФото2);
			ТабДок.Вывести(СтрокаФото2); 
			ТабДок.Вывести(ШапкаФото3);
			ТабДок.Вывести(СтрокаФото3); 
			ТабДок.Вывести(ШапкаФото4);
			ТабДок.Вывести(СтрокаФото4);
			
			ТабДок.Вывести(ВтораяТаблица);
			
			ТабДок.Вывести(ШапкаКомиссия); 
			
			ВыборкаКомиссия = РезультатЗапроса[4].Выбрать();
			
			ВывестиКомиссию(ВыборкаАкт, ВыборкаКомиссия, СтрокаКомиссия, ПустаяСтрока, ВидПечатнойФормы, ТабДок);
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТабДок;	
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
Процедура ВывестиСтрокуНаработка(ВыборкаОбъектРемонта, Наработка, ТабДок)
	
	ВыборкаПоНаработке = ВыборкаОбъектРемонта.Выбрать(); 
	Пока ВыборкаПоНаработке.Следующий() Цикл 
		Наработка.Параметры.Наработка   = ВыборкаПоНаработке.Наработка;
		Наработка.Параметры.ЕдИзмерения = ВыборкаПоНаработке.ЕдИзмерения;
		ТабДок.Вывести(Наработка);
	КонецЦикла;
	
КонецПроцедуры
//-- Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления 

Процедура ВывестиВыборкуВсеФайлыПоОбъекту(ВыборкаВсеФайлыПоОбъекту, ВыборкаОбъектРемонта,
											СтрокаВин, СтрокаФото1, СтрокаФото2, СтрокаФото3, СтрокаФото4)
	
	Пока ВыборкаВсеФайлыПоОбъекту.Следующий() Цикл
		Если ВыборкаВсеФайлыПоОбъекту.ОбъектВРемонте <> ВыборкаОбъектРемонта.ОбъектВРемонте Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаФайлыПоОбъекту = ВыборкаВсеФайлыПоОбъекту.Выбрать();  
		
		ВывестиВыборкуФайлыПоОбъекту(ВыборкаФайлыПоОбъекту, СтрокаВин, СтрокаФото1, СтрокаФото2, СтрокаФото3, СтрокаФото4); 	
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВывестиВыборкуФайлыПоОбъекту(ВыборкаФайлыПоОбъекту, СтрокаВин,
											СтрокаФото1, СтрокаФото2, СтрокаФото3, СтрокаФото4)
											
	СчетчикФайлов = 1;
	Пока ВыборкаФайлыПоОбъекту.Следующий() Цикл
		ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(ВыборкаФайлыПоОбъекту.СсылкаФайла);
		Если ЗначениеЗаполнено(ДвоичныеДанные) Тогда
			КартинкаФото = Новый Картинка(ДвоичныеДанные);
		КонецЕсли; 
		Если СчетчикФайлов = 1 Тогда
			СтрокаВИН.Рисунки.ФотоВИН.Картинка = КартинкаФото;
		ИначеЕсли СчетчикФайлов = 2 Тогда 
			СтрокаФото1.Рисунки.ФотоОбщийВид1.Картинка = КартинкаФото;
		ИначеЕсли СчетчикФайлов = 3 Тогда 
			СтрокаФото2.Рисунки.ФотоОбщийВид2.Картинка = КартинкаФото;
		ИначеЕсли СчетчикФайлов = 4 Тогда 
			СтрокаФото3.Рисунки.ФотоОбщийВид3.Картинка = КартинкаФото;
		ИначеЕсли СчетчикФайлов = 5 Тогда 
			СтрокаФото4.Рисунки.ФотоОбщийВид4.Картинка = КартинкаФото;
		Иначе
			Продолжить;
		КонецЕсли;
		СчетчикФайлов = СчетчикФайлов + 1;
	КонецЦикла;  	
	
КонецПроцедуры	

Процедура ВывестиКомиссию(ВыборкаАкт, ВыборкаКомиссия, СтрокаКомиссия, ПустаяСтрока, ВидПечатнойФормы, ТабДок)
	
	Если НЕ ВыборкаАкт.Комиссия.Пустая() Тогда				
		Счетчик = 1;
		Пока ВыборкаКомиссия.Следующий() Цикл
			Если ВыборкаКомиссия.АктСсылка = ВыборкаАкт.АктСсылка Тогда
				СтрокаКомиссия.Параметры.НомерСтроки  = Счетчик;
				СтрокаКомиссия.Параметры.ЧленКомиссии = ВыборкаКомиссия.ЧленКомиссии;
				//++ Проф-ИТ, #129, Антонов Б.Л.,15.09.2023
				Если ВидПечатнойФормы = "проф_АктТехническогоСостоянияТС" Тогда
					СтрокаКомиссия.Параметры.Должность = ВыборкаКомиссия.ТекущаяДолжность;
				КонецЕсли;
				//++ Проф-ИТ, #129, Антонов Б.Л.,15.09.2023
				ТабДок.Вывести(СтрокаКомиссия);
				Счетчик = Счетчик + 1;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Счетчик = 1 По 4 Цикл
			ПустаяСтрока.Параметры.НомерСтроки  = Счетчик;
			ТабДок.Вывести(ПустаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	

Функция ТекстЗапросаАктТехническогоСостоянияТС()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_АктОВыполненииЭтапаРабот.Ссылка КАК АктСсылка,
		|	торо_АктОВыполненииЭтапаРабот.Дата КАК Дата,
		|	торо_АктОВыполненииЭтапаРабот.Комиссия КАК Комиссия,
		|	ЕСТЬNULL(проф_МоделиТСиОборудования.ТипТС, """") КАК ТипТС,
		|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Марка, """") КАК проф_Марка,
		|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Модель, ЗНАЧЕНИЕ(Справочник.проф_МоделиТСиОборудования.ПустаяСсылка)) КАК проф_Модель,
		|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_VIN, """") КАК проф_VIN,
		|	ЕСТЬNULL(ГОД(торо_ОбъектыРемонта.ДатаВыпуска), ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВыпуска,
		|	ЕСТЬNULL(торо_ОбъектыРемонта.проф_Госномер, """") КАК проф_Госномер,
		|	ЕСТЬNULL(торо_ОбъектыРемонта.ИнвентарныйНомер, """") КАК ИнвентарныйНомер,
		|	ЕСТЬNULL(торо_ОбъектыРемонта.Ссылка, ЗНАЧЕНИЕ(Справочник.торо_ОбъектыРемонта.ПустаяСсылка)) КАК ОбъектВРемонте
		|ПОМЕСТИТЬ ВТ_ВРемонте
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот КАК торо_АктОВыполненииЭтапаРабот
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.торо_АктОВыполненииЭтапаРабот.РемонтыОборудования КАК торо_АктОВыполненииЭтапаРаботРемонтыОборудования
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.проф_МоделиТСиОборудования КАК проф_МоделиТСиОборудования
		|				ПО торо_ОбъектыРемонта.проф_Модель = проф_МоделиТСиОборудования.Ссылка
		|			ПО торо_АктОВыполненииЭтапаРаботРемонтыОборудования.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
		|		ПО торо_АктОВыполненииЭтапаРабот.Ссылка = торо_АктОВыполненииЭтапаРаботРемонтыОборудования.Ссылка
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРабот.Ссылка В(&МассивСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ВРемонте.ОбъектВРемонте КАК ОбъектВРемонте,
		|	МАКСИМУМ(проф_МестонахождениеТС.Дата) КАК Дата,
		|	МАКСИМУМ(проф_МестонахождениеТС.Подразделение) КАК Подразделение,
		|	ВТ_ВРемонте.АктСсылка КАК АктСсылка
		|ПОМЕСТИТЬ ВТ_Местонахождение
		|ИЗ
		|	ВТ_ВРемонте КАК ВТ_ВРемонте
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.проф_МестонахождениеТС КАК проф_МестонахождениеТС
		|		ПО ВТ_ВРемонте.ОбъектВРемонте = проф_МестонахождениеТС.ОбъектРемонта
		|ГДЕ
		|	проф_МестонахождениеТС.Дата <= ВТ_ВРемонте.Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВРемонте.АктСсылка,
		|	ВТ_ВРемонте.ОбъектВРемонте
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_НаработкаОбъектовРемонта.ОбъектРемонта КАК ОбъектРемонта,
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,19.09.2023, Исправления
		|	ЕСТЬNULL(торо_НаработкаОбъектовРемонта.Наработка, 0) КАК Наработка,
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,19.09.2023, Исправления
		|	ВТ_ВРемонте.АктСсылка КАК АктСсылка,
		|	ЕСТЬNULL(ЕдиницыИзмерения.Ссылка, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,20.09.2023, Исправления
		|	торо_НаработкаОбъектовРемонта.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТ_НаработкаПоРегистратору
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,20.09.2023, Исправления
		|ИЗ
		|	ВТ_ВРемонте КАК ВТ_ВРемонте
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.торо_НаработкаОбъектовРемонта КАК торо_НаработкаОбъектовРемонта
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыВыработкиОС КАК ПараметрыВыработкиОС
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
		|				ПО ПараметрыВыработкиОС.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
		|			ПО торо_НаработкаОбъектовРемонта.Показатель = ПараметрыВыработкиОС.Ссылка
		|		ПО ВТ_ВРемонте.ОбъектВРемонте = торо_НаработкаОбъектовРемонта.ОбъектРемонта
		|ГДЕ
		|	торо_НаработкаОбъектовРемонта.Период <= ВТ_ВРемонте.Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	торо_НаработкаОбъектовРемонта.ОбъектРемонта,
		|	ВТ_ВРемонте.АктСсылка,
		|	ЕСТЬNULL(ЕдиницыИзмерения.Ссылка, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)),  
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,19.09.2023, Исправления
		|	ЕСТЬNULL(торо_НаработкаОбъектовРемонта.Наработка, 0),
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,19.09.2023, Исправления
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,20.09.2023, Исправления
		|	торо_НаработкаОбъектовРемонта.Регистратор  
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,20.09.2023, Исправления
		|;
		|
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,20.09.2023, Исправления
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_НаработкаПоРегистратору.ОбъектРемонта КАК ОбъектРемонта,
		|	СУММА(ЕСТЬNULL(ВТ_НаработкаПоРегистратору.Наработка, 0)) КАК Наработка,
		|	ЕСТЬNULL(ВТ_НаработкаПоРегистратору.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ВТ_НаработкаПоРегистратору.АктСсылка КАК АктСсылка
		|ПОМЕСТИТЬ ВТ_Наработка
		|ИЗ
		|	ВТ_НаработкаПоРегистратору КАК ВТ_НаработкаПоРегистратору
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_НаработкаПоРегистратору.ОбъектРемонта,
		|	ВТ_НаработкаПоРегистратору.АктСсылка,
		|	ЕСТЬNULL(ВТ_НаработкаПоРегистратору.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка))
		|;
		|
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,20.09.2023, Исправления
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ФизическиеЛица.Ссылка, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ЧленКомиссии,
		|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ТекущаяДолжность,
		|	ВТ_ВРемонте.АктСсылка КАК АктСсылка
		|ИЗ
		|	ВТ_ВРемонте КАК ВТ_ВРемонте
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.торо_Комиссии.СоставКомиссии КАК торо_КомиссииСоставКомиссии
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|					ПО ФизическиеЛица.Ссылка = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
		|				ПО Сотрудники.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|			ПО торо_КомиссииСоставКомиссии.ЧленКомиссии = Сотрудники.Ссылка
		|		ПО ВТ_ВРемонте.Комиссия = торо_КомиссииСоставКомиссии.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВРемонте.АктСсылка,
		|	ЕСТЬNULL(ФизическиеЛица.Ссылка, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)),
		|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ВТ_Наработка.Наработка, 0) КАК Наработка,
		|	ВТ_ВРемонте.ТипТС КАК ТипТС,
		|	ВТ_ВРемонте.проф_Марка КАК МаркаТС,
		|	ВТ_ВРемонте.проф_Модель КАК МодельТС,
		|	ВТ_ВРемонте.проф_VIN КАК VIN,
		|	ВТ_ВРемонте.ДатаВыпуска КАК ДатаВыпуска,
		|	ВТ_ВРемонте.проф_Госномер КАК ГосНомер,
		|	ЕСТЬNULL(ВТ_Местонахождение.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Местонахождение,
		|	ВТ_ВРемонте.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВТ_ВРемонте.АктСсылка КАК АктСсылка,
		|	ВТ_ВРемонте.Комиссия КАК Комиссия,
		|	ВТ_ВРемонте.Дата КАК Дата,
		|	ВТ_ВРемонте.ОбъектВРемонте КАК ОбъектВРемонте,
		|	ЕСТЬNULL(ВТ_Наработка.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдИзмерения
		|ИЗ
		|	ВТ_ВРемонте КАК ВТ_ВРемонте
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Наработка КАК ВТ_Наработка
		|		ПО ВТ_ВРемонте.АктСсылка = ВТ_Наработка.АктСсылка
		|			И ВТ_ВРемонте.ОбъектВРемонте = ВТ_Наработка.ОбъектРемонта
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Местонахождение КАК ВТ_Местонахождение
		|		ПО ВТ_ВРемонте.АктСсылка = ВТ_Местонахождение.АктСсылка
		|			И ВТ_ВРемонте.ОбъектВРемонте = ВТ_Местонахождение.ОбъектВРемонте 
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,19.09.2023, Исправления
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВРемонте.ИнвентарныйНомер,
		|	ЕСТЬNULL(ВТ_Наработка.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)),
		|	ВТ_ВРемонте.АктСсылка,
		|	ВТ_ВРемонте.ОбъектВРемонте,
		|	ВТ_ВРемонте.Дата,
		|	ВТ_ВРемонте.Комиссия,
		|	ЕСТЬNULL(ВТ_Местонахождение.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)),
		|	ВТ_ВРемонте.проф_Госномер,
		|	ВТ_ВРемонте.проф_VIN,
		|	ВТ_ВРемонте.проф_Модель,
		|	ВТ_ВРемонте.ТипТС,
		|	ВТ_ВРемонте.проф_Марка,
		|	ЕСТЬNULL(ВТ_Наработка.Наработка, 0),
		|	ВТ_ВРемонте.ДатаВыпуска
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,19.09.2023, Исправления
		|ИТОГИ
		//++ Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
		|	МАКСИМУМ(ТипТС),
		|	МАКСИМУМ(МаркаТС),
		|	МАКСИМУМ(МодельТС),
		|	МАКСИМУМ(VIN),
		|	МАКСИМУМ(ДатаВыпуска),
		|	МАКСИМУМ(ГосНомер),
		|	МАКСИМУМ(Местонахождение),
		|	МАКСИМУМ(ИнвентарныйНомер),
		|	МАКСИМУМ(Комиссия),
		|	МАКСИМУМ(Дата)
		|ПО
		|	АктСсылка,
		|	ОбъектВРемонте
		|;
		//-- Проф-ИТ, #86, #129, Антонов Б.Л.,18.09.2023, Исправления
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_ОбъектыРемонтаПрисоединенныеФайлы.Ссылка КАК СсылкаФайла,
		|	ВТ_ВРемонте.АктСсылка КАК АктСсылка,
		|	ВТ_ВРемонте.ОбъектВРемонте КАК ОбъектВРемонте
		|ИЗ
		|	Справочник.торо_ОбъектыРемонтаПрисоединенныеФайлы КАК торо_ОбъектыРемонтаПрисоединенныеФайлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВРемонте КАК ВТ_ВРемонте
		|		ПО торо_ОбъектыРемонтаПрисоединенныеФайлы.ВладелецФайла = ВТ_ВРемонте.ОбъектВРемонте
		|ГДЕ
		|	торо_ОбъектыРемонтаПрисоединенныеФайлы.Расширение В(&МассивРасширений)
		|ИТОГИ ПО
		|	ОбъектВРемонте";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

#КонецОбласти

//-- Проф-ИТ, #129, Башинская А., 13.10.2023