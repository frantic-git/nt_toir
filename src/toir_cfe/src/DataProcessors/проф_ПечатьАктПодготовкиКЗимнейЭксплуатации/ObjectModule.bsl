
//++ Проф-ИТ, #78, Башинская А., 13.10.2023	

#Область ПрограммныйИнтерфейс

Функция ПечатьАктПодготовкиКЗимнейЭксплуатации(МассивОбъектов, ПараметрыПечати) Экспорт 
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("проф_АктПодготовкиКЗимнейЭксплуатации");
	
	ОбластьШапкаКомплектующие = Макет.ПолучитьОбласть("ОбластьШапкаКомплектующие");
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал"); 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаПечатьАктПодготовкиКЗимнейЭксплуатации(); 
	
	ОбщаяВыборка = Запрос.ВыполнитьПакет(); 
	ТЗОР = ОбщаяВыборка[0].Выбрать(); 
	ВыборкаРемонтныеРаботы = ОбщаяВыборка[1].Выбрать();  
	ВыборкаМатериальныеЗатраты = ОбщаяВыборка[2].Выбрать(); 
	ВыборкаСерийныеЗапчасти = ОбщаяВыборка[3].Выбрать();
	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл  
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", СсылкаНаОбъект);				
		Пока ТЗОР.НайтиСледующий(Отбор) Цикл
			
			ОбластьШапки   = Макет.ПолучитьОбласть("ОбластьШапка");
			ОбластьШапки.Параметры.ОбъектРемонта = ТЗОР.ОбъектРемонта;
			ТабДок.Вывести(ОбластьШапки);
			
			Отбор.Вставить("ID", ТЗОР.ID);
			
			ВывестиРемонтныеРаботы(ВыборкаРемонтныеРаботы, Отбор, ТабДок, Макет);
									
			ТабДок.Вывести(ОбластьШапкаКомплектующие);
			
			ВывестиМатериальныеЗатраты(ВыборкаМатериальныеЗатраты, Отбор, ТабДок, Макет);
			
			ВывестиСерийныеЗапчасти(ВыборкаСерийныеЗапчасти, Отбор, ТабДок, Макет);
						
			ТабДок.Вывести(ОбластьПодвал); 
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
		ТЗОР.Сбросить();
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПечатьАктПодготовкиКЗимнейЭксплуатации()
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	торо_АктОВыполненииЭтапаРаботРемонтыОборудования.ОбъектРемонта.Наименование КАК ОбъектРемонта,
		|	торо_АктОВыполненииЭтапаРаботРемонтыОборудования.ID КАК ID,
		|	торо_АктОВыполненииЭтапаРаботРемонтыОборудования.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот.РемонтыОборудования КАК торо_АктОВыполненииЭтапаРаботРемонтыОборудования
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРаботРемонтыОборудования.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	торо_АктОВыполненииЭтапаРаботРемонтыОборудования.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Ссылка КАК Ссылка,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.РемонтыОборудования_ID КАК ID,
		|	ПРЕДСТАВЛЕНИЕ(торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.РемонтнаяРабота) КАК РемонтнаяРабота,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Родитель_ID КАК Родитель_ID,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Выполнено КАК Выполнено
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот.РемонтныеРаботы КАК торо_АктОВыполненииЭтапаРаботРемонтныеРаботы
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.РемонтыОборудования_ID КАК ID,
//++ Проф-ИТ, #296, Горетовская М.С., 05.10.2023 - Доработки по задаче #78
		|	СпрНоменклатура.Код КАК КодНоменклатуры,
//-- Проф-ИТ, #296, Горетовская М.С., 05.10.2023 - Доработки по задаче #78
		|	ЕСТЬNULL(СпрНоменклатура.Артикул, """") КАК Артикул,
		|	ЕСТЬNULL(СпрНоменклатура.Наименование, """") КАК Наименование,
		|	ПРЕДСТАВЛЕНИЕ(торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.ХарактеристикаНоменклатуры) КАК Характеристика,
		|	ПРЕДСТАВЛЕНИЕ(СпрНоменклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|	торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.Количество КАК Количество,
		|	торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот.МатериальныеЗатраты КАК торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.Номенклатура = СпрНоменклатура.Ссылка
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	торо_АктОВыполненииЭтапаРаботМатериальныеЗатраты.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.Ссылка КАК Ссылка,
//++ Проф-ИТ, #296, Горетовская М.С., 05.10.2023 - Доработки по задаче #78
		|	СпрНоменклатура.Код КАК КодНоменклатуры,
//-- Проф-ИТ, #296, Горетовская М.С., 05.10.2023 - Доработки по задаче #78
		|	торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.РемонтыОборудования_ID КАК ID,
		|	ЕСТЬNULL(СпрНоменклатура.Артикул, """") КАК Артикул,
		|	ЕСТЬNULL(СпрНоменклатура.Наименование, """") КАК Наименование,
		|	ПРЕДСТАВЛЕНИЕ(торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.ХарактеристикаНоменклатурыНовая) КАК Характеристика,
		|	ПРЕДСТАВЛЕНИЕ(СпрНоменклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|	торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.КоличествоНовое КАК Количество
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот.СерийныеЗапчасти КАК торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.Номенклатура = СпрНоменклатура.Ссылка
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.Ссылка В(&МассивОбъектов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	торо_АктОВыполненииЭтапаРаботСерийныеЗапчасти.НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Процедура ВывестиРемонтныеРаботы(ВыборкаРемонтныеРаботы, Отбор, ТабДок, Макет)

	Пока ВыборкаРемонтныеРаботы.НайтиСледующий(Отбор) Цикл 
		ОбластьСтрокаРодитель  = Макет.ПолучитьОбласть("ОбластьСтрокаРодитель"); 
		ОбластьСтрокаРабота  = Макет.ПолучитьОбласть("ОбластьСтрокаРабота"); 	
		Если ЗначениеЗаполнено(ВыборкаРемонтныеРаботы.Родитель_ID) Тогда 
			ОбластьСтрокаРабота.Параметры.Работа = ВыборкаРемонтныеРаботы.РемонтнаяРабота;
			ОбластьСтрокаРабота.Параметры.Выполнено = ?(ВыборкаРемонтныеРаботы.Выполнено = Истина, "b", "");
			ТабДок.Вывести(ОбластьСтрокаРабота); 
		Иначе
			ОбластьСтрокаРодитель.Параметры.Родитель = ВыборкаРемонтныеРаботы.РемонтнаяРабота;
			ТабДок.Вывести(ОбластьСтрокаРодитель);
		КонецЕсли;	
	КонецЦикла;	
	ВыборкаРемонтныеРаботы.Сбросить();		
	
КонецПроцедуры	

Процедура ВывестиМатериальныеЗатраты(ВыборкаМатериальныеЗатраты, Отбор, ТабДок, Макет)
	
	Пока ВыборкаМатериальныеЗатраты.НайтиСледующий(Отбор) Цикл
		ОбластьСтрокаКомплектующие = Макет.ПолучитьОбласть("ОбластьСтрокаКомплектующие");
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаКомплектующие.Параметры, ВыборкаМатериальныеЗатраты);
		ТабДок.Вывести(ОбластьСтрокаКомплектующие);
	КонецЦикла;
	ВыборкаМатериальныеЗатраты.Сбросить();	
	
КонецПроцедуры	

Процедура ВывестиСерийныеЗапчасти(ВыборкаСерийныеЗапчасти, Отбор, ТабДок, Макет)
	
	Пока ВыборкаСерийныеЗапчасти.НайтиСледующий(Отбор) Цикл
		ОбластьСтрокаКомплектующие = Макет.ПолучитьОбласть("ОбластьСтрокаКомплектующие");
		ЗаполнитьЗначенияСвойств(ОбластьСтрокаКомплектующие.Параметры, ВыборкаСерийныеЗапчасти);
		ТабДок.Вывести(ОбластьСтрокаКомплектующие);
	КонецЦикла;
	ВыборкаСерийныеЗапчасти.Сбросить();	
	
КонецПроцедуры	

#КонецОбласти

//-- Проф-ИТ, #78, Башинская А., 13.10.2023	