
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура проф_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	ДобавляемыеРеквизиты = Новый Массив;
	НовыйРеквизит = Новый РеквизитФормы("Назначение", Новый ОписаниеТипов("СправочникСсылка.проф_Назначения"), "НоменклатураЗаказовПоставщикам");
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	НовыйЭлемент = Элементы.Добавить("НоменклатураЗаказовПоставщикамНазначение", Тип("ПолеФормы"), Элементы.НоменклатураЗаказовПоставщикам);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = НСтр("ru = 'Назначение'");
	НовыйЭлемент.ПутьКДанным = "НоменклатураЗаказовПоставщикам.Назначение";
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
&НаСервере
&ИзменениеИКонтроль("ПодготовитьДанныеЗаказовПоставщикам")
Функция проф_ПодготовитьДанныеЗаказовПоставщикам(ИдентификаторФормыВладельца)

	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	НоменклатураЗаказовПоставщикам.Номенклатура КАК Номенклатура,
	|	НоменклатураЗаказовПоставщикам.Характеристика КАК Характеристика,
	|	НоменклатураЗаказовПоставщикам.Количество КАК Количество,
	|	НоменклатураЗаказовПоставщикам.Склад КАК Склад,
	#Вставка
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	|	НоменклатураЗаказовПоставщикам.Назначение КАК Назначение,
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	#КонецВставки
	|	НоменклатураЗаказовПоставщикам.ДатаПоставки КАК ДатаПоставки,
	|	НоменклатураЗаказовПоставщикам.Поставщик КАК Поставщик,
	|	НоменклатураЗаказовПоставщикам.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	&НоменклатураЗаказовПоставщикам КАК НоменклатураЗаказовПоставщикам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.Количество КАК Количество,
	|	ВТ_ПодготовленныеДанные.Количество КАК КоличествоУпаковок,
	|	ВТ_ПодготовленныеДанные.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_ПодготовленныеДанные.Склад КАК Склад,
	#Вставка
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	|	ВТ_ПодготовленныеДанные.Назначение КАК проф_Назначение,
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	#КонецВставки
	|	ВТ_ПодготовленныеДанные.ДатаПоставки КАК ДатаПоставки,
	|	ВТ_ПодготовленныеДанные.Поставщик КАК Поставщик,
	|	ВТ_ПодготовленныеДанные.Идентификатор КАК Идентификатор
	|ИЗ
	|	ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|ИТОГИ
	|	МИНИМУМ(ДатаПоставки)
	|ПО
	|	Поставщик";

	Запрос.УстановитьПараметр("НоменклатураЗаказовПоставщикам", НоменклатураЗаказовПоставщикам.Выгрузить());

	РезультатЗапроса = Запрос.Выполнить();

	АдресаХранилищ = Новый Массив;

	ПустаяСсылкаНаЗаказ = Документы.торо_ЗаказПоставщику.ПустаяСсылка();

	ВыборкаПоставщиков = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоставщиков.Следующий() Цикл

		Товары = ПустаяСсылкаНаЗаказ.Товары.ВыгрузитьКолонки();
		ДокументыОснования = ПустаяСсылкаНаЗаказ.ДокументыОснования.ВыгрузитьКолонки();

		Выборка = ВыборкаПоставщиков.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

			ЗаказыПоТовару = РасшифровкаПотребностей.НайтиСтроки(Новый Структура("ИдентификаторТовара", Выборка.Идентификатор));
			Для Каждого Строка Из ЗаказыПоТовару Цикл
				Если ТипЗнч(Строка.Потребность) = Тип("Строка") Тогда
					Продолжить;
				КонецЕсли;	

				Если ДокументыОснования.Найти(Строка.Потребность, "ДокументОснование") = Неопределено Тогда
					НоваяСтрока = ДокументыОснования.Добавить();
					НоваяСтрока.ДокументОснование = Строка.Потребность;
				КонецЕсли;
			КонецЦикла;	
		КонецЦикла;

		ДанныеЗаказа = Новый Структура();
		ДанныеЗаказа.Вставить("Товары", Товары);
		ДанныеЗаказа.Вставить("ДокументыОснования", ДокументыОснования);
		ДанныеЗаказа.Вставить("ДатаПоставки", ВыборкаПоставщиков.ДатаПоставки);
		ДанныеЗаказа.Вставить("Контрагент", ВыборкаПоставщиков.Поставщик);

		АдресХранилища = ПоместитьВоВременноеХранилище(ДанныеЗаказа, ИдентификаторФормыВладельца);
		АдресаХранилищ.Добавить(АдресХранилища);
	КонецЦикла;

	Возврат АдресаХранилищ;

КонецФункции

&НаСервере
&ИзменениеИКонтроль("ОбновитьДанные")
Процедура проф_ОбновитьДанные(АдресВходныхДанных)

	ВходныеДанные = ПолучитьИзВременногоХранилища(АдресВходныхДанных);
	УдалитьИзВременногоХранилища(АдресВходныхДанных); 

	ТаблицаТоваров = ВходныеДанные.ТаблицаТоваров; 
	ТаблицаПотребностей = ВходныеДанные.ПотребностиПоЗаказамНаВП;

	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.КомпенсацияДоМинимальногоОстатка КАК КомпенсацияДоМинимальногоОстатка,
	|	ТаблицаТоваров.Склад КАК Склад,
	#Вставка
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	|	ТаблицаТоваров.Назначение КАК Назначение,
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	#КонецВставки
	|	ТаблицаТоваров.ДатаПоставки КАК ДатаПоставки,
	|	ТаблицаТоваров.СпособОбеспеченияПотребностей КАК СпособОбеспеченияПотребностей,
	|	ТаблицаТоваров.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОбеспеченияПотребностей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПотребностей.ЗаказНаВП КАК ЗаказНаВП,
	|	ТаблицаПотребностей.Количество КАК Количество,
	|	ТаблицаПотребностей.ИдентификаторТовара КАК ИдентификаторТовара
	|ПОМЕСТИТЬ втТаблицаПотребностей
	|ИЗ
	|	&ТаблицаПотребностей КАК ТаблицаПотребностей
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	втТаблицаТоваров.Характеристика КАК Характеристика,
	|	втТаблицаТоваров.Количество КАК Количество,
	|	втТаблицаТоваров.Склад КАК Склад,
	#Вставка
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	|	втТаблицаТоваров.Назначение КАК Назначение,
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	#КонецВставки
	|	втТаблицаТоваров.ДатаПоставки КАК ДатаПоставки,
	|	втТаблицаТоваров.СпособОбеспеченияПотребностей КАК СпособОбеспеченияПотребностей,
	|	ЕСТЬNULL(СпособыОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Поставщик,
	|	ВЫБОР
	|		КОГДА СпособыОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ИЛИ СпособыОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоступноИзменениеПоставщика,
	|	втТаблицаТоваров.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ втПодготовленныеДанные
	|ИЗ
	|	втТаблицаТоваров КАК втТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпособыОбеспеченияПотребностей
	|		ПО втТаблицаТоваров.СпособОбеспеченияПотребностей = СпособыОбеспеченияПотребностей.Ссылка";

	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров); 
	Запрос.УстановитьПараметр("ТаблицаПотребностей", ТаблицаПотребностей);
	Запрос.Выполнить();

	ОбновитьДанные_Поставщики(Запрос.МенеджерВременныхТаблиц);
	ОбновитьДанные_НоменклатураЗаказовПоставщикам(Запрос.МенеджерВременныхТаблиц); 
	ОбновитьДанные_РасшифровкаПотребностей(Запрос.МенеджерВременныхТаблиц);

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОбновитьДанные_НоменклатураЗаказовПоставщикам")
Процедура проф_ОбновитьДанные_НоменклатураЗаказовПоставщикам(МенеджерВТ)

	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "ВЫБРАТЬ
	|	втПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	втПодготовленныеДанные.Характеристика КАК Характеристика,
	|	втПодготовленныеДанные.Количество КАК РекомендуемоКЗаказу,
	|	втПодготовленныеДанные.Количество КАК Количество,
	|	втПодготовленныеДанные.Склад КАК Склад,
	#Вставка
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	|	втПодготовленныеДанные.Назначение КАК Назначение,
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	#КонецВставки
	|	втПодготовленныеДанные.ДатаПоставки КАК ДатаПоставки,
	|	втПодготовленныеДанные.СпособОбеспеченияПотребностей КАК СпособОбеспеченияПотребностей,
	|	втПодготовленныеДанные.Поставщик КАК Поставщик,
	|	втПодготовленныеДанные.ДоступноИзменениеПоставщика КАК ДоступноИзменениеПоставщика,
	|	втПодготовленныеДанные.Идентификатор КАК Идентификатор
	|ИЗ
	|	втПодготовленныеДанные КАК втПодготовленныеДанные";

	РезультатЗапроса = Запрос.Выполнить();

	НоменклатураЗаказовПоставщикам.Загрузить(РезультатЗапроса.Выгрузить());

КонецПроцедуры

#КонецОбласти