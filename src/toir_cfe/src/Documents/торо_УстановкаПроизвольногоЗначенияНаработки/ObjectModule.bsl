
#Область ОбработчикиСобытий

&Перед("ПередЗаписью")
Процедура проф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	//++ Проф-ИТ, #305, Корнилов М.С., 27.10.2023
	проф_ПроверитьТЧНаработкаОбъектовГруппаОС(Отказ);
	//-- Проф-ИТ, #305, Корнилов М.С., 27.10.2023
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Проф-ИТ, #305, Корнилов М.С., 27.10.2023
Процедура проф_ПроверитьТЧНаработкаОбъектовГруппаОС(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаработкаОбъектов.НомерСтроки КАК НомерСтроки,
	|	НаработкаОбъектов.ОбъектРемонта КАК ОбъектРемонта
	|ПОМЕСТИТЬ ВТ_НаработкаОбъектов
	|ИЗ
	|	&НаработкаОбъектов КАК НаработкаОбъектов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НаработкаОбъектов.НомерСтроки КАК НомерСтроки,
	|	торо_ОбъектыРемонта.Наименование КАК Наименование
	|ИЗ
	|	ВТ_НаработкаОбъектов КАК ВТ_НаработкаОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.торо_ОбъектыРемонта КАК торо_ОбъектыРемонта
	|		ПО ВТ_НаработкаОбъектов.ОбъектРемонта = торо_ОбъектыРемонта.Ссылка
	|ГДЕ
	|	торо_ОбъектыРемонта.проф_ГруппаОС = ""Транспортные средства""";
	
	Запрос.УстановитьПараметр("НаработкаОбъектов", НаработкаОбъектов);	
	ВыборкаНаработкаОбъектов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаНаработкаОбъектов.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю();    
		Сообщение.Текст = СтрШаблон(
			"Объект ремонта %1 является Транспортным средством в строке %2 списка ""Наработка оборудования""",
			ВыборкаНаработкаОбъектов.Наименование, ВыборкаНаработкаОбъектов.НомерСтроки);
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЦикла;
	
КонецПроцедуры
//-- Проф-ИТ, #305, Корнилов М.С., 27.10.2023

#КонецОбласти