
#Область ОбработчикиСобытий
	
&После("ОбработкаПроведения")
Процедура проф_ОбработкаПроведения(Отказ, Режим)
	
	//++ Проф-ИТ, #27, Соловьев А.А., 25.08.2023
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	торо_ВыбытиеОбъектаРемонта.Дата КАК Период,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.ХарактеристикаНоменклатуры КАК Характеристика,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Серия КАК Серия,
	|	СУММА(торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Количество) КАК ВНаличии,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Склад КАК Склад,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.проф_Назначение КАК Назначение
	|ИЗ
	|	Документ.торо_ВыбытиеОбъектаРемонта.СписокНоменклатуры КАК торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_ВыбытиеОбъектаРемонта КАК торо_ВыбытиеОбъектаРемонта
	|		ПО торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Ссылка = торо_ВыбытиеОбъектаРемонта.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТНоменклатура
	|		ПО торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Номенклатура = ТНоменклатура.Ссылка
	|ГДЕ
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Ссылка = &Ссылка
	|	И ТНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|
	|СГРУППИРОВАТЬ ПО
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Номенклатура,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.ХарактеристикаНоменклатуры,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Серия,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.Склад,
	|	торо_ВыбытиеОбъектаРемонтаСписокНоменклатуры.проф_Назначение,
	|	торо_ВыбытиеОбъектаРемонта.Дата";

	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаСписания", ДатаСписания);
	Запрос.УстановитьПараметр("ОбъектРемонта", ОбъектРемонта);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Движения.проф_ТоварыНаСкладах.Записывать = Истина;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.проф_ТоварыНаСкладах.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	//-- Проф-ИТ, #27, Соловьев А.А., 25.08.2023
	
КонецПроцедуры

#КонецОбласти