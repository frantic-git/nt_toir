#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура проф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// ++ Проф-ИТ, #180, Сергеев Д., 19.09.2023
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(Номер) Тогда
		УстановитьНовыйНомер();
	КонецЕсли;	
	
	// -- Проф-ИТ, #180, Сергеев Д., 19.09.2023
	
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура проф_ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ++ Проф-ИТ, #180, Сергеев Д., 19.09.2023
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.проф_КорректировкаЗапасовИПотребностей.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Отразитьпроф_ЗапасыИПотребности(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);	
	
	// -- Проф-ИТ, #180, Сергеев Д., 19.09.2023
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ Проф-ИТ, #180, Сергеев Д., 19.09.2023

Процедура Отразитьпроф_ЗапасыИПотребности(ДополнительныеСвойства, Движения, Отказ)
	
	Таблицапроф_ЗапасыИПотребности = ДополнительныеСвойства.ТаблицыДляДвижений.проф_ЗапасыИПотребности;
	
	Если Отказ ИЛИ Таблицапроф_ЗапасыИПотребности.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движенияпроф_ЗапасыИПотребности = Движения.проф_ЗапасыИПотребности;
	Движенияпроф_ЗапасыИПотребности.Записывать = Истина;
	Движенияпроф_ЗапасыИПотребности.Загрузить(Таблицапроф_ЗапасыИПотребности);		
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	мсвНоменклатуры = Товары.ВыгрузитьКолонку("Номенклатура");
    
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	ВидыНоменклатуры.ИспользоватьХарактеристики КАК ИспользоватьХарактеристики
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО Номенклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
	|ГДЕ
	|	Номенклатура.Ссылка В(&МассивНоменклатуры)";
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", мсвНоменклатуры);
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗнчНоменклатуры = РезультатЗапроса.Выгрузить();
	
	Для каждого текСтрока Из Товары Цикл
		Если ТаблицаЗнчНоменклатуры.Количество() > 0 Тогда
			НайденнаяСтрока = ТаблицаЗнчНоменклатуры.Найти(текСтрока.Номенклатура, "Номенклатура");	
			Если НайденнаяСтрока <> Неопределено 
			И НайденнаяСтрока.ИспользоватьХарактеристики
			И НЕ ЗначениеЗаполнено(текСтрока.Характеристика) Тогда
				
				ТекстСообщения = НСтр("ru = 'В строке %1 не заполнено поле ""Характеристика"".'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текСтрока.НомерСтроки);
				
				ПутьКРеквизиту = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
									"Товары", текСтрока.НомерСтроки, "Характеристика");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, ПутьКРеквизиту, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗаполненыРесурсыРНЗапасыИПотребности(текСтрока) Тогда				
			ТекстСообщения = НСтр("ru = 'В строке %1 при проведении документа хотя бы один из реквизитов:
				|ВНаличии, Заказано, КОбеспечению, НеОбеспечивать, ОтложитьРезервирование, Поступит,
				|РезервироватьНаСкладе, РезервироватьПоМереПоступления должен быть заполнен.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, текСтрока.НомерСтроки);

			ПутьКРеквизиту = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", текСтрока.НомерСтроки, "ВНаличии");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, ПутьКРеквизиту, "Объект", Отказ);		
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ЗаполненыРесурсыРНЗапасыИПотребности(СтрокаТовары)
	
	Если ЗначениеЗаполнено(СтрокаТовары.ВНаличии) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.Заказано) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.КОбеспечению) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.НеОбеспечивать) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.ОтложитьРезервирование) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.Поступит) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.РезервироватьНаСкладе) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТовары.РезервироватьПоМереПоступления) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;	
	
КонецФункции	

// -- Проф-ИТ, #180, Сергеев Д., 19.09.2023

#КонецОбласти

#КонецЕсли