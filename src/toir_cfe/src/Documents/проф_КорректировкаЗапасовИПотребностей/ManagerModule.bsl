
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// ++ Проф-ИТ, #180, Сергеев Д., 19.09.2023

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапроспроф_ЗапасыИПотребности(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
КонецПроцедуры

Функция ТекстЗапроспроф_ЗапасыИПотребности(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "проф_ЗапасыИПотребности";
	
	Если НЕ ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	проф_КорректировкаЗапасовИПотребностей.Ссылка КАК Ссылка,
	|	проф_КорректировкаЗапасовИПотребностей.Склад КАК Склад,
	|	проф_КорректировкаЗапасовИПотребностей.Дата КАК Период,
	|	ВЫБОР
	|		КОГДА проф_КорректировкаЗапасовИПотребностей.ВидОперации = ЗНАЧЕНИЕ(Перечисление.торо_ВидыОперацийКорректировкиОстатков.Списание)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	проф_КорректировкаЗапасовИПотребностейТовары.Номенклатура КАК Номенклатура,
	|	проф_КорректировкаЗапасовИПотребностейТовары.Характеристика КАК Характеристика,
	|	проф_КорректировкаЗапасовИПотребностейТовары.Назначение КАК Назначение,
	|	проф_КорректировкаЗапасовИПотребностейТовары.ВНаличии КАК ВНаличии,
	|	проф_КорректировкаЗапасовИПотребностейТовары.Заказано КАК Заказано,
	|	проф_КорректировкаЗапасовИПотребностейТовары.КОбеспечению КАК КОбеспечению,
	|	проф_КорректировкаЗапасовИПотребностейТовары.НеОбеспечивать КАК НеОбеспечивать,
	|	проф_КорректировкаЗапасовИПотребностейТовары.ОтложитьРезервирование КАК ОтложитьРезервирование,
	|	проф_КорректировкаЗапасовИПотребностейТовары.Поступит КАК Поступит,
	|	проф_КорректировкаЗапасовИПотребностейТовары.РезервироватьНаСкладе КАК РезервироватьНаСкладе,
	|	проф_КорректировкаЗапасовИПотребностейТовары.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления
	|ИЗ
	|	Документ.проф_КорректировкаЗапасовИПотребностей КАК проф_КорректировкаЗапасовИПотребностей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.проф_КорректировкаЗапасовИПотребностей.Товары КАК проф_КорректировкаЗапасовИПотребностейТовары
	|		ПО (проф_КорректировкаЗапасовИПотребностейТовары.Ссылка = проф_КорректировкаЗапасовИПотребностей.Ссылка)
	|ГДЕ
	|	проф_КорректировкаЗапасовИПотребностей.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры)

	Если ЗначениеЗаполнено(Регистры) Тогда
		
		Если ТипЗнч(Регистры) = Тип("Строка") Тогда
			МассивРегистров = Новый Структура(Регистры);
		Иначе
			МассивРегистров = Регистры;
		КонецЕсли;
		
		Если НЕ МассивРегистров.Свойство(ИмяРегистра) Тогда
			Возврат Ложь;
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат Истина;

КонецФункции

// -- Проф-ИТ, #180, Сергеев Д., 19.09.2023

#КонецОбласти

#КонецОбласти

#КонецЕсли