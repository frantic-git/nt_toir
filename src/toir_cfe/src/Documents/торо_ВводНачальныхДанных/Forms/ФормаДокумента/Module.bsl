#Область ОбработчикиСобытийФормы

&НаСервере
Процедура проф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// ++ Проф-ИТ, #53, Башинская А.Ю., 05.08.2023
	СтрЗагрузкаДанныхИзExcel = "ЗагрузкаДанныхИзExcel";
	НоваяКоманда			 = Команды.Добавить(СтрЗагрузкаДанныхИзExcel);
	НоваяКоманда.Действие    = "проф_НачатьЗагрузкуДанныхИзШаблона";
	НоваяКоманда.Заголовок   = "Загрузка данных из Excel";
	
	НовыйЭлемент = Элементы.Добавить(СтрЗагрузкаДанныхИзExcel, Тип("КнопкаФормы"), ЭтаФорма);
	НовыйЭлемент.ИмяКоманды  = СтрЗагрузкаДанныхИзExcel;    
	НовыйЭлемент.Отображение = ОтображениеКнопки.КартинкаИТекст;
	НовыйЭлемент.Картинка    = БиблиотекаКартинок.ЗаполнитьФорму;
	
	ИмяЭлемента = "ГруппаДополнительныеРеквизиты";    
	ГруппаФормы = Элементы.Найти(ИмяЭлемента);
	Если ГруппаФормы <> Неопределено Тогда
		Элементы.Переместить(НовыйЭлемент, ЭтаФорма, ГруппаФормы);
	КонецЕсли;
	// -- Проф-ИТ, #53, Башинская А.Ю., 05.08.2023
	
КонецПроцедуры

#КонецОбласти            

#Область ОбработчикиКомандФормы

// ++ Проф-ИТ, #53, Башинская А.Ю., 05.08.2023

&НаКлиенте
Процедура проф_НачатьЗагрузкуДанныхИзШаблона(Команда)
	
	ПрофильДоступен = проф_ПроверитьПрофильДоступа();
	
	Если Не ПрофильДоступен Тогда         		
		ТекстПредупреждения = НСтр("ru = 'Недостаточно прав доступа для выполнения операции.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе     
		ОписаниеОповещения = Новый ОписаниеОповещения("проф_ВыполнитьПослеВыбораФайла", ЭтаФорма);
		ОткрытьФорму("Обработка.проф_ЗагрузкаНачальныхДанныхПоОбъектуРемонта.Форма.Форма", , , , , ,
			ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;     
	
КонецПроцедуры	   

// -- Проф-ИТ, #53, Башинская А.Ю., 05.08.2023

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// ++ Проф-ИТ, #53, Башинская А.Ю., 05.08.2023

&НаКлиенте
Процедура проф_ВыполнитьПослеВыбораФайла(РезультатЗакрытия, ДопПараметры) Экспорт
    
    Если Не ЗначениеЗаполнено(РезультатЗакрытия) Тогда
        Возврат;
    КонецЕсли;
	
	СообщениеРезультат = "";
	
	проф_ЗагрузкаДанных(РезультатЗакрытия, СообщениеРезультат);
	Попытка  
		// Для заполненных данных нужно вызвать пересчет Итогов По Наработке
		СлужебныеРеквизитыЗаполнитьНаСервере(); 
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		Записать(ПараметрыЗаписи);
	Исключение 
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());		
		СообщениеРезультат = СообщениеРезультат + Символы.ПС + ОписаниеОшибки;	
	КонецПопытки;
	ПараметрыФ = Новый Структура("ОповещениеОРезультатах", Истина);   
	ПараметрыФ.Вставить("СообщениеРезультат", СообщениеРезультат);
	ОткрытьФорму("Обработка.проф_ЗагрузкаНачальныхДанныхПоОбъектуРемонта.Форма.Форма", ПараметрыФ, , , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаСервере
Процедура проф_ЗагрузкаДанных(Знач РезультатЗакрытия, СообщениеРезультат)
	
	ТЗ = ПолучитьИзВременногоХранилища(РезультатЗакрытия);   
	Обработки.проф_ЗагрузкаНачальныхДанныхПоОбъектуРемонта.ЗагрузитьДанныеВДокумент(Объект, ТЗ, СообщениеРезультат);

КонецПроцедуры

&НаСервере
Функция проф_ПроверитьПрофильДоступа()   

	Возврат Обработки.проф_ЗагрузкаНачальныхДанныхПоОбъектуРемонта.ЕстьПрофильДоступа(
		Пользователи.ТекущийПользователь(), Справочники.ПрофилиГруппДоступа.УтверждениеНормативов);
	
КонецФункции
	
// -- Проф-ИТ, #53, Башинская А.Ю., 05.08.2023	

#КонецОбласти 
