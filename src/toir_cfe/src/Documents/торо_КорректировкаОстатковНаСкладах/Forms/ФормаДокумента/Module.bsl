
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура проф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	проф_СтраницыТовары = Элементы.Добавить("проф_СтраницыТовары", Тип("ГруппаФормы"), ЭтотОбъект);
	проф_СтраницыТовары.Вид = ВидГруппыФормы.Страницы;
	проф_СтраницыТовары.Заголовок = НСтр("ru = 'Страницы товары'");
	
	проф_СтраницаТовары = Элементы.Добавить("проф_СтраницаТовары", Тип("ГруппаФормы"), проф_СтраницыТовары);
	проф_СтраницаТовары.Вид = ВидГруппыФормы.Страница;
	проф_СтраницаТовары.Заголовок = НСтр("ru = 'Товары'");
	проф_СтраницаТовары.ПутьКДаннымЗаголовка = "Объект.Товары.КоличествоСтрок";
	
	проф_СтраницаТоварыСНазначением = Элементы.Добавить("проф_СтраницаТоварыСНазначением", Тип("ГруппаФормы"), проф_СтраницыТовары);
	проф_СтраницаТоварыСНазначением.Вид = ВидГруппыФормы.Страница;
	проф_СтраницаТоварыСНазначением.Заголовок = НСтр("ru = 'Товары с назначением'");
	проф_СтраницаТоварыСНазначением.ПутьКДаннымЗаголовка = "Объект.проф_ТоварыСНазначением.КоличествоСтрок";
	
	ТаблицаФормы = Элементы.Добавить("проф_ТоварыСНазначением", Тип("ТаблицаФормы"), проф_СтраницаТоварыСНазначением);
	ТаблицаФормы.ПутьКДанным = "Объект.проф_ТоварыСНазначением";
	
	НовыйЭлемент  = Элементы.Добавить("проф_ТоварыСНазначениемНомерСтроки", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "Объект.проф_ТоварыСНазначением.НомерСтроки";
	
	НовыйЭлемент  = Элементы.Добавить("проф_ТоварыСНазначениемНоменклатура", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "Объект.проф_ТоварыСНазначением.Номенклатура";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "проф_ТоварыСНазначениемНоменклатураПриИзменении"); 
	
	НовыйЭлемент  = Элементы.Добавить("проф_ТоварыСНазначениемХарактеристика", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "Объект.проф_ТоварыСНазначением.Характеристика";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "проф_ТоварыСНазначениемХарактеристикаПриИзменении"); 
	
	НовыйЭлемент  = Элементы.Добавить("проф_ТоварыСНазначениемСерия", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "Объект.проф_ТоварыСНазначением.Серия";
	
	НовыйЭлемент  = Элементы.Добавить("проф_ТоварыСНазначениемНазначение", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "Объект.проф_ТоварыСНазначением.Назначение";
	
	НовыйЭлемент  = Элементы.Добавить("проф_ТоварыСНазначениемКоличество", Тип("ПолеФормы"), ТаблицаФормы);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "Объект.проф_ТоварыСНазначением.Количество";
	
	Элементы.Переместить(проф_СтраницыТовары, ЭтотОбъект, Элементы.ГруппаПричинаКорректировки);
	Элементы.Переместить(Элементы.Товары, проф_СтраницаТовары);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		проф_ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	проф_СлужебныеРеквизитыЗаполнитьНаСервере();
	проф_УстановитьУсловноеОформление();
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	
	//++ Проф-ИТ, #401, Соловьев А.А., 11.12.2023
	проф_ОбщегоНазначенияВызовСервера.УстановитьПараметрВыбораПодразделения(Элементы);
	//-- Проф-ИТ, #401, Соловьев А.А., 11.12.2023
	
КонецПроцедуры

&НаСервере
Процедура проф_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	проф_ПриЧтенииСозданииНаСервере()
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
КонецПроцедуры

&НаСервере
Процедура проф_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	проф_СлужебныеРеквизитыЗаполнитьНаСервере();
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура проф_ТоварыСНазначениемНоменклатураПриИзменении(Элемент)
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	ТекущиеДанные = Элементы.проф_ТоварыСНазначением.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
	    Возврат;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущиеДанные.Характеристика);
	
	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "проф_ТоварыСНазначением"));

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
	ТекущиеДанные.СерииИспользуются = ПолучитьСерииИспользуются(ТекущиеДанные.Номенклатура);
	ТекущиеДанные.Серия = Неопределено;
	
	ТекущиеДанные.ВидНоменклатуры = ПолучитьВидНоменклатуры(ТекущиеДанные.Номенклатура);
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
КонецПроцедуры

&НаКлиенте
Процедура проф_ТоварыСНазначениемХарактеристикаПриИзменении(Элемент)
	//++ Проф-ИТ, #27, Соловьев А.А., 31.08.2023
	ТекущиеДанные = Элементы.проф_ТоварыСНазначением.ТекущиеДанные;

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ХарактеристикаПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "проф_ТоварыСНазначением"));

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	//-- Проф-ИТ, #27, Соловьев А.А., 31.08.2023
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023

&НаСервере
Процедура ДобавитьСлужебныеРеквизитыТЧ()
	
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	РеквизитыТаблицы = ЭтотОбъект.ПолучитьРеквизиты("Объект.проф_ТоварыСНазначением");
	Для Каждого Элемент Из РеквизитыТаблицы Цикл
		Если Элемент.Имя = "ХарактеристикиИспользуются" Тогда 
			Возврат;
		КонецЕсли;	
	КонецЦикла;	
	
	ДобавляемыеРеквизиты = Новый Массив;
	НовыйРеквизит = Новый РеквизитФормы("ХарактеристикиИспользуются", Новый ОписаниеТипов("Булево"), "Объект.проф_ТоварыСНазначением"); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 	
	НовыйРеквизит = Новый РеквизитФормы("СерииИспользуются", Новый ОписаниеТипов("Булево"), "Объект.проф_ТоварыСНазначением"); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	НовыйРеквизит = Новый РеквизитФормы("ВидНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры"), "Объект.проф_ТоварыСНазначением"); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	
КонецПроцедуры

&НаСервере
Процедура проф_УстановитьУсловноеОформление()
	
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.проф_ТоварыСНазначениемХарактеристика.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.проф_ТоварыСНазначением.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.НейтральноСерый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<характеристики не используются>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
		
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.проф_ТоварыСНазначениемСерия.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.проф_ТоварыСНазначением.СерииИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.НейтральноСерый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<серии не используются>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	
КонецПроцедуры

&НаСервере
Процедура проф_СлужебныеРеквизитыЗаполнитьНаСервере()
	
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	Если Объект.проф_ТоварыСНазначением.Количество() > 0 Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	проф_ТоварыСНазначением.Номенклатура КАК Номенклатура,
		|	проф_ТоварыСНазначением.Характеристика КАК Характеристика,
		|	проф_ТоварыСНазначением.Количество КАК Количество,
		|	проф_ТоварыСНазначением.Серия КАК Серия,
		|	проф_ТоварыСНазначением.Назначение КАК Назначение
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	&проф_ТоварыСНазначением КАК проф_ТоварыСНазначением
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Характеристика КАК Характеристика,
		|	втТовары.Количество КАК Количество,
		|	втТовары.Серия КАК Серия,
		|	втТовары.Назначение КАК Назначение,
		|	СправочникНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО втТовары.Номенклатура = СправочникНоменклатура.Ссылка";
		
		Запрос.УстановитьПараметр("проф_ТоварыСНазначением", Объект.проф_ТоварыСНазначением.Выгрузить());
		Объект.проф_ТоварыСНазначением.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.проф_ТоварыСНазначением,
		Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются")));
			
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.проф_ТоварыСНазначением,
		Новый Структура("ЗаполнитьПризнакСерииИспользуются",
			Новый Структура("Номенклатура", "СерииИспользуются")));
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	
КонецПроцедуры

&НаСервере
Процедура проф_ПриЧтенииСозданииНаСервере()
	
	//++ Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	ДобавитьСлужебныеРеквизитыТЧ();
	//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023
	
КонецПроцедуры

//-- Проф-ИТ, #27, Соловьев А.А., 24.08.2023

#КонецОбласти