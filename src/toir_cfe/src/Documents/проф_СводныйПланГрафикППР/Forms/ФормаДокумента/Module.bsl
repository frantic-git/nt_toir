
#Область ОбработчикиСобытийФормы

// ++ Проф-ИТ, #227, Соловьев А.А., 19.09.2023

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборВТЧ(Команда)
	
	ЗначениеОтбора = Новый Структура("Проведен, Организация, ВидОперации", 
		Истина, Объект.Организация, ПредопределенноеЗначение("Перечисление.торо_ВидыОперацийПланаГрафикаППР.ПланРабот"));
	ПараметрыФормы = Новый Структура("Отбор, МножественныйВыбор", ЗначениеОтбора, Истина);
	ОткрытьФорму("Документ.торо_ПланГрафикРемонта.ФормаВыбора", ПараметрыФормы, Элементы.СписокППР);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписание()
	
	#Если НЕ МобильныйКлиент Тогда
		
		проф_КорректировкаПланГрафиковППРРасписание = ПолучитьРасписаниеРегламентногоЗадания();
		
		Обработчик = Новый ОписаниеОповещения("РегламентныеЗаданияПослеИзмененияРасписания",
			ЭтотОбъект, "проф_КорректировкаПланГрафиковППРРасписание");
		Диалог = Новый ДиалогРасписанияРегламентногоЗадания(проф_КорректировкаПланГрафиковППРРасписание);
		Диалог.Показать(Обработчик);
		
	#КонецЕсли
	
КонецПроцедуры

// -- Проф-ИТ, #227, Соловьев А.А., 19.09.2023

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокППР

// ++ Проф-ИТ, #227, Соловьев А.А., 19.09.2023

&НаКлиенте
Процедура СписокППРОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для Каждого ВыбранныйДокумент Из ВыбранноеЗначение Цикл
			НоваяСтрока = Объект.СписокППР.Добавить();
			НоваяСтрока.ПланГрафикППР = ВыбранныйДокумент;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

// -- Проф-ИТ, #227, Соловьев А.А., 19.09.2023

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ Проф-ИТ, #227, Соловьев А.А., 19.09.2023

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 

&НаКлиенте
Процедура РегламентныеЗаданияПослеИзмененияРасписания(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание <> Неопределено Тогда
		проф_КорректировкаПланГрафиковППРРасписание = Расписание; 
		ЗаписатьРасписаниеРегламентногоЗадания(Расписание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРасписаниеРегламентногоЗадания()
	
	Возврат РегламентныеЗадания.НайтиПредопределенное("проф_КорректировкаПланГрафиковППР").Расписание;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьРасписаниеРегламентногоЗадания(Знач Расписание)
	
	РегламентноеЗаданиеФоновыйРасчет = РегламентныеЗадания.НайтиПредопределенное("проф_КорректировкаПланГрафиковППР");
	
	Если РегламентноеЗаданиеФоновыйРасчет.Расписание <> Расписание Тогда
		РегламентноеЗаданиеФоновыйРасчет.Расписание = Расписание;
		РегламентноеЗаданиеФоновыйРасчет.Записать();
	КонецЕсли;
	
КонецПроцедуры

// -- Проф-ИТ, #227, Соловьев А.А., 19.09.2023

#КонецОбласти