
//++ Проф-ИТ, #329, Соловьев А.А., 07.11.2023

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура проф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если ЭтоНовый()
		И НЕ ЗначениеЗаполнено(Номер) Тогда
		УстановитьНовыйНомер();
	КонецЕсли;
	
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура проф_ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.проф_СписаниеТМЦНеТребуется.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Отразитьпроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура проф_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыПоУмолчанию();
	
КонецПроцедуры

&После("ОбработкаПроверкиЗаполнения")
Процедура проф_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ФОИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("торо_ИспользоватьХарактеристикиНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
	Если ФОИспользоватьХарактеристикиНоменклатуры = Истина Тогда
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыПоУмолчанию()
	
	Дата = ТекущаяДатаСеанса();
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура Отразитьпроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт(ДополнительныеСвойства, Движения, Отказ)
	
	Таблицапроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт = ДополнительныеСвойства.ТаблицыДляДвижений.проф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт;
	
	Если Отказ ИЛИ Таблицапроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движенияпроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт = Движения.проф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт;
	Движенияпроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт.Записывать = Истина;
	Движенияпроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт.Загрузить(Таблицапроф_ОстатокТМЦКСписаниюПоЗакрытымАктамНаРемонт);
	
КонецПроцедуры

#КонецОбласти

//-- Проф-ИТ, #329, Соловьев А.А., 07.11.2023