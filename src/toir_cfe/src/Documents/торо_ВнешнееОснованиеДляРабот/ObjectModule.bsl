
#Область ОбработчикиСобытий

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура проф_ОбработкаЗаполнения(Основание, СтандартнаяОбработка)

	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание);

		Если Основание.Свойство("МассивСтрокТЧОбследованноеОборудование") Тогда
			Для каждого ТекущаяСтрока Из Основание.МассивСтрокТЧОбследованноеОборудование Цикл
				НоваяСтрока = ОбследованноеОборудование.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			КонецЦикла;
		КонецЕсли;

		Если Основание.Свойство("МассивСтрокТЧРемонтныеРаботы") Тогда
			Для каждого ТекущаяСтрока Из Основание.МассивСтрокТЧРемонтныеРаботы Цикл
				НоваяСтрока = РемонтныеРаботы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.торо_АктОВыполненииРегламентногоМероприятия") Тогда
		Организация   = Основание.Организация;
		Подразделение = Основание.Подразделение;
		ДокументОснование = Основание;		
	КонецЕсли;

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.торо_АнализКоренныхПричин") Тогда
		Организация   = Основание.Организация;
		Подразделение = Основание.Подразделение;
		торо_ЗаполнениеДокументов.ЗаполнитьСтандартныеРеквизитыШапкиПоУмолчанию(ЭтотОбъект);

		Неотмененный = торо_ЗаполнениеДокументов20.ВернутьIDНеобработанногоРемонта(Основание);
		Если Неотмененный <> "" Тогда
			ЗаполнитьВнешнееОснованиеПоАнализу(Основание, ЭтотОбъект, Неотмененный);	
		КонецЕсли;
		ДокументОснование = Основание;
	КонецЕсли;	

#Вставка
	//++ Проф-ИТ, #291, Горетовская М.С., 13.10.2023
	// "Невыполненные ремонтные работы" при формировании документа "Смета (заявка на ремонт)"
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.торо_АктОВыполненииЭтапаРабот") Тогда
		СписокСвойствКопирования = "Автор, Комиссия, Комментарий, Организация, Ответственный, Подразделение,
			|ОбъектыРемонтаСтрокой";
		//++ Проф-ИТ, #326, Соловьев А.А., 01.11.2023
		СписокСвойствКопирования = СписокСвойствКопирования + ", проф_ЭксплуатацияВозможна";
		//-- Проф-ИТ, #326, Соловьев А.А., 01.11.2023
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание, СписокСвойствКопирования);
		Дата = ТекущаяДатаСеанса();
		ДокументОснование = Основание;
		
		ЗаполнитьПоАктуОВыполненииЭтапаРабот(Основание);
	КонецЕсли;	
	//-- Проф-ИТ, #291, Горетовская М.С., 13.10.2023
	// "Невыполненные ремонтные работы" при формировании документа "Смета (заявка на ремонт)"
#КонецВставки

	Если НЕ ЗначениеЗаполнено(ДатаСоздания) И ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиТОиР", 
		"ПодставлятьТекущуюДатуВоВнешнееОснованиеИВыявленныеДефекты",
		Истина) Тогда

		ДатаСоздания = ТекущаяДата();

	КонецЕсли;

	торо_ЗаполнениеДокументов.ЗаполнитьСтандартныеРеквизитыШапкиПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Проф-ИТ, #291, Горетовская М.С., 13.10.2023
// "Невыполненные ремонтные работы" при формировании документа "Смета (заявка на ремонт)"

Процедура ЗаполнитьПоАктуОВыполненииЭтапаРабот(Основание)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Ссылка КАК Ссылка,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.ID КАК ID,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Выполнено КАК Выполнено,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.ПроцентВыполненияРабот КАК ПроцентВыполненияРабот,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.РемонтнаяРабота КАК РемонтнаяРабота,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Родитель_ID КАК Родитель_ID,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Сумма КАК Сумма,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Количество КАК Количество,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Картинка КАК Картинка,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.НоваяОперация КАК НоваяОперация,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Исполнитель КАК Исполнитель,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.ДоговорБригада КАК ДоговорБригада,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Предписание КАК Предписание,
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.ID_ПараллельнойОперации КАК ID_ПараллельнойОперации
		|ПОМЕСТИТЬ ВТ_РемонтныеРаботы
		|ИЗ
		|	Документ.торо_АктОВыполненииЭтапаРабот.РемонтныеРаботы КАК торо_АктОВыполненииЭтапаРаботРемонтныеРаботы
		|ГДЕ
		|	торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Ссылка = &Ссылка
		|	И торо_АктОВыполненииЭтапаРаботРемонтныеРаботы.Выполнено = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_РемонтныеРаботы.ID КАК ID,
		|	ВТ_РемонтныеРаботы.РемонтнаяРабота КАК РемонтнаяРабота,
		|	ВТ_РемонтныеРаботы.Родитель_ID КАК Родитель_ID,
		|	ВТ_РемонтныеРаботы.Количество КАК Количество,
		|	ВТ_РемонтныеРаботы.Картинка КАК Картинка,
		|	ВТ_РемонтныеРаботы.РемонтыОборудования_ID КАК Предписание_ID,
		|	ВТ_РемонтныеРаботы.ID_ПараллельнойОперации КАК ID_ПараллельнойОперации
		|ИЗ
		|	ВТ_РемонтныеРаботы КАК ВТ_РемонтныеРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МатериальныеЗатраты.ID КАК ID,
		|	МатериальныеЗатраты.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	МатериальныеЗатраты.Количество КАК Количество,
		|	МатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	МатериальныеЗатраты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	МатериальныеЗатраты.Сумма КАК Сумма,
		|	МатериальныеЗатраты.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	МатериальныеЗатраты.КоличествоЕдиниц КАК КоличествоЕдиниц
		|ИЗ
		|	ВТ_РемонтныеРаботы КАК ВТ_РемонтныеРаботы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_АктОВыполненииЭтапаРабот.МатериальныеЗатраты КАК МатериальныеЗатраты
		|		ПО ВТ_РемонтныеРаботы.Ссылка = МатериальныеЗатраты.Ссылка
		|			И ВТ_РемонтныеРаботы.ID = МатериальныеЗатраты.ID
		//++ Проф-ИТ, #350, Соловьев А.А., 16.11.2023
		|			И (МатериальныеЗатраты.проф_Отменено)
		//-- Проф-ИТ, #350, Соловьев А.А., 16.11.2023
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	СерийныеЗапчасти.ID КАК ID,
		|	СерийныеЗапчасти.Номенклатура КАК Номенклатура,
		|	СерийныеЗапчасти.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СерийныеЗапчасти.СерияНоменклатуры КАК СерияНоменклатуры,
		|	СерийныеЗапчасти.НоменклатураНовая КАК НоменклатураНовая,
		|	СерийныеЗапчасти.ХарактеристикаНоменклатурыНовая КАК ХарактеристикаНоменклатурыНовая,
		|	СерийныеЗапчасти.СерияНоменклатурыНовая КАК СерияНоменклатурыНовая,
		|	СерийныеЗапчасти.СтатусДвиженияСерийныхЗЧ КАК СтатусДвиженияСерийныхЗЧ,
		|	СерийныеЗапчасти.Количество КАК Количество,
		|	СерийныеЗапчасти.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СерийныеЗапчасти.СтатусДвиженияНовойЗЧ КАК СтатусДвиженияНовойЗЧ,
		|	СерийныеЗапчасти.РемонтыОборудования_ID КАК РемонтыОборудования_ID,
		|	СерийныеЗапчасти.ЕдиницаИзмеренияНовая КАК ЕдиницаИзмеренияНовая,
		|	СерийныеЗапчасти.КоличествоНовое КАК КоличествоНовое
		|ИЗ
		|	ВТ_РемонтныеРаботы КАК ВТ_РемонтныеРаботы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_АктОВыполненииЭтапаРабот.СерийныеЗапчасти КАК СерийныеЗапчасти
		|		ПО ВТ_РемонтныеРаботы.Ссылка = СерийныеЗапчасти.Ссылка
		|			И ВТ_РемонтныеРаботы.РемонтыОборудования_ID = СерийныеЗапчасти.РемонтыОборудования_ID
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Пользователи.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.ПометкаУдаления
		|ПОМЕСТИТЬ ВТ_Сотрудник 
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники как Сотрудники
		|	ПО Пользователи.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
		|ГДЕ
		|	Пользователи.Ссылка = &Ответственный
		|	И не Пользователи.ФизическоеЛицо = Значение(Справочник.ФизическиеЛица.ПустаяСсылка)
		|
		|Объединить все
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.ПометкаУдаления
		|ИЗ
		|	Справочник.Сотрудники как Сотрудники
		|ГДЕ
		|	Сотрудники.Наименование = &ОтветственныйНаименование
		|
		|Упорядочить по
		|	Сотрудники.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РемонтыОборудования.ID КАК ID,
		|	РемонтыОборудования.ОбъектРемонта КАК ОбъектРемонта,
		|	ДобавитьКДате(&ТекДата, День, 2)  КАК ПлановаяДатаРемонта,
		|	ВТ_Сотрудник.Сотрудник КАК Ответственный,
		|	РемонтыОборудования.ИДДефекта КАК ИДДефекта
		|ИЗ
		|	ВТ_РемонтныеРаботы КАК ВТ_РемонтныеРаботы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.торо_АктОВыполненииЭтапаРабот.РемонтыОборудования КАК РемонтыОборудования
		|		ПО ВТ_РемонтныеРаботы.Ссылка = РемонтыОборудования.Ссылка
		|			И ВТ_РемонтныеРаботы.РемонтыОборудования_ID = РемонтыОборудования.ID
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сотрудник как ВТ_Сотрудник
		|		ПО ИСТИНА
		|";
	
	Запрос.УстановитьПараметр("Ссылка", Основание);
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДатаСеанса());
	Пользователь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Ответственный");
	Запрос.УстановитьПараметр("Ответственный", Пользователь);
	Запрос.УстановитьПараметр("ОтветственныйНаименование", Пользователь.Наименование);
	
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	пОбследованноеОборудование = РезультатЗапроса[5].Выгрузить();
	пРемонтныеРаботы = РезультатЗапроса[1].Выгрузить();
	пМатериальныеЗатраты = РезультатЗапроса[2].Выгрузить();
	пСерийныеЗапчасти = РезультатЗапроса[3].Выгрузить();
	
	Для каждого ОО из пОбследованноеОборудование цикл
		НоваяОО = ОбследованноеОборудование.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяОО,ОО);
		НоваяОО.ID = Новый УникальныйИдентификатор;
		
		М = пРемонтныеРаботы.НайтиСтроки(Новый Структура("Предписание_ID",ОО.ID));
		Для каждого стрМ из М цикл
			стрМ.Предписание_ID = НоваяОО.ID;
		КонецЦикла;
		
		М = пМатериальныеЗатраты.НайтиСтроки(Новый Структура("РемонтыОборудования_ID",ОО.ID));
		Для каждого стрМ из М цикл
			стрМ.РемонтыОборудования_ID = НоваяОО.ID;
		КонецЦикла;
		
		М = пСерийныеЗапчасти.НайтиСтроки(Новый Структура("РемонтыОборудования_ID",ОО.ID));
		Для каждого стрМ из М цикл
			стрМ.РемонтыОборудования_ID = НоваяОО.ID;
		КонецЦикла;
		
	КонецЦикла;
	
	РемонтныеРаботы.Загрузить(пРемонтныеРаботы);
	проф_МатериальныеЗатраты.Загрузить(пМатериальныеЗатраты);
	проф_СерийныеЗапчасти.Загрузить(пСерийныеЗапчасти);
	
КонецПроцедуры

Функция ЗаполнитьПоАктуОВыполненииЭтапаРаботЭкспорт(Основание) Экспорт
	
	ОбработкаЗаполнения(Основание, Истина);
	Возврат ЭтотОбъект;
	
КонецФункции

&После("ПередЗаписью")
Процедура проф_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//++ Проф-ИТ, #401, Соловьев А.А., 11.12.2023
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		проф_ОбщегоНазначенияВызовСервера.ПроверитьПризнакПодразделенияОрганизации(ЭтотОбъект["Подразделение"], Отказ);
	КонецЕсли;
	//-- Проф-ИТ, #401, Соловьев А.А., 11.12.2023
	
КонецПроцедуры

//-- Проф-ИТ, #291, Горетовская М.С., 13.10.2023
// "Невыполненные ремонтные работы" при формировании документа "Смета (заявка на ремонт)"

#КонецОбласти
